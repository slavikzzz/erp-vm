#Область СлужебныйПрограммныйИнтерфейс

// Возвращает регистрационный номер для целей обмена с ФСС.
Функция РегистрационныйНомерДляОбменаФСС(Объект) Экспорт
	Если ЗначениеЗаполнено(Объект.ДополнительныйКодФСС) Тогда
		Возврат Лев(СокрЛП(Объект.ДополнительныйКодФСС), 10);
	Иначе
		Возврат Лев(СокрЛП(Объект.РегистрационныйНомерФСС), 10);
	КонецЕсли;
КонецФункции

// АПК:558-выкл Функции прямого и обратного преобразования должны быть рядом.

// Возвращает СНИЛС для целей обмена с ФСС.
Функция СНИЛСВФорматеФСС(СтраховойНомерПФР) Экспорт
	Возврат СокрЛП(СтрЗаменить(СтрЗаменить(СтраховойНомерПФР, "-", ""), " ", ""));
КонецФункции

// Возвращает СНИЛС для целей поиска сотрудника в ИБ.
Функция СНИЛСВФорматеИБ(СНИЛСВФорматеФСС) Экспорт
	Если Не ЗначениеЗаполнено(СНИЛСВФорматеФСС) Тогда
		Возврат "";
	ИначеЕсли СтрДлина(СНИЛСВФорматеФСС) = 14 Тогда
		Возврат СНИЛСВФорматеФСС;
	КонецЕсли;
	Возврат СтрШаблон(
		"%1-%2-%3 %4",
		Сред(СНИЛСВФорматеФСС, 1, 3),
		Сред(СНИЛСВФорматеФСС, 4, 3),
		Сред(СНИЛСВФорматеФСС, 7, 3),
		Сред(СНИЛСВФорматеФСС, 10, 2))
КонецФункции

// АПК:558-вкл

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// АПК:558-выкл Константы с клиент-серверной природой, обратная совместимость.

// Возвращает дату вступления в силу статьи 1 Федерального закона от 25.02.2011 № 21-ФЗ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОтдельногоРасчетаПособийПоМатеринству() Экспорт 
	Возврат '20130101'
КонецФункции

// Возвращает дату вступления в силу Федерального Закона от 08.12.2010 № 343-ФЗ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаНачалаРеформыСоцСтрахования() Экспорт 
	Возврат '20110101'
КонецФункции

// Возвращает дату вступления в силу Федерального Закона от 14.07.2022 № 282-ФЗ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаЗакона282ФЗ() Экспорт 
	Возврат '20220701'
КонецФункции

// Возвращает дату вступления в силу Федерального закона от 05.04.2013 № 36-ФЗ "О внесении изменений в статью 9.
// Федерального закона "Об обязательном социальном страховании от несчастных случаев на производстве и профессиональных
// заболеваний" и статью 1 Федерального закона "Об обязательном социальном страховании на случай временной
// нетрудоспособности и в связи с материнством".
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВводаОграниченийДляПособийПоНС_ПЗ() Экспорт 
	Возврат '20130408'
КонецФункции

// Возвращает дату вступления в силу Федерального закона от 29.12.2015 № 388-ФЗ "О внесении изменений в отдельные 
// законодательные акты Российской Федерации в части учета и совершенствования предоставления мер социальной поддержки 
// исходя из обязанности соблюдения принципа адресности и применения критериев нуждаемости".
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ДатаОтменыЛьготФинансируемыхИзБюджета() Экспорт 
	Возврат '20160701'
КонецФункции

// Возвращает дату вступления в силу статьи 1 Федерального закона от 26.05.2021 № 151-ФЗ
//   "О внесении изменений в отдельные законодательные акты Российской Федерации".
Функция ДатаОтменыЕдиновременногоПособияВставшимНаУчетВРанниеСроки() Экспорт
	Возврат '20210701';
КонецФункции

// Возвращает дату вступления в силу частей 2 и 3 статьи 4 Федерального закона от 26.05.2021 № 151-ФЗ
//   "О внесении изменений в отдельные законодательные акты Российской Федерации".
Функция ДатаНачалаПолнойОплатыПоУходуЗаРебенкомДо8Лет() Экспорт
	Возврат '20210901';
КонецФункции

// АПК:558-вкл

#Область СреднийЗаработокФСС

Функция СреднийЗаработокФСС(ПараметрыРасчета) Экспорт
	Результат = Новый Структура("Общий, РК, СН", 0, 0, 0);
	
	Если ПараметрыРасчета.ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010") Тогда
		ЗаполнитьСреднийЗаработокФСС2010(ПараметрыРасчета, Результат);
	Иначе
		ЗаполнитьСреднийЗаработокФСС2011(ПараметрыРасчета, Результат);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьСреднийЗаработокФСС2010(ПараметрыРасчета, Результат)
	
	НачисленияРКСН = НачисленияРКСН(ПараметрыРасчета);
	НачисленияРК = НачисленияРКСН.НачисленияРК;
	НачисленияСН = НачисленияРКСН.НачисленияСН;
	
	СуммаНачислений = 0;
	СуммаНачисленийРК = 0;
	СуммаНачисленийСН = 0;
	ОтработаноДнейКалендарных = 0;
	
	Для Каждого СтрокаКоллекции Из ПараметрыРасчета.ДанныеНачислений Цикл
		
		Если СтрокаКоллекции.ПорядокРасчета <> ПараметрыРасчета.ПорядокРасчета Тогда 
			Продолжить;
		КонецЕсли;

		Если СтрокаКоллекции.Период >= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало 
			И СтрокаКоллекции.Период <= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание Тогда
			СуммаНачислений = СуммаНачислений + СтрокаКоллекции.Сумма;
			Если НачисленияРК.Найти(СтрокаКоллекции.Начисление) <> Неопределено Тогда 
				СуммаНачисленийРК = СуммаНачисленийРК + СтрокаКоллекции.Сумма;
			КонецЕсли;
			Если НачисленияСН.Найти(СтрокаКоллекции.Начисление) <> Неопределено Тогда 
				СуммаНачисленийСН = СуммаНачисленийСН + СтрокаКоллекции.Сумма;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКоллекции Из ПараметрыРасчета.ДанныеВремени Цикл
		
		Если СтрокаКоллекции.Период >= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало 
			И СтрокаКоллекции.Период <= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание Тогда
			ОтработаноДнейКалендарных = ОтработаноДнейКалендарных + СтрокаКоллекции.ОтработаноДнейКалендарных;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтработаноДнейКалендарных = 0 Тогда
		Результат.Общий = 0;
		Результат.РК    = 0;
		Результат.СН    = 0;
	Иначе
		Результат.Общий = Окр(СуммаНачислений / ОтработаноДнейКалендарных, 2);
		Результат.РК    = Окр(СуммаНачисленийРК / ОтработаноДнейКалендарных, 2);
		Результат.СН    = Окр(СуммаНачисленийСН / ОтработаноДнейКалендарных, 2);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСреднийЗаработокФСС2011(ПараметрыРасчета, Результат)
	
	// Временное переопределение флажка ПрименятьПредельнуюВеличину.
	ПрименятьПредельнуюВеличину = ПараметрыРасчета.ПрименятьПредельнуюВеличину;
	Если ПараметрыРасчета.ПрименяетсяФЗ_421_2014 Тогда
		ПараметрыРасчета.ПрименятьПредельнуюВеличину = Ложь
	ИначеЕсли ПараметрыРасчета.ПрименяетсяФЗ_20_2023 Тогда
		ПараметрыРасчета.ПрименятьПредельнуюВеличину = Ложь;
	КонецЕсли;
	
	ДанныеРасчетаСреднегоЗаработка = ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета);
	
	Заработок		= УчитываемыйЗаработокФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка);
	КоличествоДней 	= УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка);
	
	Результат.Общий   = СреднедневнойЗаработокФСС(Заработок.Всего, КоличествоДней);
	Результат.РК = СреднедневнойЗаработокФСС(Заработок.РК,    КоличествоДней);
	Результат.СН = СреднедневнойЗаработокФСС(Заработок.СН,    КоличествоДней);
	
	Если ПараметрыРасчета.ПрименяетсяФЗ_421_2014 И ПрименятьПредельнуюВеличину Тогда
		ОграничениеСверху = МаксимальныйСреднедневнойЗаработокДляФЗ_421_2014(ПараметрыРасчета.ДатаНачалаСобытия);
	ИначеЕсли ПараметрыРасчета.ПрименяетсяФЗ_20_2023 И ПрименятьПредельнуюВеличину Тогда
		ОграничениеСверху = МаксимальныйСреднедневнойЗаработокДляФЗ_20_2023(ПараметрыРасчета.ДатаНачалаСобытия);
	ИначеЕсли ПрименятьОграничениеПособияПоМатеринству(ПараметрыРасчета) Тогда 
		ОграничениеСверху = МаксимальныйСреднедневнойЗаработокДляОплатыПособияПоМатеринству(ПараметрыРасчета);
	ИначеЕсли ПрименятьМаксимальныйСреднедневнойБезУчетаИсключаемыхПериодов(ПараметрыРасчета) Тогда
		ОграничениеСверху = МаксимальныйСреднедневнойЗаработокДляОплатыПособияБезУчетаИсключаемыхПериодов(ПараметрыРасчета);
	Иначе
		ОграничениеСверху = Неопределено;
	КонецЕсли;
	Если ОграничениеСверху <> Неопределено И ОграничениеСверху < Результат.Общий Тогда
		Если Результат.Общий > 0 Тогда
			Результат.РК = Окр(Результат.РК * ОграничениеСверху / Результат.Общий, 2);
			Результат.СН = Окр(Результат.СН * ОграничениеСверху / Результат.Общий, 2);
		Иначе
			Результат.РК = 0;
			Результат.СН = 0;
		КонецЕсли;
		Результат.Общий = ОграничениеСверху;
	КонецЕсли;
	
	// Восстановление флажка ПрименятьПредельнуюВеличину.
	ПараметрыРасчета.ПрименятьПредельнуюВеличину = ПрименятьПредельнуюВеличину;
	
КонецПроцедуры

Функция ПараметрыРасчетаСреднегоДневногоЗаработкаФСС() Экспорт
	
	Параметры = Новый Структура(
		"ДатаНачалаСобытия, 
		|ПериодРасчетаСреднегоЗаработкаНачало, 
		|ПериодРасчетаСреднегоЗаработкаОкончание, 
		|РасчетныеГоды, 
		|ПрименяетсяФЗ_421_2014, 
		|ПрименяетсяФЗ_20_2023, 
		|ИсточникСреднего,
		|СреднийЗаработокСЭДО,
		|ДанныеНачислений, 
		|ДанныеВремени, 
		|ДанныеСтрахователей, 
		|УчитыватьЗаработокПредыдущихСтрахователей, 
		|ИспользоватьДниБолезниУходаЗаДетьми, 
		|УчитыватьДниПриостановленияТД,
		|ДнейПриостановленияТДЗаГод1,
		|ДнейПриостановленияТДЗаГод2,
		|ПриостановленияТрудовыхДоговоров,
		|ЭтоДоплатаДоСреднего,
		|ПрименятьПредельнуюВеличину, 
		|ПредельныеВеличиныПоГодам, 
		|ПорядокРасчета, 
		|ДоляНеполногоВремени, 
		|РайонныйКоэффициентРФ, 
		|ПолучатьДанныеПоГодам,
		|ДанныеПоГодам,
		|МинимальныйРазмерОплатыТрудаРФ, 
		|Сотрудник");
	
	// Для заполнения флажков ПрименяетсяФЗ_421_2014 и ПрименяетсяФЗ_20_2023 следует использовать 
	// одноименные функции общего модуля УчетПособийСоциальногоСтрахования.
	Параметры.ИсточникСреднего                          = ПредопределенноеЗначение("Перечисление.ИсточникиСреднегоДляПособий.УчетОрганизации");
	Параметры.РасчетныеГоды 							= Новый Массив;
	Параметры.ПрименяетсяФЗ_421_2014			 		= Ложь;
	Параметры.ПрименяетсяФЗ_20_2023			 			= Ложь;
	Параметры.УчитыватьЗаработокПредыдущихСтрахователей	= Ложь;
	Параметры.ПредельныеВеличиныПоГодам 				= Новый Соответствие;
	Параметры.ИспользоватьДниБолезниУходаЗаДетьми 		= Ложь;
	Параметры.УчитыватьДниПриостановленияТД             = Ложь;
	Параметры.ДнейПриостановленияТДЗаГод1               = 0;
	Параметры.ДнейПриостановленияТДЗаГод2               = 0;
	Параметры.ЭтоДоплатаДоСреднего                      = Ложь;
	Параметры.ПрименятьПредельнуюВеличину 				= Истина;
	Параметры.ДоляНеполногоВремени 						= 1;
	Параметры.РайонныйКоэффициентРФ 					= 1;
	Параметры.ПолучатьДанныеПоГодам                     = Ложь;
	Параметры.ДанныеПоГодам                             = Новый Соответствие;
	
	Возврат Параметры;
	
КонецФункции

Функция ПорядокРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия) Экспорт
	Если ДатаНачалаСобытия >= '20110101' Или ДатаНачалаСобытия = '00010101' Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010");
	КонецЕсли;
КонецФункции

Функция ПериодРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия, ПорядокРасчета = Неопределено) Экспорт
	
	ПериодРасчета = Новый СтандартныйПериод;
	
	Если ПорядокРасчета = Неопределено Тогда
		ПорядокРасчета = ПорядокРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия);
	КонецЕсли;
	
	Если ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010") Тогда
		ПериодРасчета.ДатаНачала	= НачалоМесяца(ДобавитьМесяц(ДатаНачалаСобытия, -12));
		ПериодРасчета.ДатаОкончания	= КонецМесяца(ДобавитьМесяц(ДатаНачалаСобытия, -1));
	КонецЕсли;
	
	Если ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011") Тогда
		ПериодРасчета.ДатаНачала	= НачалоГода(ДобавитьМесяц(ДатаНачалаСобытия, -24));
		ПериодРасчета.ДатаОкончания	= КонецГода(ДобавитьМесяц(ДатаНачалаСобытия, -12));
	КонецЕсли;
	
	Возврат ПериодРасчета;
	
КонецФункции

Функция ПрименятьОграничениеПособияПоМатеринству(ПараметрыРасчета) Экспорт
	Возврат ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми
		И ПараметрыРасчета.ДатаНачалаСобытия >= ДатаОтдельногоРасчетаПособийПоМатеринству() 
		И ПараметрыРасчета.ПрименятьПредельнуюВеличину;
КонецФункции

Функция ГодыПредельныхВеличин(ПараметрыРасчета) Экспорт
	
	ГодыПредельныхВеличин = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПараметрыРасчета.РасчетныеГоды);
	
	ГодыПредельныхВеличинДляОграниченияПособияПоМатеринству = ГодыПредельныхВеличинДляОграниченияПособияПоМатеринству(ПараметрыРасчета);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ГодыПредельныхВеличин, ГодыПредельныхВеличинДляОграниченияПособияПоМатеринству, Истина);
	
	Возврат ГодыПредельныхВеличин;
	
КонецФункции

Функция ГодыПредельныхВеличинДляОграниченияПособияПоМатеринству(ПараметрыРасчета) Экспорт
	
	ГодыПолученияПредельныхВеличинДляОграниченияПособия = Новый Массив;
	
	ПериодРасчетаСреднего = ПериодРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета.ДатаНачалаСобытия, ПараметрыРасчета.ПорядокРасчета);
	
	ГодыПолученияПредельныхВеличинДляОграниченияПособия.Добавить(Год(ПериодРасчетаСреднего.ДатаНачала));
	ГодыПолученияПредельныхВеличинДляОграниченияПособия.Добавить(Год(ПериодРасчетаСреднего.ДатаОкончания));
	
	Возврат ГодыПолученияПредельныхВеличинДляОграниченияПособия;
	
КонецФункции

#Область ДанныеРасчетаСреднегоЗаработкаФСС

Функция ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета) Экспорт
	
	ДанныеРасчета = Новый Массив;
		
	ДобавитьСтрокиРасчетныхЛет(ДанныеРасчета, ПараметрыРасчета);
	
	ДобавитьДанныеОЗаработке(ДанныеРасчета, ПараметрыРасчета);
	
	ДобавитьСреднийПоДаннымФонда(ДанныеРасчета, ПараметрыРасчета);
	
	ДобавитьДанныеОВремени(ДанныеРасчета, ПараметрыРасчета);
	
	Если ПараметрыРасчета.УчитыватьЗаработокПредыдущихСтрахователей Тогда
		ДобавитьДанныеПредыдущихСтрахователей(ДанныеРасчета, ПараметрыРасчета);
	КонецЕсли;
	
	Возврат ДанныеРасчета;
	
КонецФункции

Процедура ДобавитьСтрокиРасчетныхЛет(ДанныеРасчета, ПараметрыРасчета)
	
	ОтборПоГоду = Новый Структура("РасчетныйГод");
	
	Для каждого РасчетныйГод Из ПараметрыРасчета.РасчетныеГоды Цикл
		ОтборПоГоду.РасчетныйГод = РасчетныйГод;
		ЭлементДанных = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, ОтборПоГоду, Истина);
		ПредельнаяВеличина = ПараметрыРасчета.ПредельныеВеличиныПоГодам.Получить(РасчетныйГод);
		ЭлементДанных.ПредельнаяВеличина = ?(ПредельнаяВеличина = Неопределено, 0, ПредельнаяВеличина);
	КонецЦикла; 
	
КонецПроцедуры

Процедура ДобавитьДанныеОЗаработке(ДанныеРасчета, ПараметрыРасчета)
	Если ЗначениеЗаполнено(ПараметрыРасчета.ИсточникСреднего) // Для базовой реализации и для обратной совместимости.
		И ПараметрыРасчета.ИсточникСреднего <> ПредопределенноеЗначение("Перечисление.ИсточникиСреднегоДляПособий.УчетОрганизации") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоГоду = Новый Структура("РасчетныйГод");
	ОтборПоСтрахователю = Новый Структура("Страхователь");
	
	НачисленияРКСН = НачисленияРКСН(ПараметрыРасчета);
	НачисленияРК = НачисленияРКСН.НачисленияРК;
	НачисленияСН = НачисленияРКСН.НачисленияСН;
	
	Для каждого Начисление Из ПараметрыРасчета.ДанныеНачислений Цикл
		
		Если Начисление.ПорядокРасчета <> ПараметрыРасчета.ПорядокРасчета Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не УчитыватьСтрокуИсточникаСреднегоЗаработка(Начисление.Период, ПараметрыРасчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборПоГоду.РасчетныйГод = Год(Начисление.Период);
		СтрокаПоГоду = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, ОтборПоГоду);
		
		Если СтрокаПоГоду <> Неопределено Тогда
			СтрокаПоГоду.Заработок = СтрокаПоГоду.Заработок + Начисление.Сумма;
			
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоГоду.Страхователи, ОтборПоСтрахователю);
			СтрокаСтрахователя.Заработок = СтрокаСтрахователя.Заработок + Начисление.Сумма; 
			
			Если НачисленияРК.Найти(Начисление.Начисление) <> Неопределено Тогда
				СтрокаПоГоду.ЗаработокРК = СтрокаПоГоду.ЗаработокРК + Начисление.Сумма;
				СтрокаСтрахователя.ЗаработокРК = СтрокаСтрахователя.ЗаработокРК + Начисление.Сумма;
			КонецЕсли;
			
			Если НачисленияСН.Найти(Начисление.Начисление) <> Неопределено Тогда
				СтрокаПоГоду.ЗаработокСН = СтрокаПоГоду.ЗаработокСН + Начисление.Сумма;
				СтрокаСтрахователя.ЗаработокСН = СтрокаСтрахователя.ЗаработокСН + Начисление.Сумма;
			КонецЕсли;
			
			Если ПараметрыРасчета.ПолучатьДанныеПоГодам Тогда
				ДобавитьДанныеПоГоду(ПараметрыРасчета, СтрокаПоГоду);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьСреднийПоДаннымФонда(ДанныеРасчета, ПараметрыРасчета)
	Если ПараметрыРасчета.СреднийЗаработокСЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ПараметрыРасчета.ИсточникСреднего <> ПредопределенноеЗначение("Перечисление.ИсточникиСреднегоДляПособий.УчетФонда")
		И ПараметрыРасчета.ИсточникСреднего <> ПредопределенноеЗначение("Перечисление.ИсточникиСреднегоДляПособий.ВходящийЗапросФонда") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоГоду = Новый Структура("РасчетныйГод");
	ОтборПоСтрахователю = Новый Структура("Страхователь");
	
	Для Каждого СтрокаИсточника Из ПараметрыРасчета.СреднийЗаработокСЭДО Цикл
		Если Не УчитыватьСтрокуИсточникаСреднегоЗаработкаПоДаннымФонда(СтрокаИсточника, ПараметрыРасчета) Тогда 
			Продолжить;
		КонецЕсли;
		
		ОтборПоГоду.РасчетныйГод = СтрокаИсточника.РасчетныйГод;
		СтрокаПоГоду = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, ОтборПоГоду);
		Если СтрокаПоГоду <> Неопределено Тогда
			СтрокаПоГоду.Заработок = СтрокаПоГоду.Заработок + СтрокаИсточника.Заработок;
			
			СтрокаПоСтрахователю = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоГоду.Страхователи, ОтборПоСтрахователю);
			СтрокаПоСтрахователю.Заработок = СтрокаПоСтрахователю.Заработок + СтрокаИсточника.Заработок;
			
			Если ПараметрыРасчета.ПолучатьДанныеПоГодам Тогда
				ДобавитьДанныеПоГоду(ПараметрыРасчета, СтрокаПоГоду, СтрокаИсточника.ВидЗаработка, СтрокаИсточника.Основание);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДанныеОВремени(ДанныеРасчета, ПараметрыРасчета)
	ОтборПоГоду = Новый Структура("РасчетныйГод");
	ОтборПоСтрахователю = Новый Структура("Страхователь");
	
	Для Каждого СтрокаВремени Из ПараметрыРасчета.ДанныеВремени Цикл
		Если Не УчитыватьСтрокуИсточникаСреднегоЗаработка(СтрокаВремени.Период, ПараметрыРасчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборПоГоду.РасчетныйГод = Год(СтрокаВремени.Период);
		СтрокаПоГоду = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, ОтборПоГоду);
		
		Если СтрокаПоГоду <> Неопределено Тогда
			СтрокаПоГоду.ДнейБолезниУходаЗаДетьми = СтрокаПоГоду.ДнейБолезниУходаЗаДетьми + СтрокаВремени.ДнейБолезниУходаЗаДетьми;
			
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоГоду.Страхователи, ОтборПоСтрахователю);
			СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми = СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми + СтрокаВремени.ДнейБолезниУходаЗаДетьми; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДанныеПредыдущихСтрахователей(ДанныеРасчета, ПараметрыРасчета)
	Если ЗначениеЗаполнено(ПараметрыРасчета.ИсточникСреднего) // Для базовой реализации и для обратной совместимости.
		И ПараметрыРасчета.ИсточникСреднего <> ПредопределенноеЗначение("Перечисление.ИсточникиСреднегоДляПособий.УчетОрганизации") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоГоду = Новый Структура("РасчетныйГод");
	ОтборПоСтрахователю = Новый Структура("Страхователь, СтороннийСтрахователь", Неопределено, Истина);
	
	Для каждого ПредыдущийСтрахователь Из ПараметрыРасчета.ДанныеСтрахователей Цикл
		
		ОтборПоГоду.РасчетныйГод = ПредыдущийСтрахователь.Год;
		СтрокаПоГоду = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, ОтборПоГоду);
		
		Если СтрокаПоГоду <> Неопределено Тогда			
			СтрокаПоГоду.Заработок = СтрокаПоГоду.Заработок + ПредыдущийСтрахователь.Сумма;
			СтрокаПоГоду.ДнейБолезниУходаЗаДетьми = СтрокаПоГоду.ДнейБолезниУходаЗаДетьми + ПредыдущийСтрахователь.ДнейБолезниУходаЗаДетьми;
			
			ОтборПоСтрахователю.Страхователь = ПредыдущийСтрахователь.Страхователь;
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоГоду.Страхователи, ОтборПоСтрахователю);
			СтрокаСтрахователя.Заработок = СтрокаСтрахователя.Заработок + ПредыдущийСтрахователь.Сумма; 
			СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми = СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми + ПредыдущийСтрахователь.ДнейБолезниУходаЗаДетьми;
			
			Если ПараметрыРасчета.ПолучатьДанныеПоГодам Тогда
				ДобавитьДанныеПоГоду(ПараметрыРасчета, СтрокаПоГоду);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДанныеПоГоду(ПараметрыРасчета, СтрокаПоГоду, ВидЗаработка = Неопределено, Основание = Неопределено)
	Если ПараметрыРасчета.ДанныеПоГодам[СтрокаПоГоду.РасчетныйГод] = Неопределено Тогда
		ПараметрыРасчета.ДанныеПоГодам.Вставить(СтрокаПоГоду.РасчетныйГод, СтрокаПоГоду);
	КонецЕсли;
	Если СтрокаПоГоду.ВидЗаработка = Неопределено Тогда
		Если ВидЗаработка = Неопределено Тогда
			Если ПараметрыРасчета.УчитыватьЗаработокПредыдущихСтрахователей Тогда
				ВидЗаработка = ПредопределенноеЗначение("Перечисление.СпособыУчетаГодовогоЗаработкаСФР.ВсеСтрахователи");
			Иначе
				ВидЗаработка = ПредопределенноеЗначение("Перечисление.СпособыУчетаГодовогоЗаработкаСФР.ТекущийСтрахователь");
			КонецЕсли;
		КонецЕсли;
		СтрокаПоГоду.ВидЗаработка = ВидЗаработка;
	КонецЕсли;
	Если ЗначениеЗаполнено(Основание) И Не ЗначениеЗаполнено(СтрокаПоГоду.Основание) Тогда
		СтрокаПоГоду.Основание = Основание;
	КонецЕсли;
КонецПроцедуры

Функция ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(Коллекция, Отбор, СоздаватьНовый = Ложь)
	
	ПодходящийЭлемент = ЭлементКоллекцииПоОтбору(Коллекция, Отбор);
	
	Если СоздаватьНовый И ПодходящийЭлемент = Неопределено Тогда
		ПодходящийЭлемент = СтруктураДанныхДляРасчетаСреднегоЗаработка();
		ЗаполнитьЭлементКоллекцииПоОтбору(ПодходящийЭлемент, Отбор);
		
		Коллекция.Добавить(ПодходящийЭлемент);
	КонецЕсли;
	
	Возврат ПодходящийЭлемент;
	
КонецФункции

Функция ЭлементДанныхСтрахователяПоОтбору(Коллекция, Отбор)
	
	ПодходящийЭлемент = ЭлементКоллекцииПоОтбору(Коллекция, Отбор);
	
	Если ПодходящийЭлемент = Неопределено Тогда
		ПодходящийЭлемент = СтруктураДанныхСтороннегоСтрахователяДляРасчетаСреднего();
		ЗаполнитьЭлементКоллекцииПоОтбору(ПодходящийЭлемент, Отбор);
		
		Коллекция.Добавить(ПодходящийЭлемент);
	КонецЕсли;
	
	Возврат ПодходящийЭлемент;
КонецФункции

Функция СтруктураДанныхДляРасчетаСреднегоЗаработка()
	
	ТипЧисло = Новый ОписаниеТипов("Число");
	ПустоеЧисло = ТипЧисло.ПривестиЗначение();  
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("РасчетныйГод", 				ПустоеЧисло);
	СтруктураДанных.Вставить("Заработок", 					ПустоеЧисло);
	СтруктураДанных.Вставить("ЗаработокРК", 				ПустоеЧисло);
	СтруктураДанных.Вставить("ЗаработокСН", 				ПустоеЧисло);
	СтруктураДанных.Вставить("ДнейБолезниУходаЗаДетьми", 	ПустоеЧисло);
	СтруктураДанных.Вставить("ПредельнаяВеличина", 			ПустоеЧисло);
	СтруктураДанных.Вставить("Страхователи", 				Новый Массив);
	СтруктураДанных.Вставить("ВидЗаработка",                Неопределено);
	СтруктураДанных.Вставить("Основание",                   Неопределено);
	СтруктураДанных.Вставить("УчтенныйЗаработок",           ПустоеЧисло);
	СтруктураДанных.Вставить("УчтенныйЗаработокРК",         ПустоеЧисло);
	СтруктураДанных.Вставить("УчтенныйЗаработокСН",         ПустоеЧисло);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция СтруктураДанныхСтороннегоСтрахователяДляРасчетаСреднего()
	
	ТипЧисло = Новый ОписаниеТипов("Число");
	ПустоеЧисло = ТипЧисло.ПривестиЗначение();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Страхователь", 				Неопределено);
	СтруктураДанных.Вставить("СтороннийСтрахователь", 		Ложь);
	СтруктураДанных.Вставить("Заработок", 					ПустоеЧисло);
	СтруктураДанных.Вставить("ЗаработокРК", 				ПустоеЧисло);
	СтруктураДанных.Вставить("ЗаработокСН", 				ПустоеЧисло);
	СтруктураДанных.Вставить("ДнейБолезниУходаЗаДетьми", 	ПустоеЧисло);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция УчитыватьСтрокуИсточникаСреднегоЗаработка(Период, ПараметрыРасчета) Экспорт
	Если ПараметрыРасчета.ПрименяетсяФЗ_421_2014 И Период < '20140301' Тогда
		Возврат Ложь;
	ИначеЕсли ПараметрыРасчета.ПрименяетсяФЗ_20_2023 И Период < '20220901' Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

Функция УчитыватьСтрокуИсточникаСреднегоЗаработкаПоДаннымФонда(СтрокаИсточника, ПараметрыРасчета) Экспорт
	Если СтрокаИсточника.ИсточникСреднего <> ПараметрыРасчета.ИсточникСреднего Тогда 
		Возврат Ложь;
	КонецЕсли;
	Если СтрокаИсточника.ВидЗаработка = ПредопределенноеЗначение("Перечисление.СпособыУчетаГодовогоЗаработкаСФР.ВсеСтрахователи") Тогда
		УчитываетсяЗаработокПредыдущихСтрахователей = Истина;
	ИначеЕсли СтрокаИсточника.ВидЗаработка = ПредопределенноеЗначение("Перечисление.СпособыУчетаГодовогоЗаработкаСФР.ТекущийСтрахователь") Тогда
		УчитываетсяЗаработокПредыдущихСтрахователей = Ложь;
	Иначе
		УчитываетсяЗаработокПредыдущихСтрахователей = Неопределено;
	КонецЕсли;
	Если ПараметрыРасчета.УчитыватьЗаработокПредыдущихСтрахователей <> УчитываетсяЗаработокПредыдущихСтрахователей Тогда
		Возврат Ложь;
	КонецЕсли;
	// Ограничение расчетного периода в соответствии с Федеральным законом от 31 декабря 2014 года № 4??-ФЗ.
	Если Не УчитыватьСтрокуИсточникаСреднегоЗаработка(Дата(СтрокаИсточника.РасчетныйГод, 12, 1), ПараметрыРасчета) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

Функция НачисленияРКСН(ПараметрыРасчета) Экспорт
	НачисленияРКСН = Неопределено;
	Если Не ПараметрыРасчета.Свойство("НачисленияРКСН", НачисленияРКСН) Тогда
		НачисленияРКСН = ЗарплатаКадрыКлиентСервер.НачисленияРКСН();
		ПараметрыРасчета.Вставить("НачисленияРКСН", НачисленияРКСН);
	КонецЕсли;
	Возврат НачисленияРКСН;
КонецФункции

#КонецОбласти

#Область АгрегатыДанныхРасчетаСреднегоЗаработка

Функция СреднедневнойЗаработокФСС(Заработок, КоличествоДней) Экспорт
	Возврат ?(КоличествоДней = 0, 0, Окр(Заработок / КоличествоДней,2));
КонецФункции

Функция УчитываемыйЗаработокФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка) Экспорт
	ВсегоЗаработка = 0;
	ВсегоЗаработкаРК = 0;
	ВсегоЗаработкаСН = 0;
	ПрименятьПредельнуюВеличину = ПараметрыРасчета.ПрименятьПредельнуюВеличину
		И Не ПараметрыРасчета.ПрименяетсяФЗ_421_2014
		И Не ПараметрыРасчета.ПрименяетсяФЗ_20_2023;
	Для Каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
		Если ПрименятьПредельнуюВеличину И Элемент.ПредельнаяВеличина < Элемент.Заработок Тогда
			Элемент.УчтенныйЗаработок = Элемент.ПредельнаяВеличина;
			Если Элемент.Заработок > 0 Тогда
				Элемент.УчтенныйЗаработокРК = Окр(Элемент.ЗаработокРК * Элемент.ПредельнаяВеличина / Элемент.Заработок, 2);
				Элемент.УчтенныйЗаработокСН = Окр(Элемент.ЗаработокСН * Элемент.ПредельнаяВеличина / Элемент.Заработок, 2);
			КонецЕсли;
		Иначе
			Элемент.УчтенныйЗаработок = Элемент.Заработок;
			Элемент.УчтенныйЗаработокРК = Элемент.ЗаработокРК;
			Элемент.УчтенныйЗаработокСН = Элемент.ЗаработокСН;
		КонецЕсли;
		ВсегоЗаработка = ВсегоЗаработка + Элемент.УчтенныйЗаработок;
		ВсегоЗаработкаРК = ВсегоЗаработкаРК + Элемент.УчтенныйЗаработокРК;
		ВсегоЗаработкаСН = ВсегоЗаработкаСН + Элемент.УчтенныйЗаработокСН;
	КонецЦикла;
	Возврат Новый Структура("Всего, РК, СН", ВсегоЗаработка, ВсегоЗаработкаРК, ВсегоЗаработкаСН);
КонецФункции

Функция УчитываемыйЗаработокФССПоГоду(ПараметрыРасчета, СтрокаДанныхРасчетаСреднегоПоГоду) Экспорт
	
	ВсегоЗаработка = 0;	
	
	Если ПараметрыРасчета.ПрименятьПредельнуюВеличину И Не ПараметрыРасчета.ПрименяетсяФЗ_421_2014 Тогда
		ВсегоЗаработка = Мин(СтрокаДанныхРасчетаСреднегоПоГоду.ПредельнаяВеличина, СтрокаДанныхРасчетаСреднегоПоГоду.Заработок);
	Иначе
		ВсегоЗаработка = СтрокаДанныхРасчетаСреднегоПоГоду.Заработок;
	КонецЕсли;
	
	Возврат ВсегоЗаработка
	
КонецФункции

Функция МаксимальныйСреднедневнойЗаработокДляОплатыПособияПоМатеринству(ПараметрыРасчета) Экспорт
	
	ГодыПредельныхВеличин = ГодыПредельныхВеличинДляОграниченияПособияПоМатеринству(ПараметрыРасчета);	
	
	СуммаОграничений = 0;
	
	Для каждого РасчетныйГод Из ГодыПредельныхВеличин Цикл
		ПредельнаяВеличина = ПараметрыРасчета.ПредельныеВеличиныПоГодам.Получить(РасчетныйГод);
		СуммаОграничений = СуммаОграничений + ПредельнаяВеличина;
	КонецЦикла;

	МаксимальныйСреднедневнойЗаработок = СуммаОграничений / КоличествоКалендарныхДнейВДвухГодахФСС();
	
	Возврат Окр(МаксимальныйСреднедневнойЗаработок, 2);
	
КонецФункции

Функция ПрименятьМаксимальныйСреднедневнойБезУчетаИсключаемыхПериодов(ПараметрыРасчета) Экспорт
	Возврат ПараметрыРасчета.УчитыватьДниПриостановленияТД
		И ПараметрыРасчета.ДатаНачалаСобытия >= '20230403'
		И Не ПараметрыРасчета.ЭтоДоплатаДоСреднего;
КонецФункции

Функция МаксимальныйСреднедневнойЗаработокДляОплатыПособияБезУчетаИсключаемыхПериодов(ПараметрыРасчета) Экспорт 
	Итог = 0;
	Для Каждого Год Из ПараметрыРасчета.РасчетныеГоды Цикл
		Итог = Итог + ПараметрыРасчета.ПредельныеВеличиныПоГодам.Получить(Год);
	КонецЦикла;
	Возврат Окр(Итог / КоличествоКалендарныхДнейВДвухГодахФСС(), 2);
КонецФункции

Функция МаксимальныйСреднедневнойЗаработокДляФЗ_421_2014(ДатаНачалаСобытия) Экспорт
	
	МаксимальныйСреднедневнойЗаработок = 0;
	Если ДатаНачалаСобытия < '20160101'  Тогда
		МаксимальныйСреднедневнойЗаработок = 1632.88;
	Иначе
		МаксимальныйСреднедневнойЗаработок = (624000 + 670000) / 730;
	КонецЕсли;
	
	Возврат Окр(МаксимальныйСреднедневнойЗаработок, 2);
	
КонецФункции

Функция МаксимальныйСреднедневнойЗаработокДляФЗ_20_2023(ДатаНачалаСобытия) Экспорт
	
	МаксимальныйСреднедневнойЗаработок = 0;
	Если ДатаНачалаСобытия < '20240101'  Тогда
		МаксимальныйСреднедневнойЗаработок = 2736.99;
	Иначе
		МаксимальныйСреднедневнойЗаработок = (1032000 + 1917000) / 730;
	КонецЕсли;
	
	Возврат Окр(МаксимальныйСреднедневнойЗаработок, 2);
	
КонецФункции

Функция МинимальныйСреднедневнойЗаработокФСС(ПараметрыРасчета) Экспорт
	Возврат Окр(ПараметрыРасчета.МинимальныйРазмерОплатыТрудаРФ * ДоляНеполногоВремени(ПараметрыРасчета) / СреднееКоличествоДнейВМесяцеФСС(), 2);
КонецФункции

Функция ДоляНеполногоВремени(ПараметрыРасчета) Экспорт
	Возврат ?(ПараметрыРасчета.ДоляНеполногоВремени = 0, 1, Мин(ПараметрыРасчета.ДоляНеполногоВремени, 2));
КонецФункции

Функция УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	Если ПараметрыРасчета.ПрименяетсяФЗ_421_2014 Тогда
		Если ПараметрыРасчета.ДатаНачалаСобытия < '20160101'  Тогда
			КалендарныхДней = 289;
		Иначе
			КалендарныхДней = 654;
		КонецЕсли;
		Если ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми Тогда
			Для Каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
				КалендарныхДней = КалендарныхДней - Элемент.ДнейБолезниУходаЗаДетьми;
			КонецЦикла;
			КалендарныхДней = Макс(КалендарныхДней, 0);
		КонецЕсли;
	ИначеЕсли ПараметрыРасчета.ПрименяетсяФЗ_20_2023 Тогда
		Если ПараметрыРасчета.ДатаНачалаСобытия < '20240101' Тогда
			КалендарныхДней = 93;
		Иначе
			КалендарныхДней = 458;
		КонецЕсли;
		Если ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми Тогда
			Для Каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
				КалендарныхДней = КалендарныхДней - Элемент.ДнейБолезниУходаЗаДетьми;
			КонецЦикла;
		КонецЕсли;
		Если ПараметрыРасчета.УчитыватьДниПриостановленияТД Тогда
			Если ЗначениеЗаполнено(ПараметрыРасчета.ДнейПриостановленияТДЗаГод1) Тогда
				КалендарныхДней = КалендарныхДней - Мин(ПараметрыРасчета.ДнейПриостановленияТДЗаГод1, 365);
			КонецЕсли;
			Если ЗначениеЗаполнено(ПараметрыРасчета.ДнейПриостановленияТДЗаГод2) Тогда
				КалендарныхДней = КалендарныхДней - Мин(ПараметрыРасчета.ДнейПриостановленияТДЗаГод2, 365);
			КонецЕсли;
		КонецЕсли;
		КалендарныхДней = Макс(КалендарныхДней, 0);
	ИначеЕсли ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми Тогда
		КалендарныхДней = 0;
		Для Каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
			КалендарныхДней = КалендарныхДней + ДеньГода(Дата(Элемент.РасчетныйГод, 12, 31)) - Элемент.ДнейБолезниУходаЗаДетьми;
		КонецЦикла;
	Иначе
		КалендарныхДней = КоличествоКалендарныхДнейВДвухГодахФСС();
		Если ПараметрыРасчета.УчитыватьДниПриостановленияТД Тогда
			Если ЗначениеЗаполнено(ПараметрыРасчета.ДнейПриостановленияТДЗаГод1) Тогда
				КалендарныхДней = КалендарныхДней - Мин(ПараметрыРасчета.ДнейПриостановленияТДЗаГод1, 365);
			КонецЕсли;
			Если ЗначениеЗаполнено(ПараметрыРасчета.ДнейПриостановленияТДЗаГод2) Тогда
				КалендарныхДней = КалендарныхДней - Мин(ПараметрыРасчета.ДнейПриостановленияТДЗаГод2, 365);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КалендарныхДней;
КонецФункции

Функция КоличествоКалендарныхДнейВДвухГодахФСС() Экспорт
	Возврат 730;
КонецФункции

Функция СреднееКоличествоДнейВМесяцеФСС() Экспорт
	Возврат 730 / 24;
КонецФункции

#КонецОбласти

// Функция выполняет упаковку данных среднего заработка ФСС формы документа для передачи в форму редактирования.
//
Функция ПараметрыРедактированияСреднегоЗаработкаФССПоДокументу() Экспорт
	
	ПараметрыРедактирования = Новый Структура(
		"ДокументСсылка,
		|Сотрудник,
		|Организация,
		|ДатаНачалаСобытия,
		|НачалоПериодаРасчета,
		|ОкончаниеПериодаРасчета,
		|ГодыПериодаРасчета,
		|ФиксПериодРасчета,
		|ПорядокРасчета,
		|ИспользоватьДниБолезниУходаЗаДетьми,
		|УчитыватьДниПриостановленияТД,
		|ДнейПриостановленияТДЗаГод1,
		|ДнейПриостановленияТДЗаГод2,
		|ЭтоДоплатаДоСреднего,
		|УчитыватьЗаработокПредыдущихСтрахователей,
		|ТолькоПросмотр,
		|ДанныеОНачислениях,
		|ДанныеОВремени,
		|ДанныеСтрахователей, 
		|ПериодыОтсутствий, 
		|ПараметрыРасчета");
	
	ПараметрыРедактирования.ФиксПериодРасчета = Ложь;
	ПараметрыРедактирования.ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011");
	ПараметрыРедактирования.УчитыватьЗаработокПредыдущихСтрахователей = Ложь;
	ПараметрыРедактирования.ТолькоПросмотр = Ложь;
	ПараметрыРедактирования.УчитыватьДниПриостановленияТД = Ложь;
	ПараметрыРедактирования.ДнейПриостановленияТДЗаГод1   = 0;
	ПараметрыРедактирования.ДнейПриостановленияТДЗаГод2   = 0;
	ПараметрыРедактирования.ЭтоДоплатаДоСреднего          = Ложь;
	
	Возврат ПараметрыРедактирования;
	
КонецФункции
	   
// Функция конструирует структуру, описывающую данные о начислениях 
// для расчета среднего заработка за один месяц расчетного периода.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиДанныхОНачисленияхСреднегоЗаработкаФСС() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ПоляОписания.Вставить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаФСС"));
	ПоляОписания.Вставить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ПоляОписания.Вставить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	ПоляОписания.Вставить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ПоляОписания.Вставить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	ПоляОписания.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Сумма", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("Корректировка", Новый ОписаниеТипов("Булево"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции
	   
// Функция конструирует структуру, описывающую данные периодах отсутствия.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиПериодовДнейБолезниУходаЗаДетьми() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("Начало", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Окончание", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Дни", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("ВидБолезниУходаЗаДетьми", Новый ОписаниеТипов("Строка"));
	ПоляОписания.Вставить("Корректировка", Новый ОписаниеТипов("Булево"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

Функция ЭлементКоллекцииПоОтбору(Коллекция, Отбор) Экспорт
	
	ПодходящийЭлемент = Неопределено;
	
	Для каждого Элемент Из Коллекция Цикл
		ЭтоПодходящийЭлемент = Истина;
		
		Для каждого ЭлементОтбора Из Отбор Цикл
			Если ЭлементОтбора.Значение <> Неопределено
				И Элемент[ЭлементОтбора.Ключ] <> ЭлементОтбора.Значение  Тогда
				ЭтоПодходящийЭлемент = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоПодходящийЭлемент Тогда
			ПодходящийЭлемент = Элемент;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПодходящийЭлемент;
	
КонецФункции

Процедура ЗаполнитьЭлементКоллекцииПоОтбору(Элемент, Отбор) Экспорт
	Для каждого ЭлементОтбора Из Отбор Цикл
		Если ЭлементОтбора.Значение <> Неопределено Тогда
			Элемент[ЭлементОтбора.Ключ] = ЭлементОтбора.Значение; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

// Возвращает период освобождения от работы указанный в листке нетрудоспособности.
Функция ПериодЛН(Больничный) Экспорт
	Результат = Новый Структура("ДатаНачала, ДатаОкончания");
	
	Результат.ДатаНачала = Больничный.ОсвобождениеДатаНачала1;
	Если ЗначениеЗаполнено(Больничный.ОсвобождениеДатаНачала2)
		И (Не ЗначениеЗаполнено(Результат.ДатаНачала)
			Или Результат.ДатаНачала > Больничный.ОсвобождениеДатаНачала2) Тогда
		Результат.ДатаНачала = Больничный.ОсвобождениеДатаНачала2;
	КонецЕсли;
	Если ЗначениеЗаполнено(Больничный.ОсвобождениеДатаНачала3)
		И (Не ЗначениеЗаполнено(Результат.ДатаНачала)
			Или Результат.ДатаНачала > Больничный.ОсвобождениеДатаНачала3) Тогда
		Результат.ДатаНачала = Больничный.ОсвобождениеДатаНачала3;
	КонецЕсли;
	
	Результат.ДатаОкончания = Макс(Больничный.ОсвобождениеДатаОкончания1,
		Больничный.ОсвобождениеДатаОкончания2,
		Больничный.ОсвобождениеДатаОкончания3);
	
	Если Не ЗначениеЗаполнено(Результат.ДатаНачала) Тогда
		Результат.ДатаНачала = Больничный.ДатаНачалаСобытия;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.ДатаНачала) Тогда
		Результат.ДатаНачала = Больничный.ДатаНачала;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.ДатаОкончания) Тогда
		Результат.ДатаОкончания = Больничный.ДатаОкончания;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
