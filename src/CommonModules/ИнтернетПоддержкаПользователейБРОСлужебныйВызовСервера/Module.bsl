
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей (БРО)".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ПроверитьВозможностьВыполненияОперацииНаСервере(Знач ПараметрыАутентификации) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат "ВыполнениеРазрешено";
	Иначе
		Если НЕ ЗначениеЗаполнено(ПараметрыАутентификации)
			ИЛИ (ЗначениеЗаполнено(ПараметрыАутентификации) И ПараметрыАутентификации.Пароль = "") Тогда
			УстановитьПривилегированныйРежим(Истина);
			ПараметрыАутентификации = ОнлайнСервисыРегламентированнойОтчетности.ПараметрыАутентификацииНаСайте();
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыАутентификации) Тогда
			Возврат "ПараметрыАутентификацииНеЗаполнены";
		Иначе
			Результат = ПараметрыАутентификацииКорректные(ПараметрыАутентификации);
			Если Результат = "НекорректноеИмяПользователяИлиПароль" Тогда
				Возврат "ПараметрыАутентификацииУказаныНеВерно";
			ИначеЕсли Результат = "АутентификацияВыполнена" Тогда
				Возврат "ВыполнениеРазрешено";
			ИначеЕсли Результат = "НеизвестнаяОшибка" Тогда
				Возврат "НеизвестнаяОшибкаПриПроверке";
			Иначе
				Возврат "ВыполнениеЗапрещено";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыАутентификацииКорректные(ПараметрыАутентификацииНаСайте, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Логин)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль) Тогда
		Результат = "НекорректноеИмяПользователяИлиПароль";
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатПолученияТикета = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("its.1c.ru");
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = "НеизвестнаяОшибка";
	
	Если ЗначениеЗаполнено(РезультатПолученияТикета.Тикет) Тогда
		Результат = "АутентификацияВыполнена";
	Иначе
		Если РезультатПолученияТикета.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
			Результат = "НекорректноеИмяПользователяИлиПароль";
		КонецЕсли;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'БРО. Интернет-поддержка пользователей. Получение билета its.1c.ru';
				|en = 'БРО. Интернет-поддержка пользователей. Получение билета its.1c.ru'"),
			УровеньЖурналаРегистрации.Ошибка,,, РезультатПолученияТикета.ИнформацияОбОшибке);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти