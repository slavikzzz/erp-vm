
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьНастройкиРасчетаРезервовПоОплатеТруда(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяГоловнаяОрганизация = Неопределено;
	ЭтоНовый = Ложь;
	Источник.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый);
	Если Не Источник.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый) Тогда
		ЭтоНовый = Ложь;
	КонецЕсли;
	Если Не ЭтоНовый И НЕ Источник.ДополнительныеСвойства.Свойство("ИзмененаГоловнаяОрганизация", ПредыдущаяГоловнаяОрганизация) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "ГоловнаяОрганизация,ОбособленноеПодразделение");
	
	Если РеквизитыОрганизации.ОбособленноеПодразделение Тогда
		
		// Для обособленного подразделения продублируем существующие настройки резервов по оплате труда.
		// Но только те, которых не оказалось.
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", РеквизитыОрганизации.ГоловнаяОрганизация);
		Запрос.УстановитьПараметр("Организация",         Источник.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка,
		|	НастройкиРасчетаРезервовПоОплатеТруда.Резерв КАК Резерв,
		|	НастройкиРасчетаРезервовПоОплатеТруда.ВидРезерва КАК ВидРезерва,
		|	НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода КАК НачалоПериода,
		|	НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода КАК КонецПериода
		|ПОМЕСТИТЬ ВТ_НастройкиГоловнойОрганизации
		|ИЗ
		|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
		|ГДЕ
		|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НастройкиГоловнойОрганизации.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_СуществующиеНастройкиФилиала
		|ИЗ
		|	ВТ_НастройкиГоловнойОрганизации КАК ВТ_НастройкиГоловнойОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
		|		ПО ВТ_НастройкиГоловнойОрганизации.Резерв = НастройкиРасчетаРезервовПоОплатеТруда.Резерв
		|			И ВТ_НастройкиГоловнойОрганизации.ВидРезерва = НастройкиРасчетаРезервовПоОплатеТруда.ВидРезерва
		|			И ВТ_НастройкиГоловнойОрганизации.НачалоПериода = НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода
		|			И ВТ_НастройкиГоловнойОрганизации.КонецПериода = НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода
		|ГДЕ
		|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НастройкиГоловнойОрганизации.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_НастройкиГоловнойОрганизации КАК ВТ_НастройкиГоловнойОрганизации
		|ГДЕ
		|	НЕ ВТ_НастройкиГоловнойОрганизации.Ссылка В
		|				(ВЫБРАТЬ
		|					ВТ_СуществующиеНастройкиФилиала.Ссылка
		|				ИЗ
		|					ВТ_СуществующиеНастройкиФилиала)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Справочники.НастройкиРасчетаРезервовПоОплатеТруда.СинхронизироватьНастройки(
				Выборка.Ссылка.ПолучитьОбъект(),
				Источник.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
