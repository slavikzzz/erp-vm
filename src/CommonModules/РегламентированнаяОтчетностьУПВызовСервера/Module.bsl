////////////////////////////////////////////////////////////////////////////////
// Модуль содержит общие процедуры и функции для форм регламентированной
// отчетности.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПечатиУведомленийОбИсчисленныхСуммахНалогов(ОбъектыПечати) Экспорт
	
	ПараметрыПечати = Новый Соответствие;
	
	Уведомления = Новый Массив;
	Реквизиты = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ОбъектыПечати, "Уведомление");
	Для Каждого Ссылка Из ОбъектыПечати Цикл
		
		СтруктураПараметров = Новый Структура("Уведомление, СписокПечатаемыхЛистов, ДополнительныеПараметры, ТаблицаОшибок");
		ПараметрыПечати.Вставить(Ссылка, СтруктураПараметров);
		
		СтруктураПараметров.Уведомление = Реквизиты[Ссылка];
		
		Если ЗначениеЗаполнено(СтруктураПараметров.Уведомление) Тогда
			УведомлениеОбъект = СтруктураПараметров.Уведомление.ПолучитьОбъект();
		КонецЕсли;
		
		Если УведомлениеОбъект <> Неопределено Тогда
			ТаблицаОшибок = УведомлениеОбъект.ПроверитьДокументСВыводомВТаблицу();
			Уведомления.Добавить(СтруктураПараметров.Уведомление);
		Иначе
			ТаблицаОшибок = Новый СписокЗначений;
			ТаблицаОшибок.Добавить(НСтр("ru = 'Синхронизация документов нарушена. Необходимо перезаполнить и перезаписать уведомление.';
										|en = 'Document synchronization is violated. Refill the notification and save it again.'"));
		КонецЕсли;
		
		СтруктураПараметров.ТаблицаОшибок = ТаблицаОшибок;
		
	КонецЦикла;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Уведомления, "ВидУведомления, Организация");
	Для Каждого Ссылка Из ОбъектыПечати Цикл
		
		СтруктураПараметров = ПараметрыПечати[Ссылка];
		Если Не ЗначениеЗаполнено(СтруктураПараметров.Уведомление)
			ИЛИ СтруктураПараметров.ТаблицаОшибок.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыУведомления = Реквизиты[СтруктураПараметров.Уведомление];
		
		СтруктураПараметров.СписокПечатаемыхЛистов =
			УведомлениеОСпецрежимахНалогообложенияВызовСервера.ПечатьУведомленияБРО(СтруктураПараметров.Уведомление);
		СтруктураПараметров.ДополнительныеПараметры =
			Новый Структура("ЗаголовокФормы", "" + РеквизитыУведомления.ВидУведомления + " (" + РеквизитыУведомления.Организация + ")");
		
	КонецЦикла;
	
	Возврат ПараметрыПечати;
	
КонецФункции

#КонецОбласти
