////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Объединение ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет в текст запроса реквизиты шапки документа.
//  Реквизиты добавляются вместо параметра "&ОбъединениеОС_РеквизитыДокумента".
//
// Параметры:
//  ТекстЗапроса - Строка				 - Исходный текст запроса.
//  ИмяТаблицы	 - Строка, Неопределено	 - Синоним таблицы документа в запросе.
//  	Для получения пустых значений нужно передать "Неопределено".
//
Процедура ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, ИмяТаблицы = Неопределено) Экспорт
	
	ПоляЛокализации = "НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС";
	
	//++ Локализация
	
	ПоляЛокализации =
	"	ВЫБОР
	|		КОГДА ДанныеДокумента.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ИНАЧЕ ДанныеДокумента.НалогообложениеНДС
	|	КОНЕЦ                                                             КАК НалогообложениеНДС,
	|	ДанныеДокумента.АмортизационнаяГруппа                             КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.ОсновноеСредство.ГруппаОС В(
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МноголетниеНасаждения),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки))         КАК НедвижимоеИмущество,
	|	ДанныеДокумента.ВариантРаздельногоУчетаНДС                        КАК ВариантРаздельногоУчетаНДС,
	|	ДанныеДокумента.ПорядокУчетаНУ                                    КАК ПорядокУчетаНУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ                            КАК НачислятьАмортизациюНУ,
	|	ДанныеДокумента.СрокИспользованияНУ                               КАК СрокИспользованияНУ,
	|	ДанныеДокумента.ГрафикАмортизации                                 КАК ГрафикАмортизации,
	|	ДанныеДокумента.ГодоваяНормаАмортизацииБУ                         КАК ГодоваяНормаАмортизацииБУ,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ                          КАК СпециальныйКоэффициентНУ,
	|	ДанныеДокумента.СтатьяРасходовНалог                               КАК СтатьяРасходовНалог,
	|	ДанныеДокумента.АналитикаРасходовНалог                            КАК АналитикаРасходовНалог,
	|	ДанныеДокумента.СтатьяДоходов                                     КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                                  КАК АналитикаДоходов";
	
	ПоляЛокализации = СтрЗаменить(ПоляЛокализации, "ДанныеДокумента", ИмяТаблицы);
	
	//-- Локализация
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбъединениеОС_РеквизитыДокумента", ПоляЛокализации);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ИмущественныеНалоги");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("ПрослеживаемостьИмпортныхТоваров");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
		
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//  ПараметрыВыбораОсновныхСредств - см. ВнеоборотныеАктивыКлиентСервер.ЗначенияСвойствЗависимыхРеквизитов_ОбъединениеОС
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораОсновныхСредств) Экспорт
	
		
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  ТекстЗаполнения - Строка, Неопределено - Текст, используемый для заполнения документа. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОбъединениеОС - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.ОбъединениеОС - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	
	Команда = Документы.ПараметрыНачисленияЗемельногоНалога.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(Команда, "ОтражатьВРеглУчете", Истина);
	КонецЕсли;
	
	Команда = Документы.СнятиеСРегистрацииЗемельныхУчастков.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(Команда, "ОтражатьВРеглУчете", Истина);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// ОС-1 (Акт о приеме-передаче ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС1";
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'ОС-1 (Акт о приеме-передаче ОС)';
										|en = 'FA-1 (Certificate of FA handover)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьРеглУчет";
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ОтражатьВРеглУчете", Истина);
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОбъединениеОС);
	
	// ОС-4 (Акт о списании ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС4";
	КомандаПечати.Идентификатор = "ОС4";
	КомандаПечати.Представление = НСтр("ru = 'ОС-4 (Акт о списании ОС)';
										|en = 'FA-4 (FA retirement certificate)'");
	КомандаПечати.ФункциональныеОпции = "ИспользоватьРеглУчет";
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ОтражатьВРеглУчете", Истина);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(МассивОбъектов) Экспорт
	
	ДанныеДляПечатнойФормы = Неопределено;
	
	//++ Локализация
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В(&МассивОбъектов)
	|	И ДанныеДокументов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Организация,
	|	1 КАК НомерСтроки,
	|	Документ.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.ОбъединениеОС КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр(
		"ИспользоватьУчетДрагоценныхМатериалов",
		ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов"));
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивыЛокализация.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	Шапка.Номер КАК НомерДокумента,
	|	Шапка.Дата КАК ДатаДокумента,
	|	Шапка.Дата КАК ДатаВвода,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПередачи,
	|	Шапка.Организация.Префикс КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Сдатчик,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеСдатчика,
	|	Шапка.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	Шапка.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегл,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетСдатчика,
	|	Шапка.Организация КАК Получатель,
	|	Шапка.Подразделение КАК ПодразделениеПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК Руководитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ГлавныйБухгалтер,
	|	НЕОПРЕДЕЛЕНО КАК СдалМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьСдалМОЛ,
	|	Шапка.МОЛ КАК ПринялМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьПринялМОЛ
	|ИЗ
	|	Документ.ОбъединениеОС КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка В(&МассивОбъектов)
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОсновныеСредства.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА втОсновныеСредства.ОсновноеСредство.НаименованиеПолное = """"
	|			ТОГДА втОсновныеСредства.ОсновноеСредство
	|		ИНАЧЕ втОсновныеСредства.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаименованиеОС,
	|	втСведенияОбъектовДокументов.ГруппаОС КАК ГруппаОС,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.АмортизационнаяГруппа.Порядок + 1, 0) КАК АмортизационнаяГруппа,
	|	втСведенияОбъектовДокументов.КодПоОКОФ.Код КАК КодОКОФ,
	|	втСведенияОбъектовДокументов.ИнвентарныйНомер КАК НомерИнвентарный,
	|	ОбъектыЭксплуатации.ЗаводскойНомер КАК НомерЗаводской,
	|	НЕОПРЕДЕЛЕНО КАК ДатаНачалаСтроительства,
	|	ОбъектыЭксплуатации.ДатаВыпуска КАК ДатаВыпуска,
	|	ОбъектыЭксплуатации.Изготовитель КАК Изготовитель,
	|	втСведенияОбъектовДокументов.ДатаПринятияКУчету КАК ДатаВводаВЭксплуатацию,
	|	НЕОПРЕДЕЛЕНО КАК ДатаМодернизации,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКапитальногоРемонта,
	|	0 КАК СрокИспользованияФакт,
	|	0 КАК СрокИспользования,
	|	втСведенияОбъектовДокументов.СрокПолезногоИспользования КАК СрокИспользованияРаздел2,
	|	втСведенияОбъектовДокументов.ПервоначальнаяСтоимостьБУ КАК СтоимостьПервоначальная,
	|	втСведенияОбъектовДокументов.ОстаточнаяСтоимость КАК СтоимостьОстаточная,
	|	втСведенияОбъектовДокументов.СчетУчета КАК СчетУчетаОС,
	|	0 КАК ЦенаПродажи,
	|	втСведенияОбъектовДокументов.НакопленнаяАмортизация КАК АмортизацияСумма,
	|	втСведенияОбъектовДокументов.СпособНачисленияАмортизации КАК АмортизацияСпособ,
	|	втСведенияОбъектовДокументов.КоэффициентАмортизации КАК АмортизацияНорма,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны
	|ИЗ
	|	втОбъектыДокументов КАК втОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО втОсновныеСредства.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО втОсновныеСредства.Ссылка = втСведенияОбъектовДокументов.Ссылка
	|			И втОсновныеСредства.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	втОсновныеСредства.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал КАК ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ДрагоценныеМатериалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации.ДрагоценныеМатериалы КАК ДрагоценныеМатериалы
	|		ПО ДанныеДокумента.ОсновноеСредство = ДрагоценныеМатериалы.Ссылка
	|ГДЕ
	|	&ИспользоватьУчетДрагоценныхМатериалов
	|	И ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечатнойФормы = Новый Структура;
	ДанныеДляПечатнойФормы.Вставить("РезультатПоШапке", РезультатыЗапроса[0]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоТабличнойЧасти", РезультатыЗапроса[1]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоДрагоценнымМатериалам", РезультатыЗапроса[2]);
		
	//-- Локализация
	
	Возврат ДанныеДляПечатнойФормы;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыОС4(МассивОбъектов) Экспорт
	
	//++ Локализация
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ТабличнаяЧасть
	|		ПО ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивыЛокализация.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.СобытиеОС КАК Состояние,
	|	"""" КАК ПричинаСписания,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.КодПоОКПО КАК КодОКПО,
	|	ДанныеДокумента.Подразделение КАК ПодразделениеОрганизации,
	|	ДанныеДокумента.МоментВремени КАК МоментВремени,
	|	втОбъектыДокументов.НомерСтроки КАК НомерСтроки,
	|	втОбъектыДокументов.ОсновноеСредство КАК ОС,
	|	ОбъектыЭксплуатации.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВЫБОР
	|		КОГДА втСведенияОбъектовДокументов.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Автотранспорт,
	|	ОбъектыЭксплуатации.ДатаВыпуска КАК ГодВыпуска,
	|	ВЫБОР
	|		КОГДА ОбъектыЭксплуатации.НаименованиеПолное = """"
	|			ТОГДА ОбъектыЭксплуатации.Наименование
	|		ИНАЧЕ ОбъектыЭксплуатации.НаименованиеПолное
	|	КОНЕЦ КАК НаимОС,
	|	втСведенияОбъектовДокументов.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	втСведенияОбъектовДокументов.ДокументВводаВЭксплуатацию КАК ДокументВводаВЭксплуатацию,
	|	втСведенияОбъектовДокументов.ГруппаОС КАК ГруппаОС,
	|	втСведенияОбъектовДокументов.АмортизационнаяГруппа.Порядок КАК АмортизационнаяГруппа,
	|	втСведенияОбъектовДокументов.ПервоначальнаяСтоимостьБУ КАК НачСтоимость,
	|	втСведенияОбъектовДокументов.ИнвентарныйНомер КАК ИнвНомер,
	|	втСведенияОбъектовДокументов.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
	|	втСведенияОбъектовДокументов.Местонахождение КАК Подразделение,
	|	втСведенияОбъектовДокументов.МОЛ КАК МОЛ,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СтоимостьРасход, 0) КАК Стоимость,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.АмортизацияРасход, 0) КАК НачАмортизация,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны,
	|	ОбъектыЭксплуатации.ДрагоценныеМатериалы.(
	|		ДрагоценныйМатериал КАК ДМНаименование,
	|		Количество КАК ДММасса,
	|		ЕдиницаИзмерения КАК ДМЕдиницаИзмерения
	|	) КАК ДрагоценныеМатериалы,
	|	ЛОЖЬ КАК ЧастичнаяЛиквидация
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъектыДокументов КАК втОбъектыДокументов
	|		ПО (втОбъектыДокументов.Ссылка = ДанныеДокумента.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО (втОбъектыДокументов.Ссылка = втСведенияОбъектовДокументов.Ссылка)
	|			И (втОбъектыДокументов.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО (втОбъектыДокументов.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ
	|	МИНИМУМ(СведенияАктуальны),
	|	МИНИМУМ(ЧастичнаяЛиквидация)
	|ПО
	|	Ссылка";
	
	Результат = Запрос.Выполнить();
	
	Возврат Новый Структура(
		"ПоДокументам, ПоПрочимОприходованиям",
		Результат,
		Неопределено);
		
	//-- Локализация
	
КонецФункции

#КонецОбласти

#Область ФормаДокумента

// Настраивает элементы формы, в зависимости от измененных реквизитов.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - 
// 	СтруктураИзмененныхРеквизитов - Структура - 
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СтруктураИзмененныхРеквизитов) Экспорт

	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОсновноеСредство")
		ИЛИ ОбновитьВсе Тогда
		
		Если ЗначениеЗаполнено(Объект.ОсновноеСредство) Тогда
			ГруппаОС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОсновноеСредство, "ГруппаОС");
		Иначе
			ГруппаОС = Неопределено;
		КонецЕсли;
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ГруппаОС",
			ГруппаОС,
			Форма.СлужебныеПараметрыФормы);
			
		КонецЕсли;
		
	Если СтруктураИзмененныхРеквизитов.Свойство("ОсновноеСредство")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ ОбновитьВсе Тогда
		
		Если ЗначениеЗаполнено(Объект.ОсновноеСредство) Тогда
			АмортизационныеГруппыОС = ОбъектыЭксплуатацииЛокализация.АмортизационныеГруппыОС(Объект.ОсновноеСредство, Объект.Дата);
			АмортизационнаяГруппа = АмортизационныеГруппыОС.Получить(Объект.ОсновноеСредство);
		Иначе
			АмортизационнаяГруппа = Неопределено;
		КонецЕсли;
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"АмортизационнаяГруппа",
			АмортизационнаяГруппа,
			Форма.СлужебныеПараметрыФормы);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОсновноеСредство")
		ИЛИ ОбновитьВсе Тогда
		
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = 
			ВнеоборотныеАктивыЛокализация.ТребуетсяНастройкаОтраженияРасходовПоНалогам(
					Объект.Организация, 
					Объект.Дата, 
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ОсновноеСредство),
					Форма.СлужебныеПараметрыФормы.ГруппаОС,
					Форма.СлужебныеПараметрыФормы.АмортизационнаяГруппа);
				
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ТребуетсяНастройкаОтраженияРасходовПоНалогам",
			ТребуетсяНастройкаОтраженияРасходовПоНалогам,
			Форма.СлужебныеПараметрыФормы);
				
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПлательщикНалогаНаПрибыль",
			УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПрименяетсяУСНДоходыМинусРасходы",
			УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
			
		//
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
			Объект.Организация,
			Объект.Дата);
			
		//
		РаздельныйУчетВНАПоНалогообложениюНДС = 
			ПараметрыУчетнойПолитики <> Неопределено
			И ПараметрыУчетнойПолитики.РаздельныйУчетВНАПоНалогообложениюНДС;
			
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"РаздельныйУчетВНАПоНалогообложениюНДС",
			РаздельныйУчетВНАПоНалогообложениюНДС,
			Форма.СлужебныеПараметрыФормы);
			
		//	
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНалогаНаПрибыль",
			Объект.Организация,
			Объект.Дата);
		
		ВключатьВСоставНалоговыхРасходовАрендныеПлатежи = 
			ПараметрыУчетнойПолитики <> Неопределено 
			И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи;
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ВключатьВСоставНалоговыхРасходовАрендныеПлатежи",
			ВключатьВСоставНалоговыхРасходовАрендныеПлатежи,
			Форма.СлужебныеПараметрыФормы);
			
		//
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПрименяетсяФСБУ6",
			НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ЕстьСвязанныеОрганизации",
			Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация),
			Форма.СлужебныеПараметрыФормы);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОсновноеСредство") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ ОбновитьВсе Тогда
		
		Если Объект.ОтражатьВРеглУчете
			И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОбъединениеОС
			И Форма.СлужебныеПараметрыФормы.ИспользуетсяЦФ Тогда
			Элементы.СтраницаОтражениеРасходов.Заголовок = НСтр("ru = 'Отражение доходов и расходов';
																|en = 'Record income and expenses'");
		Иначе
			Элементы.СтраницаОтражениеРасходов.Заголовок = НСтр("ru = 'Отражение расходов';
																|en = 'Expense parameters'");
		КонецЕсли;
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьСлужебныеПараметрыФормы(СлужебныеПараметрыФормы) Экспорт

	//++ Локализация
	
	СлужебныеПараметрыФормы.Вставить("ИспользоватьРеглУчет", ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет"));
	СлужебныеПараметрыФормы.Вставить("НачислениеИзносаПоЕНАОФ", Ложь);
	
	СлужебныеПараметрыФормы.РеквизитыКоторыеДолжныСовпадать = 
		СлужебныеПараметрыФормы.РеквизитыКоторыеДолжныСовпадать
		+ ",ГруппаОС,АмортизационнаяГруппа,СрокИспользованияНУ,НалогообложениеНДС";
		
	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаДляПроверкиОС() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Организация КАК Организация,
	|	МестонахождениеОС.Местонахождение КАК Местонахождение,
	|	МестонахождениеОС.Арендатор КАК Арендатор
	|ПОМЕСТИТЬ МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В (&СписокВсехОС)) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ
	|ПОМЕСТИТЬ ПервоначальныеСведенияОС
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И (ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство,
	|						МестонахождениеОС.Организация
	|					ИЗ
	|						МестонахождениеОС)) КАК ПервоначальныеСведенияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ИспользуетсяЦФ
	|ПОМЕСТИТЬ ПроверкаИспользованияЦФ
	|ИЗ
	|	РегистрНакопления.СтоимостьОС.Остатки(
	|			&Дата,
	|			(ОсновноеСредство, Организация) В
	|				(ВЫБРАТЬ
	|					МестонахождениеОС.ОсновноеСредство,
	|					МестонахождениеОС.Организация
	|				ИЗ
	|					МестонахождениеОС)) КАК СтоимостьОС
	|ГДЕ
	|	(СтоимостьОС.СтоимостьЦФОстаток <> 0
	|			ИЛИ СтоимостьОС.СтоимостьНУЦФОстаток <> 0
	|			ИЛИ СтоимостьОС.СтоимостьПРЦФОстаток <> 0
	|			ИЛИ СтоимостьОС.СтоимостьВРЦФОстаток <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУ
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И (ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство,
	|						МестонахождениеОС.Организация
	|					ИЗ
	|						МестонахождениеОС)) КАК ПараметрыАмортизацииОСУУ
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСБУ
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И (ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство,
	|						МестонахождениеОС.Организация
	|					ИЗ
	|						МестонахождениеОС)) КАК ПараметрыАмортизацииОСБУ
	|ГДЕ
	|	&ОтражатьВРеглУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСУУ.Состояние КАК Состояние,
	|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ
	|ПОМЕСТИТЬ ПорядокУчетаОСУУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И (ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство,
	|						МестонахождениеОС.Организация
	|					ИЗ
	|						МестонахождениеОС)) КАК ПорядокУчетаОСУУ
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ПорядокУчетаОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В (&СписокВсехОС)) КАК ПорядокУчетаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСБУ.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПорядокУчетаОСБУ.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ПорядокУчетаОСБУ.СостояниеБУ КАК СостояниеБУ,
	|	ПорядокУчетаОСБУ.СостояниеНУ КАК СостояниеНУ,
	|	ПорядокУчетаОСБУ.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ПорядокУчетаОСБУ.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ
	|ПОМЕСТИТЬ ПорядокУчетаОСБУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И (ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство,
	|						МестонахождениеОС.Организация
	|					ИЗ
	|						МестонахождениеОС)) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	&ОтражатьВРеглУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Договор КАК Договор
	|ПОМЕСТИТЬ АрендованныеОС
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						МестонахождениеОС.ОсновноеСредство
	|					ИЗ
	|						МестонахождениеОС)) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Состояние В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПервоеОС.Представление, """") КАК ПервоеОСПредставление,
	|
	|	ВЫБОР
	|		КОГДА НовыйОС.ГруппаОС <> ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПустаяСсылка)
	|			ТОГДА НовыйОС.ГруппаОС
	|		ИНАЧЕ ПервоеОС.ГруппаОС
	|	КОНЕЦ КАК ГруппаОС,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.АмортизационнаяГруппа, ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка)) 
	|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСБУНовыйОС.АмортизационнаяГруппа, ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка))
	|	КОНЕЦ КАК АмортизационнаяГруппа,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаПервогоОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) 
	|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаНовыйОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) 
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	ВЫБОР
	|		КОГДА &Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК Местонахождение,
	|
	|	ВЫБОР
	|		КОГДА &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА &Организация
	|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК Организация,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияНУ, 0) КАК СрокИспользованияНУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) КАК НалогообложениеНДС,
	|	НЕ ПроверкаИспользованияЦФ.ИспользуетсяЦФ ЕСТЬ NULL КАК ИспользуетсяЦФ,
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.МетодНачисленияАмортизации, 0) КАК МетодНачисленияАмортизацииУУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ, НЕОПРЕДЕЛЕНО) КАК МетодНачисленияАмортизацииБУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) КАК ПорядокУчетаБУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаУУ, НЕОПРЕДЕЛЕНО) КАК ПорядокУчетаУУ,
	|
	|	ЕСТЬNULL(ПорядокУчетаОСУУПервогоОС.НачислятьАмортизациюУУ, 0) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НачислятьАмортизациюБУ, 0) КАК НачислятьАмортизациюБУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НачислятьАмортизациюНУ, 0) КАК НачислятьАмортизациюНУ,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСУУПервогоОС.Состояние, НЕОПРЕДЕЛЕНО) 
	|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСУУНовыйОС.Состояние, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК СостояниеУУ,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.СостояниеБУ, НЕОПРЕДЕЛЕНО) 
	|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСБУНовыйОС.СостояниеБУ, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК СостояниеБУ,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.СостояниеНУ, НЕОПРЕДЕЛЕНО) 
	|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСБУНовыйОС.СостояниеНУ, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК СостояниеНУ
	|
	|ПОМЕСТИТЬ ЗначенияПараметров
	|
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК НовыйОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ПервоеОС
	|		ПО (ПервоеОС.Ссылка = &ПервоеОС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииПервогоОСУУ
	|		ПО (ПараметрыАмортизацииПервогоОСУУ.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииПервогоОСБУ
	|		ПО (ПараметрыАмортизацииПервогоОСБУ.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаНовыйОС
	|		ПО (ПорядокУчетаНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаПервогоОС
	|		ПО (ПорядокУчетаПервогоОС.ОсновноеСредство = &ПервоеОС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУПервогоОС
	|		ПО (ПорядокУчетаОСБУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУПервогоОС
	|		ПО (ПорядокУчетаОСУУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУНовыйОС
	|		ПО (ПорядокУчетаОСБУНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУНовыйОС
	|		ПО (ПорядокУчетаОСУУНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаИспользованияЦФ КАК ПроверкаИспользованияЦФ
	|		ПО ИСТИНА
	|ГДЕ
	|	НовыйОС.Ссылка = &ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПервоеОС.Представление,
	|	ПервоеОС.ГруппаОС,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.АмортизационнаяГруппа, ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка)),
	|	ЕСТЬNULL(ПорядокУчетаПервогоОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА &Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК Местонахождение,
	|	ВЫБОР
	|		КОГДА &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА &Организация
	|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) 
	|	КОНЕЦ КАК Организация,
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.СрокИспользования, 0),
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияБУ, 0),
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияНУ, 0),
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)),
	|	НЕ ПроверкаИспользованияЦФ.ИспользуетсяЦФ ЕСТЬ NULL,
	|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.МетодНачисленияАмортизации, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаУУ, НЕОПРЕДЕЛЕНО),
	|
	|	ЕСТЬNULL(ПорядокУчетаОСУУПервогоОС.НачислятьАмортизациюУУ, 0) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НачислятьАмортизациюБУ, 0) КАК НачислятьАмортизациюБУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.НачислятьАмортизациюНУ, 0) КАК НачислятьАмортизациюНУ,
	|
	|	ЕСТЬNULL(ПорядокУчетаОСУУПервогоОС.Состояние, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.СостояниеБУ, НЕОПРЕДЕЛЕНО) КАК СостояниеБУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.СостояниеНУ, НЕОПРЕДЕЛЕНО) КАК СостояниеНУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ПервоеОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииПервогоОСУУ
	|		ПО (ПараметрыАмортизацииПервогоОСУУ.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииПервогоОСБУ
	|		ПО (ПараметрыАмортизацииПервогоОСБУ.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаПервогоОС
	|		ПО (ПорядокУчетаПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУПервогоОС
	|		ПО (ПорядокУчетаОСБУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУПервогоОС
	|		ПО (ПорядокУчетаОСУУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = ПервоеОС.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаИспользованияЦФ КАК ПроверкаИспользованияЦФ
	|		ПО ИСТИНА
	|ГДЕ
	|	ПервоеОС.Ссылка = &ПервоеОС
	|	И &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УзлыКомпоненты.УзелКомпонент,
	|	УзлыКомпоненты.ОсновноеСредство
	|ПОМЕСТИТЬ УзлыКомпонентыВсехОС
	|ИЗ
	|	РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Дата, УзелКомпонент В (&СписокВсехОС)) КАК УзлыКомпоненты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УзлыКомпонентыТаблицы.УзелКомпонент КАК УзелКомпонент,
	|	ИСТИНА КАК ОСВладелецНеСовпадает
	|ПОМЕСТИТЬ НесоответствияУзловКомпонентов
	|ИЗ
	|	УзлыКомпонентыВсехОС КАК УзлыКомпонентыШапки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УзлыКомпонентыВсехОС КАК УзлыКомпонентыТаблицы
	|		ПО УзлыКомпонентыШапки.ОсновноеСредство <> УзлыКомпонентыТаблицы.ОсновноеСредство
	|ГДЕ
	|	УзлыКомпонентыШапки.УзелКомпонент = &ОсновноеСредство
	|	И УзлыКомпонентыТаблицы.УзелКомпонент В (&СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УзлыКомпонентыТаблицы.УзелКомпонент КАК УзелКомпонент,
	|	ИСТИНА КАК ОСВладелецНеСовпадает
	|ИЗ
	|	УзлыКомпонентыВсехОС КАК УзлыКомпонентыТаблицы
	|ГДЕ
	|	УзлыКомпонентыТаблицы.УзелКомпонент В (&СписокОС)
	|	И УзлыКомпонентыТаблицы.ОсновноеСредство <> &ОсновноеСредство
	|	И ВЫРАЗИТЬ(&ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ТипОС В
	|	(ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации), ЗНАЧЕНИЕ(Перечисление.ТипыОС.ГрупповоеОС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияПараметров.ПервоеОСПредставление КАК ПервоеОСПредставление,
	|	ЗначенияПараметров.ГруппаОС КАК ГруппаОС,
	|	ЗначенияПараметров.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ЗначенияПараметров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗначенияПараметров.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	ЗначенияПараметров.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	ЗначенияПараметров.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|	ЗначенияПараметров.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ЗначенияПараметров.ИспользуетсяЦФ КАК ИспользуетсяЦФ,
	|	ЗначенияПараметров.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииУУ,
	|	ЗначенияПараметров.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ЗначенияПараметров.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ЗначенияПараметров.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ЗначенияПараметров.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ЗначенияПараметров.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ЗначенияПараметров.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ
	|ИЗ
	|	ЗначенияПараметров КАК ЗначенияПараметров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатПроверки.ОсновноеСредство КАК ОсновноеСредство,
	|	РезультатПроверки.ОсновноеСредствоПредставление КАК ОсновноеСредствоПредставление,
	|	РезультатПроверки.ГруппаОС КАК ГруппаОС,
	|	РезультатПроверки.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	РезультатПроверки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РезультатПроверки.НаправлениеДеятельности.Представление КАК НаправлениеДеятельностиПредставление,
	|	РезультатПроверки.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	РезультатПроверки.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	РезультатПроверки.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|	РезультатПроверки.НалогообложениеНДС КАК НалогообложениеНДС,
	|	РезультатПроверки.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|
	|	РезультатПроверки.ОтличаетсяГруппаОС КАК ОтличаетсяГруппаОС,
	|	РезультатПроверки.ОтличаетсяАмортизационнаяГруппа КАК ОтличаетсяАмортизационнаяГруппа,
	|	РезультатПроверки.ОтличаетсяНаправлениеДеятельности КАК ОтличаетсяНаправлениеДеятельности,
	|	РезультатПроверки.ОтличаетсяМестонахождение КАК ОтличаетсяМестонахождение,
	|	РезультатПроверки.ОтличаетсяСрокИспользованияУУ КАК ОтличаетсяСрокИспользованияУУ,
	|	РезультатПроверки.ОтличаетсяСрокИспользованияБУ КАК ОтличаетсяСрокИспользованияБУ,
	|	РезультатПроверки.ОтличаетсяСрокИспользованияНУ КАК ОтличаетсяСрокИспользованияНУ,
	|	РезультатПроверки.ОтличаетсяНалогообложениеНДС КАК ОтличаетсяНалогообложениеНДС,
	|	РезультатПроверки.ОСВладелецНеСовпадает КАК ОСВладелецНеСовпадает,
	|	РезультатПроверки.ПоДоговоруЛизинга КАК ПоДоговоруЛизинга,
	|	РезультатПроверки.НачислениеИзносаПоЕНАОФ КАК НачислениеИзносаПоЕНАОФ,
	|	РезультатПроверки.ПереданоВАренду КАК ПереданоВАренду,
	|	РезультатПроверки.НеПринятоУУ КАК НеПринятоУУ,
	|	РезультатПроверки.НеПринятоБУ КАК НеПринятоБУ,
	|	РезультатПроверки.НеПринятоНУ КАК НеПринятоНУ,
	|	РезультатПроверки.ПринятоУУ КАК ПринятоУУ,
	|	РезультатПроверки.ПринятоБУ КАК ПринятоБУ,
	|	РезультатПроверки.ПринятоНУ КАК ПринятоНУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсходныеОС.Ссылка КАК ОсновноеСредство,
	|		ИсходныеОС.Представление КАК ОсновноеСредствоПредставление,
	|		ЗначенияПараметров.ГруппаОС КАК ГруппаОС,
	|		ЗначенияПараметров.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|		ЗначенияПараметров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗначенияПараметров.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|		ЗначенияПараметров.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|		ЗначенияПараметров.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|		ЗначенияПараметров.НалогообложениеНДС КАК НалогообложениеНДС,
	|		ЗначенияПараметров.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|
	|		ИсходныеОС.ГруппаОС <> ЗначенияПараметров.ГруппаОС
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяГруппаОС,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСБУ.АмортизационнаяГруппа, ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка)) <> ЗначенияПараметров.АмортизационнаяГруппа
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяАмортизационнаяГруппа,
	|
	|		(&ОбъединениеОднотипныхОС
	|				И ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗначенияПараметров.НаправлениеДеятельности
	|			ИЛИ НЕ &ОбъединениеОднотипныхОС
	|				И ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗначенияПараметров.НаправлениеДеятельности
	|				И ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК ОтличаетсяНаправлениеДеятельности,
	|
	|		ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) <> ЗначенияПараметров.Организация
	|			ИЛИ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) <> ЗначенияПараметров.Местонахождение КАК ОтличаетсяМестонахождение,
	|
	|		&ОтражатьВУпрУчете
	|			И ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) <> ЗначенияПараметров.СрокИспользованияУУ
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяСрокИспользованияУУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0) <> ЗначенияПараметров.СрокИспользованияБУ
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяСрокИспользованияБУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ, 0) <> ЗначенияПараметров.СрокИспользованияНУ
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяСрокИспользованияНУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЕСТЬNULL(ПорядокУчетаОСБУ.НалогообложениеНДС, НЕОПРЕДЕЛЕНО) <> ЗначенияПараметров.НалогообложениеНДС
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяНалогообложениеНДС,
	|
	|		ЕСТЬNULL(НесоответствияУзловКомпонентов.ОСВладелецНеСовпадает, ЛОЖЬ) КАК ОСВладелецНеСовпадает,
	|
	|		ВЫБОР
	|			КОГДА НЕ АрендованныеОС.Договор ЕСТЬ NULL
	|				ТОГДА АрендованныеОС.Договор ССЫЛКА Справочник.ДоговорыАренды
	|						И АрендованныеОС.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ПоДоговоруЛизинга,
	|
	|		&ОтражатьВРеглУчете
	|			И (ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|					И ЕСТЬNULL(ЗначенияПараметров.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|				ИЛИ ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|					И ЕСТЬNULL(ЗначенияПараметров.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ))
	|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК НачислениеИзносаПоЕНАОФ,
	|
	|		НЕ ЕСТЬNULL(МестонахождениеОС.Арендатор, НЕОПРЕДЕЛЕНО) В (
	|					ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|					ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|				НЕОПРЕДЕЛЕНО) КАК ПереданоВАренду,
	|
	|		&ОтражатьВУпрУчете
	|			И ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС) КАК НеПринятоУУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЗначенияПараметров.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС) КАК НеПринятоБУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЗначенияПараметров.СостояниеНУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеНУ, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС) КАК НеПринятоНУ,
	|
	|		&ОтражатьВУпрУчете
	|			И ЗначенияПараметров.СостояниеУУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятоУУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЗначенияПараметров.СостояниеБУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятоБУ,
	|
	|		&ОтражатьВРеглУчете
	|			И ЗначенияПараметров.СостояниеНУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеНУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И НЕ ИсходныеОС.ТипОС В(ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации), ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел)) КАК ПринятоНУ
	|
	|	ИЗ
	|		Справочник.ОбъектыЭксплуатации КАК ИсходныеОС
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ЗначенияПараметров КАК ЗначенияПараметров
	|			ПО (ИСТИНА)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|			ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|			ПО (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|			ПО (ПорядокУчетаОС.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|			ПО (ПорядокУчетаОСУУ.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|			ПО (ПорядокУчетаОСБУ.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|			ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|			ПО (МестонахождениеОС.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ АрендованныеОС КАК АрендованныеОС
	|			ПО (АрендованныеОС.ОсновноеСредство = ИсходныеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ НесоответствияУзловКомпонентов КАК НесоответствияУзловКомпонентов
	|			ПО (НесоответствияУзловКомпонентов.УзелКомпонент = ИсходныеОС.Ссылка)
	|
	|	ГДЕ
	|		ИсходныеОС.Ссылка В(&СписокОС)
	|
	|	) КАК РезультатПроверки
	|
	|ГДЕ
	|	(РезультатПроверки.ОтличаетсяГруппаОС
	|		ИЛИ РезультатПроверки.ОтличаетсяАмортизационнаяГруппа
	|		ИЛИ РезультатПроверки.ОтличаетсяНаправлениеДеятельности
	|		ИЛИ РезультатПроверки.ОтличаетсяМестонахождение
	|		ИЛИ РезультатПроверки.ОтличаетсяСрокИспользованияУУ
	|		ИЛИ РезультатПроверки.ОтличаетсяСрокИспользованияБУ
	|		ИЛИ РезультатПроверки.ОтличаетсяСрокИспользованияНУ
	|		ИЛИ РезультатПроверки.ОтличаетсяНалогообложениеНДС
	|		ИЛИ РезультатПроверки.ОСВладелецНеСовпадает
	|		ИЛИ РезультатПроверки.ПоДоговоруЛизинга
	|		ИЛИ РезультатПроверки.НачислениеИзносаПоЕНАОФ
	|		ИЛИ РезультатПроверки.ПереданоВАренду
	|		ИЛИ РезультатПроверки.НеПринятоУУ
	|		ИЛИ РезультатПроверки.НеПринятоБУ
	|		ИЛИ РезультатПроверки.НеПринятоНУ
	|		ИЛИ РезультатПроверки.ПринятоУУ
	|		ИЛИ РезультатПроверки.ПринятоБУ
	|		ИЛИ РезультатПроверки.ПринятоНУ)";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДополнитьПараметрыВыбораОсновныхСредств(ВыборкаПараметры, ПараметрыВыбораОсновныхСредств) Экспорт

	//++ Локализация
	
	ПараметрыВыбораОсновныхСредств.ЗначенияПараметров.Вставить("ГруппаОС", ВыборкаПараметры.ГруппаОС);
	ПараметрыВыбораОсновныхСредств.ЗначенияПараметров.Вставить("АмортизационнаяГруппа", ВыборкаПараметры.АмортизационнаяГруппа);
	ПараметрыВыбораОсновныхСредств.ЗначенияПараметров.Вставить("СрокИспользованияНУ", ВыборкаПараметры.СрокИспользованияНУ);
	ПараметрыВыбораОсновныхСредств.ЗначенияПараметров.Вставить("НалогообложениеНДС", ВыборкаПараметры.НалогообложениеНДС);
	
	ПоНаработкеБУ = 
		ВыборкаПараметры.МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции
		ИЛИ ВыборкаПараметры.МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега;
	ПараметрыВыбораОсновныхСредств.Вставить("ПоНаработкеБУ", ПоНаработкеБУ);
	
	ПараметрыВыбораОсновныхСредств.Вставить("ИспользуетсяЦФ", ВыборкаПараметры.ИспользуетсяЦФ);
	
	//-- Локализация
	
КонецПроцедуры

Функция ЭтоНеОднотипноеОСПоРезультатамПроверки(РезультатПроверки) Экспорт

	Результат = Ложь;
	
	//++ Локализация
	Результат = РезультатПроверки.ОтличаетсяГруппаОС
				ИЛИ РезультатПроверки.ОтличаетсяАмортизационнаяГруппа
				ИЛИ РезультатПроверки.ОтличаетсяНаправлениеДеятельности
				ИЛИ РезультатПроверки.ОтличаетсяНалогообложениеНДС;
	//-- Локализация

	Возврат Результат;
	
КонецФункции

Функция МожноОбъединитьПоРезультатамПроверки(РезультатПроверки) Экспорт
	
	Результат = Истина;
	//++ Локализация
	Результат =
		НЕ РезультатПроверки.НачислениеИзносаПоЕНАОФ
		И НЕ РезультатПроверки.ПереданоВАренду
		И НЕ РезультатПроверки.ПоДоговоруЛизинга;
	//-- Локализация

	Возврат Результат;

КонецФункции

// Дополняет описание проблем.
// 
// Параметры:
// 	РезультатПроверки - см. Документы.ОбъединениеОС.ПараметрыВыбораОсновныхСредств 
// 	ОписаниеПроблем - Массив -
Процедура ДополнитьОписаниеПроблемПриЗаполненииНаОснованииОбъектаЭксплуатации(РезультатПроверки, ОписаниеПроблем) Экспорт

	//++ Локализация
	
	Если РезультатПроверки.НачислениеИзносаПоЕНАОФ Тогда
		
		ТекстСообщения = НСтр("ru = '- не допускается объединять основное средство ""%1"", для которого начисляется износ по ЕНАОФ, с основными средствами, у которых другой порядок учета';
								|en = '- it is not allowed to merge the fixed asset ""%1"", for which depreciation is calculated according to UFADR, with fixed assets that have a different accounting procedure '");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
							
		ОписаниеПроблем.Добавить(ТекстСообщения);
		
	КонецЕсли;
	
	Если РезультатПроверки.ПоДоговоруЛизинга Тогда
		
		ТекстСообщения = НСтр("ru = '- не допускается объединять основное средство ""%1"", которое получено по договору лизинга';
								|en = '- it is not allowed to merge the fixed asset ""%1"", which is acquired under lease agreements'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОписаниеПроблем.Добавить(ТекстСообщения);
		
	КонецЕсли;
	
	Если РезультатПроверки.ПереданоВАренду Тогда
	
		ТекстСообщения = НСтр("ru = '- не допускается объединять основное средство ""%1"", которое передано в аренду';
								|en = '- it is not allowed to merge the fixed asset ""%1"", which is leased'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОписаниеПроблем.Добавить(ТекстСообщения);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Функция ДополнитьТекстЗапросаДляЗаполненияТекущихЗначенийПараметров() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаПринятияКУчетуУУ,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизацииУУ,
	|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользованияУУ
	|	,ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаПринятияКУчетуБУ
	|	,ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ
	|	,ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ КАК СрокИспользованияБУ
	|	,ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ КАК СрокИспользованияНУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)";
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции
 
#КонецОбласти

#Область Проведение

Процедура ТаблицыОтложенногоФормированияДвижений(Запрос, ТекстыЗапроса) Экспорт

	//++ Локализация
	ТекстЗапросаТаблицаСобытияОСОрганизаций(Запрос, ТекстыЗапроса, Неопределено);
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Неопределено);
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаСобытияОСОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрослеживаемыеТоварыВСоставеОС(Запрос, ТекстыЗапроса, Регистры);
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);

	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ПринятиеКУчетуНеОднотипные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Регистратор,
	|	ДанныеДокумента.Период           КАК Период,
	|	ДанныеДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация      КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаНУ,
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюНУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюНУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаНУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаНУ
	|	КОНЕЦ КАК ПорядокУчетаНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьНУ + СтоимостьВНА.СтоимостьНУЦФ, 0) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл + СтоимостьВНА.СтоимостьЦФ, 0)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.Стоимость, 0)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияПервогоОС
	|		ПО ПервоначальныеСведенияПервогоОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПервоначальныеСведенияПервогоОС.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПринятиеКУчетуОднотипные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Регистратор,
	|	ДанныеДокумента.Период           КАК Период,
	|	ДанныеДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация      КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаНУ,
	|	НЕОПРЕДЕЛЕНО                    КАК ДокументСнятияСУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюНУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюНУ = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ПервоначальныеСведенияПервогоОС.МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ПервоначальныеСведенияПервогоОС.ПорядокУчетаБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ПервоначальныеСведенияПервогоОС.ПорядокУчетаНУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаНУ
	|	КОНЕЦ КАК ПорядокУчетаНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ПервоначальныеСведенияПервогоОС.ПорядокУчетаУУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьНУ, 0) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.Стоимость, 0)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияПервогоОС
	|		ПО ПервоначальныеСведенияПервогоОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПервоначальныеСведенияПервогоОС.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СнятиеСУчета
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Регистратор,
	|	ДанныеДокумента.Период           КАК Период,
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация      КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ
	|
	|	КОНЕЦ КАК ДокументСнятияСУчетаБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаНУ
	|	КОНЕЦ КАК ДокументСнятияСУчетаНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ
	|	КОНЕЦ КАК ДокументСнятияСУчетаУУ,
	|
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ КАК ДатаВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ КАК ДокументВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство <> ДанныеДокумента.ОсновноеСредство
	|
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СообщитьОРезультатеПроверкиСтроки(Объект, Поле, РезультатПроверки, Отказ) Экспорт

	//++ Локализация
	
	Если Объект.ОбъединениеОднотипныхОС
		И РезультатПроверки.ОтличаетсяГруппаОС Тогда
		
		ТекстСообщения = НСтр("ru = 'У основного средства ""%1"" должна быть группа ОС ""%2""';
								|en = 'The fixed asset ""%1"" should have the FA group ""%2""'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, 
							РезультатПроверки.ОсновноеСредствоПредставление, 
							РезультатПроверки.ГруппаОС);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если Объект.ОбъединениеОднотипныхОС
		И РезультатПроверки.ОтличаетсяАмортизационнаяГруппа Тогда
		
		ТекстСообщения = НСтр("ru = 'У основного средства ""%1"" должна быть амортизационная группа ""%2""';
								|en = 'The fixed asset ""%1"" should have the depreciation group ""%2""'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, 
							РезультатПроверки.ОсновноеСредствоПредставление, 
							РезультатПроверки.АмортизационнаяГруппа);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если Объект.ОбъединениеОднотипныхОС
		И РезультатПроверки.ОтличаетсяНалогообложениеНДС Тогда
		
		ТекстСообщения = НСтр("ru = 'У основного средства ""%1"" должно быть налогообложение НДС ""%2""';
								|en = 'The fixed asset ""%1"" should have the VAT taxation ""%2""'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, 
							РезультатПроверки.ОсновноеСредствоПредставление, 
							РезультатПроверки.НалогообложениеНДС);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если Объект.ОбъединениеОднотипныхОС
		И РезультатПроверки.ОтличаетсяСрокИспользованияНУ
		И НЕ РезультатПроверки.НеПринятоНУ
		И НЕ РезультатПроверки.ПринятоНУ Тогда
		
		ТекстСообщения = НСтр("ru = 'У основного средства ""%1"" должен быть срок использования в налоговом учете %2 мес.';
								|en = 'The fixed asset ""%1"" should have the useful life in tax accounting %2 months.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, 
							РезультатПроверки.ОсновноеСредствоПредставление, 
							РезультатПроверки.СрокИспользованияНУ);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если РезультатПроверки.НачислениеИзносаПоЕНАОФ Тогда
		
		Если РезультатПроверки.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ Тогда
			ТекстСообщения = НСтр("ru = 'Не допускается объединять основное средство ""%1"" с основными средствами, для которых начисляется износ по ЕНАОФ, т.к. у него другой порядок учета';
									|en = 'It is not allowed to merge the fixed asset ""%1"" with the fixed assets, for which depreciation is calculated according to UFADR, since it has a different accounting procedure'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Не допускается объединять основное средство ""%1"", для которого начисляется износ по ЕНАОФ, с основными средствами, у которых другой порядок учета';
									|en = 'It is not allowed to merge the fixed asset ""%1"", for which depreciation is calculated according to UFADR, with fixed assets that have a different accounting procedure'");
		КонецЕсли; 
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если РезультатПроверки.ПоДоговоруЛизинга Тогда
		
		ТекстСообщения = НСтр("ru = 'Не допускается объединять основное средство ""%1"", которое получено по договору аренды';
								|en = 'It is not allowed to merge the fixed asset ""%1"", which is acquired under lease agreements'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если РезультатПроверки.ПереданоВАренду Тогда
		
		ТекстСообщения = НСтр("ru = 'Не допускается объединять основное средство ""%1"", которое передано в аренду';
								|en = 'It is not allowed to merge the fixed asset ""%1"", which is leased'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если РезультатПроверки.НеПринятоНУ Тогда
		
		ТекстСообщения = НСтр("ru = 'Для отражения документа в регламентированном учете необходимо, чтобы основное средство ""%1"" было принято к учету в налоговом учете.
							  |Не допускается объединять основные средства, где одно ОС принято в налоговом учете, а другие не приняты.';
							  |en = 'The fixed asset ""%1"" must be recognized in tax accounting to record the document in local accounting.
							  |Combining fixed assets where one fixed asset is recognized in tax accounting, and others are not is not allowed.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	Если РезультатПроверки.ПринятоНУ Тогда
		
		ТекстСообщения = НСтр("ru = 'Для отражения документа в регламентированном учете необходимо, чтобы основное средство ""%1"" не было принято к учету в налоговом учете.
							  |Не допускается объединять основные средства, где одно ОС принято в налоговом учете, а другие не приняты.';
							  |en = 'The fixed asset ""%1"" must not be recognized in tax accounting to record the document in local accounting.
							  |Combining fixed assets where one fixed asset is recognized in tax accounting, and others are not is not allowed.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПроверки.ОсновноеСредствоПредставление);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	ТекстЗапроса = "";
		
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	#Область ВыделениеСтоимости
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение стоимости ОС (Дт СчетУчетаСтоимости :: Кт СчетУчетаСтоимости)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Строки.КорГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.КорКонтрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	Строки.Арендатор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|
	|	""Выделение стоимости ОС"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|		ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";

	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВыделениеСтоимостиЦФ_Дт_СчетУчетаСтоимости_ЦФ__Кт_СчетУчетаСтоимости_ЦФ
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение стоимости ОС (Дт СчетУчетаСтоимости_ЦФ :: Кт СчетУчетаСтоимости_ЦФ)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.КорГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Арендатор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	Строки.Арендатор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|
	|	""Выделение стоимости ОС"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|		ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)";

	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеАмортизации_Дт_СчетУчетаАмортизации__Кт_СчетУчетаАмортизации
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение амортизации ОС (Дт СчетУчетаАмортизации :: Кт СчетУчетаАмортизации)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияРегл 
	|	КОНЕЦ КАК Сумма,
	|	Строки.Амортизация КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаКт,
	|	Строки.КорГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРКт,
	|	""Выделение амортизации ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И (Строки.Амортизация <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос 
	|			И (Строки.АмортизацияРегл <> 0
	|				ИЛИ Строки.АмортизацияНУ <> 0
	|				ИЛИ Строки.АмортизацияПР <> 0
	|				ИЛИ Строки.АмортизацияВР <> 0))";

	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВыделениеАмортизацииЦФ_Дт_СчетУчетаАмортизации_ЦФ__Кт_СчетУчетаАмортизации_ЦФ
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение амортизации ОС (Дт СчетУчетаАмортизации_ЦФ :: Кт СчетУчетаАмортизации_ЦФ) за счет целевого финансирования
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчетаКт,
	|	Строки.КорГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	""Выделение амортизации ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НЕ Строки.НачислятьИзнос
	|	И (Строки.АмортизацияЦФ <> 0
	|		ИЛИ Строки.АмортизацияНУЦФ <> 0
	|		ИЛИ Строки.АмортизацияПРЦФ <> 0
	|		ИЛИ Строки.АмортизацияВРЦФ <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеИзносаИсходящиеДвижения // Дт  :: Кт 010
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение амортизации ОС исходящие проводки (Дт  :: Кт 010)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияРегл + Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИзносОсновныхСредств) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выделение накопленного износа ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.НачислятьИзнос
	|	И (Строки.АмортизацияРегл <> 0
	|		ИЛИ Строки.АмортизацияЦФ <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеИзносаВходящиеДвижения // Дт 010 :: Кт
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение амортизации ОС исходящие проводки (Дт 010 :: Кт)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияРегл + Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИзносОсновныхСредств) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Перемещение накопленного износа ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.НачислятьИзнос
	|	И (Строки.АмортизацияРегл <> 0
	|		ИЛИ Строки.АмортизацияЦФ <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеРезерваДооценки // (Дт 83.01 :: Кт 83.01)
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение резерва дооценки (Дт 83.01 :: Кт 83.01)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.РезервПереоценкиСтоимостиРегл > 0
	|			ТОГДА Строки.РезервПереоценкиСтоимостиРегл - ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизацииРегл, 0)
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.РезервПереоценкиСтоимости > 0
	|			ТОГДА Строки.РезервПереоценкиСтоимости - ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизации, 0)
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаКт,
	|	Строки.КорГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Выделение резерва дооценки"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО ДокументыКОтражению.Ссылка = Строки.Регистратор
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|	ПО АмортизацияОС.Регистратор = Строки.Регистратор
	|		И АмортизацияОС.ОсновноеСредство = Строки.ОсновноеСредство
	|		И АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (АмортизацияОС.РезервПереоценкиАмортизацииРегл > 0
	|			ИЛИ АмортизацияОС.РезервПереоценкиАмортизации > 0)
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Строки.РезервПереоценкиСтоимостиРегл > 0
	|		ИЛИ Строки.РезервПереоценкиСтоимости > 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеДоходыОтЦелевогоФинансирования // Дт 98 :: Кт 98
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выделение доходов от целевого финансирования (Дт 98 :: Кт 98)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ - ЕСТЬNULL(СтрокиАмортизация.АмортизацияЦФ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ - ЕСТЬNULL(СтрокиАмортизация.АмортизацияЦФ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетКт,
	|	Операция.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ - ЕСТЬNULL(СтрокиАмортизация.АмортизацияЦФ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Выделение доходов от целевого финансирования"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО ДокументыКОтражению.Ссылка = Строки.Регистратор
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиАмортизация
	|	ПО СтрокиАмортизация.Регистратор = Строки.Регистратор
	|		И СтрокиАмортизация.ОсновноеСредство = Строки.ОсновноеСредство
	|		И СтрокиАмортизация.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|ГДЕ
	|	(Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область Прочее

Процедура ДополнитьВспомогательныеРеквизиты(Объект, ПараметрыВыбораОсновныхСредств, ВспомогательныеРеквизиты) Экспорт

	//++ Локализация
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());
	
	ВспомогательныеРеквизиты.Вставить("ЕстьСвязанныеОрганизации", Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация));
	ВспомогательныеРеквизиты.Вставить("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, ДатаДок));
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяФСБУ6", НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, Объект.Дата));
	
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Объект.Организация, ДатаДок);
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяУСНДоходыМинусРасходы", ПрименяетсяУСНДоходыМинусРасходы);
	
	ГруппаОС = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.ОсновноеСредство) Тогда
		ГруппаОС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОсновноеСредство, "ГруппаОС");
	КонецЕсли;
	
	Если НЕ Объект.ОбъединениеОднотипныхОС Тогда
	
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = 
			ВнеоборотныеАктивыЛокализация.ТребуетсяНастройкаОтраженияРасходовПоНалогам(
					Объект.Организация, 
					ДатаДок, 
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ОсновноеСредство), 
					ГруппаОС, 
					Объект.АмортизационнаяГруппа);
	Иначе
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = Ложь;
	КонецЕсли;
	
	ВспомогательныеРеквизиты.Вставить("ТребуетсяНастройкаОтраженияРасходовПоНалогам", ТребуетсяНастройкаОтраженияРасходовПоНалогам);
	ВспомогательныеРеквизиты.Вставить("ГруппаОС", ГруппаОС);

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
			Объект.Организация,
			Объект.Дата);
	
	ВспомогательныеРеквизиты.Вставить("РаздельныйУчетВНАПоНалогообложениюНДС", 
		ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.РаздельныйУчетВНАПоНалогообложениюНДС);
		
	//-- Локализация
	
КонецПроцедуры

// Дополняет параметры выбора статей и аналитик.
// 
// Параметры:
// 	ПараметрыВыбораСтатьиИАналитики - Массив из см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики -
Процедура ДополнитьПараметрыВыбораСтатейИАналитик(ПараметрыВыбораСтатьиИАналитики) Экспорт

	//++ Локализация
	
	// СтатьяРасходовНалог
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовНалог";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовНалог";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовНалог");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовНалог");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПорядокУчетаОСБУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ПринятиеКУчетуНеОднотипные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство        КАК ОсновноеСредство,
	|
	|	ДанныеРегистра.СостояниеБУ              КАК СостояниеБУ,
	|	ДанныеРегистра.СостояниеНУ              КАК СостояниеНУ,
	|
	|	ДанныеДокумента.НачислятьАмортизациюБУ  КАК НачислятьАмортизациюБУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ  КАК НачислятьАмортизациюНУ,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СостояниеНУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СостояниеНУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходовНУ,
	|	
	|	НЕОПРЕДЕЛЕНО                                           КАК СпособОтраженияРасходовБУ,
	|	НЕОПРЕДЕЛЕНО                                           КАК СпособОтраженияРасходовНУ,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов,
	|	ДанныеДокумента.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	ДанныеДокумента.НалогообложениеНДС                     КАК НалогообложениеНДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПринятиеКУчетуОднотипные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                КАК Регистратор,
	|	ДанныеДокумента.Период                                КАК Период,
	|	ДанныеДокумента.Организация                           КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеРегистра.СостояниеБУ                            КАК СостояниеБУ,
	|	ДанныеРегистра.СостояниеНУ                            КАК СостояниеНУ,
	|	ДанныеРегистра.НачислятьАмортизациюБУ                 КАК НачислятьАмортизациюБУ,
	|	ДанныеРегистра.НачислятьАмортизациюНУ                 КАК НачислятьАмортизациюНУ,
	|	ДанныеРегистра.СтатьяРасходовБУ                       КАК СтатьяРасходовБУ,
	|	ДанныеРегистра.АналитикаРасходовБУ                    КАК АналитикаРасходовБУ,
	|	ДанныеРегистра.СтатьяРасходовНУ                       КАК СтатьяРасходовНУ,
	|	ДанныеРегистра.АналитикаРасходовНУ                    КАК АналитикаРасходовНУ,
	|	ДанныеРегистра.СпособОтраженияРасходовБУ              КАК СпособОтраженияРасходовБУ,
	|	ДанныеРегистра.СпособОтраженияРасходовНУ              КАК СпособОтраженияРасходовНУ,
	|	ДанныеРегистра.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеРегистра.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов,
	|	ДанныеРегистра.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	ДанныеРегистра.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	ДанныеРегистра.НалогообложениеНДС                     КАК НалогообложениеНДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|ГДЕ
	|	ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СнятиеСУчета
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                КАК Регистратор,
	|	ДанныеДокумента.Период                                КАК Период,
	|	ДанныеДокумента.Организация                           КАК Организация,
	|	ДанныеРегистра.ОсновноеСредство                       КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)        КАК СостояниеБУ,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)        КАК СостояниеНУ,
	|	ЛОЖЬ                                                  КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ                                                  КАК НачислятьАмортизациюНУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК СтатьяРасходовБУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК АналитикаРасходовБУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК СтатьяРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК АналитикаРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК СпособОтраженияРасходовБУ,
	|	НЕОПРЕДЕЛЕНО                                          КАК СпособОтраженияРасходовНУ,
	|	ЛОЖЬ                                                  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	НЕОПРЕДЕЛЕНО                                          КАК ОрганизацияПолучательРасходов,
	|	ДанныеРегистра.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	ДанныеРегистра.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	ДанныеРегистра.НалогообложениеНДС                     КАК НалогообложениеНДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство <> ДанныеДокумента.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ() Экспорт
	
	ТекстЗапроса = Неопределено;

	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	ДанныеДокумента.Период                         КАК ДатаПоследнегоИзменения,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство               КАК ОсновноеСредство,
	|	ДанныеДокумента.СрокИспользованияБУ            КАК СрокПолезногоИспользованияБУ,
	|	ДанныеДокумента.СрокИспользованияНУ            КАК СрокПолезногоИспользованияНУ,
	|	ДанныеДокумента.ГодоваяНормаАмортизацииБУ      КАК КоэффициентАмортизацииБУ,
	|	ДанныеДокумента.КоэффициентУскоренияБУ         КАК КоэффициентУскорения,
	|	ДанныеДокумента.ГрафикАмортизации              КАК ГрафикАмортизации,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ       КАК СпециальныйКоэффициент,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьРегл    КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ОбъемНаработкиОстаточный       КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ДанныеДокумента.СрокИспользованияОстаточныйБУ  КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьПР, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПР, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПРЦФ, 0) КАК ПРДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
	|		ПО АмортизацияВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                        КАК Регистратор,
	|	ДанныеДокумента.Период                        КАК Период,
	|	ДанныеДокумента.Период                        КАК ДатаПоследнегоИзменения,
	|	ДанныеДокумента.Организация                   КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство              КАК ОсновноеСредство,
	|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
	|	ДанныеРегистра.СрокПолезногоИспользованияНУ   КАК СрокПолезногоИспользованияНУ,
	|	ДанныеРегистра.КоэффициентАмортизацииБУ       КАК КоэффициентАмортизацииБУ,
	|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
	|	ДанныеРегистра.ГрафикАмортизации              КАК ГрафикАмортизации,
	|	ДанныеРегистра.СпециальныйКоэффициент         КАК СпециальныйКоэффициент,
	|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ОбъемНаработкиОстаточный      КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ДанныеДокумента.СрокИспользованияОстаточныйБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьПР, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПР, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПРЦФ, 0) КАК ПРДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСБУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
	|		ПО АмортизацияВНА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                        КАК Регистратор,
	|	ДанныеДокумента.Период                        КАК Период,
	|	ДанныеДокумента.Период                        КАК ДатаПоследнегоИзменения,
	|	ДанныеДокумента.Организация                   КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство              КАК ОсновноеСредство,
	|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
	|	ДанныеРегистра.СрокПолезногоИспользованияНУ   КАК СрокПолезногоИспользованияНУ,
	|	ДанныеРегистра.КоэффициентАмортизацииБУ       КАК КоэффициентАмортизацииБУ,
	|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
	|	ДанныеРегистра.ГрафикАмортизации              КАК ГрафикАмортизации,
	|	ДанныеРегистра.СпециальныйКоэффициент         КАК СпециальныйКоэффициент,
	|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
	|
	|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ДанныеРегистра.ДатаПоследнегоИзменения, ДанныеДокумента.Период), ДанныеДокумента.Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьПР, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПР, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияПРЦФ, 0)
	|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьПР, 0) 
	|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияПР, 0)
	|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияПРЦФ, 0) КАК ПРДляВычисленияАмортизации,
	|
	// Для расчета амортизации используется остаточная стоимость
	|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
	|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0)
	|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьРегл, 0) 
	|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСБУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ДанныеДокумента.Ссылка
	|			И СтоимостьВНА.ОбъектУчета = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияВНА
	|		ПО АмортизацияВНА.Ссылка = ДанныеДокумента.Ссылка
	|			И АмортизацияВНА.ОбъектУчета = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьИсходныхОС
	|		ПО СтоимостьИсходныхОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияИсходныхОС
	|		ПО АмортизацияИсходныхОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.Ссылка = ДанныеДокумента.Ссылка
	|			И ПорядокУчетаОС.ОсновноеСредство = ДанныеДокумента.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО НаработкиОбъектовЭксплуатации.Ссылка = ПорядокУчетаОС.Ссылка
	|			И НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации = ПорядокУчетаОС.ОсновноеСредство
	|			И НаработкиОбъектовЭксплуатации.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
	|";
	
	//-- Локализация	
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ Локализация

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство  КАК ОсновноеСредство,
	|	&НазваниеДокумента                КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер             КАК НомерДокумента,
	|	ДанныеДокумента.СобытиеОС         КАК Событие,
	|
	|	СУММА(ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)) КАК СуммаЗатратБУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.Ссылка = ТаблицаОС.Ссылка
	|			И ВТ_СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ОсновноеСредство,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.СобытиеОС
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(ВТ_СтоимостьВНА.СтоимостьРегл + ВТ_СтоимостьВНА.СтоимостьЦФ) ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка      КАК Регистратор,
	|	ДанныеДокумента.Период      КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство  КАК ОсновноеСредство,
	|	&НазваниеДокумента          КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер       КАК НомерДокумента,
	|	ДанныеДокумента.СобытиеОС   КАК Событие,
	|	0                           КАК СуммаЗатратБУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство          КАК ОсновноеСредство,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) КАК ВидНалога
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|	И ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС <> ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство          КАК ОсновноеСредство,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог) КАК ВидНалога
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|	И ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС В (
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МашиныИОборудование))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство          КАК ОсновноеСредство,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог) КАК ВидНалога
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	НЕ ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|	И ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Объединение однотипных ОС 
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство          КАК ОсновноеСредство,
	|	ДанныеРегистра.СтатьяРасходов             КАК СтатьяРасходов,
	|	ДанныеРегистра.АналитикаРасходов          КАК АналитикаРасходов,
	|	ДанныеРегистра.ВидНалога                  КАК ВидНалога
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОтраженияРасходовПоИмущественнымНалогам КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеРегистра.ОсновноеСредство = ДанныеДокумента.ПервоеОС
	|
	|ГДЕ
	|	ДанныеДокумента.ОбъединениеОднотипныхОС
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|	И (ДанныеРегистра.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) 
	|			И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС <> ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|		ИЛИ ДанныеРегистра.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)
	|			И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС В (
	|							ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства),
	|							ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МашиныИОборудование))
	|		ИЛИ ДанныеРегистра.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)
	|			И ВЫРАЗИТЬ(ДанныеДокумента.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ОсновноеСредство  КАК ОсновноеСредство,
	|	ИСТИНА                            КАК ПрименениеЦелевогоФинансирования,
	|	ДанныеДокумента.СтатьяДоходов     КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов  КАК АналитикаДоходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрослеживаемыеТоварыВСоставеОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрослеживаемыеТоварыВСоставеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка				            КАК Регистратор,
	|	ПрослеживаемыеТовары.НомерСтроки				КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)			КАК ВидДвижения,
	|	ДанныеДокумента.Период							КАК Период,
	|	ДанныеДокумента.Организация						КАК Организация,
	|	ПрослеживаемыеТовары.НомерГТД					КАК РНПТ,
	|	ПрослеживаемыеТовары.ОсновноеСредство			КАК ОсновноеСредство,
	|	ПрослеживаемыеТовары.КодТНВЭД					КАК КодТНВЭД,
	|	ПрослеживаемыеТовары.КоличествоПоРНПТ			КАК КоличествоПоРНПТ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|		ПО ПрослеживаемыеТовары.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ПрослеживаемыеТовары.НомерГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка				            КАК Регистратор,
	|	ПрослеживаемыеТовары.НомерСтроки				КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			КАК ВидДвижения,
	|	ДанныеДокумента.Период							КАК Период,
	|	ДанныеДокумента.Организация						КАК Организация,
	|	ПрослеживаемыеТовары.НомерГТД					КАК РНПТ,
	|	ДанныеДокумента.ОсновноеСредство				КАК ОсновноеСредство,
	|	ПрослеживаемыеТовары.КодТНВЭД					КАК КодТНВЭД,
	|	ПрослеживаемыеТовары.КоличествоПоРНПТ			КАК КоличествоПоРНПТ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|		ПО ПрослеживаемыеТовары.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ПрослеживаемыеТовары.НомерГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- Локализация

#КонецОбласти

#КонецОбласти
