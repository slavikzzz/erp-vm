#Область СлужебныеПроцедурыИФункции

Функция УдержанияПоРабочимМестам(РеквизитыДляПроведения) Экспорт 
	
	ФизическоеЛицо 	= РеквизитыДляПроведения.ФизическоеЛицо;
	Организация 	= РеквизитыДляПроведения.Организация;
	Период 			= РеквизитыДляПроведения.Месяц;
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	НакопленныеКорректировки = ВзаиморасчетыССотрудниками.КорректировкиВыплаты(Организация, Период, ФизическиеЛица, РеквизитыДляПроведения.Ссылка);
	НакопленныеКорректировки.Колонки.СуммаКорректировки.Имя = "Сумма";
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаНДФЛ.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидУдержания", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	ТаблицаНДФЛ.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидДоходаИсполнительногоПроизводства", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
	ТаблицаНДФЛ.Колонки.Добавить("БазаПоУмолчанию", Новый ОписаниеТипов("Булево"));
	
	ИменаРесурсовНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Налог");
	ВидыУдержанийПоКолонкамДанных = Перечисления.ВидыОсобыхНачисленийИУдержаний.СоответствиеКолонкамДанных("Налог");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВозвратНДФЛ.ИдентификаторСтрокиНДФЛ КАК ИдентификаторСтрокиНДФЛ,
	|	0 КАК Сумма,
	|	ВозвратНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВозвратНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента
	|ИЗ
	|	Документ.ВозвратНДФЛ.СуммыВозврата КАК ВозвратНДФЛ
	|ГДЕ
	|	ВозвратНДФЛ.Ссылка = &Ссылка";
	ТекстНалогов = "";
	Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
		ТекстНалогов = ТекстНалогов + "ВозвратНДФЛ." + ИмяРесурса + " КАК " + ИмяРесурса + "," + Символы.ПС;
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "0 КАК Сумма,", ТекстНалогов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	РегистрацииВНалоговомОргане = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		
		Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
			Если Выборка[ИмяРесурса] > 0 Тогда
				НоваяСтрока = ТаблицаНДФЛ.Добавить();
				НоваяСтрока.ИдентификаторСтроки = Выборка.ИдентификаторСтрокиНДФЛ;
				НоваяСтрока.ФизическоеЛицо 		= ФизическоеЛицо;
				НоваяСтрока.Сумма 				= Выборка[ИмяРесурса];
				НоваяСтрока.ВидУдержания 		= ВидыУдержанийПоКолонкамДанных[ИмяРесурса];
				НоваяСтрока.БазаПоУмолчанию 	= (Выборка.СтавкаНалогообложенияРезидента <> Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
				НоваяСтрока.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
			КонецЕсли;
		КонецЦикла;
		
		РегистрацииВНалоговомОргане.Вставить(Выборка.ИдентификаторСтрокиНДФЛ, Выборка.РегистрацияВНалоговомОргане);
	
	КонецЦикла;
	
	РезультатыРаспределения = ОтражениеЗарплатыВУчете.ВозвратНДФЛПоРабочимМестамИСтатьям(ТаблицаНДФЛ, НакопленныеКорректировки, Организация, Период);
	
	УдержанияПоРабочимМестам = УчетНачисленнойЗарплаты.ТаблицаРаспределенияПоРабочимМестам();
	
	Для каждого СтрокаТЗ Из РезультатыРаспределения Цикл
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыДляПроведения);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.НачислениеУдержание = СтрокаТЗ.ВидУдержания;
		НоваяСтрока.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане[СтрокаТЗ.ИдентификаторСтроки];
		НоваяСтрока.Сумма = СтрокаТЗ.Результат;
	КонецЦикла;
	
	Возврат УдержанияПоРабочимМестам;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Документ, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти