
#Область ПрограммныйИнтерфейс

#Область ПодключаемыеКоманды
// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	//++ Локализация
	
	//++ НЕ УТ
	КомандаОтчет = Отчеты.ПаспортКонтракта.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет, "ПлатежиПо275ФЗ", Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли; 
	
	ТипыДоговоровГОЗ = Новый СписокЗначений;
	ТипыДоговоровГОЗ.Добавить(Перечисления.ТипыДоговоров.СПокупателем);
	//++ НЕ УТКА
	ТипыДоговоровГОЗ.Добавить(Перечисления.ТипыДоговоров.СДавальцем2_5);
	//-- НЕ УТКА
	
	КомандаОтчет = Отчеты.СведенияОКооперации.ДобавитьКомандуСведенияОКооперацииПриложение1(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет, "ПлатежиПо275ФЗ", Истина, ВидСравненияКомпоновкиДанных.Равно);
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет, "ТипДоговора", ТипыДоговоровГОЗ, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	КомандаОтчет = Отчеты.СведенияОКооперации.ДобавитьКомандуСведенияОКооперацииПриложения2и3(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет, "ПлатежиПо275ФЗ", Истина, ВидСравненияКомпоновкиДанных.Равно);
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет, "ТипДоговора", ТипыДоговоровГОЗ, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	//-- НЕ УТ

	//-- Локализация
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

// Обработчик события ПриСозданииНаСервере форм элемента справочника ДоговорыКонтрагентов.
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//  Отказ                - Булево - признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ПриСозданииНаСервереФормаЭлемента(Форма, Отказ, СтандартнаяОбработка) Экспорт
	НастройкиСистемыЛокализация.УстановитьВидимостьЭлементовЛокализации(Форма);
	//++ Локализация
	Объект = Форма.Объект;
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ПараметрыПроцедуры = Новый Структура("ВариантОтображения", "Однострочный");
	СПАРКРиски.ПриСозданииНаСервере(
		Форма,
		Неопределено,
		Объект.Контрагент,
		Перечисления.ЮрФизЛицо.ВидКонтрагентаСПАРКРиски(Объект.Контрагент),
		ПараметрыПроцедуры);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	//-- Локализация
КонецПроцедуры

// Обработчик события ОбработкаПроверкиЗаполненияНаСервере
// 
// Параметры:
// 	Отказ - Булево - признак отказа.
// 	ПроверяемыеРеквизиты - Массив - массив путей к проверяемым реквизитам.
// 	Форма - ФормаКлиентскогоПриложения - форма обработчика.
//
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты, Форма) Экспорт
	//++ Локализация
	//-- Локализация
	Возврат
КонецПроцедуры

// Вызывается при создании/чтении формы на сервере
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма обработчика
//
Процедура ПриЧтенииСозданииНаСервере(Форма) Экспорт
	//++ Локализация

	//++ НЕ УТ
	Форма.ПоддержкаПлатежей275ФЗ = ПолучитьФункциональнуюОпцию("ПоддержкаБанковскогоИКазначейскогоСопровожденияГосконтрактов");
	//-- НЕ УТ
	Форма.ПоддержкаЭлектронногоАктированияВЕИС = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронноеАктированиеВЕИС");
	Форма.Элементы.ЛокализацияГруппаНастроитьГОЗ.Видимость = Форма.ПоддержкаПлатежей275ФЗ ИЛИ Форма.ПоддержкаЭлектронногоАктированияВЕИС;
	
	ДоговорыКонтрагентовЛокализацияКлиентСервер.УправлениеНастройкойГОЗ(Форма);
	
	//++ НЕ УТ
	Если Форма.Объект.ДоговорыСЗаказчиками.Количество() Тогда
		Форма.КонтрактСЗаказчиком = Форма.Объект.ДоговорыСЗаказчиками[0].ДоговорСЗаказчиком;
	КонецЕсли;
	
	Форма.ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(Форма, Форма.Объект);
	Форма.Элементы.ЛокализацияГруппаЗаполнениеРаздела7.Видимость = Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем;
	Форма.АдресПодтверждающихДокументовВоВременномХранилище = ПоместитьВоВременноеХранилище(
		Форма.Объект.ПодтверждающиеДокументы.Выгрузить(),
		Форма.УникальныйИдентификатор);
	//-- НЕ УТ
	
	ИспользоватьЭДО = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	ИспользоватьМаркировку = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ") или ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП");
	
	Если (Не ИспользоватьЭДО) Или (Не ИспользоватьМаркировку) Или Форма.Объект.ДоговорССамозанятым Тогда
		Форма.Элементы.ЛокализацияГруппаВариантВыбытияМаркируемойПродукции.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма);
	
	Форма.ИспользоватьВыплатыСамозанятым = ПолучитьФункциональнуюОпцию("ИспользоватьВыплатыСамозанятым");
	
	УстановитьВидимостьФлагаДоговорССамозанятым(Форма);
	
	Если Форма.Объект.ДоговорССамозанятым Тогда
		Форма.Элементы.ГруппаНалогообложениеНДС.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма);
	
	//++ НЕ УТ
	УстановитьВидимостьВидаДоходаИностранногоКонтрагента(Форма);
	//-- НЕ УТ
	
	//-- Локализация
КонецПроцедуры

// Обработчик события ПослеЗаписиНаСервере формы элемента справочника ДоговорыКонтрагентов
//
// Параметры:
//  ТекущийОбъект   - СправочникОбъект - объект, который будет прочитан.
//  ПараметрыЗаписи - Структура - структура, содержащая параметры записи.
//  Форма           - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, Форма) Экспорт
	//++ Локализация
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРиски.ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	//-- Локализация
КонецПроцедуры

// Обработчик события ПередЗаписьюНаСервере формы элемента справочника ДоговорыКонтрагентов
//
// Параметры:
//  Отказ           - Булево - признак отказа.
//  ТекущийОбъект   - СправочникОбъект - объект, который будет прочитан.
//  ПараметрыЗаписи - Структура - структура, содержащая параметры записи.
//  Форма           - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, Форма) Экспорт
	//++ Локализация
	
	//++ НЕ УТ
	Если ТекущийОбъект.ПлатежиПо275ФЗ Тогда
		Если (Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком
				//++ Устарело_Переработка24
				Или Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком
				//-- Устарело_Переработка24
				Или Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5
				Или Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5_ЕАЭС)
			И Форма.ВариантПлатежаГОЗ = 1 Тогда
			ТекущийОбъект.ДоговорыСЗаказчиками.Очистить();
			НоваяСтрока = ТекущийОбъект.ДоговорыСЗаказчиками.Добавить();
			НоваяСтрока.ДоговорСЗаказчиком = Форма.КонтрактСЗаказчиком;
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	
	
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ПриОкончанииИзмененияРеквизита(ИмяЭлемента, Форма, ПараметрыОбработки) Экспорт
	Если ТипЗнч(ИмяЭлемента) = Тип("Массив") Тогда
		Для каждого ТекЭлемент Из ИмяЭлемента Цикл
			ПриОкончанииИзмененияРеквизита(ТекЭлемент, Форма, ПараметрыОбработки);
		КонецЦикла;
	КонецЕсли;
	//++ Локализация

	//++ НЕ УТ
	Если ИмяЭлемента = "БанковскийСчет" Тогда
		БанковскийСчетПриИзменении(Форма);
	КонецЕсли;
	Если ИмяЭлемента = "ГосударственныйКонтракт" Тогда
		ГосударственныйКонтрактПриИзменении(Форма);
	КонецЕсли;
	Если ИмяЭлемента = "Организация" Тогда
		Если ЗначениеЗаполнено(Форма.КонтрактСЗаказчиком) Тогда
			ОрганизацияДоговораСЗаказчиком = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.КонтрактСЗаказчиком, "Организация");
			Если Форма.Объект.Организация <> ОрганизацияДоговораСЗаказчиком Тогда
				Форма.КонтрактСЗаказчиком = Неопределено;
				Форма.Объект.ДоговорыСЗаказчиками.Очистить();
			КонецЕсли;
		КонецЕсли;
		УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма);
		УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма);
	КонецЕсли;
	//-- НЕ УТ
	Если ИмяЭлемента = "ТипДоговора" Тогда
		ТипДоговораПриИзменении(Форма);
	КонецЕсли;
	Если ИмяЭлемента = "НалогообложениеНДС" Тогда
		НалогообложениеНДСПриИзменении(Форма);
	КонецЕсли;
	Если ИмяЭлемента = "ИспользоватьОформлениеДокументовРаздельнойЗакупки" Тогда
		УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма);
	КонецЕсли;
	Если ИмяЭлемента = "ВариантОформленияРаздельнойЗакупки" Тогда
		УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма);
	КонецЕсли;
	
	Если ИмяЭлемента = "ДоговорССамозанятым" Тогда
		ДоговорССамозанятымПриИзменении(Форма);
	КонецЕсли;
	
	Если ИмяЭлемента = "Контрагент" Тогда
		ВыключитьПризнакДоговораССамозанятым(Форма);
		УстановитьВидимостьФлагаДоговорССамозанятым(Форма);
		УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма);
		//++ НЕ УТ
		УстановитьВидимостьВидаДоходаИностранногоКонтрагента(Форма);
		//-- НЕ УТ
	КонецЕсли;
	
	Если ИмяЭлемента = "Валюта" Тогда
		УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма);
	КонецЕсли; 
	
	//++ НЕ УТ
	Если ИмяЭлемента = "ВидДоходаИностранногоКонтрагента" Тогда
		УстановитьВидимостьПояснениеКВидуДохода(Форма);
	КонецЕсли; 
	//-- НЕ УТ 
	
	//-- Локализация
КонецПроцедуры

Процедура ВыполнитьКомандуЛокализации(Форма, ИмяКоманды, ПараметрыОбработки) Экспорт
	
	Если ТипЗнч(ИмяКоманды) = Тип("Массив") Тогда
		Для каждого ТекЭлемент Из ИмяКоманды Цикл
			ВыполнитьКомандуЛокализации(Форма, ТекЭлемент, ПараметрыОбработки);
		КонецЦикла;
	КонецЕсли;
	//++ Локализация
	
	//++ НЕ УТ
	Если ИмяКоманды = "ЗаполнитьДоговорПоДаннымФормыНастройкиГОЗ" Тогда
		ЗаполнитьДоговорПоДаннымФормыНастройкиГОЗ(ПараметрыОбработки, Форма)
	КонецЕсли;
	//-- НЕ УТ
	
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийМодуляОбъекта

// Обработчик события ОбработкаПроверкиЗаполнения объекта справочника ДоговорыКонтрагентов.
// См. описание платформенного метода ОбработкаПроверкиЗаполнения.
// 
// Параметры:
//   Отказ - Булево
//   ПроверяемыеРеквизиты - Массив из Строка
//   МассивНепроверяемыхРеквизитов - Массив из Строка
//   СправочникОбъект - СправочникОбъект.ДоговорыКонтрагентов
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов, СправочникОбъект) Экспорт
	//++ Локализация

	Перем Ошибки;
	
	//++ НЕ УТ
	ПроверяемыеРеквизиты.Добавить("ГосударственныйКонтракт");
	Если Не СправочникОбъект.ДоговорСУчастникомГОЗ Тогда
	//-- НЕ УТ
		МассивНепроверяемыхРеквизитов.Добавить("ГосударственныйКонтракт");
	//++ НЕ УТ
	КонецЕсли;
	//-- НЕ УТ
	
	//++ НЕ УТ
	Если Не СправочникОбъект.ПлатежиПо275ФЗ
		Или (СправочникОбъект.ТипДоговора <> Перечисления.ТипыДоговоров.СПоставщиком
			//++ Устарело_Переработка24
			И СправочникОбъект.ТипДоговора <> Перечисления.ТипыДоговоров.СПереработчиком
			//-- Устарело_Переработка24
			И СправочникОбъект.ТипДоговора <> Перечисления.ТипыДоговоров.СПереработчиком2_5
			И СправочникОбъект.ТипДоговора <> Перечисления.ТипыДоговоров.СПереработчиком2_5_ЕАЭС) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТипПлатежаФЗ275");
	КонецЕсли;
	
	Если СправочникОбъект.ПлатежиПо275ФЗ
		И (СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком
			//++ Устарело_Переработка24
			Или СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком
			//-- Устарело_Переработка24
			Или СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5
			Или СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5_ЕАЭС) Тогда
		
		ДенежныеСредстваСерверЛокализация.ОбработкаПроверкиЗаполненияПодтверждающиеДокументы(СправочникОбъект, Отказ, ПроверяемыеРеквизиты, Ошибки);
		ДенежныеСредстваСерверЛокализация.ПроверитьЗаполнениеРеквизитовПлатежаГОЗ(СправочникОбъект, Отказ);
		
		СведенияПоБанковскомуСчету = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникОбъект.БанковскийСчет,
										"ГосударственныйКонтракт, СчетПоГосконтракту, НаправлениеДеятельности.ГосударственныйКонтракт");
		
		Если СправочникОбъект.ДоговорСУчастникомГОЗ Тогда
			Если ЗначениеЗаполнено(СправочникОбъект.БанковскийСчет) И ЗначениеЗаполнено(СправочникОбъект.ГосударственныйКонтракт)
				И ((СправочникОбъект.ГосударственныйКонтракт <> СведенияПоБанковскомуСчету.ГосударственныйКонтракт
				И Не ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.ГосударственныйКонтракт, "ВнутреннийИдентификаторЕИС"))
				И Не СведенияПоБанковскомуСчету.СчетПоГосконтракту)
				ИЛИ (СведенияПоБанковскомуСчету.СчетПоГосконтракту
				И СправочникОбъект.ГосударственныйКонтракт <> СведенияПоБанковскомуСчету.НаправлениеДеятельностиГосударственныйКонтракт)) Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
						|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
					СправочникОбъект,
					"БанковскийСчет",,
					Отказ);
			КонецЕсли;
			Если ЗначениеЗаполнено(СправочникОбъект.БанковскийСчетКонтрагента) И ЗначениеЗаполнено(СправочникОбъект.ГосударственныйКонтракт)
				И СправочникОбъект.ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.БанковскийСчетКонтрагента, "ГосударственныйКонтракт")
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.ГосударственныйКонтракт, "ТипНаправленияДеятельности") <> Перечисления.ТипыНаправленийДеятельности.ГосударственныйКонтракт Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
						|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
					СправочникОбъект,
					"БанковскийСчетКонтрагента",,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли СправочникОбъект.ПлатежиПо275ФЗ
		И (СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем
			//++ НЕ УТКА

			//++ Устарело_Переработка24
			Или СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем
			//-- Устарело_Переработка24
			Или СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем2_5
			//-- НЕ УТКА
			Или Ложь) Тогда
		
		СведенияПоБанковскомуСчету = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникОбъект.БанковскийСчет,
										"ГосударственныйКонтракт, СчетПоГосконтракту, НаправлениеДеятельности.ГосударственныйКонтракт");
		
		Если ЗначениеЗаполнено(СправочникОбъект.БанковскийСчет) И ЗначениеЗаполнено(СправочникОбъект.ГосударственныйКонтракт)
			И ((СправочникОбъект.ГосударственныйКонтракт <> СведенияПоБанковскомуСчету.ГосударственныйКонтракт
				И Не ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.ГосударственныйКонтракт, "ВнутреннийИдентификаторЕИС"))
				И Не СведенияПоБанковскомуСчету.СчетПоГосконтракту)
				ИЛИ (СведенияПоБанковскомуСчету.СчетПоГосконтракту
				И СправочникОбъект.ГосударственныйКонтракт <> СведенияПоБанковскомуСчету.НаправлениеДеятельностиГосударственныйКонтракт)) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
					|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
				СправочникОбъект,
				"БанковскийСчет",,
				Отказ);
		КонецЕсли;
		Если ЗначениеЗаполнено(СправочникОбъект.БанковскийСчетКонтрагента) И ЗначениеЗаполнено(СправочникОбъект.ГосударственныйКонтракт)
			И СправочникОбъект.ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.БанковскийСчетКонтрагента, "ГосударственныйКонтракт")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.ГосударственныйКонтракт, "ТипНаправленияДеятельности") <> Перечисления.ТипыНаправленийДеятельности.ГосударственныйКонтракт Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
					|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
				СправочникОбъект,
				"БанковскийСчетКонтрагента",,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	
	КонтрагентЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.Контрагент, "ЮрФизЛицо");
	
	Если СправочникОбъект.УникальныйНомерВалютногоКонтроля <> ""
		И ИспользоватьУникальныйНомерВалютногоКонтроля(СправочникОбъект, КонтрагентЮрФизЛицо) Тогда
		ДенежныеСредстваКлиентСерверЛокализация.ПроверитьУникальныйНомерВалютногоКонтракта(
			СправочникОбъект, СправочникОбъект.УникальныйНомерВалютногоКонтроля, Отказ);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);

	//-- Локализация
КонецПроцедуры

//  см. описание платформенного метода ПередЗаписью
// Обработчик события ПередЗаписью объекта справочника ДоговорыКонтрагентов
//
// Параметры:
// Отказ - Булево
// СправочникОбъект - СправочникОбъект
//
Процедура ПередЗаписью(Отказ, СправочникОбъект) Экспорт
	//++ Локализация

	Если СправочникОбъект.ДоговорССамозанятым Тогда
		ЗаполнитьДоговорПоУмолчаниюСПризнакомДоговорССамозанятым(СправочникОбъект);
	КонецЕсли;
	
	//++ НЕ УТ
	Если Не СправочникОбъект.ПлатежиПо275ФЗ Тогда
		СправочникОбъект.ТипПлатежаФЗ275 = Неопределено;
		СправочникОбъект.ПодтверждающиеДокументы.Очистить();
	КонецЕсли;
	
	Если Не СправочникОбъект.ПлатежиПо275ФЗ Или Не СправочникОбъект.ДоговорСУчастникомГОЗ Тогда
		СправочникОбъект.ДоговорыСЗаказчиками.Очистить();
	КонецЕсли;
	//-- НЕ УТ
	
	Если ЗначениеЗаполнено(СправочникОбъект.Дата) И ЗначениеЗаполнено(СправочникОбъект.Номер) И Не СправочникОбъект.ПлатежиПо275ФЗ 
		И НЕ ЗначениеЗаполнено(СправочникОбъект.ИдентификаторПлатежа) Тогда
		СправочникОбъект.ИдентификаторПлатежа = ОбщегоНазначенияУТ.ПолучитьУникальныйИдентификаторПлатежа(СправочникОбъект);
	//++ НЕ УТ
	ИначеЕсли СправочникОбъект.ПлатежиПо275ФЗ И ЗначениеЗаполнено(СправочникОбъект.БанковскийСчет) Тогда
		СправочникОбъект.ИдентификаторПлатежа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.БанковскийСчет, "ГосударственныйКонтракт.НомерГОЗ");
	//-- НЕ УТ
	КонецЕсли;
	
	Если СправочникОбъект.УникальныйНомерВалютногоКонтроля <> "" Тогда
		
		КонтрагентЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникОбъект.Контрагент, "ЮрФизЛицо");
		
		Если Не ИспользоватьУникальныйНомерВалютногоКонтроля(СправочникОбъект, КонтрагентЮрФизЛицо) Тогда
			СправочникОбъект.УникальныйНомерВалютногоКонтроля = "";
		КонецЕсли;
		
	КонецЕсли;
	
	//-- Локализация	
КонецПроцедуры

// Обработчик события ПриКопировании объекта справочника ДоговорыКонтрагентов
//  см. описание платформенного метода ПриКопировании
//
// Параметры:
// ОбъектКопирования - СправочникОбъект
// СправочникОбъект - СправочникОбъект
Процедура ПриКопировании(ОбъектКопирования, СправочникОбъект) Экспорт
	//++ Локализация
	ИдентификаторПлатежа = Неопределено;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область УсловноеОформление

// Устанавливает условное оформление формы
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма обработчика
//
Процедура УстановитьУсловноеОформление(Форма) Экспорт
	УсловноеОформление = Форма.УсловноеОформление;
	Элементы =  Форма.Элементы;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормы_Служебные
//++ Локализация

//++ НЕ УТ

Процедура БанковскийСчетПриИзменении(Форма)
	
	Госконтракт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.БанковскийСчет, "ГосударственныйКонтракт");
	Если ЗначениеЗаполнено(Госконтракт) Тогда
		Форма.Объект.ПлатежиПо275ФЗ = Истина;
		Форма.Объект.ДоговорСУчастникомГОЗ = Истина;
		Форма.Объект.ГосударственныйКонтракт = Госконтракт;
		Форма.ВариантПлатежаГОЗ = 1;
		Форма.Объект.ТипПлатежаФЗ275 = Справочники.ТипыПлатежейФЗ275.СписаниеНаОтдельныйСчет;
		ПриОкончанииИзмененияРеквизита("ГосударственныйКонтракт", Форма, Неопределено);
		ДоговорыКонтрагентовЛокализацияКлиентСервер.УправлениеНастройкойГОЗ(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ГосударственныйКонтрактПриИзменении(Форма)
	
	Форма.КонтрактСЗаказчиком = ПолучитьКонтрактСЗаказчиком(Форма.Объект.ГосударственныйКонтракт, Форма.Объект.Организация);
	
КонецПроцедуры
//-- НЕ УТ

Процедура НалогообложениеНДСПриИзменении(Форма)
	
	Если Форма.Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС  Тогда 
		Форма.Объект.УчетАгентскогоНДС = Истина;
	Иначе
		Форма.Объект.УчетАгентскогоНДС = Ложь;
		Форма.Объект.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.ПустаяСсылка();
	КонецЕсли;	
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
//
Процедура ТипДоговораПриИзменении(Форма)
	Объект = Форма.Объект;
	
	ОперацииЗакупки = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииИмпорта = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	ОперацииВСтранахЕАЭС = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	
	РеквизитыПартнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Партнер, "Клиент, Поставщик");
	Если ЗначениеЗаполнено(Объект.Партнер)
		И (Не РеквизитыПартнера.Клиент
			И (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию))
		И (Не РеквизитыПартнера.Поставщик
			И (ОперацииЗакупки.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
				Или ОперацииИмпорта.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
				Или ОперацииВСтранахЕАЭС.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)) Тогда
		
		Объект.Партнер    = Справочники.Партнеры.ПустаяСсылка();
		Объект.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
	КонецЕсли;
	
	Если Не (Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем
		//++ НЕ УТ

		//++ Устарело_Переработка24
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком
		//-- Устарело_Переработка24
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком2_5_ЕАЭС
		//++ НЕ УТКА

		//++ Устарело_Переработка24
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем
		//-- Устарело_Переработка24
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем2_5
		//-- НЕ УТКА

		//-- НЕ УТ
		Или Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком) Тогда
		Объект.ПлатежиПо275ФЗ = Ложь;
		Объект.ДоговорСУчастникомГОЗ = Ложь;
	КонецЕсли;
	ДоговорыКонтрагентовЛокализацияКлиентСервер.УправлениеНастройкойГОЗ(Форма);
	//++ НЕ УТ
	Форма.Элементы.ЛокализацияГруппаЗаполнениеРаздела7.Видимость = (Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем);
	//-- НЕ УТ
	УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма);
	
	ВыключитьПризнакДоговораССамозанятым(Форма);
	УстановитьВидимостьФлагаДоговорССамозанятым(Форма);
	УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма);
	
КонецПроцедуры

Процедура УстановитьДоступностьПорядкаОтраженияНеотфактурованныхПоставокНУ(Форма)
	
	ВидимостьПорядкаОтраженияВНУ = Ложь;
	
	//++ НЕ УТ
	Объект = Форма.Объект;
		
	СистемаНалогообложения = Неопределено;
	ДействующиеПараметры = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиСистемыНалогообложения", Объект.Организация);
	Если ЗначениеЗаполнено(ДействующиеПараметры) Тогда
		СистемаНалогообложения = ДействующиеПараметры.СистемаНалогообложения;
	КонецЕсли;
	
	Если Форма.ИспользоватьОформлениеДокументовРаздельнойЗакупки
		И СистемаНалогообложения =  Перечисления.СистемыНалогообложения.Общая 
		И (Форма.Объект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставкиТоваров
			Или Форма.Объект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставкиТоваровИУслуг) Тогда
		ВидимостьПорядкаОтраженияВНУ = Истина;
	КонецЕсли;
	//-- НЕ УТ
	
	Элементы = Форма.Элементы;
	
	Элементы.ПорядокОтраженияНеотфактурованныхПоставокПриПоступлении.Видимость = ВидимостьПорядкаОтраженияВНУ;
	Элементы.ПорядокОтраженияНеотфактурованныхПоставокПриФактуровке.Видимость = ВидимостьПорядкаОтраженияВНУ;
		
КонецПроцедуры

Процедура ДоговорССамозанятымПриИзменении(Форма)

	Объект = Форма.Объект;
	
	Если Объект.ДоговорССамозанятым Тогда
	
		Форма.РежимНалогообложения = 0;
		ЗаполнитьДоговорПоУмолчаниюСПризнакомДоговорССамозанятым(Объект);
	
	КонецЕсли;
	
	ДоговорыКонтрагентовЛокализацияКлиентСервер.УправлениеНастройкойГОЗ(Форма);
	
	ИспользоватьЭДО = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	ИспользоватьМаркировку = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ")
								Или ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП");
	
	Форма.Элементы.ЛокализацияГруппаВариантВыбытияМаркируемойПродукции.Видимость = 
		ИспользоватьЭДО
		И ИспользоватьМаркировку
		И Не Объект.ДоговорССамозанятым;

КонецПроцедуры

Процедура ВыключитьПризнакДоговораССамозанятым(Форма)

	Если Не ДопустимоИспользоватьДоговорССамозанятым(Форма) Тогда
		
		Форма.Объект.ДоговорССамозанятым = Ложь;
		ДоговорССамозанятымПриИзменении(Форма);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДоговорПоУмолчаниюСПризнакомДоговорССамозанятым(Объект)

	Объект.НалогообложениеНДСОпределяетсяВДокументе = Ложь;
	Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
	
	УчетНДСУП.ЗаполнитьСтавкуНДСДляПлатежей(Объект.СтавкаНДС, Объект.НалогообложениеНДС, Объект.Организация, Объект.ДатаОкончанияДействия);
	
	Объект.УчетАгентскогоНДС = Ложь;
	Объект.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.ПустаяСсылка();
	
	Объект.ПлатежиПо275ФЗ = Ложь;
	Объект.ДоговорСУчастникомГОЗ = Ложь;
	Объект.ГосударственныйКонтракт = Справочники.ГосударственныеКонтракты.ПустаяСсылка();
	
	Объект.ВариантВыбытияМаркируемойПродукции = Перечисления.ВариантыВыбытияМаркируемойПродукции.ПустаяСсылка();

КонецПроцедуры

// Отображает или скрывает флаг "Договор плательщика НПД" в карточке договора.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой изменяется отображение.
//
Процедура УстановитьВидимостьФлагаДоговорССамозанятым(Форма)

	Элементы = Форма.Элементы;
	Элементы.ДоговорССамозанятым.Видимость = ДопустимоИспользоватьДоговорССамозанятым(Форма);

КонецПроцедуры

Функция ДопустимоИспользоватьДоговорССамозанятым(Форма)

	ВидыСамозанятых = Новый Массив;
	ВидыСамозанятых.Добавить(Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель);
	ВидыСамозанятых.Добавить(Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Возврат ВидыСамозанятых.Найти(Форма.КонтрагентЮрФизЛицо) <> Неопределено
			И Форма.Объект.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком;

КонецФункции

Процедура УстановитьВидимостьИдентификатораВалютногоКонтракта(Форма)

	Элементы = Форма.Элементы;
	Элементы.УникальныйНомерВалютногоКонтроля.Видимость = ИспользоватьУникальныйНомерВалютногоКонтроля(Форма.Объект, Форма.КонтрагентЮрФизЛицо);
	
КонецПроцедуры

Функция ИспользоватьУникальныйНомерВалютногоКонтроля(Объект, КонтрагентЮрФизЛицо)

	ТипыДоговоров = Новый Массив;
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.СПоставщиком);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.Импорт);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.ВвозИзЕАЭС);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.СКомитентом);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.СКомиссионером);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.СПокупателем);
	ТипыДоговоров.Добавить(Перечисления.ТипыДоговоров.РеализацияЧерезКомиссионера);
	
	Возврат ТипыДоговоров.Найти(Объект.ТипДоговора) <> Неопределено
		И ДенежныеСредстваКлиентСерверЛокализация.ПрименяетсяВалютныйКонтроль(КонтрагентЮрФизЛицо);

КонецФункции

//++ НЕ УТ	
Процедура УстановитьВидимостьВидаДоходаИностранногоКонтрагента(Форма)

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "СтранаРегистрации");
	Элементы.ВидДоходаИностранногоКонтрагента.Видимость = ЗначениеЗаполнено(СтранаРегистрацииКонтрагента)
		И СтранаРегистрацииКонтрагента <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
		
	УстановитьВидимостьПояснениеКВидуДохода(Форма);
	
КонецПроцедуры

Процедура УстановитьВидимостьПояснениеКВидуДохода(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	ВидимостьПояснениеКВидуДохода = (Объект.ВидДоходаИностранногоКонтрагента = 
		ПредопределенноеЗначение("Перечисление.ВидыДоходовИностранныхОрганизаций.ДоходыУслугиВзаимозависимым")
		ИЛИ Объект.ВидДоходаИностранногоКонтрагента = 
		ПредопределенноеЗначение("Перечисление.ВидыДоходовИностранныхОрганизаций.ДоходПриравненныйДивидендам"))
		И ТекущаяДатаСеанса() < Дата(2025,4,1)
		И Элементы.ВидДоходаИностранногоКонтрагента.Видимость; 
		
	Элементы.ПояснениеКВидуДохода.Видимость = ВидимостьПояснениеКВидуДохода;
	
КонецПроцедуры
//-- НЕ УТ

//-- Локализация
#КонецОбласти

#Область ПрочиеСлужебныеПроцедуры

//++ Локализация

Функция ПолучитьКонтрактСЗаказчиком(ГосударственныйКонтракт, Организация) Экспорт
	
	КонтрактСЗаказчиком = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ссылка КАК ДоговорСсылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов
	|ГДЕ
	|	ГосударственныйКонтракт = &Госконтракт
	|	И Организация = &Организация
	|	И ТипДоговора В(
	//++ НЕ УТКА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СДавальцем2_5),
	//-- НЕ УТКА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем))
	|	И НЕ ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями
	|ГДЕ
	|	ГосударственныйКонтракт = &Госконтракт
	|	И НЕ ПометкаУдаления
	|";
	Запрос.УстановитьПараметр("Госконтракт", ГосударственныйКонтракт);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 1 Тогда
		КонтрактСЗаказчиком = Результат[0].ДоговорСсылка;
	КонецЕсли;
	
	Возврат КонтрактСЗаказчиком
КонецФункции

//++ НЕ УТ

Процедура ЗаполнитьДоговорПоДаннымФормыНастройкиГОЗ(Результат, Форма) Экспорт
	ЗаполнитьЗначенияСвойств(Форма, Результат);
	ЗаполнитьЗначенияСвойств(Форма.Объект, Результат);
	ПодтверждающиеДокументы = ПолучитьИзВременногоХранилища(Результат.АдресПодтверждающихДокументовВоВременномХранилище);
	ПодтверждающиеДокументыОбъекта = Форма.Объект.ПодтверждающиеДокументы;
	ПодтверждающиеДокументыОбъекта.Очистить();
	Для каждого ТекСтрока Из ПодтверждающиеДокументы Цикл
		НоваяСтрока = ПодтверждающиеДокументыОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	Форма.Модифицированность = Истина;
	ДоговорыКонтрагентовЛокализацияКлиентСервер.УправлениеНастройкойГОЗ(Форма);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(Форма, Форма.Объект)
КонецПроцедуры

//-- НЕ УТ

//-- Локализация
#КонецОбласти

#КонецОбласти
