	
#Область СлужебныйПрограммныйИнтерфейс

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам, Истина);
	Списки.Вставить(Метаданные.РегистрыНакопления.ОтработанноеВремяПоСотрудникам, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.ВзаиморасчетыССотрудниками.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("fd8ef2c7-fa24-480f-84f2-b5b0481e212c"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений взаиморасчетов с сотрудниками.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records of mutual settlements with employees.'");

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.ЗарплатаКВыплате.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("69aa0568-f501-49fd-8e4e-4ab0534baa8d"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений зарплаты к выплате.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records of salary for payment.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.ЗарплатаКВыплатеАвансом.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("74b1c9e3-6ab8-4092-b46c-2040a2da6919"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений зарплаты к выплате авансом.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records of salary for payment as advance.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.НачисленияУдержанияПоСотрудникам.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("06a949e3-f5cb-4065-b893-e96405219937"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений начислений и удержаний по сотрудникам.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in employee accruals and deductions.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.18.121";
	Обработчик.Процедура = "РегистрыНакопления.ВзаиморасчетыССотрудниками.УстановитьПериодВзаиморасчетов";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0831f1d4-e4b3-11eb-8339-e16ec9eb6fec"); 
	Обработчик.Комментарий = НСтр("ru = 'Установка периода взаиморасчетов во взаиморасчетах с сотрудниками.';
									|en = 'Set a payment period in payments to employees.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.18.121";
	Обработчик.Процедура = "РегистрыНакопления.БухгалтерскиеВзаиморасчетыССотрудниками.УстановитьПериодВзаиморасчетов";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("89cdad6d-b551-494b-8137-089fa7363b78"); 
	Обработчик.Комментарий = НСтр("ru = 'Установка периода взаиморасчетов в бухгалтерских взаиморасчетах с сотрудниками.';
									|en = 'Set a payment period in accounting payments to employees.'");
	
КонецПроцедуры

#КонецОбласти

// Для методов служебного API использование не контролируем          
// АПК:581-выкл 
// АПК:299-выкл

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, описание см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		Начисления - регистрируемые начисления.
//					Если передано Неопределено, то начисления не регистрируются.
//		ПрочиеДоходы - регистрируемые натуральные доходы.
//					Если передано Неопределено, то доходы не регистрируются.
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//					если Истина - наборы записей будут записаны после заполнения.
//
Процедура ЗарегистрироватьНачисления(ДанныеДляПроведения, Отказ, Начисления, ПрочиеДоходы, ЗаписыватьДвижения = Ложь) Экспорт
	
	Движения 				= ДанныеДляПроведения.Движения;
	Организация 			= ДанныеДляПроведения.Организация;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	ПланируемаяДатаВыплаты	= ДанныеДляПроведения.ПланируемаяДатаВыплаты;
	ПорядокВыплаты 			= ДанныеДляПроведения.ПорядокВыплаты;
	ВыплатитьКакАванс 		= ДанныеДляПроведения.ВыплатитьКакАванс;
	
	ДанныеМежрасчетногоПериода = ЭтоМежрасчетнаяВыплата(ПорядокВыплаты);
	
	ТаблицаНачислений 		= ТаблицаРаспределенияПоРабочимМестам();
	ТаблицаПрочихНачислений = ТаблицаРаспределенияПоРабочимМестам();
	СтрокиВзаиморасчетов    = Новый Массив;
	
	ВидыДоходаИсполнительногоПроизводства = ВидыДоходовИсполнительногоПроизводстваНачислений(ПланируемаяДатаВыплаты);
	
	Если Начисления <> Неопределено Тогда
		ПравилаУчетаНачислений = УчетНачисленнойЗарплатыПовтИсп.ПравилаУчетаНачисленийСотрудников();
		// Заполняем движения
		ДобавленныеСтрокиНачислений = Новый Массив;
		Для Каждого Строка Из Начисления Цикл
			ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			УчитыватьВоВзаиморасчетах = Истина;
			ПравилаУчетаНачисления = ПравилаУчетаНачислений[Строка.Начисление];
			Если ПравилаУчетаНачисления <> Неопределено Тогда
				ГруппаНачисленияУдержанияВыплаты = ПравилаУчетаНачисления.ГруппаНачисленияУдержанияВыплаты;
				УчитыватьВоВзаиморасчетах = ПравилаУчетаНачисления.УчитыватьВоВзаиморасчетах;
			КонецЕсли;
			НоваяСтрока = ТаблицаНачислений.Добавить();
			ДобавленныеСтрокиНачислений.Добавить(НоваяСтрока);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ГруппаНачисленияУдержанияВыплаты;
			НоваяСтрока.ДанныеМежрасчетногоПериода  = ДанныеМежрасчетногоПериода;
			НоваяСтрока.УчитыватьВРаспределенииНДФЛ = ВыплатитьКакАванс;
			Если УчитыватьВоВзаиморасчетах Тогда
				СтрокиВзаиморасчетов.Добавить(НоваяСтрока);
			КонецЕсли;
			// Заполняем даты начала/окончания.
			Если Не ЗначениеЗаполнено(НоваяСтрока.ДатаНачала) Тогда
				НоваяСтрока.ДатаНачала = ПериодРегистрации;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(НоваяСтрока.ДатаОкончания) Тогда
				НоваяСтрока.ДатаОкончания = КонецМесяца(ПериодРегистрации);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(НоваяСтрока.ПериодДействия) Тогда
				НоваяСтрока.ПериодДействия = НачалоМесяца(НоваяСтрока.ДатаНачала);
			КонецЕсли;
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
		КонецЦикла;
		УчетНачисленнойЗарплатыВнутренний.СкорректироватьДатыНачисленийБезПериодаДействия(ТаблицаНачислений, ПериодРегистрации);
		УчетНачисленнойЗарплатыВнутренний.ПриЗаполненииСтрокРегистрацииНачисленнойЗарплаты(ТаблицаНачислений, СтрокиВзаиморасчетов);
	КонецЕсли;
	
	// Перенесем начисления в коллекцию движений
	Если ТаблицаНачислений.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНачислений, Движения.НачисленияУдержанияПоСотрудникам);
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	КонецЕсли;
	
	Если ПрочиеДоходы <> Неопределено Тогда
		Для Каждого Строка Из ПрочиеДоходы Цикл
			НоваяСтрока = ТаблицаПрочихНачислений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Период = ПериодРегистрации;
			НоваяСтрока.Организация		= Организация;
			НоваяСтрока.НачислениеУдержание = Строка.Начисление;
			НоваяСтрока.ДанныеМежрасчетногоПериода  = ДанныеМежрасчетногоПериода;
			НоваяСтрока.УчитыватьВРаспределенииНДФЛ = ВыплатитьКакАванс;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
		КонецЦикла;
	КонецЕсли;
	
	// Перенесем удержания и прочие доходы в коллекцию движений
	Если ТаблицаПрочихНачислений.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПрочихНачислений, Движения.НачисленияУдержанияПоСотрудникам);
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
	КонецЕсли;
	
	Если ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс
		И ПроведениеСервер.ЕстьНаборЗаписей(Движения, "НачисленияУдержанияПоСотрудникамАвансом") Тогда
		
		Если ТаблицаНачислений.Количество() + ТаблицаПрочихНачислений.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНачислений, Движения.НачисленияУдержанияПоСотрудникамАвансом);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПрочихНачислений, Движения.НачисленияУдержанияПоСотрудникамАвансом);
			Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Взаиморасчеты
	Если ЗначениеЗаполнено(ПорядокВыплаты) Тогда
		НачисленияДляВзаиморасчетов	= ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
		Для Каждого Строка Из СтрокиВзаиморасчетов Цикл
			НоваяСтрока = НачисленияДляВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		Если ВыплатитьКакАванс Тогда
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, Перечисления.ХарактерВыплатыЗарплаты.Зарплата, 
				НачисленияДляВзаиморасчетов, Неопределено);
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
				Движения, Отказ, 
				Организация, ПериодРегистрации, 
				НачисленияДляВзаиморасчетов, Неопределено);
		Иначе 
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, ПорядокВыплаты,
				НачисленияДляВзаиморасчетов, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, описание см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		Начисления - таблица значений.
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
//
Процедура ЗарегистрироватьОтработанноеВремя(ДанныеДляПроведения, Отказ, Начисления, ЗаписыватьДвижения = Ложь) Экспорт
	
	Движения 			= ДанныеДляПроведения.Движения;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	
	ТаблицаОтработанноеВремя = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраНакопления("ОтработанноеВремяПоСотрудникам");;
	
	Для Каждого Строка Из Начисления Цикл
		
		НоваяСтрока = ТаблицаОтработанноеВремя.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.ПериодДействия = НачалоМесяца(Строка.ДатаНачала);
		
		НоваяСтрока.Период = ПериодРегистрации;
		НоваяСтрока.Организация = Организация;
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
		
	КонецЦикла;
	
	УчетНачисленнойЗарплатыВнутренний.СкорректироватьДатыНачисленийБезПериодаДействия(ТаблицаОтработанноеВремя, ПериодРегистрации, "Начисление");
		
	// Перенесем отработанное время в коллекцию движений
	Если ТаблицаОтработанноеВремя.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтработанноеВремя, Движения.ОтработанноеВремяПоСотрудникам);
		Движения.ОтработанноеВремяПоСотрудникам.Записывать = Истина;
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.ОтработанноеВремяПоСотрудникам.Записать();
		Движения.ОтработанноеВремяПоСотрудникам.Записывать = Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		ЕстьДвиженияОтработанноеВремяПоСотрудникамАвансом = Движения.Свойство("ОтработанноеВремяПоСотрудникамАвансом");
	Иначе
		ЕстьДвиженияОтработанноеВремяПоСотрудникамАвансом = Движения.Найти("ОтработанноеВремяПоСотрудникамАвансом") <> Неопределено;
	КонецЕсли;
	
	Если ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс
		И ЕстьДвиженияОтработанноеВремяПоСотрудникамАвансом Тогда
		
		Если ТаблицаОтработанноеВремя.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтработанноеВремя, Движения.ОтработанноеВремяПоСотрудникамАвансом);
			Движения.ОтработанноеВремяПоСотрудникамАвансом.Записывать = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	УчетНачисленнойЗарплатыВнутренний.ПриРегистрацииОтработанногоВремени(Движения, ЗаписыватьДвижения);
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		Удержания
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
Процедура ЗарегистрироватьУдержания(ДанныеДляПроведения, Отказ, Удержания, ЗаписыватьДвижения = Ложь) Экспорт

	Если Удержания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	ВыплатитьКакАванс 	= ДанныеДляПроведения.ВыплатитьКакАванс;
	
	ДанныеМежрасчетногоПериода = ЭтоМежрасчетнаяВыплата(ПорядокВыплаты);
	
	УдержанияПоРабочимМестам = ТаблицаРаспределенияПоРабочимМестам();
	
	Для Каждого Строка Из Удержания Цикл
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Период				= ПериодРегистрации;
		НоваяСтрока.Организация			= Организация;
		НоваяСтрока.НачислениеУдержание = Строка.Удержание;
		НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
	КонецЦикла;

	// Заполняем движения
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, Движения.НачисленияУдержанияПоСотрудникам);
	Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
	КонецЕсли;

	Если ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс
		И ПроведениеСервер.ЕстьНаборЗаписей(Движения, "НачисленияУдержанияПоСотрудникамАвансом") Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, Движения.НачисленияУдержанияПоСотрудникамАвансом);
		Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Истина;
		
	КонецЕсли;

	// Взаиморасчеты
	Если ЗначениеЗаполнено(ПорядокВыплаты) Тогда
		УдержанияДляВзаиморасчетов	= ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
		Для Каждого Строка Из УдержанияПоРабочимМестам Цикл
			НоваяСтрока = УдержанияДляВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		Если ВыплатитьКакАванс Тогда
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, Перечисления.ХарактерВыплатыЗарплаты.Зарплата, 
				Неопределено, УдержанияДляВзаиморасчетов);
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
				Движения, Отказ, 
				Организация, ПериодРегистрации, 
				Неопределено, УдержанияДляВзаиморасчетов);
		Иначе 
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, ПорядокВыплаты,
				Неопределено, УдержанияДляВзаиморасчетов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		РезультатыРасчетаНДФЛ
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
Процедура ЗарегистрироватьНДФЛ(ДанныеДляПроведения, Отказ, РезультатыРасчетаНДФЛ, ЗаписыватьДвижения = Ложь) Экспорт
	
	Если РезультатыРасчетаНДФЛ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	Авансом 			= ДанныеДляПроведения.Авансом;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	ВыплатитьКакАванс 	= ДанныеДляПроведения.ВыплатитьКакАванс;
	
	ДанныеМежрасчетногоПериода = ЭтоМежрасчетнаяВыплата(ПорядокВыплаты);
		
	Если Авансом Тогда
		ДвиженияНачисленийУдержаний = Движения.НачисленияУдержанияПоСотрудникамАвансом;
	Иначе
		ДвиженияНачисленийУдержаний = Движения.НачисленияУдержанияПоСотрудникам;
	КонецЕсли;

	ЕстьПодразделениеСотрудника = (РезультатыРасчетаНДФЛ.Колонки.Найти("ПодразделениеСотрудника") <> Неопределено);
	НетТерритории = (РезультатыРасчетаНДФЛ.Колонки.Найти("ТерриторияВыполненияРаботВОрганизации") = Неопределено);
	
	УдержанияПоРабочимМестам = ТаблицаРаспределенияПоРабочимМестам();
	Для Каждого Строка Из РезультатыРасчетаНДФЛ Цикл
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Период				= ПериодРегистрации;
		НоваяСтрока.ПериодДействия		= ПериодРегистрации;
		Если Не Авансом Тогда
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			НоваяСтрока.ДатаПолученияДохода = Строка.МесяцНалоговогоПериода;
		КонецЕсли;	
		НоваяСтрока.Организация			= Организация;
		Если ЕстьПодразделениеСотрудника Тогда
			НоваяСтрока.Подразделение = Строка.ПодразделениеСотрудника;
		КонецЕсли;
		Если НетТерритории Тогда
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации = Строка.Подразделение;
		КонецЕсли;
		НоваяСтрока.НачислениеУдержание 				= Строка.ВидУдержания;
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты 	= Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
	КонецЦикла;
	
	// Заполняем движения начислений (удержаний) удержанными суммами.
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, ДвиженияНачисленийУдержаний);
	Если ПорядокВыплаты <> Неопределено И Не Авансом
		И ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс
		И ПроведениеСервер.ЕстьНаборЗаписей(Движения, "НачисленияУдержанияПоСотрудникамАвансом") Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, Движения.НачисленияУдержанияПоСотрудникамАвансом);
		
	КонецЕсли;

	ДвиженияНачисленийУдержаний.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		ДвиженияНачисленийУдержаний.Записать();
		ДвиженияНачисленийУдержаний.Записывать = Ложь;
	КонецЕсли;
	
	// Взаиморасчеты
	Если ЗначениеЗаполнено(ПорядокВыплаты) Или Авансом Тогда
		УдержанияДляВзаиморасчетов = ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, УдержанияДляВзаиморасчетов);
		Если ВыплатитьКакАванс Тогда
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, Перечисления.ХарактерВыплатыЗарплаты.Зарплата, 
				Неопределено, УдержанияДляВзаиморасчетов);
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
				Движения, Отказ, 
				Организация, ПериодРегистрации, 
				Неопределено, УдержанияДляВзаиморасчетов);
		ИначеЕсли Авансом Тогда
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
				Движения, Отказ, 
				Организация, ПериодРегистрации, 
				Неопределено, УдержанияДляВзаиморасчетов);
		Иначе	
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, ПорядокВыплаты, 
				Неопределено, УдержанияДляВзаиморасчетов);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Регистрирует передачу налогов в налоговый орган.
//
// Параметры:
//		Движения          - КоллекцияДвижений - коллекция наборов записей движений расчетного документа.
//  	Отказ             - Булево            - признак отказа выполнения операции.
//		Организация       - СправочникСсылка.Организации - организация
//		ПериодРегистрации - Дата - первое число месяца периода регистрации.
//		НалоговыйПериод   - Дата - первое декабря года налогового периода.
//		НДФЛ              - ТаблицаЗначений   - таблица с колонками:
//			* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо.
//			* Сумма          - Число                           - сумма налога.	
//
Процедура ЗарегистрироватьНалогиПереданныеВНалоговыйОрган(Движения, Отказ, Организация, ПериодРегистрации, НалоговыйПериод, 
	НДФЛ, ЗаписыватьДвижения = Ложь) Экспорт

	Если НДФЛ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор = Движения.НачисленияУдержанияПоСотрудникам.Отбор.Регистратор.Значение;
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.СвернутьМассив(НДФЛ.ВыгрузитьКолонку("ФизическоеЛицо"));
	
	ПериодыРегистрации = Новый Массив;
	Период = ПериодРегистрации;
	Пока Период >= НачалоГода(НалоговыйПериод) Цикл
		ПериодыРегистрации.Добавить(Период);
		Период = ДобавитьМесяц(Период, -1);
	КонецЦикла;	
		
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор",				Регистратор);
	Запрос.УстановитьПараметр("Организация",				Организация);
	Запрос.УстановитьПараметр("ФизическиеЛица",				ФизическиеЛица);
	Запрос.УстановитьПараметр("ПериодыРегистрации",			ПериодыРегистрации);
	Запрос.УстановитьПараметр("СтрокиИсчисленныхНалогов",	Перечисления.ВидыОсобыхНачисленийИУдержаний.СтрокиИсчисленныхНалогов());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.Период КАК ПериодРегистрации,
	|	Остатки.Сотрудник КАК Сотрудник,
	|	Остатки.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Остатки.Подразделение КАК Подразделение,
	|	Остатки.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Остатки.СтатьяРасходов КАК СтатьяРасходов,
	|	Остатки.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	СУММА(Остатки.Сумма) КАК Сумма
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Остатки
	|ГДЕ
	|	Остатки.НачислениеУдержание В(&СтрокиИсчисленныхНалогов)
	|	И Остатки.Период В(&ПериодыРегистрации)
	|	И Остатки.Организация = &Организация
	|	И Остатки.ФизическоеЛицо В(&ФизическиеЛица)
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Период,
	|	Остатки.Сотрудник,
	|	Остатки.ФизическоеЛицо,
	|	Остатки.Подразделение,
	|	Остатки.СтатьяФинансирования,
	|	Остатки.СтатьяРасходов,
	|	Остатки.ВидДоходаИсполнительногоПроизводства";
	
	ИсчисленныеНалоги = Запрос.Выполнить().Выгрузить();
	
	НалогиДляВзаиморасчетов = ВзаиморасчетыССотрудниками.НоваяТаблицаНалоговПередаваемыхВНалоговыйОрган();
	
	Отбор = Новый Структура("ФизическоеЛицо, ПериодРегистрации");		
	Для Каждого Налог Из НДФЛ Цикл
		
		Отбор.ФизическоеЛицо = Налог.ФизическоеЛицо;
		
		НераспределеннаяСумма = Налог.Сумма;
		
		Для Каждого Период Из ПериодыРегистрации Цикл
			
			Отбор.ПериодРегистрации = Период;
			
			НалогиРаботникаВПериоде = ИсчисленныеНалоги.Скопировать(Отбор);
			
			ПогашаемаяСуммаВПериоде = МИН(НалогиРаботникаВПериоде.Итог("Сумма"), НераспределеннаяСумма);
			
			Если ПогашаемаяСуммаВПериоде = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(НалогиРаботникаВПериоде, "Сумма");
			РаспределенныеСуммы = ЗарплатаКадры.РаспределитьСуммуПропорциональноБазе(ПогашаемаяСуммаВПериоде, Коэффициенты);
			
			Для ИндексСтроки = 0 По НалогиРаботникаВПериоде.Количество() - 1 Цикл
			
				НалогРаботникаВПериоде = НалогиРаботникаВПериоде[ИндексСтроки];
				
				ПогашаемаяСумма = РаспределенныеСуммы[ИндексСтроки];
				
				НалогДляВзаиморасчетов = НалогиДляВзаиморасчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НалогДляВзаиморасчетов, НалогРаботникаВПериоде);
				НалогДляВзаиморасчетов.Сумма = ПогашаемаяСумма;
				
				НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(НачисленияУдержания, НалогРаботникаВПериоде);
				НачисленияУдержания.Период			= ПериодРегистрации;
				НачисленияУдержания.Организация		= Организация;
				НачисленияУдержания.ПериодДействия	= ПериодРегистрации;
				НачисленияУдержания.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛПередачаЗадолженностиВНалоговыйОрган;
				НачисленияУдержания.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
				НачисленияУдержания.Сумма = ПогашаемаяСумма;
				НачисленияУдержания.РегистрацияВНалоговомОргане = Налог.РегистрацияВНалоговомОргане;
				
			КонецЦикла;	
			
			НераспределеннаяСумма = НераспределеннаяСумма - ПогашаемаяСуммаВПериоде;
			
			Если НераспределеннаяСумма = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ВзаиморасчетыССотрудниками.ЗарегистрироватьНалогиПереданныеВНалоговыйОрган(
		Движения, Отказ, Организация, ПериодРегистрации, НалогиДляВзаиморасчетов);
	
	Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		
		ВзаиморасчетыССотрудниками.ЗаписатьДвижения(Движения);
		
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Дорабатывает таблицу с данными расчета НДФЛ в вид, пригодный для регистрации в учете начисленной и удержанной зарплаты
// Вызывается перед ЗарегистрироватьНДФЛ
// Обрабатывает колонки НалогПоСтавке13, ЗачтеноАвансовыхПлатежейПоСтавке13 если они есть.
Процедура ПодготовитьДанныеНДФЛКРегистрации(ТаблицаНДФЛ, Организация, ДатаОперации) Экспорт
	
	РегистрацияВНалоговомОргане = ЗарплатаКадры.РегистрацияВНалоговомОргане(Организация, ДатаОперации);
	ТаблицаНДФЛ = УчетНДФЛ.ТаблицаДвиженийСРегистрациейВНалоговомОргане(ТаблицаНДФЛ, "МесяцНалоговогоПериода");
	
	ЗаполнитьПодразделениеСотрудника = Ложь;
	Если ТаблицаНДФЛ.Колонки.Найти("ПодразделениеСотрудника") = Неопределено Тогда
		ТаблицаНДФЛ.Колонки.Добавить("ПодразделениеСотрудника");
		ЗаполнитьПодразделениеСотрудника = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
		УчетНДФЛ.ПроставитьРегистрациюВНалоговомОрганеВСтроке(СтрокаТЗ, СтрокаТЗ.РегистрацияВНалоговомОргане, РегистрацияВНалоговомОргане);
		Если ЗаполнитьПодразделениеСотрудника Тогда
			СтрокаТЗ.ПодразделениеСотрудника = СтрокаТЗ.Подразделение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Предназначена для регистрации в учете возврата НДФЛ физлицу
//
// Параметры:
//		Движения - коллекция движений регистратора.
//		Отказ - признак отказа от заполнения движений.
//		Организация
//		ПериодРегистрации
//		РезультатыРасчетаНДФЛ - таблица значений с колонками.
//			ФизическоеЛицо - обязательная
//			Подразделение - обязательная
//          одна из пар колонок Сумма, СуммаКорректировкиВыплаты (необязательна при регистрации без распределения)
//          или НалогПоСтавке13, ЗачтеноАвансовыхПлатежейПоСтавке13.
//
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//			
//		Допустимо присутствие других колонок в передаваемых таблицах
//
Процедура ЗарегистрироватьВозвратНДФЛ(Движения, Отказ, Организация, ПериодРегистрации, РезультатыРасчетаНДФЛ, ХарактерВыплаты = Неопределено, ЗаписыватьДвижения = Ложь) Экспорт
	
	РегистрацияВНалоговомОргане = ЗарплатаКадры.РегистрацияВНалоговомОргане(Организация, КонецМесяца(ПериодРегистрации));
	ДанныеМежрасчетногоПериода = ?(ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата
									Или ХарактерВыплаты = Неопределено, Ложь, Истина);
									
	УдержанияПоРабочимМестам = ТаблицаРаспределенияПоРабочимМестам();
	
	Для Каждого СтрокаТаблицы Из РезультатыРасчетаНДФЛ Цикл
		// УдержанияПоРабочимМестам требует колонки ТерриторияВыполненияРаботВОрганизации и Подразделение.
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		Если Не ЗначениеЗаполнено(НоваяСтрока.ТерриторияВыполненияРаботВОрганизации) Тогда 
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		НоваяСтрока.Период = ПериодРегистрации;
		НоваяСтрока.ПериодДействия = ПериодРегистрации;
		НоваяСтрока.Организация = Организация;
		Если Не ЗначениеЗаполнено(НоваяСтрока.РегистрацияВНалоговомОргане) Тогда
			НоваяСтрока.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
		КонецЕсли;
		НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
		НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛЗачтено;
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, Движения.НачисленияУдержанияПоСотрудникам);
	Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
	КонецЕсли;
	
	// Взаиморасчеты
	Если ЗначениеЗаполнено(ХарактерВыплаты) Тогда
		// Во взаиморасчеты передаем только СуммаКорректировкиВыплаты
		Для Каждого Строка Из УдержанияПоРабочимМестам Цикл
			Строка.СуммаКорректировкиВыплаты = Строка.Сумма;
			Строка.Сумма = 0;
		КонецЦикла;
		УдержанияДляВзаиморасчетов = ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, УдержанияДляВзаиморасчетов);
		ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
			Движения, Отказ, 
			Организация, ПериодРегистрации, ХарактерВыплаты, 
			Неопределено, УдержанияДляВзаиморасчетов);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет регистрацию в учете начисленной зарплаты результатов перерасчета НДФЛ.
// 	Параметры:
// 		ДанныеДляПроведения - Структура - см. ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения.
// 		Отказ - Булево - признак отказа от заполнения движений..
// 		НДФЛ - ТаблицаЗначений
// 		КорректировкиВыплатыПоСотрудникам - ТаблицаЗначений
// 		ЗаписыватьДвижения - Булево.
//
Процедура ЗарегистрироватьПерерасчетНДФЛ(ДанныеДляПроведения, Отказ, НДФЛ, КорректировкиВыплатыПоСотрудникам, ЗаписыватьДвижения = Ложь) Экспорт

	РезультатРасчетаНДФЛ = РезультатыРаспределенияНДФЛ(ДанныеДляПроведения, НДФЛ);
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из РезультатРасчетаНДФЛ Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Сотрудник) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТз Из СтрокиКУдалению Цикл
		РезультатРасчетаНДФЛ.Удалить(СтрокаТЗ);
	КонецЦикла;
	ЗарегистрироватьНДФЛ(ДанныеДляПроведения, Отказ, РезультатРасчетаНДФЛ, ЗаписыватьДвижения);
	
	КорректировкиВыплаты = РезультатыРаспределенияКорректировокВыплаты(ДанныеДляПроведения, КорректировкиВыплатыПоСотрудникам, НДФЛ);
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из КорректировкиВыплаты Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Сотрудник) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТз Из СтрокиКУдалению Цикл
		КорректировкиВыплаты.Удалить(СтрокаТЗ);
	КонецЦикла;
	ЗарегистрироватьКорректировкиВыплаты(ДанныеДляПроведения, Отказ, КорректировкиВыплаты, ЗаписыватьДвижения);

КонецПроцедуры

// Формирует временную таблицу ВТРаспределениеНачисленийТекущегоДокумента.
// Если ведется учет по статьям финансирования, таблица не формируется.
//
// Параметры:
// 	ДанныеДляПроведения - Структура, описание см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения.
// 
Процедура СоздатьВТРаспределениеНачисленийТекущегоДокумента(ДанныеДляПроведения) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Движения 				= ДанныеДляПроведения.Движения;
	МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	Авансом 				= ДанныеДляПроведения.Авансом;
	
	Если Авансом Тогда
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникамАвансом";
	Иначе
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникам";
	КонецЕсли;
	
	ДвиженияНачисленийУдержаний = Движения[ИмяТаблицыРегистра];
	
	ТаблицаДокумента = ДвиженияНачисленийУдержаний.Выгрузить();
	
	Отбор = Новый Структура("ГруппаНачисленияУдержанияВыплаты,Период", Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено, ПериодРегистрации);
	Начисления = ТаблицаДокумента.Скопировать(Отбор);
	
	Отбор.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
	НайденныеСтроки = ТаблицаДокумента.НайтиСтроки(Отбор);
	Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
		Если ТипЗнч(СтрокаТЗ.НачислениеУдержание) = Тип("ПланВидовРасчетаСсылка.Начисления") Или СтрокаТЗ.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов Тогда
			ЗаполнитьЗначенияСвойств(Начисления.Добавить(), СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	Отбор.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы;
	НайденныеСтроки = ТаблицаДокумента.НайтиСтроки(Отбор);
	Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
		ЗаполнитьЗначенияСвойств(Начисления.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТРаспределениеНачисленийТекущегоДокумента") Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРаспределениеНачисленийТекущегоДокумента");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачисленияДокумента", Начисления);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленияДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДокумента.Сотрудник КАК Сотрудник,
	|	НачисленияДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	НачисленияДокумента.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	НачисленияДокумента.МестоПолученияДохода КАК МестоПолученияДохода,
	|	НачисленияДокумента.НачислениеУдержание КАК Начисление,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	НачисленияДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияДокумента.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	НачисленияДокумента.ДатаНачала КАК ДатаНачала,
	|	НачисленияДокумента.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТРаспределениеНачисленийТекущегоДокумента
	|ИЗ
	|	&НачисленияДокумента КАК НачисленияДокумента
	|ГДЕ
	|	НачисленияДокумента.ИдентификаторСтроки > 0";
	// Условие "НачисленияДокумента.ИдентификаторСтроки > 0" учитывает исправление документа текущим периодом.
	Запрос.Выполнить();
	
КонецПроцедуры 

// Формирует пустую временную таблицу ВТРаспределениеНачисленийТекущегоДокумента.
// 
// Параметры:
// 		МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Процедура СоздатьПустуюВТРаспределениеНачисленийТекущегоДокумента(МенеджерВременныхТаблиц) Экспорт

	ПустоеЗначениеТерриторияНаЯзыкеЗапросов = УчетНачисленнойЗарплатыВнутренний.ПустоеЗначениеТерриторияНаЯзыкеЗапросов();

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	&ПустоеЗначениеТерриторияНаЯзыкеЗапросов КАК Территория,
	|	&ПустоеЗначениеТерриторияНаЯзыкеЗапросов КАК МестоПолученияДохода,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка) КАК ВидДоходаИсполнительногоПроизводства,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	0 КАК Сумма
	|ПОМЕСТИТЬ ВТРаспределениеНачисленийТекущегоДокумента";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПустоеЗначениеТерриторияНаЯзыкеЗапросов", ПустоеЗначениеТерриторияНаЯзыкеЗапросов);
	Запрос.Выполнить();

КонецПроцедуры

// Создает коллекцию сумм для распределения по рабочим местам (удержания, корректировки выплаты)
// Параметры:
//	Авансом - признак того, что коллекция создается для удержаний первой половины месяца. По умолчанию - Ложь.
//
// Возвращаемое значение: таблица значений 
//	
Функция ТаблицаРаспределенияПоРабочимМестам(Авансом = Ложь) Экспорт
	
	ИмяРегистраНачисленийУдержаний = "НачисленияУдержанияПоСотрудникам";
	Если Авансом Тогда
		ИмяРегистраНачисленийУдержаний = ИмяРегистраНачисленийУдержаний + "Авансом";
	КонецЕсли;
	
	УдержанияПоРабочимМестам = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраНакопления(ИмяРегистраНачисленийУдержаний);
	УдержанияПоРабочимМестам.Колонки.Добавить("СуммаКорректировкиВыплаты", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
	
	ТипыПоляПервичныйРегистратор = ОтражениеЗарплатыВУчете.ТипыПоляПервичныйРегистратор();
	УдержанияПоРабочимМестам.Колонки.Добавить("ПервичныйРегистратор", Новый ОписаниеТипов(ТипыПоляПервичныйРегистратор));
	
	Возврат УдержанияПоРабочимМестам;
	
КонецФункции

// Возвращает признак межрасчетной выплаты с точки зрения учета начисленной зарплаты.
//
// Параметры:
// 	ПорядокВыплаты - Неопределено, ПеречислениеСсылка.ХарактерВыплатыЗарплаты.
//
// Возвращаемое значение:
// 	Булево.
//
Функция ЭтоМежрасчетнаяВыплата(ПорядокВыплаты) Экспорт

	Возврат ?(ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата Или ПорядокВыплаты = Неопределено, Ложь, Истина);

КонецФункции

Функция ЗапросВТНачисленныеДоходы(ИмяВТНачисленныеДоходы  = "ВТНачисленныеДоходы") Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ЗапросВТНачисленныеДоходы(ИмяВТНачисленныеДоходы);
КонецФункции

Процедура УстановитьПериодВзаиморасчетов(МетаданныеРегистраВзаиморасчетов, ПараметрыОбновления = Неопределено) Экспорт

	ОбновлениеИБ = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый;
	
	ИмяРегистраВзаиморасчетов = МетаданныеРегистраВзаиморасчетов.ПолноеИмя();

	РегистраторыЗарплатыКВыплате = 
		Метаданные.РегистрыНакопления.ЗарплатаКВыплате.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	РегистраторыОплатыВедомостейНаВыплатуЗарплаты = 
		Метаданные.РегистрыСведений.ОплатаВедомостейНаВыплатуЗарплаты.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	
	Запрос = Новый Запрос;

	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВзаиморасчетыССотрудниками.Регистратор КАК Регистратор,
		|	ВзаиморасчетыССотрудниками.Период КАК Период
		|ИЗ
		|	&ИмяРегистраВзаиморасчетов КАК ВзаиморасчетыССотрудниками
		|ГДЕ
		|	ВзаиморасчетыССотрудниками.ПериодВзаиморасчетов = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяРегистраВзаиморасчетов", ИмяРегистраВзаиморасчетов);
	
	БлокировкиДляЗарплатыКВыплате = Новый Массив;
	БлокировкиДляЗарплатыКВыплате.Добавить(ОбновлениеИБ.ОписаниеБлокируемыхДанных(МетаданныеРегистраВзаиморасчетов));
	БлокировкиДляЗарплатыКВыплате.Добавить(ОбновлениеИБ.ОписаниеБлокируемыхДанных(Метаданные.РегистрыНакопления.ЗарплатаКВыплате));
	
	БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты = Новый Массив;
	БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты.Добавить(ОбновлениеИБ.ОписаниеБлокируемыхДанных(МетаданныеРегистраВзаиморасчетов));
	БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты.Добавить(ОбновлениеИБ.ОписаниеБлокируемыхДанных(Метаданные.РегистрыСведений.ОплатаВедомостейНаВыплатуЗарплаты));
	
	Блокировка = ОбновлениеИБ.ОписаниеБлокируемыхДанных(МетаданныеРегистраВзаиморасчетов);
		
	ОбновляемыеДанные = ОбновлениеИБ.ВыполнитьЗапросПолученияОбновляемыхДанных(Запрос, ПараметрыОбновления);
	Если ОбновляемыеДанные.Пустой() Тогда
		ОбновлениеИБ.ЗавершитьОбработчик(ПараметрыОбновления);
		Возврат;
	Иначе
		ОбновлениеИБ.ПродолжитьОбработчик(ПараметрыОбновления);
	КонецЕсли;	
	
	МенеджерРегистра = РегистрыНакопления[МетаданныеРегистраВзаиморасчетов.Имя];
	
	МенеджерРегистра.УстановитьИспользованиеИтогов(Ложь);
	
	ВыборкаОбновляемыхДанных = ОбновляемыеДанные.Выбрать();
	Пока ВыборкаОбновляемыхДанных.Следующий() Цикл 
		
		Регистратор = ВыборкаОбновляемыхДанных.Регистратор;
		
		ЭтоРегистраторЗарплатыКВыплате = РегистраторыЗарплатыКВыплате.Найти(ТипЗнч(Регистратор)) <> Неопределено;
		ЭтоРегистраторОплатыВедомостейНаВыплатуЗарплаты = РегистраторыОплатыВедомостейНаВыплатуЗарплаты.Найти(ТипЗнч(Регистратор)) <> Неопределено;
		
		Если ЭтоРегистраторЗарплатыКВыплате = ЭтоРегистраторОплатыВедомостейНаВыплатуЗарплаты Тогда
			Блокировка.ПоляБлокировки.Регистратор = Регистратор;
			Если Не ОбновлениеИБ.НачатьОбновлениеДанных(Блокировка, ПараметрыОбновления) Тогда
				Продолжить
			КонецЕсли;
		ИначеЕсли ЭтоРегистраторЗарплатыКВыплате Тогда
			БлокировкиДляЗарплатыКВыплате[0].ПоляБлокировки.Регистратор = Регистратор;
			БлокировкиДляЗарплатыКВыплате[1].ПоляБлокировки.Регистратор = Регистратор;
			Если Не ОбновлениеИБ.НачатьОбновлениеДанных(БлокировкиДляЗарплатыКВыплате, ПараметрыОбновления) Тогда
				Продолжить
			КонецЕсли;
		ИначеЕсли ЭтоРегистраторОплатыВедомостейНаВыплатуЗарплаты тогда
			БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты[0].ПоляБлокировки.Регистратор = Регистратор;
			БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты[1].ПоляБлокировки.Регистратор = Регистратор;
			Если Не ОбновлениеИБ.НачатьОбновлениеДанных(БлокировкиДляОплатыВедомостейНаВыплатуЗарплаты, ПараметрыОбновления) Тогда
				Продолжить
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоРегистраторЗарплатыКВыплате = ЭтоРегистраторОплатыВедомостейНаВыплатуЗарплаты Тогда
			НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
			НаборЗаписей.Прочитать();
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ПериодВзаиморасчетов = Дата(1900,1,1);
			КонецЦикла;
		ИначеЕсли ЭтоРегистраторЗарплатыКВыплате Тогда
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ЗарплатаКВыплате.ПериодВзаиморасчетов
				|ИЗ
				|	РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
				|ГДЕ
				|	ЗарплатаКВыплате.Регистратор = &Регистратор";
				
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				ПериодВзаиморасчетов = Дата(1900,1,1);
			Иначе
				Выборка = РезультатЗапроса.Выбрать();
				Выборка.Следующий();
				ПериодВзаиморасчетов = Выборка.ПериодВзаиморасчетов;
			КонецЕсли;
			НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ПериодВзаиморасчетов = ПериодВзаиморасчетов;
			КонецЦикла;
			
		ИначеЕсли ЭтоРегистраторОплатыВедомостейНаВыплатуЗарплаты Тогда
			Запрос.Текст=
				"ВЫБРАТЬ
				|	ОплатаВедомостейНаВыплатуЗарплаты.Ведомость,
				|	ОплатаВедомостейНаВыплатуЗарплаты.ФизическоеЛицо,
				|	ОплатаВедомостейНаВыплатуЗарплаты.Организация
				|ПОМЕСТИТЬ ВТОплаченныеВедомости
				|ИЗ
				|	РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК ОплатаВедомостейНаВыплатуЗарплаты
				|ГДЕ
				|	ОплатаВедомостейНаВыплатуЗарплаты.Регистратор = &Регистратор
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВзаиморасчетыССотрудниками.ПериодВзаиморасчетов,
				|	ВзаиморасчетыССотрудниками.Регистратор КАК Ведомость,
				|	ВзаиморасчетыССотрудниками.Организация,
				|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
				|	ВзаиморасчетыССотрудниками.СтатьяФинансирования,
				|	ВзаиморасчетыССотрудниками.Сотрудник,
				|	ВзаиморасчетыССотрудниками.Подразделение,
				|	ВзаиморасчетыССотрудниками.СтатьяРасходов,
				|	СУММА(ВзаиморасчетыССотрудниками.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
				|ИЗ
				|	ВТОплаченныеВедомости КАК ВТОплаченныеВедомости
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
				|		ПО ВТОплаченныеВедомости.Ведомость = ВзаиморасчетыССотрудниками.Регистратор
				|		И ВТОплаченныеВедомости.ФизическоеЛицо = ВзаиморасчетыССотрудниками.ФизическоеЛицо
				|		И ВТОплаченныеВедомости.Организация = ВзаиморасчетыССотрудниками.Организация
				|СГРУППИРОВАТЬ ПО
				|	ВзаиморасчетыССотрудниками.ПериодВзаиморасчетов,
				|	ВзаиморасчетыССотрудниками.Регистратор,
				|	ВзаиморасчетыССотрудниками.Организация,
				|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
				|	ВзаиморасчетыССотрудниками.СтатьяФинансирования,
				|	ВзаиморасчетыССотрудниками.Сотрудник,
				|	ВзаиморасчетыССотрудниками.Подразделение,
				|	ВзаиморасчетыССотрудниками.СтатьяРасходов";
				
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Запись.ПериодВзаиморасчетов = Дата(1900,1,1);;
				КонецЦикла;
			Иначе
				ТаблицаРезультата = РезультатЗапроса.Выгрузить();
				НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Отбор = Новый Структура(
						"Организация,
						|ФизическоеЛицо,
						|СтатьяФинансирования,
						|Сотрудник,
						|Подразделение,
						|СтатьяРасходов,
						|СуммаВзаиморасчетов");
					ЗаполнитьЗначенияСвойств(Отбор, Запись);
					СтрокиРезультата = ТаблицаРезультата.НайтиСтроки(Отбор);
					Если СтрокиРезультата.Количество() <> 1 Тогда 
						ПериодВзаиморасчетов = Дата(1900,1,1);
					Иначе
						ПериодВзаиморасчетов = СтрокиРезультата[0].ПериодВзаиморасчетов;
					КонецЕсли;
					Запись.ПериодВзаиморасчетов = ПериодВзаиморасчетов;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
		ОбновлениеИБ.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
	МенеджерРегистра.УстановитьИспользованиеИтогов(Истина);

КонецПроцедуры

#Область ВидыДоходовИсполнительногоПроизводства

// Возвращает виды доходов исполнительного производства начислений
// Параметры:
//	Период - дата, на которую необходимо получить виды доходов исполнительного производства.
//
// Возвращаемое значение:
// 	Соответствие:
// 	 	* Ключ     - ПланВидовРасчетаСсылка.Начисления, ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний
// 	 	* Значение - ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства
// 
Функция ВидыДоходовИсполнительногоПроизводстваНачислений(Период) Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ВидыДоходовИсполнительногоПроизводстваНачислений(Период);
КонецФункции

// Возвращает виды доходов исполнительного производства всех ссылочных объектов указанного типа
//
// Параметры
// 	МетаданныеОбъекта - ОбъектМетаданных - Метаданные объекта.
//  ИмяРеквизита      - Строка           - Имя реквизита вида дохода исполнительного производства
// 	
// Возвращаемое значение:
// 	Соответствие:
// 	 	* Ключ     - ЛюбаяСсылка
// 	 	* Значение - ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства
// 
Функция ВидыДоходовИсполнительногоПроизводстваОбъектов(МетаданныеОбъекта, ИмяРеквизита = "ВидДоходаИсполнительногоПроизводства") Экспорт
	
	ВидыДоходовИсполнительногоПроизводства = Новый Соответствие;
	
	Запрос = Новый  Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	&Реквизит КАК ВидДоходаИсполнительногоПроизводства
		|ИЗ
		|	&ПолноеИмяОбъектаМетаданных КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Реквизит", ИмяРеквизита);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолноеИмяОбъектаМетаданных", МетаданныеОбъекта.ПолноеИмя());

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВидыДоходовИсполнительногоПроизводства.Вставить(Выборка.Ссылка, Выборка.ВидДоходаИсполнительногоПроизводства);
	КонецЦикла;	

	Возврат ВидыДоходовИсполнительногоПроизводства
	
КонецФункции	
	
// До появления обязанности передавать коды видов доходов в банк 
// виды доходов исполнительных не имеют прикладного смысла и должны быть очищены.
//
Процедура ОчиститьВидыДоходовИсполнительногоПроизводстваНабораЗаписей(НаборЗаписей, Знач ИменаПолейПериодов = "Период", Знач ИмяПоляВидаДохода = "ВидДоходаИсполнительногоПроизводства") Экспорт
	
	ПоляПериодов = ОбщегоНазначения.ВыгрузитьКолонку(Новый Структура(ИменаПолейПериодов), "Ключ");
	
	ПустойВидДохода = Перечисления.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка();
	Для Каждого Запись Из НаборЗаписей Цикл
		ОчищатьВидДохода = Ложь;
		Для Каждого ИмяПоляПериода Из ПоляПериодов Цикл
			Если Не ВидыДоходовИсполнительногоПроизводстваКлиентСервер.ВидДоходаОбязателенДляБанков(Запись[ИмяПоляПериода]) Тогда
				ОчищатьВидДохода = Истина;
				Прервать;
			КонецЕсли	
		КонецЦикла;	
		
		Если ОчищатьВидДохода Тогда
			Запись[ИмяПоляВидаДохода] = ПустойВидДохода;
		КонецЕсли	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьВидыДоходовИсполнительногоПроизводстваРегистра(МетаданныеРегистра, ПараметрыОбновления, Знач ИменаПолейПериодов = "Период") Экспорт
	
	ОбновлениеИБ = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистр.Регистратор КАК Регистратор
	|ИЗ
	|	#Регистр КАК Регистр
	|ГДЕ
	|	&ПериодыДоНачалаПередачиВидаДоходаБанку
	|	И Регистр.ВидДоходаИсполнительногоПроизводства <> ЗНАЧЕНИЕ(Перечисление.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр(
		"ДатаНачалаПередачиВидаДоходаБанку", 
		ВидыДоходовИсполнительногоПроизводстваКлиентСервер.ДатаНачалаПередачиВидаДоходаБанку());
		
	ОтборПериодов = Новый Массив;
	Для Каждого ИмяПоляПериода Из ОбщегоНазначения.ВыгрузитьКолонку(Новый Структура(ИменаПолейПериодов), "Ключ") Цикл
		ОтборПериодов.Добавить(СтрШаблон("Регистр.%1 < &ДатаНачалаПередачиВидаДоходаБанку", ИмяПоляПериода));
	КонецЦикла;	
	ОтборПериодов = СтрСоединить(ОтборПериодов, " ИЛИ ");
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ПериодыДоНачалаПередачиВидаДоходаБанку", 
		СтрШаблон("(%1)", ОтборПериодов)); 
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"#Регистр",  
		МетаданныеРегистра.ПолноеИмя()); 
	
	
	ОписаниеБлокировки = ОбновлениеИБ.ОписаниеБлокируемыхДанных(МетаданныеРегистра);
	
	ОбновляемыеДанные = ОбновлениеИБ.ВыполнитьЗапросПолученияОбновляемыхДанных(Запрос, ПараметрыОбновления);
	
	Если ОбновляемыеДанные.Пустой() Тогда
		ОбновлениеИБ.ЗавершитьОбработчик(ПараметрыОбновления);
		Возврат;
	Иначе
		ОбновлениеИБ.ПродолжитьОбработчик(ПараметрыОбновления);
	КонецЕсли;	
	
	КолонкиГруппировок = Новый Массив;
	КолонкиГруппировок.Добавить("Регистратор");
	Если МетаданныеРегистра.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
		КолонкиГруппировок.Добавить("ВидДвижения");
	КонецЕсли;	
	КолонкиГруппировок.Добавить("Период");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		КолонкиГруппировок, 
		ОбщегоНазначения.ВыгрузитьКолонку(МетаданныеРегистра.Измерения, "Имя"));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		КолонкиГруппировок, 
		ОбщегоНазначения.ВыгрузитьКолонку(МетаданныеРегистра.Реквизиты, "Имя"));
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(КолонкиГруппировок, "НомерСтроки");	
	КолонкиСуммирования = ОбщегоНазначения.ВыгрузитьКолонку(МетаданныеРегистра.Ресурсы, "Имя");
	
	КолонкиГруппировок  = СтрСоединить(КолонкиГруппировок, ", ");	
	КолонкиСуммирования = СтрСоединить(КолонкиСуммирования, ", ");	
	ОтборНулевыхСтрок = Новый Структура(КолонкиСуммирования, 0); // считаем, что ресурс единственный
	
	МенеджерРегистра = РегистрыНакопления[МетаданныеРегистра.Имя];
	
	ИспользованиеИтогов = МенеджерРегистра.ПолучитьИспользованиеИтогов();
	МенеджерРегистра.УстановитьИспользованиеИтогов(Ложь);
	
	ВыборкаОбновляемыхДанных = ОбновляемыеДанные.Выбрать();
	Пока ВыборкаОбновляемыхДанных.Следующий() Цикл
		
		ОписаниеБлокировки.ПоляБлокировки.Регистратор = ВыборкаОбновляемыхДанных.Регистратор;
		Если Не ОбновлениеИБ.НачатьОбновлениеДанных(ОписаниеБлокировки, ПараметрыОбновления) Тогда
			Продолжить	
		КонецЕсли;
		
		НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаОбновляемыхДанных.Регистратор);
		НаборЗаписей.Прочитать();
		
		ОчиститьВидыДоходовИсполнительногоПроизводстваНабораЗаписей(НаборЗаписей, ИменаПолейПериодов);
		
		ВыгруженныйНабор = НаборЗаписей.Выгрузить(, КолонкиГруппировок + "," + КолонкиСуммирования);
		ВыгруженныйНабор.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
		Для Каждого НулеваяСтрока Из ВыгруженныйНабор.НайтиСтроки(ОтборНулевыхСтрок) Цикл
			ВыгруженныйНабор.Удалить(НулеваяСтрока)
		КонецЦикла;	
		НаборЗаписей.Загрузить(ВыгруженныйНабор);
		
		// АПК:1327-выкл Блокировка выполнена в ОбновлениеИБ.НачатьОбновлениеДанных
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		// АПК:1327-вкл
		
		ОбновлениеИБ.ЗавершитьОбновлениеДанных(ПараметрыОбновления);			
		
	КонецЦикла;
	
	МенеджерРегистра.УстановитьИспользованиеИтогов(ИспользованиеИтогов);
	
КонецПроцедуры

#КонецОбласти

// АПК:299-вкл
// АПК:581-вкл

#Область Отчеты

// Формирование отчета Анализ начислений и удержаний.
//
Процедура ПриКомпоновкеОтчетаАнализНачисленийИУдержаний(Отчет, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	УчетНачисленнойЗарплатыВнутренний.ПриКомпоновкеОтчетаАнализНачисленийИУдержаний(Отчет, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс);
	
КонецПроцедуры

// Возвращает описание добавляемых пользовательских полей.
//
// Возвращаемое значение:
//		Имя - Имя колонки
//		Заголовок - Представление колонки в отчете.
//		ПорядокКолонки - Порядок, в котором должна располагаться колонка отчета.
//		ПриоритетКолонки - Приоритет отображения колонки, как отдельной колонки.
//		НомерКолонки - Номер колонки - используется для получения имени параметра отчета ("Колонка" + НомерКолонки).
//		ДобавитьПользовательскоеПоле - Булево - Если Истина, то пользовательское поле добавляется в коллекцию
//		                                        "ПользовательскиеПоля" варианта отчета, Ложь - поле не добавляется в
//		                                        коллекцию.
//		ВыводитьНулевыеЗначения - Булево - Если Истина, то пользовательское поле выводится в отчет всегда, Ложь - поле не выводится в отчет, если оно нулевое
//		КатегорииКолонки - Массив - Категории, которые выводятся в колонке, используется при составлении условия в
//										выражении пользовательского поля.
//		ПользовательскоеУсловие - Строка - Условие, которое будет использовано в выражении пользовательского поля,
//										при использовании условия "КатегорииКолонки" игнорируются.
//		УсловиеИсключенияИзПрочих - Строка - Условие исключения суммы из колонки "ПрочиеНачисления" или "ПрочиеУдержания",
//										используется в паре с "ПользовательскоеУсловие".
//		РезультатУсловия - Строка - Результат выполнения условия, если в выражении нужно получить не сумму по начислению, а
//										например дни.
//		ВыводитьКолонку - Булево - Если Истина, то колонка будет включена в список выводимых колонок,
//										используется, например, если пользовательское поле нужно создать, но выводить в отчет в определенном месте
//										всегда.
//
Функция ПорядокДополнительныхНачисленийИУдержаний() Экспорт
	
	ТаблицаНачисленийИУдержаний = Новый ТаблицаЗначений;
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("Имя", 								Новый ОписаниеТипов("Строка"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("Заголовок",							Новый ОписаниеТипов("Строка"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ПорядокКолонки",						Новый ОписаниеТипов("Число"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ПриоритетКолонки",					Новый ОписаниеТипов("Число"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("НомерКолонки",						Новый ОписаниеТипов("Число"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ДобавитьПользовательскоеПоле",		Новый ОписаниеТипов("Булево"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ВыводитьНулевыеЗначения",				Новый ОписаниеТипов("Булево"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("КатегорииКолонки",					Новый ОписаниеТипов("Массив"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ПользовательскоеУсловие",				Новый ОписаниеТипов("Строка"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("УсловиеИсключенияИзПрочих",			Новый ОписаниеТипов("Строка"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("РезультатУсловия",					Новый ОписаниеТипов("Строка"));
	ТаблицаНачисленийИУдержаний.Колонки.Добавить("ВыводитьКолонку",						Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаНачисленийИУдержаний;
	
КонецФункции

Функция ДополнительныеНачисленияОтчетаАнализНачисленийИУдержанийТ49() Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ДополнительныеНачисленияОтчетаАнализНачисленийИУдержанийТ49();
КонецФункции

Функция ДополнительныеУдержанияОтчетаАнализНачисленийИУдержанийТ49() Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ДополнительныеУдержанияОтчетаАнализНачисленийИУдержанийТ49();
КонецФункции

Процедура ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеНачисленияИУдержания, НастройкиОтчета, КоличествоНачисленийУдержаний, ВидПолей = "Начисления", НаАванс = Ложь) Экспорт
	УчетНачисленнойЗарплатыВнутренний.ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеНачисленияИУдержания, НастройкиОтчета, КоличествоНачисленийУдержаний, ВидПолей, НаАванс);
КонецПроцедуры

// Возвращает начисления в том порядке, в котором они должны быть выведены в отчете.
//
Функция ПорядокДополнительныхНачислений(Начисления, ДанныеОтчета, СоответствиеПользовательскихПолей, НачальныйНомерКолонки) Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ПорядокДополнительныхНачислений(Начисления, ДанныеОтчета, СоответствиеПользовательскихПолей, НачальныйНомерКолонки);
КонецФункции

// Возвращает удержания в том порядке, в котором они должны быть выведены в отчете.
//
Функция ПорядокДополнительныхУдержаний(Удержания, ДанныеОтчета, СоответствиеПользовательскихПолей, НачальныйНомерКолонки) Экспорт
	Возврат УчетНачисленнойЗарплатыВнутренний.ПорядокДополнительныхУдержаний(Удержания, ДанныеОтчета, СоответствиеПользовательскихПолей, НачальныйНомерКолонки);
КонецФункции

#КонецОбласти

// Выполняет регистрацию в учете начисленной зарплаты НДФЛ.
// 	Параметры:
// 		ДанныеДляПроведения - Структура - см. ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения
// 		Отказ - Булево
// 		НДФЛПоСотрудникам - ТаблицаЗначений
// 		ЗаписыватьДвижения - Булево.
//
Процедура ЗарегистрироватьНДФЛПоСотрудникам(ДанныеДляПроведения, Отказ, НДФЛПоСотрудникам, ЗаписыватьДвижения = Ложь) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		РезультатРасчетаНДФЛ = НДФЛПоСотрудникам;
	Иначе
		РезультатРасчетаНДФЛ = РезультатыРаспределенияНДФЛ(ДанныеДляПроведения, НДФЛПоСотрудникам);
	КонецЕсли;
	
	ЗарегистрироватьНДФЛ(ДанныеДляПроведения, Отказ, РезультатРасчетаНДФЛ, ЗаписыватьДвижения);
	
КонецПроцедуры

// Выполняет регистрацию в учете начисленной зарплаты НДФЛ и корректировок выплаты.
// 	Параметры:
// 		ДанныеДляПроведения - Структура - см. ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения
// 		Отказ - Булево
// 		НДФЛКЗачетуВозврату - ТаблицаЗначений
// 		НДФЛПоСотрудникам - ТаблицаЗначений
// 		ЗаписыватьДвижения - Булево.
//
Процедура ЗарегистрироватьНДФЛКЗачетуВозврату(ДанныеДляПроведения, Отказ, НДФЛКЗачетуВозврату, НДФЛПоСотрудникам, ЗаписыватьДвижения = Ложь) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		КорректировкиВыплаты = НДФЛКЗачетуВозврату;
	Иначе
		КорректировкиВыплаты = РезультатыРаспределенияКорректировокВыплаты(ДанныеДляПроведения, НДФЛКЗачетуВозврату, НДФЛПоСотрудникам);
	КонецЕсли;
	
	ЗарегистрироватьКорректировкиВыплаты(ДанныеДляПроведения, Отказ, КорректировкиВыплаты, ЗаписыватьДвижения);
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, описание см ДанныеДляПроведения()
//		Начисления - регистрируемые начисления.
//					Если передано Неопределено, то начисления не регистрируются.
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//					если Истина - наборы записей будут записаны после заполнения.
//		Показатели - (необязательный), таблица значений, описание см ДанныеДляПроведения()
//
Процедура ЗарегистрироватьНачисленияАвансом(ДанныеДляПроведения, Отказ, Начисления, ЗаписыватьДвижения = Ложь, Показатели = Неопределено) Экспорт
	
	Если Начисления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 				= ДанныеДляПроведения.Движения;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	ПланируемаяДатаВыплаты 	= ДанныеДляПроведения.ПланируемаяДатаВыплаты;
	Организация 			= ДанныеДляПроведения.Организация;
	
	СтрокиНачислений = Новый Массив;
	
	ТаблицаНачислений = УчетНачисленнойЗарплаты.ТаблицаРаспределенияПоРабочимМестам();
	
	ВидыДоходаИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений(ПланируемаяДатаВыплаты);
		
	Для Каждого Строка Из Начисления Цикл
		
		НоваяСтрока = ТаблицаНачислений.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.Период				= ПериодРегистрации;
		НоваяСтрока.ПериодДействия		= ПериодРегистрации;
		НоваяСтрока.Организация			= Организация;
		НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
		НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
		
		СтрокиНачислений.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНачислений, Движения.НачисленияУдержанияПоСотрудникамАвансом);
	Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Истина;
	
	Если Показатели <> Неопределено Тогда
		Регистратор = Движения.ЗначенияПоказателейНачислений.Отбор.Регистратор.Значение;
		Для Каждого Строка Из Показатели Цикл
			НоваяСтрока = Движения.ЗначенияПоказателейНачислений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.РегистраторИзмерение = Регистратор;
		КонецЦикла;
		Движения.ЗначенияПоказателейНачислений.Записывать = Истина;
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.НачисленияУдержанияПоСотрудникамАвансом.Записать();
		Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Ложь;
		
		Если Показатели <> Неопределено Тогда
			Движения.ЗначенияПоказателейНачислений.Записать();
			Движения.ЗначенияПоказателейНачислений.Записывать = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Взаиморасчеты
	НачисленияДляВзаиморасчетов	= ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
	Для Каждого Строка Из СтрокиНачислений Цикл
		НоваяСтрока = НачисленияДляВзаиморасчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
		Движения, Отказ, 
		Организация, ПериодРегистрации, 
		НачисленияДляВзаиморасчетов, Неопределено);
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы для авансовых расчетов.
//
// Параметры:
//		Движения - коллекция движений регистратора.
//		Отказ - признак отказа от заполнения движений.
//		Организация
//		ПериодРегистрации
//		Начисления - таблица значений с колонками (не обязательно)
//			ФизическоеЛицо.
//			Сотрудник
//			Подразделение
//			Начисление - ссылка на план видов расчета 
//			ОтработаноДней
//			ОтработаноЧасов
//			ОплаченоДней
//			ОплаченоЧасов.
//			ПериодДействия - не обязательно.
//			ДатаНачала - не обязательно.
//
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
//		Допустимо присутствие других колонок в передаваемых таблицах значений.
//
Процедура ЗарегистрироватьОтработанноеВремяАвансом(ДанныеДляПроведения, Отказ, Начисления, ЗаписыватьДвижения = Ложь) Экспорт
	
	Если Начисления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	Организация 		= ДанныеДляПроведения.Организация;
	
	Если Начисления.Колонки.Найти("ПериодДействия") = Неопределено Тогда
		Начисления.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
		Начисления.ЗаполнитьЗначения(ПериодРегистрации, "ПериодДействия");
	КонецЕсли;
	
	Для Каждого Строка Из Начисления Цикл
		
		НоваяСтрока = Движения.ОтработанноеВремяПоСотрудникамАвансом.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.Период 		= ПериодРегистрации;
		НоваяСтрока.Организация = Организация;
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			
	КонецЦикла;
	
	Движения.ОтработанноеВремяПоСотрудникамАвансом.Записывать = Истина;
		
	Если ЗаписыватьДвижения Тогда
		Движения.ОтработанноеВремяПоСотрудникамАвансом.Записать();
		Движения.ОтработанноеВремяПоСотрудникамАвансом.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, описание см ДанныеДляПроведения()
//		Начисления - регистрируемые начисления.
//					Если передано Неопределено, то начисления не регистрируются.
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//					если Истина - наборы записей будут записаны после заполнения.
//		Показатели - (необязательный), таблица значений, описание см ДанныеДляПроведения()
//
Процедура ЗарегистрироватьУдержанияАвансом(ДанныеДляПроведения, Отказ, Удержания, ЗаписыватьДвижения = Ложь, Показатели = Неопределено) Экспорт
	
	Если Удержания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Удержания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата")
		И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		
		МодульУчетНачисленнойЗарплатыРасширенный = ОбщегоНазначения.ОбщийМодуль("УчетНачисленнойЗарплатыРасширенный");
		УдержанияПоСотрудникам = МодульУчетНачисленнойЗарплатыРасширенный.РезультатыРаспределенияУдержаний(ДанныеДляПроведения, Удержания);
	Иначе
		УдержанияПоСотрудникам    	= Удержания;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	Организация 		= ДанныеДляПроведения.Организация;
	
	УдержанияПоРабочимМестам = УчетНачисленнойЗарплаты.ТаблицаРаспределенияПоРабочимМестам();
	Для Каждого Строка Из УдержанияПоСотрудникам Цикл
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Период				= ПериодРегистрации;
		НоваяСтрока.ПериодДействия		= ПериодРегистрации;
		НоваяСтрока.Организация			= Организация;
		НоваяСтрока.НачислениеУдержание = Строка.Удержание;
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
	КонецЦикла;
	// Заполняем движения 
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УдержанияПоРабочимМестам, Движения.НачисленияУдержанияПоСотрудникамАвансом);
	Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Истина;
	
	Если Показатели <> Неопределено Тогда
		Регистратор = Движения.ЗначенияПоказателейУдержаний.Отбор.Регистратор.Значение;
		Для Каждого Строка Из Показатели Цикл
			НоваяСтрока = Движения.ЗначенияПоказателейУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.РегистраторИзмерение = Регистратор;
		КонецЦикла;
		Движения.ЗначенияПоказателейУдержаний.Записывать = Истина;
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.НачисленияУдержанияПоСотрудникамАвансом.Записать();
		Движения.НачисленияУдержанияПоСотрудникамАвансом.Записывать = Ложь;
		
		Если Показатели <> Неопределено Тогда
			Движения.ЗначенияПоказателейУдержаний.Записать();
			Движения.ЗначенияПоказателейУдержаний.Записывать = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Взаиморасчеты
	УдержанияДляВзаиморасчетов	= ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
	Для Каждого Строка Из УдержанияПоРабочимМестам Цикл
		НоваяСтрока = УдержанияДляВзаиморасчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
		Движения, Отказ, 
		Организация, ПериодРегистрации, 
		Неопределено, УдержанияДляВзаиморасчетов);
	
КонецПроцедуры

#Область СформироватьСтрокиНДФЛКакЗачетАванса

Процедура СформироватьСтрокиНДФЛКакЗачетАванса(Движения, НДФЛПоСотрудникам, ПериодРегистрации) Экспорт

	Если ПериодРегистрации < УчетНДФЛ.ДатаЗакона263ФЗ() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНДФЛ = УчетНДФЛ.НДФЛЗарегистрированоПриНачисленииАванса(Движения);
	Если Не ЗначениеЗаполнено(ТаблицаНДФЛ) Тогда
		Возврат;
	КонецЕсли;
	
	РесурсыИсчисленногоНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаСтрокой();
	ИменаНалоговыхРесурсов = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве();
	ВидыУдержанийПоКолонкамДанных = Перечисления.ВидыОсобыхНачисленийИУдержаний.СоответствиеКолонкамДанных();
	НДФЛЗачетАванса = ТаблицаНДФЛ.СкопироватьКолонки();
	НДФЛЗачетАванса.Колонки.Добавить("ВидУдержания", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	Для каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
		Для каждого ИмяРесурса Из ИменаНалоговыхРесурсов Цикл
			ВидУдержания = ВидыУдержанийПоКолонкамДанных[ИмяРесурса];
			Если СтрокаТЗ[ИмяРесурса] < 0 Тогда
				НоваяСтрока = НДФЛЗачетАванса.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,,РесурсыИсчисленногоНалога);
				НоваяСтрока.Сумма = СтрокаТЗ[ИмяРесурса]*-1;
				НоваяСтрока.ВидУдержания = ВидУдержания;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если НДФЛПоСотрудникам.Колонки.Найти("ЗачетАвансаНДФЛ") = Неопределено Тогда
		НДФЛПоСотрудникам.Колонки.Добавить("ЗачетАвансаНДФЛ", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	ПоляПоиска = "ФизическоеЛицо,МесяцНалоговогоПериода,КатегорияДохода,РегистрацияВНалоговомОргане,Подразделение,ВидУдержания";
	ОтметитьСтрокиНДФЛКакЗачетАванса(НДФЛЗачетАванса, НДФЛПоСотрудникам, ПоляПоиска);
	
КонецПроцедуры

Процедура ОтметитьСтрокиНДФЛКакЗачетАванса(НДФЛЗачетАванса, НДФЛПоСотрудникам, ПоляПоиска) Экспорт

	ДобавленныеСтроки = НДФЛПоСотрудникам.СкопироватьКолонки();
	НДФЛПоСотрудникам.Индексы.Добавить(ПоляПоиска);
	Отбор = Новый Структура(ПоляПоиска);
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаЗачетАванса Из НДФЛЗачетАванса Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаЗачетАванса);
		НайденныеСтроки = НДФЛПоСотрудникам.НайтиСтроки(Отбор);
		УдаляемыеСтроки = СформироватьСтрокиЗачетаАвансаНДФЛВКоллекции(СтрокаЗачетАванса.Сумма, НайденныеСтроки, ДобавленныеСтроки);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СтрокиКУдалению, УдаляемыеСтроки);
	КонецЦикла;
	
	НДФЛПоСотрудникам.Индексы.Удалить(0);
	Для каждого СтрокаКУдалению  Из СтрокиКУдалению Цикл
		НДФЛПоСотрудникам.Удалить(СтрокаКУдалению);
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДобавленныеСтроки, НДФЛПоСотрудникам);

КонецПроцедуры


#КонецОбласти

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		НДФЛ - ТаблицаЗначений.
//		ДокументОснование
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
Процедура ЗарегистрироватьНДФЛПереданныйВНалоговыйОрган(ДанныеДляПроведения, Отказ, НДФЛ, ДокументОснование, ЗаписыватьДвижения = Ложь) Экспорт
	
	
	Если НДФЛ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	
	НалогиДляВзаиморасчетов = ВзаиморасчетыССотрудниками.НоваяТаблицаНалоговПередаваемыхВНалоговыйОрган();
	
	Для каждого СтрокаТЗ Из НДФЛ Цикл
	
		НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Добавить();
		
		ЗаполнитьЗначенияСвойств(НачисленияУдержания, СтрокаТЗ);
		НачисленияУдержания.Период			= ПериодРегистрации;
		НачисленияУдержания.ПериодДействия	= ПериодРегистрации;
		НачисленияУдержания.Организация		= Организация;
		НачисленияУдержания.НачислениеУдержание 				= Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛПередачаЗадолженностиВНалоговыйОрган;
		НачисленияУдержания.ГруппаНачисленияУдержанияВыплаты 	= Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
		
		НалогДляВзаиморасчетов = НалогиДляВзаиморасчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НалогДляВзаиморасчетов, СтрокаТЗ);
	
	КонецЦикла;
	
	ВзаиморасчетыССотрудниками.ЗарегистрироватьНалогиПереданныеВНалоговыйОрган(
		Движения, Отказ, Организация, ПериодРегистрации, НалогиДляВзаиморасчетов, ДокументОснование);
	
	Движения.НачисленияУдержанияПоСотрудникам.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		
		ВзаиморасчетыССотрудниками.ЗаписатьДвижения(Движения);
		
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения - структура, см ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения().
//		Отказ - Булево.
//		КорректировкиВыплаты - таблица значений с корректировками выплаты.
//
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
Процедура ЗарегистрироватьКорректировкиВыплаты(ДанныеДляПроведения, Отказ, КорректировкиВыплаты, ЗаписыватьДвижения = Ложь)

	Если КорректировкиВыплаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	Авансом				= ДанныеДляПроведения.Авансом;
	
	ДанныеМежрасчетногоПериода = ЭтоМежрасчетнаяВыплата(ПорядокВыплаты);
	
	КорректировкиПоРабочимМестам = ТаблицаРаспределенияПоРабочимМестам();
	Для Каждого СтрокаКорректировки Из КорректировкиВыплаты Цикл
		Если СтрокаКорректировки.КорректировкаВыплаты <> 0 Тогда
			НоваяСтрока = КорректировкиПоРабочимМестам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.ПериодДействия		= ПериодРегистрации;
			Если СтрокаКорректировки.КорректировкаВыплаты < 0 Тогда
				НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛКЗачету;
			Иначе
				НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛЗачтено;
			КонецЕсли;
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			НоваяСтрока.Сумма = СтрокаКорректировки.КорректировкаВыплаты;
		КонецЕсли;
	КонецЦикла;
	
	// Взаиморасчеты
	Если ЗначениеЗаполнено(ПорядокВыплаты) Тогда
		Взаиморасчеты = ВзаиморасчетыССотрудниками.НоваяТаблицаНачисленнойЗарплаты();
		Для Каждого СтрокаКорректировки Из КорректировкиПоРабочимМестам Цикл
			НоваяСтрока = Взаиморасчеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
			НоваяСтрока.СуммаКорректировкиВыплаты = НоваяСтрока.Сумма;
			НоваяСтрока.Сумма = 0;
		КонецЦикла;
		Если Авансом Тогда
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленныйАванс(
				Движения, Отказ, 
				Организация, ПериодРегистрации, 
				Неопределено, Взаиморасчеты);
		Иначе
			ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
				Движения, Отказ, 
				Организация, ПериодРегистрации, ПорядокВыплаты, 
				Неопределено, Взаиморасчеты);
		КонецЕсли;
	КонецЕсли;
	
	// Инвертируем суммы НДФЛКЗачету для отражения в НачисленияУдержанияПоСотрудникам
	ОтборСтрок = Новый Структура("НачислениеУдержание", Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛКЗачету);
	НайденныеСтроки = КорректировкиПоРабочимМестам.НайтиСтроки(ОтборСтрок);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		НайденнаяСтрока.Сумма = -НайденнаяСтрока.Сумма;
	КонецЦикла;
	
	Если Авансом Тогда
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникамАвансом";
	Иначе
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникам";
	КонецЕсли;
	ДвиженияНачисленийУдержаний = Движения[ИмяТаблицыРегистра];
	
	// Заполняем движения начислений (удержаний) удержанными суммами.
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(КорректировкиПоРабочимМестам, ДвиженияНачисленийУдержаний);
	
	ДвиженияНачисленийУдержаний.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		ДвиженияНачисленийУдержаний.Записать();
		ДвиженияНачисленийУдержаний.Записывать = Ложь;
	КонецЕсли;

КонецПроцедуры

Функция РезультатыРаспределенияНДФЛ(ДанныеДляПроведения, РезультатРасчетаНДФЛ)
	
	Если РезультатРасчетаНДФЛ.Количество() = 0 Тогда
		Возврат ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	КонецЕсли;
	
	ТаблицаНДФЛ = ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаНДФЛ();
	
	Движения 				= ДанныеДляПроведения.Движения;
	Организация 			= ДанныеДляПроведения.Организация;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	ОкончательныйРасчет		= ДанныеДляПроведения.ОкончательныйРасчет;
	Авансом					= ДанныеДляПроведения.Авансом;
	МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	
	Если Авансом Тогда
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникамАвансом";
	Иначе
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникам";
	КонецЕсли;
	ДвиженияНачисленийУдержаний = Движения[ИмяТаблицыРегистра];
	
	ИсключаемыйРегистратор = ДвиженияНачисленийУдержаний.Отбор.Регистратор.Значение;
	
	СтрокиНДФЛ = Новый Соответствие;
	НоваяКоллекцияРаспределениеНДФЛ = РезультатРасчетаНДФЛ.СкопироватьКолонки();
	ИдентификаторСтроки = 1;
	Для каждого СтрокаТЗ Из РезультатРасчетаНДФЛ Цикл
		Если СтрокаТЗ.Сумма <> 0 Тогда
			СтрокиНДФЛ.Вставить(ИдентификаторСтроки, СтрокаТЗ);
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
			НоваяСтрока.ДатаПолученияДохода = СтрокаТЗ.МесяцНалоговогоПериода;
			НоваяСтрока.Территория 			= СтрокаТЗ.Подразделение;
			ИдентификаторСтроки = ИдентификаторСтроки + 1;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДляПроведения.ДанныеДляНДФЛКЗачету.ТаблицаНДФЛ = ТаблицаНДФЛ;
	
	УдалитьВТ = Новый Массив;

	ИмяВТРаспределениеНачислений = "ВТРаспределениеНачисленийТекущегоДокумента";
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, ИмяВТРаспределениеНачислений) Тогда
		СоздатьПустуюВТРаспределениеНачисленийТекущегоДокумента(МенеджерВременныхТаблиц);
		УдалитьВТ.Добавить(ИмяВТРаспределениеНачислений);
	КонецЕсли;
	
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ")
		И Движения.Найти("СведенияОДоходахНДФЛ") <> Неопределено Тогда
		ЗарплатаКадры.СоздатьВТПоНаборуЗаписей(МенеджерВременныхТаблиц, Движения.СведенияОДоходахНДФЛ, Истина);
	КонецЕсли;
	
	Если Движения.Найти("ДокументыУчтенныеПриРасчетеНДФЛ") <> Неопределено
		И ПериодРегистрации >= ОтражениеЗарплатыВУчете.ДатаНачалаИспользованияОснованийВРаспределенииНДФЛ() Тогда
		ОснованияУчтенныеПриРасчетеНДФЛ = Движения.ДокументыУчтенныеПриРасчетеНДФЛ.Выгрузить();
		ОтражениеЗарплатыВУчете.СоздатьВТУсловияОтбораДляРаспределенияНДФЛ(МенеджерВременныхТаблиц, ОснованияУчтенныеПриРасчетеНДФЛ, ИсключаемыйРегистратор);
		УдалитьВТ.Добавить("ВТУсловияОтбораДляРаспределенияНДФЛ");
		ОснованияУчтенныеПриРасчетеНДФЛ = Неопределено;
	КонецЕсли;
	
	ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНДФЛ, "ФизическоеЛицо", Истина);
	УдержанияМассив 	 = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНДФЛ, "ВидУдержания", Истина);
	
	// Получение данных для распределения, ДанныеДляРаспределенияНДФЛ - структура
	// БазаВсеНачисления - таблица значений с базовыми начислениями
	// СтрокиУжеУдержано - соответствие, содержит данные об уже выполненных удержаниях по физическому лиц
	// в менеджере временных таблиц будет создана таблица ВТРаспределениеНачисленийДляБазыНДФЛ для получения базы НДФЛ.
	
	ПараметрыРаспределения = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровРаспределенияНДФЛ();
	ПараметрыРаспределения.МенеджерВременныхТаблиц 	= МенеджерВременныхТаблиц;
	ПараметрыРаспределения.Организация 				= Организация;
	ПараметрыРаспределения.ПериодРегистрации 		= ПериодРегистрации;
	ПараметрыРаспределения.МассивФизическихЛиц 		= ФизическиеЛицаМассив;
	ПараметрыРаспределения.МассивУдержаний 			= УдержанияМассив;
	ПараметрыРаспределения.ИсключаемыйРегистратор 	= ИсключаемыйРегистратор;
	ПараметрыРаспределения.ОкончательныйРасчет 		= ОкончательныйРасчет;
	ПараметрыРаспределения.ИмяВТДанныеТекущегоДокумента = ИмяВТРаспределениеНачислений;
	
	ДанныеДляРаспределенияНДФЛ = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияНДФЛ(ПараметрыРаспределения);
	УдалитьВТ.Добавить("ВТРаспределениеНачисленийДляБазыНДФЛ");
	
	БазаВсеНачисления = ДанныеДляРаспределенияНДФЛ.БазаВсеНачисления;
	СтрокиУжеУдержано = ДанныеДляРаспределенияНДФЛ.СтрокиУжеУдержано;
	
	Если Не Авансом И ПериодРегистрации >= УчетНДФЛ.ДатаЗакона263ФЗ() Тогда
		Запрос = Новый Запрос; 
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ТаблицаНДФЛ", ТаблицаНДФЛ);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&Организация КАК Организация,
		|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Таблица.ДатаПолученияДохода КАК ДатаПолученияДохода,
		|	Таблица.КатегорияДохода КАК КатегорияДохода
		|ПОМЕСТИТЬ ВТОтборЗачетАванса
		|ИЗ
		|	&ТаблицаНДФЛ КАК Таблица";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТОтборЗачетАванса");
	КонецЕсли;
	
	БазаРасчетаНДФЛ = ОтражениеЗарплатыВУчете.БазаДляРаспределенияНДФЛ(ПараметрыРаспределения);
	
	РаспределениеНДФЛ = ОтражениеЗарплатыВУчете.НДФЛПоРабочимМестамИСтатьям(ТаблицаНДФЛ, БазаРасчетаНДФЛ, БазаВсеНачисления,
																				СтрокиУжеУдержано, Организация, ПериодРегистрации);
																				
	ДанныеДляПроведения.ДанныеДляНДФЛКЗачету.РаспределениеНДФЛ = РаспределениеНДФЛ;
	
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	Для каждого ЭлементКоллекции Из СтрокиНДФЛ Цикл
		Отбор.ИдентификаторСтроки = ЭлементКоллекции.Ключ;
		СтрокиРаспределения = РаспределениеНДФЛ.НайтиСтроки(Отбор);
		Если СтрокиРаспределения.Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(НоваяКоллекцияРаспределениеНДФЛ.Добавить(),ЭлементКоллекции.Значение);
		Иначе
			Для каждого СтрокаТЗ Из СтрокиРаспределения Цикл
				НоваяСтрока = НоваяКоллекцияРаспределениеНДФЛ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементКоллекции.Значение);
				НоваяСтрока.Сотрудник 				= СтрокаТЗ.Сотрудник;
				НоваяСтрока.ПодразделениеСотрудника = СтрокаТЗ.Подразделение;
				НоваяСтрока.СтатьяРасходов 			= СтрокаТЗ.СтатьяРасходов;
				НоваяСтрока.СтатьяФинансирования 	= СтрокаТЗ.СтатьяФинансирования;
				НоваяСтрока.ВидДоходаИсполнительногоПроизводства = СтрокаТЗ.ВидДоходаИсполнительногоПроизводства;
				НоваяСтрока.Сумма 					= СтрокаТЗ.Результат;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат НоваяКоллекцияРаспределениеНДФЛ;
	
КонецФункции

Функция РезультатыРаспределенияКорректировокВыплаты(ДанныеДляПроведения, КорректировкиВыплаты, РезультатРасчетаНДФЛ)

	РаспределениеКорректировок = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	Если КорректировкиВыплаты.Количество() = 0 Тогда
		Возврат РаспределениеКорректировок;
	КонецЕсли;
	
	Движения 				= ДанныеДляПроведения.Движения;
	Организация 			= ДанныеДляПроведения.Организация;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	Авансом					= ДанныеДляПроведения.Авансом;
	
	ИсчисленныеНалоги = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(Перечисления.ВидыОсобыхНачисленийИУдержаний.СтрокиИсчисленныхНалогов());
	
	Если Авансом Тогда
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникамАвансом";
	Иначе
		ИмяТаблицыРегистра = "НачисленияУдержанияПоСотрудникам";
	КонецЕсли;
	ДвиженияНачисленийУдержаний = Движения[ИмяТаблицыРегистра];
	
	ИсключаемыйРегистратор = ДвиженияНачисленийУдержаний.Отбор.Регистратор.Значение;
	
	НДФЛКЗачету = ОтражениеЗарплатыВУчете.НоваяТаблицаКорректировкиВыплаты();
	НДФЛЗачтено = ОтражениеЗарплатыВУчете.НоваяТаблицаКорректировкиВыплаты(); 
	
	СтрокиКорректировок = Новый Соответствие;
	НоваяКоллекцияКорректировкиВыплаты = КорректировкиВыплаты.СкопироватьКолонки();
	ИдентификаторСтроки = 1;
	Для каждого СтрокаТЗ Из КорректировкиВыплаты Цикл
		Сумма = СтрокаТЗ.КорректировкаВыплаты;
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиКорректировок.Вставить(ИдентификаторСтроки, СтрокаТЗ);
		Если Сумма < 0 Тогда
			НоваяСтрока = НДФЛКЗачету.Добавить();
			НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
			НоваяСтрока.ФизическоеЛицо 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.Сумма 				= Сумма;
			НоваяСтрока.ВидУдержания 		= Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛКЗачету;
		ИначеЕсли Сумма > 0 Тогда
			НоваяСтрока = НДФЛЗачтено.Добавить();
			НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
			НоваяСтрока.ФизическоеЛицо 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.Сумма 				= Сумма;
			НоваяСтрока.ВидУдержания 		= Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛЗачтено;
		КонецЕсли;
		ИдентификаторСтроки = ИдентификаторСтроки + 1;
	КонецЦикла;
	
	Если НДФЛКЗачету.Количество() > 0 Тогда
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(НДФЛКЗачету, "ФизическоеЛицо", Истина);
		
		ТаблицаНДФЛ = ДанныеДляПроведения.ДанныеДляНДФЛКЗачету.ТаблицаНДФЛ;
		РаспределениеНДФЛ = ДанныеДляПроведения.ДанныеДляНДФЛКЗачету.РаспределениеНДФЛ;
		Если ТаблицаНДФЛ = Неопределено Тогда
			
			ФизическиеЛица = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ФизическиеЛицаМассив);
			
			ТаблицаНДФЛ = ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаНДФЛ();
			Для каждого СтрокаТЗ Из РезультатРасчетаНДФЛ Цикл
				Если СтрокаТЗ.Сумма <> 0 Тогда
					НоваяСтрока = ТаблицаНДФЛ.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
					НоваяСтрока.ДатаПолученияДохода = СтрокаТЗ.МесяцНалоговогоПериода;
					НоваяСтрока.Территория 			= СтрокаТЗ.Подразделение;
					ИдентификаторСтроки = ИдентификаторСтроки + 1;
				КонецЕсли;
			КонецЦикла;
			
			РаспределениеНДФЛ = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
			ИдентификаторСтроки = 1;
			Для каждого СтрокаДвижений Из ДвиженияНачисленийУдержаний Цикл
				Если СтрокаДвижений.Сумма <> 0 И ФизическиеЛица[СтрокаДвижений.ФизическоеЛицо] <> Неопределено 
					И ИсчисленныеНалоги[СтрокаДвижений.НачислениеУдержание] <> Неопределено Тогда
					НоваяСтрока = РаспределениеНДФЛ.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДвижений);
					НоваяСтрока.ВидУдержания 	= СтрокаДвижений.НачислениеУдержание;
					НоваяСтрока.Результат 		= СтрокаДвижений.Сумма;
					НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
					ИдентификаторСтроки = ИдентификаторСтроки + 1;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		База = ОтражениеЗарплатыВУчете.БазаДляРаспределенияНДФЛКЗачету(ФизическиеЛицаМассив, РаспределениеНДФЛ, ТаблицаНДФЛ);
		РаспределениеНДФЛКЗачету = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛКЗачету, База, Организация, ПериодРегистрации);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РаспределениеНДФЛКЗачету, РаспределениеКорректировок);
		
	КонецЕсли;
	
	Если НДФЛЗачтено.Количество()>0 Тогда
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(НДФЛЗачтено, "ФизическоеЛицо", Истина);
		
		// НакопленныеКорректировки могут служить базой для распределения, нужно переименовать колонку "КорректировкаВыплаты".
		НакопленныеКорректировки = ВзаиморасчетыССотрудниками.КорректировкиВыплаты(Организация, ПериодРегистрации, ФизическиеЛицаМассив, ИсключаемыйРегистратор);
		НакопленныеКорректировки.Колонки.СуммаКорректировки.Имя = "Сумма";
		
		РаспределениеНДФЛЗачтено = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛЗачтено, НакопленныеКорректировки, Организация, ПериодРегистрации);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РаспределениеНДФЛЗачтено, РаспределениеКорректировок);
		
	КонецЕсли;
	
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	Для каждого ЭлементКоллекции Из СтрокиКорректировок Цикл
		Отбор.ИдентификаторСтроки = ЭлементКоллекции.Ключ;
		СтрокиРаспределения = РаспределениеКорректировок.НайтиСтроки(Отбор);
		Если СтрокиРаспределения.Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(НоваяКоллекцияКорректировкиВыплаты.Добавить(),ЭлементКоллекции.Значение);
		Иначе
			Для каждого СтрокаТЗ Из СтрокиРаспределения Цикл
				НоваяСтрока = НоваяКоллекцияКорректировкиВыплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,ЭлементКоллекции.Значение);
				НоваяСтрока.Сотрудник 			 = СтрокаТЗ.Сотрудник;
				НоваяСтрока.Подразделение 		 = СтрокаТЗ.Подразделение;
				НоваяСтрока.СтатьяРасходов 		 = СтрокаТЗ.СтатьяРасходов;
				НоваяСтрока.СтатьяФинансирования = СтрокаТЗ.СтатьяФинансирования;
				НоваяСтрока.ВидДоходаИсполнительногоПроизводства = СтрокаТЗ.ВидДоходаИсполнительногоПроизводства;
				НоваяСтрока.КорректировкаВыплаты = СтрокаТЗ.Результат;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	Возврат НоваяКоллекцияКорректировкиВыплаты;
	
КонецФункции

Функция СформироватьСтрокиЗачетаАвансаНДФЛВКоллекции(СуммаЗачета, СтрокиНДФЛ, ДобавленныеСтроки)

	СтрокиКУдалению = Новый Массив;
	Если СтрокиНДФЛ.Количество() = 0 Тогда
		Возврат СтрокиКУдалению;
	КонецЕсли;
	
	Если СтрокиНДФЛ.Количество() = 1 Тогда
		Если СтрокиНДФЛ[0].Сумма > 0 Тогда
			Если СуммаЗачета >= СтрокиНДФЛ[0].Сумма Тогда
				СтрокиНДФЛ[0].ЗачетАвансаНДФЛ = Истина;
			Иначе
				СуммаОстаток = СтрокиНДФЛ[0].Сумма - СуммаЗачета;
				СтрокиНДФЛ[0].ЗачетАвансаНДФЛ = Истина;
				СтрокиНДФЛ[0].Сумма = СуммаЗачета;
				НоваяСтрока = ДобавленныеСтроки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиНДФЛ[0]);
				НоваяСтрока.ЗачетАвансаНДФЛ = Ложь;
				НоваяСтрока.Сумма = СуммаОстаток;
			КонецЕсли;
		КонецЕсли;
	Иначе
		
		Итог = 0;
		ОбрабатываемыеСтроки = Новый Массив;
		Для каждого СтрокаТЗ Из СтрокиНДФЛ Цикл
			Если СтрокаТЗ.Сумма > 0 Тогда
				Итог = Итог + СтрокаТЗ.Сумма;
				ОбрабатываемыеСтроки.Добавить(СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
		
		Если ОбрабатываемыеСтроки.Количество() > 0 Тогда
			
			Если СуммаЗачета >= Итог Тогда
				Для каждого СтрокаТЗ Из ОбрабатываемыеСтроки Цикл
					СтрокаТЗ.ЗачетАвансаНДФЛ = Истина;
				КонецЦикла;
			Иначе
				
				Доля = СуммаЗачета/Итог;
				Для каждого СтрокаТЗ Из ОбрабатываемыеСтроки Цикл
					
					СтрокиКУдалению.Добавить(СтрокаТЗ);
					Сумма1 = Окр(СтрокаТЗ.Сумма *Доля, 0);
					Сумма2 = СтрокаТЗ.Сумма - Сумма1;
					
					НоваяСтрока = ДобавленныеСтроки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.ЗачетАвансаНДФЛ = Истина;
					НоваяСтрока.Сумма = Сумма1;
					
					НоваяСтрока = ДобавленныеСтроки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.ЗачетАвансаНДФЛ = Ложь;
					НоваяСтрока.Сумма = Сумма2;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокиКУдалению;

КонецФункции

#Область ФормированиеПечатныхФорм

Процедура ЗаполнитьДополнительныеПоляОтчетаАнализНачисленийИУдержаний(ОтчетОбъект, ДополнительныеПоля) Экспорт
	
	Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		УчетНачисленнойЗарплатыВнутренний.ЗаполнитьДополнительныеПоляОтчетаАнализНачисленийИУдержаний(ОтчетОбъект, ДополнительныеПоля);
	КонецЕсли;
	
КонецПроцедуры

Функция РасчетныйЛистокПоДаннымДокумента(Организация, ФизическиеЛица, Месяц, ИсключаемыеСсылки = Неопределено, ДанныеДокумента = Неопределено, ЗаПервуюПоловинуМесяца = Ложь) Экспорт
	
	ДокументРезультат = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		
		Если ЗаПервуюПоловинуМесяца Тогда
			ОтчетРасчетныйЛисток = ЗарплатаКадрыОтчеты.ОтчетРасчетныйЛистокЗаПервуюПоловинуМесяца();
		Иначе
			ОтчетРасчетныйЛисток = ЗарплатаКадрыОтчеты.ОтчетРасчетныйЛисток();
		КонецЕсли;
		
		Если ИсключаемыеСсылки <> Неопределено Тогда
			ПараметрИсключаемыеСсылки = ОтчетРасчетныйЛисток.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ИсключаемыеСсылки");
			ПараметрИсключаемыеСсылки.Значение = ИсключаемыеСсылки;
		КонецЕсли;
		
		Если ДанныеДокумента <> Неопределено Тогда
			ОтчетРасчетныйЛисток.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ДанныеДокумента", ДанныеДокумента);
			ОтчетРасчетныйЛисток.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПредварительныйПросмотр", Ложь);
		КонецЕсли;
		
		ДокументРезультат = Отчеты.АнализНачисленийИУдержаний.РасчетныйЛисток(
			ФизическиеЛица, Организация, Месяц, ОтчетРасчетныйЛисток);
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'Не удалось, сформировать отчет.';
								|en = 'Cannot generate report.'") + " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДокументРезультат = Неопределено Тогда
		
		ДокументРезультат = Новый ТабличныйДокумент;
		ДокументРезультат.АвтоМасштаб = Истина;
		ДокументРезультат.ОтображатьЗаголовки = Ложь;
		ДокументРезультат.ОтображатьСетку = Ложь;
		
	КонецЕсли; 
	
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти

#КонецОбласти