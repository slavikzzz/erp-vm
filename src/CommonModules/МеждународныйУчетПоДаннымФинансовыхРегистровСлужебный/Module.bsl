#Область СлужебныйПрограммныйИнтерфейс

#Область РеклассификацияОстатковРегистров

// Возвращает текст запроса выборки остатков регистра со сроком погашения равным длительности операционного
// цикла организации для реклассификации долгосрочных активов и обязательств в краткосрочные.
// Состав и имена полей выборки соответствуют всем измерениям и ресурсам регистра накопления.
// Результат выборки помещается во временную таблицу с именем регистра накопления.
// В тексте запроса используются параметры:
//  МассивОрганизаций - Массив из СправочникСсылка.Организации - отбор по организациям, по которым выполняется анализ остатков
//  НачалоПериода - Дата - дата начала месяца, в котором происходит анализ остатков регистра
//  КонецПериода - Дата - дата окончания месяца, в котором происходит анализ остатков регистра
//  ГраницаПериода - Граница с видом "Включая" - граница окончания месяца, на которую рассчитываются остатки регистра
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаОстатковДляРеклассификации(ИмяРегистра) Экспорт
	
	Если ИмяРегистра = "РасчетыСКлиентамиПоСрокам" Тогда
		ТекстЗапроса = ТекстЗапросаОстатковДляРеклассификацииРасчетыСКлиентамиПоСрокам();
	ИначеЕсли ИмяРегистра = "РасчетыСПоставщикамиПоСрокам" Тогда
		ТекстЗапроса = ТекстЗапросаОстатковДляРеклассификацииРасчетыСПоставщикамиПоСрокам();
	ИначеЕсли ИмяРегистра = "РасчетыПоФинансовымИнструментам" Тогда
		ТекстЗапроса = ТекстЗапросаОстатковДляРеклассификацииРасчетыПоФинансовымИнструментам();
	Иначе
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Для регистра ""%1"" отсутствует обработчик получения остатков для реклассификации';
				|en = 'Handler of receiving balance for reclassification is missing in the ""%1"" register'"),
			ИмяРегистра);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки данных для выполнения операции реклассификации долгосрочных активов и
// обязательств в краткосрочные на основании полученных ранее остатков со сроком погашения равным длительности
// операционного цикла организации. По данным остатков формируются пары приходных и расходных "движений",
// подаваемых на вход транслятора МФУ по объектам учета, которые обрабатываются в нем по общим правилам,
// как обычные движения регистров.
// Источником данных запроса является временная таблица с именем регистра накопления, полученная при выполнении
// запроса с текстом из функции ТекстЗапросаОстатковДляРеклассификации.
// Состав и имена полей выборки соответствуют всем измерениям и ресурсам регистра накопления и необходимым
// реквизитам, в том числе стандартным.
// Результат выборки помещается во временную таблицу, именованную по правилу "Движения" + имя регистра накопления.
// В тексте запроса используются параметры:
//  Регистратор - ДокументСсылка.РегламентнаяОперацияМеждународныйУчет - ссылка на документ, по которому будут сформированы проводки реклассификации
//  ДатаОперации - Дата - дата операции реклассификации долгосрочных активов и обязательств в краткосрочные
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаДвиженийРеклассификацииОстатков(ИмяРегистра) Экспорт
	
	Если ИмяРегистра = "РасчетыСКлиентамиПоСрокам" Тогда
		ТекстЗапроса = ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыСКлиентамиПоСрокам();
	ИначеЕсли ИмяРегистра = "РасчетыСПоставщикамиПоСрокам" Тогда
		ТекстЗапроса = ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыСПоставщикамиПоСрокам();
	ИначеЕсли ИмяРегистра = "РасчетыПоФинансовымИнструментам" Тогда
		ТекстЗапроса = ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыПоФинансовымИнструментам();
	Иначе
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Для регистра ""%1"" отсутствует обработчик формирования движений реклассификации остатков';
				|en = 'Handler of generating balance reclassification records is missing in the ""%1"" register'"),
			ИмяРегистра);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Возврат СтрЗаменить(ТекстЗапроса, "ТаблицаДляПомещения", "Движения" + ИмяРегистра);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РеклассификацияОстатковРегистраРасчетыСКлиентамиПоСрокам

Функция ТекстЗапросаОстатковДляРеклассификацииРасчетыСКлиентамиПоСрокам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	|	РасчетыСКлиентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСКлиентамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСКлиентамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСКлиентамиОстатки.ДолгОстаток КАК Долг,
	|	РасчетыСКлиентамиОстатки.ДолгРеглОстаток КАК ДолгРегл,
	|	РасчетыСКлиентамиОстатки.ДолгУпрОстаток КАК ДолгУпр,
	|	РасчетыСКлиентамиОстатки.ПредоплатаОстаток КАК Предоплата,
	|	РасчетыСКлиентамиОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРегл,
	|	РасчетыСКлиентамиОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпр
	|ПОМЕСТИТЬ РасчетыСКлиентамиПоСрокам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(
	|		&ГраницаПериода,
	|		АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|		И ДатаПлановогоПогашения > &КонецПериода
	|	) КАК РасчетыСКлиентамиОстатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|	ПО РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|	И РасчетыСКлиентамиОстатки.ДатаПлановогоПогашения МЕЖДУ
	|		ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла)
	|		И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), МЕСЯЦ)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыСКлиентамиПоСрокам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	|	РасчетыСКлиентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК ДатаПлановогоПогашения,
	|	РасчетыСКлиентамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСКлиентамиОстатки.Долг КАК Долг,
	|	РасчетыСКлиентамиОстатки.ДолгРегл КАК ДолгРегл,
	|	РасчетыСКлиентамиОстатки.ДолгУпр КАК ДолгУпр,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	РасчетыСКлиентамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ПОМЕСТИТЬ ТаблицаДляПомещения
	|ИЗ
	|	РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	РасчетыСКлиентамиОстатки.Долг <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ДолгРегл <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	|	РасчетыСКлиентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСКлиентамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСКлиентамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСКлиентамиОстатки.Долг КАК Долг,
	|	РасчетыСКлиентамиОстатки.ДолгРегл КАК ДолгРегл,
	|	РасчетыСКлиентамиОстатки.ДолгУпр КАК ДолгУпр,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	РасчетыСКлиентамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	РасчетыСКлиентамиОстатки.Долг <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ДолгРегл <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	|	РасчетыСКлиентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСКлиентамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСКлиентамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	РасчетыСКлиентамиОстатки.Предоплата КАК Предоплата,
	|	РасчетыСКлиентамиОстатки.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыСКлиентамиОстатки.ПредоплатаУпр КАК ПредоплатаУпр,
	|	РасчетыСКлиентамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	РасчетыСКлиентамиОстатки.Предоплата <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ПредоплатаРегл <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ПредоплатаУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиОстатки.Валюта КАК Валюта,
	|	РасчетыСКлиентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК ДатаПлановогоПогашения,
	|	РасчетыСКлиентамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	РасчетыСКлиентамиОстатки.Предоплата КАК Предоплата,
	|	РасчетыСКлиентамиОстатки.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыСКлиентамиОстатки.ПредоплатаУпр КАК ПредоплатаУпр,
	|	РасчетыСКлиентамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	РасчетыСКлиентамиОстатки.Предоплата <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ПредоплатаРегл <> 0
	|	ИЛИ РасчетыСКлиентамиОстатки.ПредоплатаУпр <> 0
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область РеклассификацияОстатковРегистраРасчетыСПоставщикамиПоСрокам

Функция ТекстЗапросаОстатковДляРеклассификацииРасчетыСПоставщикамиПоСрокам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСПоставщикамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСПоставщикамиОстатки.ДолгОстаток КАК Долг,
	|	РасчетыСПоставщикамиОстатки.ДолгРеглОстаток КАК ДолгРегл,
	|	РасчетыСПоставщикамиОстатки.ДолгУпрОстаток КАК ДолгУпр,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаОстаток КАК Предоплата,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРегл,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпр
	|ПОМЕСТИТЬ РасчетыСПоставщикамиПоСрокам
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(
	|		&ГраницаПериода,
	|		АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|		И ДатаПлановогоПогашения > &КонецПериода
	|	) КАК РасчетыСПоставщикамиОстатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|	ПО РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|	И РасчетыСПоставщикамиОстатки.ДатаПлановогоПогашения МЕЖДУ
	|		ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла)
	|		И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), МЕСЯЦ)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыСПоставщикамиПоСрокам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСПоставщикамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСПоставщикамиОстатки.Долг КАК Долг,
	|	РасчетыСПоставщикамиОстатки.ДолгРегл КАК ДолгРегл,
	|	РасчетыСПоставщикамиОстатки.ДолгУпр КАК ДолгУпр,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	РасчетыСПоставщикамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ПОМЕСТИТЬ ТаблицаДляПомещения
	|ИЗ
	|	РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	РасчетыСПоставщикамиОстатки.Долг <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ДолгРегл <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК ДатаПлановогоПогашения,
	|	РасчетыСПоставщикамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыСПоставщикамиОстатки.Долг КАК Долг,
	|	РасчетыСПоставщикамиОстатки.ДолгРегл КАК ДолгРегл,
	|	РасчетыСПоставщикамиОстатки.ДолгУпр КАК ДолгУпр,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	РасчетыСПоставщикамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	РасчетыСПоставщикамиОстатки.Долг <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ДолгРегл <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК ДатаПлановогоПогашения,
	|	РасчетыСПоставщикамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	РасчетыСПоставщикамиОстатки.Предоплата КАК Предоплата,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаУпр КАК ПредоплатаУпр,
	|	РасчетыСПоставщикамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	РасчетыСПоставщикамиОстатки.Предоплата <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ПредоплатаРегл <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ПредоплатаУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыСПоставщикамиОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	РасчетыСПоставщикамиОстатки.Предоплата КАК Предоплата,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыСПоставщикамиОстатки.ПредоплатаУпр КАК ПредоплатаУпр,
	|	РасчетыСПоставщикамиОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	РасчетыСПоставщикамиОстатки.Предоплата <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ПредоплатаРегл <> 0
	|	ИЛИ РасчетыСПоставщикамиОстатки.ПредоплатаУпр <> 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область РеклассификацияОстатковРегистраРасчетыПоФинансовымИнструментам

Функция ТекстЗапросаОстатковДляРеклассификацииРасчетыПоФинансовымИнструментам()
	
	ТекстыЗапросов = Новый Массив();
	
#Область РасчетыПоКредитамИДепозитам
	
#Область РеклассификацияПоступившихКредитовИДепозитовВДолгосрочные
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла + 1), ДЕНЬ, -1) КАК НачалоДолгосрочногоПериода,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.РасчетныйДокумент) КАК РасчетныйДокумент,
	|	СУММА(ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|		ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|	КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ втПоступленияКредитовИДепозитов
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(
	|		&НачалоПериода,
	|		&КонецПериода,
	|		ПЕРИОД,
	|		Договор ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|		И АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	) КАК РасчетыПоФинансовымИнструментам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|		ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|	КОНЕЦ > 0
	|	И УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоФинансовымИнструментам.Договор,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы,
	|	РасчетыПоФинансовымИнструментам.Валюта,
	|	УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ТипСуммы,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияКредитовИДепозитов.Договор КАК Договор,
	|	ПоступленияКредитовИДепозитов.ТипСуммы КАК ТипСуммы,
	|	СУММА(ВЫБОР
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.Сумма
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.Проценты
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.Комиссия
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаИтог,
	|	СУММА(ВЫБОР
	|		КОГДА ПоступленияКредитовИДепозитов.НачалоДолгосрочногоПериода > ГрафикОплат.Период
	|			ТОГДА 0
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.Сумма
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.Проценты
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.Комиссия
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаДолгосрочная
	|ПОМЕСТИТЬ втГрафикОплатКредитовИДепозитов
	|ИЗ
	|	втПоступленияКредитовИДепозитов КАК ПоступленияКредитовИДепозитов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплат
	|		ПО ПоступленияКредитовИДепозитов.Договор = ГрафикОплат.ВариантГрафика.Владелец
	|		 И НЕ ГрафикОплат.ВариантГрафика.ПометкаУдаления
	|		 И ГрафикОплат.ВариантГрафика.Используется
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияКредитовИДепозитов.Договор,
	|	ПоступленияКредитовИДепозитов.ТипСуммы
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|		КОГДА ПоступленияКредитовИДепозитов.НачалоДолгосрочногоПериода > ГрафикОплат.Период
	|			ТОГДА 0
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.Сумма
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.Проценты
	|		КОГДА ПоступленияКредитовИДепозитов.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.Комиссия
	|		ИНАЧЕ 0
	|	КОНЕЦ) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ТипСуммы
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступленияКредитовИДепозитов.Договор КАК Договор
	|ПОМЕСТИТЬ втДоговорыКредитовИДепозитов
	|ИЗ
	|	втПоступленияКредитовИДепозитов КАК ПоступленияКредитовИДепозитов
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|		ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втПоступленияКредитовИДепозитовИтог
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,
	|		&КонецПериода,,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыКредитовИДепозитов.Договор
	|		ИЗ
	|			втДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|		ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|	КОНЕЦ > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ТипСуммы,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетыПоФинансовымИнструментам.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ втОстаткиКредитовИДепозитов
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|		&ГраницаПериода,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыКредитовИДепозитов.Договор
	|		ИЗ
	|			втДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетыПоФинансовымИнструментам.СуммаОстаток > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ТипСуммы,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияКредитовИДепозитов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПоступленияКредитовИДепозитов.Договор КАК Договор,
	|	ПоступленияКредитовИДепозитов.ТипСуммы КАК ТипСуммы,
	|	ПоступленияКредитовИДепозитов.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ПоступленияКредитовИДепозитов.Валюта КАК Валюта,
	|	ПоступленияКредитовИДепозитов.НачалоДолгосрочногоПериода КАК ДатаПлановогоПогашения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0) < ВЫБОР
	|			КОГДА (ЕСТЬNULL(ПоступленияКредитовИДепозитовИтог.Сумма, ПоступленияКредитовИДепозитов.Сумма) - ПоступленияКредитовИДепозитов.Сумма)
	|				< (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.СуммаДолгосрочная)
	|				ТОГДА ЕСТЬNULL(ПоступленияКредитовИДепозитовИтог.Сумма, ПоступленияКредитовИДепозитов.Сумма)
	|					- (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.СуммаДолгосрочная)
	|			ИНАЧЕ ПоступленияКредитовИДепозитов.Сумма
	|		КОНЕЦ
	|			ТОГДА ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0)
	|		ИНАЧЕ ВЫБОР
	|			КОГДА (ЕСТЬNULL(ПоступленияКредитовИДепозитовИтог.Сумма, ПоступленияКредитовИДепозитов.Сумма) - ПоступленияКредитовИДепозитов.Сумма)
	|				< (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.СуммаДолгосрочная)
	|				ТОГДА ЕСТЬNULL(ПоступленияКредитовИДепозитовИтог.Сумма, ПоступленияКредитовИДепозитов.Сумма)
	|					- (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.СуммаДолгосрочная)
	|			ИНАЧЕ ПоступленияКредитовИДепозитов.Сумма
	|		КОНЕЦ
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втРеклассификацияПоступленийКредитовИДепозитов
	|ИЗ
	|	втПоступленияКредитовИДепозитов КАК ПоступленияКредитовИДепозитов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|		ПО ПоступленияКредитовИДепозитов.Договор = ГрафикОплатКредитовИДепозитов.Договор
	|		 И ПоступленияКредитовИДепозитов.ТипСуммы = ГрафикОплатКредитовИДепозитов.ТипСуммы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПоступленияКредитовИДепозитовИтог КАК ПоступленияКредитовИДепозитовИтог
	|		ПО ПоступленияКредитовИДепозитов.Договор = ПоступленияКредитовИДепозитовИтог.Договор
	|		 И ПоступленияКредитовИДепозитов.ТипСуммы = ПоступленияКредитовИДепозитовИтог.ТипСуммы
	|		 И ПоступленияКредитовИДепозитов.Валюта = ПоступленияКредитовИДепозитовИтог.Валюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиКредитовИДепозитов КАК ОстаткиКредитовИДепозитов
	|		ПО ПоступленияКредитовИДепозитов.Договор = ОстаткиКредитовИДепозитов.Договор
	|		 И ПоступленияКредитовИДепозитов.ТипСуммы = ОстаткиКредитовИДепозитов.ТипСуммы
	|		 И ПоступленияКредитовИДепозитов.Валюта = ОстаткиКредитовИДепозитов.Валюта
	|ГДЕ
	|	ЕСТЬNULL(ПоступленияКредитовИДепозитовИтог.Сумма, ПоступленияКредитовИДепозитов.Сумма)
	|		> (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.СуммаДолгосрочная)
	|	И ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0) > 0
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПоступленияКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПоступленияКредитовИДепозитовИтог");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОстаткиКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втГрафикОплатКредитовИДепозитов");
	
#КонецОбласти
	
#Область РеклассификацияОстатковКредитовИДепозитовВКраткосрочные
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ДоговорыКредитовИДепозитов.Ссылка КАК Ссылка,
	|	ДоговорыКредитовИДепозитов.ВалютаВзаиморасчетов КАК Валюта,
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), ДЕНЬ, -1) КАК НачалоПериодаПогашения,
	|	ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), МЕСЯЦ), ДЕНЬ, -1) КАК КонецПериодаПогашения
	|ПОМЕСТИТЬ втДоговорыКредитовИДепозитов
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ПО ДоговорыКредитовИДепозитов.Организация = АналитикаУчетаПоПартнерам.Организация
	|		 И ДоговорыКредитовИДепозитов.Контрагент = АналитикаУчетаПоПартнерам.Контрагент
	|		 И ДоговорыКредитовИДепозитов.Партнер = АналитикаУчетаПоПартнерам.Партнер
	|		 И АналитикаУчетаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		 И ДоговорыКредитовИДепозитов.НаправлениеДеятельности = АналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО ДоговорыКредитовИДепозитов.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	ДоговорыКредитовИДепозитов.Организация В (&МассивОрганизаций)
	|	И УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ДоговорыКредитовИДепозитов.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ДоговорыКредитовИДепозитов.Ссылка КАК Договор,
	|	ДоговорыКредитовИДепозитов.Валюта КАК Валюта,
	|	ТипыСуммГрафика.Ссылка КАК ТипСуммы,
	|	МАКСИМУМ(ГрафикОплат.Период) КАК ДатаПлановогоПогашения,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.СуммаИтог
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.ПроцентыИтог
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.КомиссияИтог
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаИтог,
	|	СУММА(ВЫБОР
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.Сумма
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.Проценты
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.Комиссия
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ втГрафикОплатКредитовИДепозитов
	|ИЗ
	|	втДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплат
	|		ПО ДоговорыКредитовИДепозитов.Ссылка = ГрафикОплат.ВариантГрафика.Владелец
	|		 И НЕ ГрафикОплат.ВариантГрафика.ПометкаУдаления
	|		 И ГрафикОплат.ВариантГрафика.Используется
	|		 И ГрафикОплат.Период МЕЖДУ ДоговорыКредитовИДепозитов.НачалоПериодаПогашения И ДоговорыКредитовИДепозитов.КонецПериодаПогашения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ТипыСуммГрафикаКредитовИДепозитов КАК ТипыСуммГрафика
	|		ПО ИСТИНА
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ГрафикОплат.Сумма
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ГрафикОплат.Проценты
	|		КОГДА ТипыСуммГрафика.Ссылка = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
	|			ТОГДА ГрафикОплат.Комиссия
	|		ИНАЧЕ 0
	|	КОНЕЦ > 0
	|СГРУППИРОВАТЬ ПО
	|	ДоговорыКредитовИДепозитов.КлючАналитики,
	|	ДоговорыКредитовИДепозитов.Ссылка,
	|	ДоговорыКредитовИДепозитов.Валюта,
	|	ТипыСуммГрафика.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта,
	|	ТипСуммы
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГрафикОплатКредитовИДепозитов.Договор КАК Договор
	|ПОМЕСТИТЬ втДоговорыКредитовИДепозитов
	|ИЗ
	|	втГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы КАК ТипСуммы,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.РасчетныйДокумент) КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА СУММА(ВЫБОР
	|			КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|			ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|		КОНЕЦ) < 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ВЫБОР
	|			КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА РасчетыПоФинансовымИнструментам.СуммаРасход
	|			ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаПриход
	|		КОНЕЦ)
	|	КОНЕЦ КАК СуммаПоступления,
	|	ВЫБОР
	|		КОГДА СУММА(ВЫБОР
	|			КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА РасчетыПоФинансовымИнструментам.СуммаПриход
	|			ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаРасход
	|		КОНЕЦ) < 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ВЫБОР
	|			КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА РасчетыПоФинансовымИнструментам.СуммаПриход
	|			ИНАЧЕ РасчетыПоФинансовымИнструментам.СуммаРасход
	|		КОНЕЦ)
	|	КОНЕЦ КАК СуммаОплаты
	|ПОМЕСТИТЬ втОборотыКредитовИДепозитов
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,
	|		&КонецПериода,,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыКредитовИДепозитов.Договор
	|		ИЗ
	|			втДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов)
	|	) КАК РасчетыПоФинансовымИнструментам
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоФинансовымИнструментам.Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта,
	|	ТипСуммы
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	РасчетыПоФинансовымИнструментам.ТипСуммы КАК ТипСуммы,
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетыПоФинансовымИнструментам.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ втОстаткиКредитовИДепозитов
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|		&НачалоПериода,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыКредитовИДепозитов.Договор
	|		ИЗ
	|			втДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВЫБОР
	|		КОГДА РасчетыПоФинансовымИнструментам.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетыПоФинансовымИнструментам.СуммаОстаток > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта,
	|	ТипСуммы
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ГрафикОплатКредитовИДепозитов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ГрафикОплатКредитовИДепозитов.Договор КАК Договор,
	|	ГрафикОплатКредитовИДепозитов.ТипСуммы КАК ТипСуммы,
	|	ЕСТЬNULL(ОборотыКредитовИДепозитов.РасчетныйДокумент, НЕОПРЕДЕЛЕНО) КАК РасчетныйДокумент,
	|	ГрафикОплатКредитовИДепозитов.Валюта КАК Валюта,
	|	ГрафикОплатКредитовИДепозитов.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0) < ВЫБОР
	|			КОГДА ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаПоступления, 0) < ГрафикОплатКредитовИДепозитов.СуммаИтог
	|				ТОГДА ГрафикОплатКредитовИДепозитов.Сумма + ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаПоступления, 0) - ГрафикОплатКредитовИДепозитов.СуммаИтог
	|			КОГДА ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаОплаты, 0) > ГрафикОплатКредитовИДепозитов.СуммаИтог
	|				ТОГДА ГрафикОплатКредитовИДепозитов.Сумма + ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаОплаты, 0) - ГрафикОплатКредитовИДепозитов.СуммаИтог
	|			ИНАЧЕ ГрафикОплатКредитовИДепозитов.Сумма
	|		КОНЕЦ
	|			ТОГДА ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0)
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаПоступления, 0) < ГрафикОплатКредитовИДепозитов.СуммаИтог
	|				ТОГДА ГрафикОплатКредитовИДепозитов.Сумма + ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаПоступления, 0) - ГрафикОплатКредитовИДепозитов.СуммаИтог
	|			КОГДА ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаОплаты, 0) > ГрафикОплатКредитовИДепозитов.СуммаИтог
	|				ТОГДА ГрафикОплатКредитовИДепозитов.Сумма + ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаОплаты, 0) - ГрафикОплатКредитовИДепозитов.СуммаИтог
	|			ИНАЧЕ ГрафикОплатКредитовИДепозитов.Сумма
	|		КОНЕЦ
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втРеклассификацияОстатковКредитовИДепозитов
	|ИЗ
	|	втГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОборотыКредитовИДепозитов КАК ОборотыКредитовИДепозитов
	|		ПО ГрафикОплатКредитовИДепозитов.Договор = ОборотыКредитовИДепозитов.Договор
	|		 И ГрафикОплатКредитовИДепозитов.Валюта = ОборотыКредитовИДепозитов.Валюта
	|		 И ГрафикОплатКредитовИДепозитов.ТипСуммы = ОборотыКредитовИДепозитов.ТипСуммы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиКредитовИДепозитов КАК ОстаткиКредитовИДепозитов
	|		ПО ГрафикОплатКредитовИДепозитов.Договор = ОстаткиКредитовИДепозитов.Договор
	|		 И ГрафикОплатКредитовИДепозитов.Валюта = ОстаткиКредитовИДепозитов.Валюта
	|		 И ГрафикОплатКредитовИДепозитов.ТипСуммы = ОстаткиКредитовИДепозитов.ТипСуммы
	|ГДЕ
	|	ЕСТЬNULL(ОборотыКредитовИДепозитов.СуммаПоступления, 0) > (ГрафикОплатКредитовИДепозитов.СуммаИтог - ГрафикОплатКредитовИДепозитов.Сумма)
	|	И ЕСТЬNULL(ОстаткиКредитовИДепозитов.Сумма, 0) > 0
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втГрафикОплатКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОборотыКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОстаткиКредитовИДепозитов");
	
#КонецОбласти
	
#КонецОбласти
	
#Область РасчетыПоАренде
	
#Область РеклассификацияПоступившихУслугПоАрендеВДолгосрочные
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Период КАК Период,
	|	РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла КАК ДлительностьОперационногоЦикла,
	|	РасчетыПоФинансовымИнструментам.СуммаРасход КАК Сумма
	|ПОМЕСТИТЬ втПоступленияАрендныхОбязательствПоПериодам
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(
	|		&НачалоПериода,
	|		&КонецПериода,
	|		ДЕНЬ,
	|		Договор ССЫЛКА Справочник.ДоговорыАренды
	|		И АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	) КАК РасчетыПоФинансовымИнструментам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	РасчетыПоФинансовымИнструментам.СуммаРасход > 0
	|	И УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|	И РасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор,
	|	МИНИМУМ(УсловияДоговоровАренды.Период) КАК Период
	|ПОМЕСТИТЬ втПериодыУсловийДоговоровАренды
	|ИЗ
	|	втПоступленияАрендныхОбязательствПоПериодам КАК ПоступленияАрендныхОбязательств
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ПО ПоступленияАрендныхОбязательств.Договор = УсловияДоговоровАренды.Договор
	|		 И ПоступленияАрендныхОбязательств.Период <= УсловияДоговоровАренды.Период
	|ГДЕ
	|	УсловияДоговоровАренды.Активность
	|	И УсловияДоговоровАренды.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияАрендныхОбязательств.Договор
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Период
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПериодыУсловийДоговоровАренды.Договор КАК Договор,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	втПериодыУсловийДоговоровАренды КАК ПериодыУсловийДоговоровАренды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ПО ПериодыУсловийДоговоровАренды.Договор = УсловияДоговоровАренды.Договор
	|		 И ПериодыУсловийДоговоровАренды.Период = УсловияДоговоровАренды.Период
	|ГДЕ
	|	УсловияДоговоровАренды.Активность
	|	И УсловияДоговоровАренды.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	АктуальныеУсловияДоговора
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПериодыУсловийДоговоровАренды");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор,
	|	ПоступленияАрендныхОбязательств.Валюта КАК Валюта,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, ПоступленияАрендныхОбязательств.ДлительностьОперационногоЦикла + 1), ДЕНЬ, -1) КАК НачалоДолгосрочногоПериода,
	|	МАКСИМУМ(ПоступленияАрендныхОбязательств.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(ПоступленияАрендныхОбязательств.РасчетныйДокумент) КАК РасчетныйДокумент,
	|	СУММА(ПоступленияАрендныхОбязательств.Сумма) КАК Сумма
	|ПОМЕСТИТЬ втПоступленияАрендныхОбязательств
	|ИЗ
	|	втПоступленияАрендныхОбязательствПоПериодам КАК ПоступленияАрендныхОбязательств
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияАрендныхОбязательств.Договор,
	|	ПоступленияАрендныхОбязательств.Валюта,
	|	ПоступленияАрендныхОбязательств.ДлительностьОперационногоЦикла
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПоступленияАрендныхОбязательствПоПериодам");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияПроцентов.Дата < ПоступленияАрендныхОбязательств.НачалоДолгосрочногоПериода
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияПроцентов.Проценты
	|	КОНЕЦ) КАК СуммаДолгосрочная
	|ПОМЕСТИТЬ втНачисленияПроцентовПоАренде
	|ИЗ
	|	втПоступленияАрендныхОбязательств КАК ПоступленияАрендныхОбязательств
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ПО ПоступленияАрендныхОбязательств.Договор = УсловияДоговоровАренды.Договор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентов
	|		ПО УсловияДоговоровАренды.Договор = ГрафикНачисленияПроцентов.Договор
	|		 И УсловияДоговоровАренды.АктуальныеУсловияДоговора = ГрафикНачисленияПроцентов.АктуальныеУсловияДоговора
	|		 И ГрафикНачисленияПроцентов.Активность
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияАрендныхОбязательств.Договор,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияПроцентов.Дата < ПоступленияАрендныхОбязательств.НачалоДолгосрочногоПериода
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияПроцентов.Проценты
	|	КОНЕЦ) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	АктуальныеУсловияДоговора
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	СУММА(ГрафикНачисленияУслуг.УслугаПоАренде
	|		+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж
	|		+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость
	|	) КАК СуммаИтог,
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияУслуг.Дата < ПоступленияАрендныхОбязательств.НачалоДолгосрочногоПериода
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияУслуг.УслугаПоАренде - ГрафикНачисленияУслуг.УслугаПоАрендеНДС
	|			+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж - ГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС
	|			+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость - ГрафикНачисленияУслуг.ВыкупнаяСтоимостьНДС
	|	КОНЕЦ) КАК СуммаДолгосрочная
	|ПОМЕСТИТЬ втНачисленияУслугПоАренде
	|ИЗ
	|	втПоступленияАрендныхОбязательств КАК ПоступленияАрендныхОбязательств
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ПО ПоступленияАрендныхОбязательств.Договор = УсловияДоговоровАренды.Договор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслуг
	|		ПО УсловияДоговоровАренды.Договор = ГрафикНачисленияУслуг.Договор
	|		 И УсловияДоговоровАренды.АктуальныеУсловияДоговора = ГрафикНачисленияУслуг.АктуальныеУсловияДоговора
	|		 И ГрафикНачисленияУслуг.Активность
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияАрендныхОбязательств.Договор,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияУслуг.Дата < ПоступленияАрендныхОбязательств.НачалоДолгосрочногоПериода
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияУслуг.УслугаПоАренде - ГрафикНачисленияУслуг.УслугаПоАрендеНДС
	|			+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж - ГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС
	|			+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость - ГрафикНачисленияУслуг.ВыкупнаяСтоимостьНДС
	|	КОНЕЦ) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	АктуальныеУсловияДоговора
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втУсловияДоговоровАренды");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	НачисленияУслугПоАренде.Договор КАК Договор,
	|	СУММА(НачисленияУслугПоАренде.СуммаИтог) КАК СуммаИтог,
	|	СУММА(НачисленияУслугПоАренде.СуммаДолгосрочная - ЕСТЬNULL(НачисленияПроцентовПоАренде.СуммаДолгосрочная, 0)) КАК СуммаДолгосрочная
	|ПОМЕСТИТЬ втНачисленияПоАрендеПоГрафикам
	|ИЗ
	|	втНачисленияУслугПоАренде КАК НачисленияУслугПоАренде
	|	ЛЕВОЕ СОЕДИНЕНИЕ втНачисленияПроцентовПоАренде КАК НачисленияПроцентовПоАренде
	|		ПО НачисленияУслугПоАренде.Договор = НачисленияПроцентовПоАренде.Договор
	|		 И НачисленияУслугПоАренде.АктуальныеУсловияДоговора = НачисленияПроцентовПоАренде.АктуальныеУсловияДоговора
	|ГДЕ
	|	НачисленияУслугПоАренде.СуммаДолгосрочная > ЕСТЬNULL(НачисленияПроцентовПоАренде.СуммаДолгосрочная, 0)
	|СГРУППИРОВАТЬ ПО
	|	НачисленияУслугПоАренде.Договор
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияПроцентовПоАренде");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияУслугПоАренде");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор
	|ПОМЕСТИТЬ втДоговорыАренды
	|ИЗ
	|	втПоступленияАрендныхОбязательств КАК ПоступленияАрендныхОбязательств
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	РасчетыПоФинансовымИнструментам.СуммаРасход КАК Сумма
	|ПОМЕСТИТЬ втПоступленияАрендныхОбязательствИтог
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,
	|		&КонецПериода,,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыАренды.Договор
	|		ИЗ
	|			втДоговорыАренды КАК ДоговорыАренды)
	|		И ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	РасчетыПоФинансовымИнструментам.СуммаРасход > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	-РасчетыПоФинансовымИнструментам.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ втОстаткиАрендныхОбязательств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|		&ГраницаПериода,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыАренды.Договор
	|		ИЗ
	|			втДоговорыАренды КАК ДоговорыАренды)
	|		И ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	РасчетыПоФинансовымИнструментам.СуммаОстаток < 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");

	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ПоступленияАрендныхОбязательств.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ПоступленияАрендныхОбязательств.Договор КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|	ПоступленияАрендныхОбязательств.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ПоступленияАрендныхОбязательств.Валюта КАК Валюта,
	|	ПоступленияАрендныхОбязательств.НачалоДолгосрочногоПериода КАК ДатаПлановогоПогашения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0) < ВЫБОР
	|			КОГДА (ЕСТЬNULL(ПоступленияАрендныхОбязательствИтог.Сумма, ПоступленияАрендныхОбязательств.Сумма) - ПоступленияАрендныхОбязательств.Сумма)
	|				< (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.СуммаДолгосрочная)
	|				ТОГДА ЕСТЬNULL(ПоступленияАрендныхОбязательствИтог.Сумма, ПоступленияАрендныхОбязательств.Сумма)
	|					- (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.СуммаДолгосрочная)
	|			ИНАЧЕ ПоступленияАрендныхОбязательств.Сумма
	|		КОНЕЦ
	|			ТОГДА ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0)
	|		ИНАЧЕ ВЫБОР
	|			КОГДА (ЕСТЬNULL(ПоступленияАрендныхОбязательствИтог.Сумма, ПоступленияАрендныхОбязательств.Сумма) - ПоступленияАрендныхОбязательств.Сумма)
	|				< (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.СуммаДолгосрочная)
	|				ТОГДА ЕСТЬNULL(ПоступленияАрендныхОбязательствИтог.Сумма, ПоступленияАрендныхОбязательств.Сумма)
	|					- (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.СуммаДолгосрочная)
	|			ИНАЧЕ ПоступленияАрендныхОбязательств.Сумма
	|		КОНЕЦ
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втРеклассификацияПоступленийУслугПоАренде
	|ИЗ
	|	втПоступленияАрендныхОбязательств КАК ПоступленияАрендныхОбязательств
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНачисленияПоАрендеПоГрафикам КАК НачисленияПоАрендеПоГрафикам
	|		ПО ПоступленияАрендныхОбязательств.Договор = НачисленияПоАрендеПоГрафикам.Договор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПоступленияАрендныхОбязательствИтог КАК ПоступленияАрендныхОбязательствИтог
	|		ПО ПоступленияАрендныхОбязательств.Договор = ПоступленияАрендныхОбязательствИтог.Договор
	|		 И ПоступленияАрендныхОбязательств.Валюта = ПоступленияАрендныхОбязательствИтог.Валюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиАрендныхОбязательств КАК ОстаткиАрендныхОбязательств
	|		ПО ПоступленияАрендныхОбязательств.Договор = ОстаткиАрендныхОбязательств.Договор
	|		 И ПоступленияАрендныхОбязательств.Валюта = ОстаткиАрендныхОбязательств.Валюта
	|ГДЕ
	|	ЕСТЬNULL(ПоступленияАрендныхОбязательствИтог.Сумма, ПоступленияАрендныхОбязательств.Сумма)
	|		> (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.СуммаДолгосрочная)
	|	И ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0) > 0
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыАренды");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПоступленияАрендныхОбязательств");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втПоступленияАрендныхОбязательствИтог");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОстаткиАрендныхОбязательств");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияПоАрендеПоГрафикам");
	
#КонецОбласти
	
#Область РеклассификацияОстатковРасчетовПоАрендеВКраткосрочные
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ДоговорыАренды.Ссылка КАК Ссылка,
	|	ДоговорыАренды.ВалютаВзаиморасчетов КАК Валюта,
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), ДЕНЬ, -1) КАК НачалоПериодаПогашения,
	|	ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла), МЕСЯЦ), ДЕНЬ, -1) КАК КонецПериодаПогашения
	|ПОМЕСТИТЬ втДоговорыАренды
	|ИЗ
	|	Справочник.ДоговорыАренды КАК ДоговорыАренды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ПО ДоговорыАренды.Организация = АналитикаУчетаПоПартнерам.Организация
	|		 И ДоговорыАренды.Контрагент = АналитикаУчетаПоПартнерам.Контрагент
	|		 И ДоговорыАренды.Партнер = АналитикаУчетаПоПартнерам.Партнер
	|		 И АналитикаУчетаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		 И ДоговорыАренды.НаправлениеДеятельности = АналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|		&КонецПериода,
	|		Организация В (&МассивОрганизаций)
	|	) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО ДоговорыАренды.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|ГДЕ
	|	ДоговорыАренды.Организация В (&МассивОрганизаций)
	|	И УчетнаяПолитикаФинансовогоУчета.ДлительностьОперационногоЦикла > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор КАК Договор,
	|	УсловияДоговоровАренды.НачалоПериодаПогашения КАК НачалоПериодаПогашения,
	|	МАКСИМУМ(УсловияДоговоровАренды.КонецПериодаДействия) КАК КонецПериодаПогашения
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	(ВЫБРАТЬ
	|		УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|		УсловияДоговоровАренды.Договор КАК Договор,
	|		ДоговорыАренды.НачалоПериодаПогашения КАК НачалоПериодаПогашения,
	|		МИНИМУМ(ЕСТЬNULL(
	|			ДОБАВИТЬКДАТЕ(УсловияДоговоровАрендыНеДействует.Период, ДЕНЬ, -1),
	|			ДоговорыАренды.КонецПериодаПогашения)
	|		) КАК КонецПериодаДействия
	|	ИЗ
	|		РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|			&НачалоПериода,
	|			Договор В
	|			(ВЫБРАТЬ
	|				ДоговорыАренды.Ссылка
	|			ИЗ
	|				втДоговорыАренды КАК ДоговорыАренды)
	|		) КАК УсловияДоговоровАренды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоговорыАренды КАК ДоговорыАренды
	|			ПО УсловияДоговоровАренды.Договор = ДоговорыАренды.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАрендыНеДействует
	|			ПО УсловияДоговоровАрендыНеДействует.Договор = УсловияДоговоровАренды.Договор
	|			 И УсловияДоговоровАрендыНеДействует.Период >= &НачалоПериода
	|			 И УсловияДоговоровАрендыНеДействует.Период <= ДоговорыАренды.КонецПериодаПогашения
	|			 И УсловияДоговоровАрендыНеДействует.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Закрыт)
	|			 И УсловияДоговоровАрендыНеДействует.Активность
	|	ГДЕ
	|		УсловияДоговоровАренды.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)
	|	СГРУППИРОВАТЬ ПО
	|		УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|		УсловияДоговоровАренды.Договор,
	|		ДоговорыАренды.НачалоПериодаПогашения,
	|		ДоговорыАренды.КонецПериодаПогашения
	|	ИМЕЮЩИЕ
	|		МИНИМУМ(ЕСТЬNULL(
	|			ДОБАВИТЬКДАТЕ(УсловияДоговоровАрендыНеДействует.Период, ДЕНЬ, -1),
	|			ДоговорыАренды.КонецПериодаПогашения)
	|		) >= ДоговорыАренды.НачалоПериодаПогашения
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|		УсловияДоговоровАренды.Договор КАК Договор,
	|		ДоговорыАренды.НачалоПериодаПогашения КАК НачалоПериодаПогашения,
	|		МИНИМУМ(ЕСТЬNULL(
	|			ДОБАВИТЬКДАТЕ(УсловияДоговоровАрендыНеДействует.Период, ДЕНЬ, -1),
	|			ДоговорыАренды.КонецПериодаПогашения)
	|		) КАК КонецПериодаДействия
	|	ИЗ
	|		РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоговорыАренды КАК ДоговорыАренды
	|			ПО УсловияДоговоровАренды.Договор = ДоговорыАренды.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАрендыНеДействует
	|			ПО УсловияДоговоровАрендыНеДействует.Договор = УсловияДоговоровАренды.Договор
	|			 И УсловияДоговоровАрендыНеДействует.Период > УсловияДоговоровАренды.Период
	|			 И УсловияДоговоровАрендыНеДействует.Период <= ДоговорыАренды.КонецПериодаПогашения
	|			 И УсловияДоговоровАрендыНеДействует.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Закрыт)
	|			 И УсловияДоговоровАрендыНеДействует.Активность
	|	ГДЕ
	|		УсловияДоговоровАренды.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И УсловияДоговоровАренды.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)
	|		И УсловияДоговоровАренды.Активность
	|	СГРУППИРОВАТЬ ПО
	|		УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|		УсловияДоговоровАренды.Договор,
	|		ДоговорыАренды.НачалоПериодаПогашения,
	|		ДоговорыАренды.КонецПериодаПогашения
	|	ИМЕЮЩИЕ
	|		МИНИМУМ(ЕСТЬNULL(
	|			ДОБАВИТЬКДАТЕ(УсловияДоговоровАрендыНеДействует.Период, ДЕНЬ, -1),
	|			ДоговорыАренды.КонецПериодаПогашения)
	|		) >= ДоговорыАренды.НачалоПериодаПогашения
	|	) КАК УсловияДоговоровАренды
	|СГРУППИРОВАТЬ ПО
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор,
	|	УсловияДоговоровАренды.НачалоПериодаПогашения
	|ИНДЕКСИРОВАТЬ ПО
	|	АктуальныеУсловияДоговора,
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор КАК Договор,
	|	СУММА(ГрафикНачисленияПроцентов.Проценты) КАК Сумма
	|ПОМЕСТИТЬ втНачисленияПроцентовПоАренде
	|ИЗ
	|	втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентов
	|		ПО УсловияДоговоровАренды.АктуальныеУсловияДоговора = ГрафикНачисленияПроцентов.АктуальныеУсловияДоговора
	|		 И УсловияДоговоровАренды.Договор = ГрафикНачисленияПроцентов.Договор
	|		 И ГрафикНачисленияПроцентов.Дата МЕЖДУ УсловияДоговоровАренды.НачалоПериодаПогашения И УсловияДоговоровАренды.КонецПериодаПогашения
	|		 И ГрафикНачисленияПроцентов.Активность
	|СГРУППИРОВАТЬ ПО
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор
	|ИМЕЮЩИЕ
	|	СУММА(ГрафикНачисленияПроцентов.Проценты) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	АктуальныеУсловияДоговора,
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор КАК Договор,
	|	МАКСИМУМ(ГрафикНачисленияУслуг.Дата) КАК ДатаПлановогоПогашения,
	|	СУММА(ГрафикНачисленияУслуг.УслугаПоАренде
	|		+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж
	|		+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость
	|	) КАК СуммаИтог,
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияУслуг.Дата < УсловияДоговоровАренды.НачалоПериодаПогашения
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияУслуг.УслугаПоАренде - ГрафикНачисленияУслуг.УслугаПоАрендеНДС
	|			+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж - ГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС
	|			+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость - ГрафикНачисленияУслуг.ВыкупнаяСтоимостьНДС
	|	КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ втНачисленияУслугПоАренде
	|ИЗ
	|	втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслуг
	|		ПО УсловияДоговоровАренды.АктуальныеУсловияДоговора = ГрафикНачисленияУслуг.АктуальныеУсловияДоговора
	|		 И УсловияДоговоровАренды.Договор = ГрафикНачисленияУслуг.Договор
	|		 И ГрафикНачисленияУслуг.Дата <= УсловияДоговоровАренды.КонецПериодаПогашения
	|		 И ГрафикНачисленияУслуг.Активность
	|СГРУППИРОВАТЬ ПО
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.Договор
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|		КОГДА ГрафикНачисленияУслуг.Дата < УсловияДоговоровАренды.НачалоПериодаПогашения
	|			ТОГДА 0
	|		ИНАЧЕ ГрафикНачисленияУслуг.УслугаПоАренде - ГрафикНачисленияУслуг.УслугаПоАрендеНДС
	|			+ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж - ГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС
	|			+ ГрафикНачисленияУслуг.ВыкупнаяСтоимость - ГрафикНачисленияУслуг.ВыкупнаяСтоимостьНДС
	|	КОНЕЦ) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	АктуальныеУсловияДоговора,
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втУсловияДоговоровАренды");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	ДоговорыАренды.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ДоговорыАренды.Ссылка КАК Договор,
	|	ДоговорыАренды.Валюта КАК Валюта,
	|	МАКСИМУМ(НачисленияУслугПоАренде.ДатаПлановогоПогашения) КАК ДатаПлановогоПогашения,
	|	СУММА(НачисленияУслугПоАренде.СуммаИтог) КАК СуммаИтог,
	|	СУММА(НачисленияУслугПоАренде.Сумма - ЕСТЬNULL(НачисленияПроцентовПоАренде.Сумма, 0)) КАК Сумма
	|ПОМЕСТИТЬ втНачисленияПоАрендеПоГрафикам
	|ИЗ
	|	втДоговорыАренды КАК ДоговорыАренды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНачисленияУслугПоАренде КАК НачисленияУслугПоАренде
	|		ПО ДоговорыАренды.Ссылка = НачисленияУслугПоАренде.Договор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втНачисленияПроцентовПоАренде КАК НачисленияПроцентовПоАренде
	|		ПО НачисленияУслугПоАренде.АктуальныеУсловияДоговора = НачисленияПроцентовПоАренде.АктуальныеУсловияДоговора
	|		 И НачисленияУслугПоАренде.Договор = НачисленияПроцентовПоАренде.Договор
	|ГДЕ
	|	НачисленияУслугПоАренде.Сумма > ЕСТЬNULL(НачисленияПроцентовПоАренде.Сумма, 0)
	|СГРУППИРОВАТЬ ПО
	|	ДоговорыАренды.КлючАналитики,
	|	ДоговорыАренды.Ссылка,
	|	ДоговорыАренды.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыАренды");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияПроцентовПоАренде");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияУслугПоАренде");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияПоАрендеПоГрафикам.Договор КАК Договор
	|ПОМЕСТИТЬ втДоговорыАренды
	|ИЗ
	|	втНачисленияПоАрендеПоГрафикам КАК НачисленияПоАрендеПоГрафикам
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|	МАКСИМУМ(РасчетыПоФинансовымИнструментам.РасчетныйДокумент) КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА СУММА(РасчетыПоФинансовымИнструментам.СуммаРасход) < 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(РасчетыПоФинансовымИнструментам.СуммаРасход)
	|	КОНЕЦ КАК СуммаПоступления,
	|	ВЫБОР
	|		КОГДА СУММА(РасчетыПоФинансовымИнструментам.СуммаПриход) < 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(РасчетыПоФинансовымИнструментам.СуммаПриход)
	|	КОНЕЦ КАК СуммаПогашения
	|ПОМЕСТИТЬ втОборотыАрендныхОбязательств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(,
	|		&КонецПериода,,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыАренды.Договор
	|		ИЗ
	|			втДоговорыАренды КАК ДоговорыАренды)
	|		И ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|	) КАК РасчетыПоФинансовымИнструментам
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоФинансовымИнструментам.Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментам.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментам.Валюта КАК Валюта,
	|	-РасчетыПоФинансовымИнструментам.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ втОстаткиАрендныхОбязательств
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|		&НачалоПериода,
	|		Договор В
	|		(ВЫБРАТЬ
	|			ДоговорыАренды.Договор
	|		ИЗ
	|			втДоговорыАренды КАК ДоговорыАренды)
	|		И ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|	) КАК РасчетыПоФинансовымИнструментам
	|ГДЕ
	|	РасчетыПоФинансовымИнструментам.СуммаОстаток < 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	Валюта
	|");
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	НачисленияПоАрендеПоГрафикам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	НачисленияПоАрендеПоГрафикам.Договор КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|	ЕСТЬNULL(ОборотыАрендныхОбязательств.РасчетныйДокумент, НЕОПРЕДЕЛЕНО) КАК РасчетныйДокумент,
	|	НачисленияПоАрендеПоГрафикам.Валюта КАК Валюта,
	|	НачисленияПоАрендеПоГрафикам.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0) < ВЫБОР
	|			КОГДА ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПоступления, 0) < НачисленияПоАрендеПоГрафикам.СуммаИтог
	|				ТОГДА НачисленияПоАрендеПоГрафикам.Сумма + ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПоступления, 0) - НачисленияПоАрендеПоГрафикам.СуммаИтог
	|			КОГДА ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПогашения, 0) > НачисленияПоАрендеПоГрафикам.СуммаИтог
	|				ТОГДА НачисленияПоАрендеПоГрафикам.Сумма + ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПогашения, 0) - НачисленияПоАрендеПоГрафикам.СуммаИтог
	|			ИНАЧЕ НачисленияПоАрендеПоГрафикам.Сумма
	|		КОНЕЦ
	|			ТОГДА ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0)
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПоступления, 0) < НачисленияПоАрендеПоГрафикам.СуммаИтог
	|				ТОГДА НачисленияПоАрендеПоГрафикам.Сумма + ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПоступления, 0) - НачисленияПоАрендеПоГрафикам.СуммаИтог
	|			КОГДА ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПогашения, 0) > НачисленияПоАрендеПоГрафикам.СуммаИтог
	|				ТОГДА НачисленияПоАрендеПоГрафикам.Сумма + ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПогашения, 0) - НачисленияПоАрендеПоГрафикам.СуммаИтог
	|			ИНАЧЕ НачисленияПоАрендеПоГрафикам.Сумма
	|		КОНЕЦ
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втРеклассификацияОстатковУслугПоАренде
	|ИЗ
	|	втНачисленияПоАрендеПоГрафикам КАК НачисленияПоАрендеПоГрафикам
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОборотыАрендныхОбязательств КАК ОборотыАрендныхОбязательств
	|		ПО НачисленияПоАрендеПоГрафикам.Договор = ОборотыАрендныхОбязательств.Договор
	|		 И НачисленияПоАрендеПоГрафикам.Валюта = ОборотыАрендныхОбязательств.Валюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиАрендныхОбязательств КАК ОстаткиАрендныхОбязательств
	|		ПО НачисленияПоАрендеПоГрафикам.Договор = ОстаткиАрендныхОбязательств.Договор
	|		 И НачисленияПоАрендеПоГрафикам.Валюта = ОстаткиАрендныхОбязательств.Валюта
	|ГДЕ
	|	ЕСТЬNULL(ОборотыАрендныхОбязательств.СуммаПоступления, 0) > (НачисленияПоАрендеПоГрафикам.СуммаИтог - НачисленияПоАрендеПоГрафикам.Сумма)
	|	И ЕСТЬNULL(ОстаткиАрендныхОбязательств.Сумма, 0) > 0
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втДоговорыАренды");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втНачисленияПоАрендеПоГрафикам");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОборотыАрендныхОбязательств");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втОстаткиАрендныхОбязательств");
	
#КонецОбласти
	
#КонецОбласти
	
#Область КурсыВалют
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	КурсыВалют.КурсЧислитель КАК КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&КонецПериода) КАК КурсыВалют
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта,
	|	БазоваяВалюта
	|");
	
#КонецОбласти
	
#Область ИтоговаяТаблица
	
	ТекстыЗапросов.Добавить("
	|ВЫБРАТЬ
	|	РеклассификацияФинансовыхИнструментов.РеклассификацияОстатков КАК РеклассификацияОстатков,
	|	РеклассификацияФинансовыхИнструментов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РеклассификацияФинансовыхИнструментов.Договор КАК Договор,
	|	РеклассификацияФинансовыхИнструментов.ТипСуммы КАК ТипСуммы,
	|	РеклассификацияФинансовыхИнструментов.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РеклассификацияФинансовыхИнструментов.Валюта КАК Валюта,
	|	РеклассификацияФинансовыхИнструментов.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РеклассификацияФинансовыхИнструментов.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА РеклассификацияФинансовыхИнструментов.Валюта = Организации.ВалютаРегламентированногоУчета
	|			ТОГДА РеклассификацияФинансовыхИнструментов.Сумма
	|		КОГДА КурсыВалют.Валюта ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КурсыВалют.КурсЗнаменатель = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеклассификацияФинансовыхИнструментов.Сумма
	|			* КурсыВалют.КурсЧислитель / КурсыВалют.КурсЗнаменатель
	|			КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА РеклассификацияФинансовыхИнструментов.Валюта = Константы.ВалютаУправленческогоУчета
	|			ТОГДА РеклассификацияФинансовыхИнструментов.Сумма
	|		КОГДА КурсыВалют.Валюта ЕСТЬ NULL ИЛИ КурсыВалютУпр.Валюта ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КурсыВалют.КурсЗнаменатель = 0 ИЛИ КурсыВалютУпр.КурсЧислитель = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеклассификацияФинансовыхИнструментов.Сумма
	|			* КурсыВалют.КурсЧислитель / КурсыВалют.КурсЗнаменатель
	|			/ КурсыВалютУпр.КурсЧислитель * КурсыВалютУпр.КурсЗнаменатель
	|			КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаУпр
	|ПОМЕСТИТЬ РасчетыПоФинансовымИнструментам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЛОЖЬ КАК РеклассификацияОстатков,
	|		РеклассификацияКредитовИДепозитов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РеклассификацияКредитовИДепозитов.Договор КАК Договор,
	|		РеклассификацияКредитовИДепозитов.ТипСуммы КАК ТипСуммы,
	|		РеклассификацияКредитовИДепозитов.РасчетныйДокумент КАК РасчетныйДокумент,
	|		РеклассификацияКредитовИДепозитов.Валюта КАК Валюта,
	|		РеклассификацияКредитовИДепозитов.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		РеклассификацияКредитовИДепозитов.Сумма КАК Сумма
	|	ИЗ
	|		втРеклассификацияПоступленийКредитовИДепозитов КАК РеклассификацияКредитовИДепозитов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА КАК РеклассификацияОстатков,
	|		РеклассификацияКредитовИДепозитов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РеклассификацияКредитовИДепозитов.Договор КАК Договор,
	|		РеклассификацияКредитовИДепозитов.ТипСуммы КАК ТипСуммы,
	|		РеклассификацияКредитовИДепозитов.РасчетныйДокумент КАК РасчетныйДокумент,
	|		РеклассификацияКредитовИДепозитов.Валюта КАК Валюта,
	|		РеклассификацияКредитовИДепозитов.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		РеклассификацияКредитовИДепозитов.Сумма КАК Сумма
	|	ИЗ
	|		втРеклассификацияОстатковКредитовИДепозитов КАК РеклассификацияКредитовИДепозитов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛОЖЬ КАК РеклассификацияОстатков,
	|		РеклассификацияУслугПоАренде.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РеклассификацияУслугПоАренде.Договор КАК Договор,
	|		РеклассификацияУслугПоАренде.ТипСуммы КАК ТипСуммы,
	|		РеклассификацияУслугПоАренде.РасчетныйДокумент КАК РасчетныйДокумент,
	|		РеклассификацияУслугПоАренде.Валюта КАК Валюта,
	|		РеклассификацияУслугПоАренде.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		РеклассификацияУслугПоАренде.Сумма КАК Сумма
	|	ИЗ
	|		втРеклассификацияПоступленийУслугПоАренде КАК РеклассификацияУслугПоАренде
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА КАК РеклассификацияОстатков,
	|		РеклассификацияУслугПоАренде.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РеклассификацияУслугПоАренде.Договор КАК Договор,
	|		РеклассификацияУслугПоАренде.ТипСуммы КАК ТипСуммы,
	|		РеклассификацияУслугПоАренде.РасчетныйДокумент КАК РасчетныйДокумент,
	|		РеклассификацияУслугПоАренде.Валюта КАК Валюта,
	|		РеклассификацияУслугПоАренде.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		РеклассификацияУслугПоАренде.Сумма КАК Сумма
	|	ИЗ
	|		втРеклассификацияОстатковУслугПоАренде КАК РеклассификацияУслугПоАренде
	|	) КАК РеклассификацияФинансовыхИнструментов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО РеклассификацияФинансовыхИнструментов.АналитикаУчетаПоПартнерам.Организация = Организации.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалют
	|		ПО РеклассификацияФинансовыхИнструментов.Валюта = КурсыВалют.Валюта
	|		 И Организации.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалютУпр
	|		ПО Константы.ВалютаУправленческогоУчета = КурсыВалютУпр.Валюта
	|		 И Организации.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втРеклассификацияПоступленийКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втРеклассификацияОстатковКредитовИДепозитов");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втРеклассификацияПоступленийУслугПоАренде");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втРеклассификацияОстатковУслугПоАренде");
	
	ТекстыЗапросов.Добавить("
	|УНИЧТОЖИТЬ втКурсыВалют");
	
#КонецОбласти
	
	Возврат СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

Функция ТекстЗапросаДвиженийРеклассификацииОстатковРасчетыПоФинансовымИнструментам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ВЫБОР
	|		КОГДА РасчетыПоИнструментам.Договор.ХарактерДоговора В (
	|			ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм),
	|			ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда))
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК Регистратор,
	|	РасчетыПоИнструментам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоИнструментам.Договор КАК Договор,
	|	РасчетыПоИнструментам.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоИнструментам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоИнструментам.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА РасчетыПоИнструментам.РеклассификацияОстатков
	|			ТОГДА РасчетыПоИнструментам.ДатаПлановогоПогашения
	|		ИНАЧЕ КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ)
	|	КОНЕЦ КАК ДатаПлановогоПогашения,
	|	РасчетыПоИнструментам.Сумма КАК Сумма,
	|	РасчетыПоИнструментам.СуммаРегл КАК СуммаРегл,
	|	РасчетыПоИнструментам.СуммаУпр КАК СуммаУпр,
	|	РасчетыПоИнструментам.ИдентификаторФинЗаписи КАК ИдентификаторСтроки,
	|	РасчетыПоИнструментам.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ПОМЕСТИТЬ ТаблицаДляПомещения
	|ИЗ
	|	РасчетыПоФинансовымИнструментам КАК РасчетыПоИнструментам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	ВЫБОР
	|		КОГДА РасчетыПоИнструментам.Договор.ХарактерДоговора В (
	|			ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм),
	|			ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда))
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ) КАК Период,
	|	&Регистратор КАК Регистратор,
	|	РасчетыПоИнструментам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоИнструментам.Договор КАК Договор,
	|	РасчетыПоИнструментам.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоИнструментам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоИнструментам.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА РасчетыПоИнструментам.РеклассификацияОстатков
	|			ТОГДА КОНЕЦПЕРИОДА(&ДатаОперации, МЕСЯЦ)
	|		ИНАЧЕ РасчетыПоИнструментам.ДатаПлановогоПогашения
	|	КОНЕЦ КАК ДатаПлановогоПогашения,
	|	РасчетыПоИнструментам.Сумма КАК Сумма,
	|	РасчетыПоИнструментам.СуммаРегл КАК СуммаРегл,
	|	РасчетыПоИнструментам.СуммаУпр КАК СуммаУпр,
	|	РасчетыПоИнструментам.ИдентификаторФинЗаписи КАК ИдентификаторСтроки,
	|	РасчетыПоИнструментам.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств) КАК НастройкаХозяйственнойОперации,
	|	ЛОЖЬ КАК Сторно
	|ИЗ
	|	РасчетыПоФинансовымИнструментам КАК РасчетыПоИнструментам
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
