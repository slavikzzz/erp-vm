//@strict-types

#Область ПрограммныйИнтерфейс

// Процедура предназначена для установки параметров выбора статьи расходов признания по исследованиям и разработкам.
//
// Параметры:
//	Элемент - ПолеФормы - элемент, для которого устанавливаются параметры выбора:
//	*ПараметрыВыбора - ФиксированныйМассив -
//	ВидПризнания - ПеречислениеСсылка.ВидыПризнанияРасходовПоИсследованиямИРазработкам, Неопределено - вид признания расходов
//
Процедура УстановитьПараметрыВыбораСтатьиРасхода(Элемент, ВидПризнания = Неопределено) Экспорт
	
	ЭлементыБУ = Новый Массив();
	ЭлементыБУ.Добавить("РаспределениеЗавершенияЭтапаСтатьяРасходовБУ");
	ЭлементыБУ.Добавить("РаспределениеПоИсследованиямСтатьяРасходовБУ");
	ЭлементыБУ.Добавить("СтатьяРасходовБУЗавершенияЭтапа");
	ЭлементыБУ.Добавить("СтатьяРасходовБУПоИсследованиям");
	
	ЭлементыНУ = Новый Массив();
	ЭлементыНУ.Добавить("РаспределениеЗавершенияЭтапаСтатьяРасходовНУ");
	ЭлементыНУ.Добавить("РаспределениеПоИсследованиямСтатьяРасходовНУ");
	ЭлементыНУ.Добавить("СтатьяРасходовНУЗавершенияЭтапа");
	ЭлементыНУ.Добавить("СтатьяРасходовНУПоИсследованиям");

	ЭлементыУУ = Новый Массив();
	ЭлементыУУ.Добавить("РаспределениеЗавершенияЭтапаСтатьяРасходовУУ");
	ЭлементыУУ.Добавить("РаспределениеПоИсследованиямСтатьяРасходовУУ");
	ЭлементыУУ.Добавить("СтатьяРасходовУУЗавершенияЭтапа");
	ЭлементыУУ.Добавить("СтатьяРасходовУУПоИсследованиям");
	
	ПараметрыВыбораСтатьи = Новый Массив();

	Если ВидПризнания = ПредопределенноеЗначение("Перечисление.ВидыПризнанияРасходовПоИсследованиямИРазработкам.ПризнаниеВСоставеНМА") Тогда
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовРегл", ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы")));
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.РасходыНаНМАиНИОКР", Истина));
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.РасходыНаИсследованияИРазработки", Ложь));
	ИначеЕсли ВидПризнания = ПредопределенноеЗначение("Перечисление.ВидыПризнанияРасходовПоИсследованиямИРазработкам.ПереносНаДругуюТему") Тогда
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.РасходыНаИсследованияИРазработки", Истина));
	ИначеЕсли ВидПризнания = ПредопределенноеЗначение("Перечисление.ВидыПризнанияРасходовПоИсследованиямИРазработкам.ВключитьВСоставПроизводственныхЗатрат") Тогда
		ВариантыРаспределения = Новый Массив;
		ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства"));
		ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты"));
		Если ЭлементыБУ.Найти(Элемент.Имя) <> Неопределено Тогда
			ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовРегл", ВариантыРаспределения));
		ИначеЕсли ЭлементыНУ.Найти(Элемент.Имя) <> Неопределено Тогда
			ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовНУ", ВариантыРаспределения));
		ИначеЕсли ЭлементыУУ.Найти(Элемент.Имя) <> Неопределено Тогда
			ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовУпр", ВариантыРаспределения));
		КонецЕсли;
	ИначеЕсли ВидПризнания = ПредопределенноеЗначение("Перечисление.ВидыПризнанияРасходовПоИсследованиямИРазработкам.ЗакрытьТему") Тогда
		ВидыДеятельностиРасходов = Новый Массив();
		ВидыДеятельностиРасходов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность"));
		ВидыДеятельностиРасходов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ПрочаяДеятельность"));
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВидДеятельностиРасходов", ВидыДеятельностиРасходов));
	ИначеЕсли ВидПризнания = ПредопределенноеЗначение("Перечисление.ВидыПризнанияРасходовПоИсследованиямИРазработкам.ПризнаниеВСоставеРБП") Тогда
		ВидыДеятельностиРасходов = Новый Массив();
		ВидыДеятельностиРасходов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ОсновнаяДеятельность"));
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВидДеятельностиРасходов", ВидыДеятельностиРасходов));
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовРегл", ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов")));
	Иначе
		ПараметрыВыбораСтатьи.Добавить(Новый ПараметрВыбора("Отбор.ВариантРаспределенияРасходовРегл", ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности")));
	КонецЕсли;

	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораСтатьи);
	
КонецПроцедуры

#КонецОбласти 