
#Область ПрограммныйИнтерфейс

// Возвращает статьи доходов и расходов по умолчанию для подстановки в документах.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация,
//   ВидОперации - ПеречислениеСсылка.ВидыОперацийРезервовПоОплатеТруда - вид операции,
//   ВидРезерва - СправочникСсылка.Резервы - вид резерва.
//
// Возвращаемое значение:
//   Структура - структура с полями:
//       * СтатьяДоходов  - Неопределено, ПланВидовХарактеристикСсылка.СтатьиДоходов - статья доходов,
//       * АналитикаДоходов - Неопределено, Характеристика.СтатьиДоходов - аналитика статьи доходов,
//
Функция СтатьиДоходовИРасходовПоУмолчанию(Знач Организация, Знач ВидОперации, Знач ВидРезерва) Экспорт
	
	СтатьиПоУмолчанию = Новый Структура("СтатьяДоходов, АналитикаДоходов");
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат СтатьиПоУмолчанию;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РезервыПоОплатеТруда.Дата КАК Дата,
	|	РезервыПоОплатеТруда.ВидОперации КАК ВидОперации,
	|	РезервыПоОплатеТруда.Резерв КАК ВидРезерва,
	|	РезервыПоОплатеТруда.СтатьяДоходов КАК СтатьяДоходов,
	|	РезервыПоОплатеТруда.АналитикаДоходов КАК АналитикаДоходов,
	|	ВЫБОР
	|		КОГДА РезервыПоОплатеТруда.Резерв = &ВидРезерва
	|		И РезервыПоОплатеТруда.ВидОперации = &ВидОперации
	|			ТОГДА 10
	|		КОГДА РезервыПоОплатеТруда.Резерв = &ВидРезерва
	|			ТОГДА 5
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ВТАналитикаДругихДокументов
	|ИЗ
	|	Документ.РезервыПоОплатеТруда КАК РезервыПоОплатеТруда
	|ГДЕ
	|	РезервыПоОплатеТруда.Организация = &Организация
	|	И РезервыПоОплатеТруда.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаДругихДокументов.СтатьяДоходов КАК Статья,
	|	АналитикаДругихДокументов.АналитикаДоходов КАК Аналитика,
	|	АналитикаДругихДокументов.Приоритет КАК Приоритет,
	|	АналитикаДругихДокументов.Дата КАК Дата
	|ИЗ
	|	ВТАналитикаДругихДокументов КАК АналитикаДругихДокументов
	|ГДЕ
	|	АналитикаДругихДокументов.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет УБЫВ,
	|	Дата УБЫВ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	Запрос.УстановитьПараметр("ВидРезерва", ВидРезерва);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтатьиПоУмолчанию.СтатьяДоходов = Выборка.Статья;
		СтатьиПоУмолчанию.АналитикаДоходов = Выборка.Аналитика;
	КонецЕсли;
	
	Возврат СтатьиПоУмолчанию;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Если НЕ ПроверитьНаличиеБазыРаспределения(Объект) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("СтатьяДоходов");
	Если (Объект.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		И Не ЗначениеЗаполнено(Объект.СтатьяДоходов) Тогда
		
		Для Каждого СтрокаТаблицы Из Объект.ОценочныеОбязательства Цикл
			
			Если Мин(СтрокаТаблицы.СуммаРезерва, СтрокаТаблицы.СуммаРезерваНУ,
				СтрокаТаблицы.СуммаРезерваСтраховыхВзносов, СтрокаТаблицы.СуммаРезерваСтраховыхВзносовНУ,
				СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаи, СтрокаТаблицы.СуммаРезерваФССНесчастныеСлучаиНУ) >= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Статья доходов""';
					|en = 'The ""Income item"" field is not filled'"), Объект, "СтатьяДоходов", , Отказ);
			Прервать;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик());
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура РаспределитьТаблицуОбязательствПоПодразделениямВыработки(ОценочныеОбязательстваПоСотрудникам, Организация, МесяцНачисления) Экспорт
	
	СпособыОтражения = Новый Массив;
	СписокСотрудников = Новый Массив;
	СтрокиКРаспределению = Новый Массив;
	
	Для Каждого СпособОтражения Из ОбщегоНазначения.ВыгрузитьКолонку(ОценочныеОбязательстваПоСотрудникам, "СпособОтраженияЗарплатыВБухучете", Истина) Цикл
		Если ЗначениеЗаполнено(СпособОтражения) Тогда
			СпособыОтражения.Добавить(СпособОтражения);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиОтражения = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		СпособыОтражения, "ОплатаСдельныхРабот, БазаРаспределенияПоСдельнымРаботам");
	
	Для Каждого СтрокаТаблицы Из ОценочныеОбязательстваПоСотрудникам Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете) Или НастройкиОтражения[СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете].ОплатаСдельныхРабот <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиКРаспределению.Добавить(СтрокаТаблицы);
		Если ЗначениеЗаполнено(СтрокаТаблицы.Сотрудник) Тогда
			СписокСотрудников.Добавить(СтрокаТаблицы.Сотрудник);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокиКРаспределению.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("КонецПериода",      КонецМесяца(МесяцНачисления));
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСписокФизическихЛиц
	|ИЗ Справочник.Сотрудники КАК Сотрудники
	|ГДЕ Ссылка В (&СписокСотрудников)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТрудозатратыНЗП.Сотрудник КАК ФизическоеЛицо,
	|	ТрудозатратыНЗП.Подразделение КАК Подразделение,
	|	СУММА(ТрудозатратыНЗП.НормативнаяСтоимость) КАК НормативнаяСтоимость,
	|	СУММА(ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыРабот.КратностьТрудоемкости, 0) > 0
	|			ТОГДА ВЫРАЗИТЬ(ТрудозатратыНЗП.Количество * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК ЧИСЛО(15,3))
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Длительность
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизическихЛиц КАК СписокФизическихЛиц
	|		ПО ТрудозатратыНЗП.Сотрудник = СписокФизическихЛиц.ФизическоеЛицо
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРабот
	|		ПО ТрудозатратыНЗП.ВидРабот = ВидыРабот.Ссылка
	|
	|ГДЕ
	|	ТрудозатратыНЗП.Регистратор ССЫЛКА Документ.ВыработкаСотрудников
	|	И ТрудозатратыНЗП.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТрудозатратыНЗП.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНЗП.Подразделение,
	|	ТрудозатратыНЗП.Сотрудник
	|ИТОГИ ПО
	|	ФизическоеЛицо";
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеВыработки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ФизЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокСотрудников),
		"ФизическоеЛицо");
	
	РаспределяемыеКолонки =
		"СуммаРезерва,СуммаРезерваСтраховыхВзносов,СуммаРезерваФССНесчастныеСлучаи,
		|СуммаРезерваНУ,СуммаРезерваСтраховыхВзносовНУ,СуммаРезерваФССНесчастныеСлучаиНУ";
	РаспределяемыеЗначения = Новый Структура(РаспределяемыеКолонки);
	
	Для Каждого СтрокаТаблицы Из СтрокиКРаспределению Цикл
		
		ДанныеВыработки.Сбросить();
		Если ДанныеВыработки.НайтиСледующий(ФизЛицаСотрудников[СтрокаТаблицы.Сотрудник], "ФизическоеЛицо") Тогда
			ВыработкаПоПодразделениям = ДанныеВыработки.Выбрать();
		Иначе Продолжить;
		КонецЕсли;
		
		БазаРаспределения = "НормативнаяСтоимость";
		Если НастройкиОтражения[СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете].БазаРаспределенияПоСдельнымРаботам =
			Перечисления.ТипыБазыРаспределенияРасходов.ДлительностьВыполненияРабот Тогда
			БазаРаспределения = "Длительность";
		КонецЕсли;
		
		ПодразделенияВыработки = Новый Массив;
		Коэффициенты = Новый Массив;
		Результат = Новый Структура;
		
		Пока ВыработкаПоПодразделениям.Следующий() Цикл
			ПодразделенияВыработки.Добавить(ВыработкаПоПодразделениям.Подразделение);
			Коэффициенты.Добавить(ВыработкаПоПодразделениям[БазаРаспределения]);
		КонецЦикла;
		
		Если Коэффициенты.ВГраница() = 0 Тогда
			СтрокаТаблицы.МестоВСтруктуреПредприятия = ПодразделенияВыработки[0];
			Продолжить;
		ИначеЕсли Коэффициенты.ВГраница() < 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РаспределяемыеЗначения, СтрокаТаблицы);
		
		Для Каждого Колонка Из РаспределяемыеЗначения Цикл
			Если ЗначениеЗаполнено(Колонка.Значение) Тогда
				РезультатРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Колонка.Значение, Коэффициенты);
				Результат.Вставить(Колонка.Ключ, РезультатРаспределения);
			КонецЕсли;
		КонецЦикла;
		
		ИндексСтроки = ОценочныеОбязательстваПоСотрудникам.Индекс(СтрокаТаблицы);
		Для Индекс = 0 По ПодразделенияВыработки.ВГраница() Цикл
			
			НоваяСтрока = ОценочныеОбязательстваПоСотрудникам.Вставить(ИндексСтроки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы,, РаспределяемыеКолонки);
			НоваяСтрока.МестоВСтруктуреПредприятия = ПодразделенияВыработки[Индекс];
			
			Для Каждого Колонка Из Результат Цикл
				НоваяСтрока[Колонка.Ключ] = Колонка.Значение[Индекс];
			КонецЦикла;
			
		КонецЦикла;
		
		ОценочныеОбязательстваПоСотрудникам.Удалить(СтрокаТаблицы);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПроведениеДокумента

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОтражениеЗарплатыВФинансовомУчете");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНЗП");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка или объект на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	ИмяДокумента = Документ.Метаданные().Имя;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект." + ИмяДокумента) Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос        = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТрудозатратыНезавершенногоПроизводства(ТекстыЗапроса, Регистры);
		ТекстЗапросаОтражениеЗарплатыВФинансовомУчете(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаПрочиеДоходы(ТекстыЗапроса, Регистры);
		ТекстЗапросаПрочиеРасходы(ТекстыЗапроса, Регистры);
		ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
		ТекстЗапросаПартииПрочихРасходов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса, Регистры);
		ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
		ТекстЗапросаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ДополнитьПараметрыЗапроса(Запрос, ТекстыЗапроса);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	ПолноеИмяДокумента = Метаданные.Документы.РезервыПоОплатеТруда.ПолноеИмя();
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	ЗначенияПараметров.Вставить("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.НачислениеОценочныхОбязательствПоОтпускам);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ПереопределениеРасчетаПараметров = Новый Структура;
		ПереопределениеРасчетаПараметров.Вставить("Период", "КОНЕЦПЕРИОДА(ТаблицаДокументаОбновлениеИБ.Ссылка.Дата, МЕСЯЦ)");
		
		ТекстЗапроса = ТекстЗапросаРеестрДокументов(Новый СписокЗначений, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		
	Иначе
		
		ТекстИсключения = НСтр("ru = 'В документе %1 не реализована адаптация текста запроса формирования движений по регистру %2.';
								|en = 'In the %1 document, adaptation of request for generating records of the %2 register is not implemented.'");
		ВызватьИсключение СтрШаблон(ТекстИсключения, ПолноеИмяДокумента, ИмяРегистра);
		
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Ложь, ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПроведениеДокументаРегламентированныйУчет

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Параметры:
//  ИмяДокумента - Строка - имя документа
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете(ИмяДокумента) Экспорт
	
	ТекстЗапроса =
	// на расходы (Дт <счет затрат> :: Кт <96.01.1, 96.01.2>)
	"ВЫБРАТЬ
	|	РасходыДокумента.Ссылка КАК Ссылка,
	|	РасходыДокумента.ПериодРегистрации КАК Период,
	|	РасходыДокумента.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	РасходыДокумента.СуммаРегл КАК Сумма,
	|	ВЫРАЗИТЬ(РасходыДокумента.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31, 2)) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПорядокОтражения.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РасходыДокумента.Подразделение КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РасходыДокумента.Подразделение КАК ПодразделениеДт,
	|	ПорядокОтражения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ПорядокОтражения.Счет КАК СчетДт,
	|	ПорядокОтражения.СтатьяРасходов КАК СубконтоДт1,
	|	ВЫБОР ПорядокОтражения.АналитикаРасходов
	|		КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА РасходыДокумента.Подразделение
	|		КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА РасходыДокумента.Организация
	|		ИНАЧЕ ПорядокОтражения.АналитикаРасходов
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР ПорядокОтражения.ВариантРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|			ВЫБОР
	|				КОГДА ПорядокОтражения.АналитикаРасходов ССЫЛКА Справочник.ОбъектыСтроительства
	|					И ОбъектыСтроительства.СпособСтроительства = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ)
	|				ИНАЧЕ ОбъектыСтроительства.СпособСтроительства
	|			КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	РасходыДокумента.СуммаНУ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА ПорядокОтражения.ПринимаютсяКНалоговомуУчету ТОГДА 0
	|		ИНАЧЕ РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР
	|		КОГДА НЕ ПорядокОтражения.ПринимаютсяКНалоговомуУчету ТОГДА 0
	|		ИНАЧЕ РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ
	|	КОНЕЦ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	РасходыДокумента.СчетУчета КАК СчетКт,
	|	РасходыДокумента.ВидРезервов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	РасходыДокумента.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыДокумента.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ КАК СуммаВРКт,
	|
	|	СодержаниеПроводок.Содержание КАК Содержание
	|
	|ИЗ
	|	ВТРасходыДокумента КАК РасходыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСодержаниеПроводок КАК СодержаниеПроводок
	|		ПО РасходыДокумента.ВидОперации = СодержаниеПроводок.ВидОперации
	|			И РасходыДокумента.ВидНачисления = СодержаниеПроводок.ВидНачисления
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокОтражения КАК ПорядокОтражения
	|		ПО РасходыДокумента.ВидНачисления = ПорядокОтражения.ВидНачисления
	|			И РасходыДокумента.СпособОтражения = ПорядокОтражения.СпособОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|		ПО ПорядокОтражения.АналитикаРасходов = ОбъектыСтроительства.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(РасходыДокумента.ПериодРегистрации, День)
	|
	|ГДЕ
	|	ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|	И НЕ (ПорядокОтражения.ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|		И ПорядокОтражения.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие операции (устаревшее) (Дт <прочие> :: Кт <96.01.1, 96.01.2>)
	|ВЫБРАТЬ
	|	РасходыДокумента.Ссылка КАК Ссылка,
	|	РасходыДокумента.ПериодРегистрации КАК Период,
	|	РасходыДокумента.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	РасходыДокумента.СуммаРегл КАК Сумма,
	|	ВЫРАЗИТЬ(РасходыДокумента.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31, 2)) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РасходыДокумента.Подразделение КАК ПодразделениеДт,
	|	ПорядокОтражения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ПорядокОтражения.Счет КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	РасходыДокумента.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	РасходыДокумента.СчетУчета КАК СчетКт,
	|	РасходыДокумента.ВидРезервов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	РасходыДокумента.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыДокумента.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ КАК СуммаВРКт,
	|
	|	СодержаниеПроводок.Содержание КАК Содержание
	|
	|ИЗ
	|	ВТРасходыДокумента КАК РасходыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСодержаниеПроводок КАК СодержаниеПроводок
	|		ПО РасходыДокумента.ВидОперации = СодержаниеПроводок.ВидОперации
	|			И РасходыДокумента.ВидНачисления = СодержаниеПроводок.ВидНачисления
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокОтражения КАК ПорядокОтражения
	|		ПО РасходыДокумента.ВидНачисления = ПорядокОтражения.ВидНачисления
	|			И РасходыДокумента.СпособОтражения = ПорядокОтражения.СпособОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(РасходыДокумента.ПериодРегистрации, День)
	|
	|ГДЕ
	|	ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|	И ПорядокОтражения.ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И ПорядокОтражения.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие пассивы (Дт <прочие> :: Кт <96.01.1, 96.01.2>)
	|ВЫБРАТЬ
	|	РасходыДокумента.Ссылка КАК Ссылка,
	|	РасходыДокумента.ПериодРегистрации КАК Период,
	|	РасходыДокумента.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	РасходыДокумента.СуммаРегл КАК Сумма,
	|	ВЫРАЗИТЬ(РасходыДокумента.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31, 2)) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РасходыДокумента.Подразделение КАК ПодразделениеДт,
	|	ПорядокОтражения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ПорядокОтражения.Счет КАК СчетДт,
	|	ПорядокОтражения.Субконто1 КАК СубконтоДт1,
	|	ПорядокОтражения.Субконто2 КАК СубконтоДт2,
	|	ПорядокОтражения.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	РасходыДокумента.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	РасходыДокумента.СчетУчета КАК СчетКт,
	|	РасходыДокумента.ВидРезервов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	РасходыДокумента.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыДокумента.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	РасходыДокумента.СуммаРегл - РасходыДокумента.СуммаНУ КАК СуммаВРКт,
	|
	|	СодержаниеПроводок.Содержание КАК Содержание
	|
	|ИЗ
	|	ВТРасходыДокумента КАК РасходыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСодержаниеПроводок КАК СодержаниеПроводок
	|		ПО РасходыДокумента.ВидОперации = СодержаниеПроводок.ВидОперации
	|			И РасходыДокумента.ВидНачисления = СодержаниеПроводок.ВидНачисления
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокОтражения КАК ПорядокОтражения
	|		ПО РасходыДокумента.ВидНачисления = ПорядокОтражения.ВидНачисления
	|			И РасходыДокумента.СпособОтражения = ПорядокОтражения.СпособОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(РасходыДокумента.ПериодРегистрации, День)
	|
	|ГДЕ
	|	ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие доходы (Дт <96.01.1, 96.01.2> :: Кт <прочие>)
	|ВЫБРАТЬ
	|	ДоходыДокумента.Ссылка КАК Ссылка,
	|	ДоходыДокумента.ПериодРегистрации КАК Период,
	|	ДоходыДокумента.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	ДоходыДокумента.СуммаРегл КАК Сумма,
	|	ВЫРАЗИТЬ(ДоходыДокумента.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31, 2)) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ДоходыДокумента.Подразделение КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	
	|	ДоходыДокумента.СчетУчета КАК СчетДт,
	|	ДоходыДокумента.ВидРезервов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	ДоходыДокумента.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	ДоходыДокумента.СуммаРегл - ДоходыДокумента.СуммаНУ КАК СуммаВРДт,
	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	ДоходыДокумента.СтатьяДоходов КАК АналитикаУчетаКт,
	|	ДоходыДокумента.Подразделение КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	ДоходыДокумента.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ДоходыДокумента.СтатьяДоходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	ДоходыДокумента.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ДоходыДокумента.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	ДоходыДокумента.СуммаРегл - ДоходыДокумента.СуммаНУ КАК СуммаВРКт,
	|	СодержаниеПроводок.Содержание КАК Содержание
	|
	|ИЗ
	|	ВТДоходыДокумента КАК ДоходыДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСодержаниеПроводок КАК СодержаниеПроводок
	|		ПО ДоходыДокумента.ВидОперации = СодержаниеПроводок.ВидОперации
	|			И ДоходыДокумента.ВидНачисления = СодержаниеПроводок.ВидНачисления
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(ДоходыДокумента.ПериодРегистрации, День)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// прямые производственные
	|ВЫБРАТЬ
	|	РасходыСдельные.Ссылка КАК Ссылка,
	|	РасходыСдельные.ПериодРегистрации КАК Период,
	|	РасходыСдельные.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	РасходыСдельные.СуммаРегл КАК Сумма,
	|	РасходыСдельные.СуммаУУ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	РасходыСдельные.ГруппаПродукции КАК АналитикаУчетаДт,
	|	РасходыСдельные.Подразделение КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РасходыСдельные.Подразделение КАК ПодразделениеДт,
	|	РасходыСдельные.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	РасходыСдельные.ВидРабот КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.СдельнаяОплатаТруда) КАК СубконтоДт2,
	|	РасходыСдельные.ГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	РасходыСдельные.СуммаРегл - РасходыСдельные.ПостояннаяРазница - РасходыСдельные.ВременнаяРазница КАК СуммаНУДт,
	|	РасходыСдельные.ПостояннаяРазница КАК СуммаПРДт,
	|	РасходыСдельные.ВременнаяРазница КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	РасходыСдельные.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	РасходыСдельные.СчетУчета КАК СчетКт,
	|	РасходыСдельные.ВидРезервов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	РасходыСдельные.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыСдельные.СуммаРегл - РасходыСдельные.ПостояннаяРазница - РасходыСдельные.ВременнаяРазница КАК СуммаНУКт,
	|	РасходыСдельные.ПостояннаяРазница КАК СуммаПРКт,
	|	РасходыСдельные.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Распределение оценочных обязательств по выработке"" КАК Содержание
	|
	|ИЗ
	|	ВТРасходыСдельные КАК РасходыСдельные
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете.
//
// Параметры:
//  ИмяДокумента - Строка - имя документа
//
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчете(ИмяДокумента) Экспорт
	
	ТекстыЗапроса = Новый СписокЗначений;
	ДобавитьТекстЗапросаВТДанныеДокументовРегл(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТРасходыДокумента(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТДоходыДокумента(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТПорядокОтражения(ТекстыЗапроса);
	
	ТекстЗапросаВТРасходыСдельные = 
	"ВЫБРАТЬ
	|	СдельныеНачисления.Организация КАК Организация,
	|	СдельныеНачисления.Подразделение КАК Подразделение,
	|	СдельныеНачисления.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СдельныеНачисления.ТипНалога КАК ТипОбязательства,
	|	СдельныеНачисления.ГруппаПродукции КАК ГруппаПродукции,
	|	СдельныеНачисления.ВидРабот КАК ВидРабот,
	|
	|	СУММА(СдельныеНачисления.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВЫРАЗИТЬ(СдельныеНачисления.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31, 2))) КАК СуммаУУ,
	|	СУММА(СдельныеНачисления.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(СдельныеНачисления.ВременнаяРазница) КАК ВременнаяРазница,
	|
	|	ВЫБОР СдельныеНачисления.ТипНалога
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоСтраховымВзносам)
	|	КОНЕЦ КАК СчетУчета,
	|	ДанныеДокументов.ВидРезервов КАК ВидРезервов,
	|
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ) КАК ПериодРегистрации,
	|	ДанныеДокументов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВТРасходыСдельные
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК СдельныеНачисления
	|	ПО ДанныеДокументов.Ссылка = СдельныеНачисления.Регистратор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(СдельныеНачисления.Период, МЕСЯЦ), День)
	|
	|СГРУППИРОВАТЬ ПО
	|	СдельныеНачисления.Организация,
	|	СдельныеНачисления.Подразделение,
	|	СдельныеНачисления.НаправлениеДеятельности,
	|	СдельныеНачисления.ТипНалога,
	|	СдельныеНачисления.ГруппаПродукции,
	|	СдельныеНачисления.ВидРабот,
	|	ДанныеДокументов.ВидРезервов,
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ),
	|	ДанныеДокументов.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТРасходыСдельные, "ВТРасходыСдельные");
	
	ТекстЗапросаВТСодержаниеПроводок = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление) КАК ВидОперации,
	|	""Начисление обязательств (резервов) по оплате труда"" КАК Содержание,
	|	1 КАК ВидНачисления
	|ПОМЕСТИТЬ ВТСодержаниеПроводок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|	""Начисление страховых взносов с обязательств (резервов) по оплате труда"",
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|	""Начисление взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда"",
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация),
	|	""Инвентаризация обязательств (резервов) по оплате труда"",
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация),
	|	""Инвентаризация страховых взносов с обязательств (резервов) по оплате труда"",
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация),
	|	""Инвентаризация взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда"",
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание),
	|	""Списание обязательств (резервов) по оплате труда"",
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание),
	|	""Списание страховых взносов с обязательств (резервов) по оплате труда"",
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание),
	|	""Списание взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда"",
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка),
	|	""Корректировка обязательств (резервов) по оплате труда"",
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка),
	|	""Корректировка страховых взносов с обязательств (резервов) по оплате труда"",
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка),
	|	""Корректировка взносов в ФСС от НС и ПЗ с обязательств (резервов) по оплате труда"",
	|	3";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТСодержаниеПроводок, "ВТСодержаниеПроводок");
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса.ВыгрузитьЗначения(), ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоэффициентПересчетаВВалютуУпр", "1");
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

#КонецОбласти

#Область ОбработчикиЭтапаЗакрытияМесяца

Процедура Использование_НачислениеРезервовПоОплатеТруда(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(ПараметрыОбработчика,
			НСтр("ru = 'Учет зарплаты не ведется.';
				|en = 'Payroll accounting is not kept.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	ИспользуютсяРезервыПоОплатеТруда = ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда");
	ИспользуютсяРезервыОтпусков = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетРезервовОтпусков");
	
	Если Не (ИспользуютсяРезервыПоОплатеТруда ИЛИ ИспользуютсяРезервыОтпусков) Тогда
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(ПараметрыОбработчика,
			НСтр("ru = 'Учет резервов по оплате труда не ведется.';
				|en = 'Payroll funds are not accounted.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.УстановитьПараметр("ИспользуютсяРезервыПоОплатеТруда", ИспользуютсяРезервыПоОплатеТруда);
	Запрос.УстановитьПараметр("ИспользуютсяРезервыОтпусков", ИспользуютсяРезервыОтпусков);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Настройки.Резерв КАК Резерв,
	|	Настройки.Организация КАК Организация
	|ПОМЕСТИТЬ ОрганизацииФормирующиеРезервы
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК Настройки
	|ГДЕ
	|	&ИспользуютсяРезервыПоОплатеТруда
	|	И НЕ Настройки.ПометкаУдаления
	|	И Настройки.Организация В (&МассивОрганизаций)
	|	И &НачалоПериода МЕЖДУ НАЧАЛОПЕРИОДА(Настройки.НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(Настройки.КонецПериода, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков),
	|	Настройки.Организация
	|ИЗ
	|	РегистрСведений.НастройкиРасчетаРезервовОтпусков.СрезПоследних(
	|			&НачалоПериода, Организация В (&МассивОрганизаций)
	|		) КАК Настройки
	|ГДЕ
	|	&ИспользуютсяРезервыОтпусков
	|	И Настройки.ФормироватьРезервОтпусковБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Организация
	|ПОМЕСТИТЬ ОрганизацииСНачислениемЗарплаты
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам.Обороты(
	|			&НачалоПериода, &ГраницаКонецПериода, ,
	|			Организация В (ВЫБРАТЬ Т.Организация ИЗ ОрганизацииФормирующиеРезервы КАК Т)
	|		) КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентныйДокумент.Резерв КАК Резерв,
	|	РегламентныйДокумент.Организация КАК Организация,
	|	РегламентныйДокумент.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РегламентныеДокументы
	|ИЗ
	|	ОрганизацииФормирующиеРезервы КАК Организации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РезервыПоОплатеТруда КАК РегламентныйДокумент
	|		ПО Организации.Организация = РегламентныйДокумент.Организация
	|			И РегламентныйДокумент.МесяцНачисления = &НачалоПериода
	|			И РегламентныйДокумент.Резерв = Организации.Резерв
	|			И РегламентныйДокумент.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Резерв,
	|	РегламентныйДокумент.Организация КАК Организация,
	|	РегламентныйДокумент.Ссылка КАК Ссылка
	|ИЗ
	|	ОрганизацииФормирующиеРезервы КАК Организации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам КАК РегламентныйДокумент
	|		ПО Организации.Организация = РегламентныйДокумент.Организация
	|			И РегламентныйДокумент.ПериодРегистрации = &НачалоПериода
	|			И РегламентныйДокумент.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Резерв,
	|	Организации.Организация
	|ПОМЕСТИТЬ ОрганизацииБезРегламентныхДокументов
	|ИЗ
	|	ОрганизацииФормирующиеРезервы КАК Организации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОрганизацииСНачислениемЗарплаты КАК НачисленияЗарплаты
	|		ПО Организации.Организация = НачисленияЗарплаты.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегламентныеДокументы КАК РегламентныйДокумент
	|		ПО РегламентныйДокумент.Организация = Организации.Организация
	|			И РегламентныйДокумент.Резерв = Организации.Резерв
	|ГДЕ
	|	РегламентныйДокумент.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Организация,
	|	Организации.Резерв
	|ИЗ
	|	ОрганизацииБезРегламентныхДокументов КАК Организации";
	
	Результат = Запрос.Выполнить();
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	Если РазмерыВременныхТаблиц.ОрганизацииФормирующиеРезервы = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(ПараметрыОбработчика,
			НСтр("ru = 'Нет организаций, формирующих резервы на оплату труда.';
				|en = 'No companies that generate payroll funds.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	ИначеЕсли РазмерыВременныхТаблиц.ОрганизацииСНачислениемЗарплаты = 0 Тогда
			
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(ПараметрыОбработчика,
			НСтр("ru = 'По организациям, формирующим резервы на оплату труда, отсутствуют начисления зарплаты.';
				|en = 'There are no payroll accruals by companies that generate payroll funds.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Иначе
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(ПараметрыОбработчика,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'По организации ""%1"" за период %2 не сформирован регламентный документ для резерва ""%3"".';
						|en = 'For the %1 company, a period-end transaction document is not generated for the %3 payroll fund for the period %2.'", ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Организация, РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации), Выборка.Резерв));
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_НачислениеРезервовПоОплатеТруда(ПараметрыОбработчика) Экспорт
	
	// Этап не выполняется при автоматическом тестировании.
	Если ПараметрыОбработчика.АвтоматическоеТестирование Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуютсяРезервыПоОплатеТруда = ПолучитьФункциональнуюОпцию("ИспользоватьРезервыПоОплатеТруда");
	ИспользуютсяРезервыОтпусков = Не ИспользуютсяРезервыПоОплатеТруда
		И ПолучитьФункциональнуюОпцию("ИспользоватьРасчетРезервовОтпусков");
	
	Если ИспользуютсяРезервыПоОплатеТруда Тогда
		СформироватьДокументыНачисленияРезервовПоОплатеТруда(
			ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
			ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации);
	КонецЕсли;
	
	Если ИспользуютсяРезервыОтпусков Тогда
		ОтражениеЗарплатыВФинансовомУчетеУП.НачислениеООПриЗакрытииМесяца(
			ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
			ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Возвращает параметры выбора статей в документе.
//
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	МассивПараметровВыбора = Новый Массив;
	
	// Статья доходов
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	
	Возврат МассивПараметровВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверитьНаличиеБазыРаспределения(Объект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Объект.МесяцНачисления));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Объект.МесяцНачисления));
	Запрос.УстановитьПараметр("Таблица", Объект.ОценочныеОбязательства.Выгрузить(,
		"МестоВСтруктуреПредприятия, СпособОтраженияЗарплатыВБухучете, НомерСтроки"));
	Запрос.УстановитьПараметр("Сводно", Истина);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	Таблица.МестоВСтруктуреПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	Таблица.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ТЧОценочныеОбязательства
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОценочныеОбязательства.Организация КАК Организация,
	|	ОценочныеОбязательства.Подразделение КАК Подразделение,
	|	ОценочныеОбязательства.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПорядокОтражения.БазаРаспределенияПоСдельнымРаботам КАК БазаРаспределения,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТРасходыРаспределяемые
	|ИЗ
	|	ТЧОценочныеОбязательства КАК ОценочныеОбязательства
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК ПорядокОтражения
	|	ПО ОценочныеОбязательства.СпособОтражения = ПорядокОтражения.Ссылка
	|		И ПорядокОтражения.ОплатаСдельныхРабот";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТРасходыРаспределяемые");
	
	ОтражениеЗарплатыВФинансовомУчетеУП.ДобавитьТекстЗапросаВТТрудозатратыНЗП(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РаспределяемыеРасходы.НомерСтроки КАК НомерСтроки,
	|	РаспределяемыеРасходы.Подразделение КАК Подразделение,
	|	РаспределяемыеРасходы.БазаРаспределения КАК БазаРаспределения,
	|	СУММА(ЕСТЬNULL(ТрудозатратыНЗП.НормативнаяСтоимость, 0)) КАК НормативнаяСтоимость
	|ИЗ
	|	ВТРасходыРаспределяемые КАК РаспределяемыеРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТТрудозатраты КАК ТрудозатратыНЗП
	|	ПО РаспределяемыеРасходы.Организация = ТрудозатратыНЗП.Организация
	|		И РаспределяемыеРасходы.Подразделение = ТрудозатратыНЗП.Подразделение
	|		И РаспределяемыеРасходы.ФизическоеЛицо = ТрудозатратыНЗП.ФизическоеЛицо
	|		И РаспределяемыеРасходы.БазаРаспределения = ТрудозатратыНЗП.БазаРаспределения
	|
	|ГДЕ
	|	ВЫБОР РаспределяемыеРасходы.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
	|			ТОГДА ЕСТЬNULL(ТрудозатратыНЗП.НормативнаяСтоимость, 0)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ДлительностьВыполненияРабот)
	|			ТОГДА ЕСТЬNULL(ТрудозатратыНЗП.Длительность, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределяемыеРасходы.НомерСтроки,
	|	РаспределяемыеРасходы.Подразделение,
	|	РаспределяемыеРасходы.БазаРаспределения
	|
	|УПОРЯДОЧИТЬ ПО
	|	РаспределяемыеРасходы.НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса.ВыгрузитьЗначения(), ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ДлительностьВыполненияРабот И Выборка.НормативнаяСтоимость > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не задана трудоемкость выполннных работ в подразделении ""%1"" (строка %2 списка ""Обязательства и резервы"")';
									|en = 'Labor input of the works performed is not specified in the %1 business unit (line %2 of the ""Provisions and payroll funds"" list)'");
		Иначе
			ТекстСообщения = НСтр("ru = 'В подразделении ""%1"" не регистрировалась выработка сотрудников (строка %2 списка ""Обязательства и резервы"")';
									|en = 'In the %1 business unit, employee output was not registered (line %2 of the ""Provisions and payroll funds"" list)'")
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, Выборка.Подразделение, Выборка.НомерСтроки), Объект,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОценочныеОбязательства", Выборка.НомерСтроки, "МестоВСтруктуреПредприятия"));
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Резерв КАК ВидРезервов,
	|	ДанныеШапки.МесяцНачисления КАК Период,
	|	ДанныеШапки.МесяцНачисления КАК МесяцНачисления,
	|	ДанныеШапки.Организация КАК Организация,
	|	ДанныеШапки.ВидОперации КАК ВидОперации,
	|	ДанныеШапки.Проведен КАК Проведен,
	|	ДанныеШапки.Ссылка КАК Ссылка,
	|	ДанныеШапки.Номер КАК Номер,
	|	ДанныеШапки.СтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеШапки.АналитикаДоходов КАК АналитикаДоходов,
	|	ВЫРАЗИТЬ(ДанныеШапки.Комментарий КАК СТРОКА(100)) КАК Комментарий,
	|	ДанныеШапки.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеШапки.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.РезервыПоОплатеТруда КАК ДанныеШапки
	|
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);
	Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		ВалютаРегл, ВалютаРегл, КонецМесяца(Реквизиты.МесяцНачисления), Реквизиты.Организация);
	
	Запрос.УстановитьПараметр("Период",                         КонецМесяца(Реквизиты.МесяцНачисления));
	Запрос.УстановитьПараметр("ДатаНачала",                     НачалоМесяца(Реквизиты.МесяцНачисления));
	Запрос.УстановитьПараметр("ДатаОкончания",                  КонецМесяца(Реквизиты.МесяцНачисления));
	Запрос.УстановитьПараметр("Сводно",                         Истина);
	Запрос.УстановитьПараметр("ПроводкиПоРаботникам",           Ложь);
	Запрос.УстановитьПараметр("ВалютаРегл",                     ВалютаРегл);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("НомерНаПечать",                  ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("ХозОперацияДляРеестра",          Перечисления.ХозяйственныеОперации.НачислениеОценочныхОбязательствПоОтпускам);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",        ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РезервыПоОплатеТруда"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ДополнитьПараметрыЗапроса(Запрос, ТекстыЗапроса)
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТВыработкаСотрудников", ТекстыЗапроса) Тогда
		
		ТекстыВложенногоЗапроса = Новый СписокЗначений;
		ДобавитьТекстЗапросаВТРасходыРаспределяемые(ТекстыВложенногоЗапроса);
		
		Запрос.УстановитьПараметр("ТаблицаВыработкаСотрудников", 
			ОтражениеЗарплатыВФинансовомУчетеУП.РаспределитьРасходыНаОплатуТрудаПоВыработке(Запрос.Параметры, ТекстыВложенногоЗапроса));
		
	КонецЕсли;
	
КонецПроцедуры

#Область ТекстыЗапросаВременныхТаблиц

Процедура ДобавитьТекстЗапросаВТДанныеДокументов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТДанныеДокументов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	&МесяцНачисления КАК МесяцНачисления,
	|	&ВидРезервов КАК ВидРезервов,
	|	&Организация КАК Организация,
	|	&ВидОперации КАК ВидОперацииДокумента,
	|	&СтатьяДоходов КАК СтатьяДоходов,
	|	&АналитикаДоходов КАК АналитикаСтатьиДоходов
	|
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ГДЕ
	|	&Проведен";
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТДанныеДокументовРегл(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТДанныеДокументов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.МесяцНачисления КАК МесяцНачисления,
	|	ДанныеДокумента.Резерв КАК ВидРезервов,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ВидОперации КАК ВидОперацииДокумента,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов КАК АналитикаСтатьиДоходов
	|
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РезервыПоОплатеТруда КАК ДанныеДокумента
	|		ПО ДокументыКОтражению.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеДокумента.Проведен";
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаТЧОценочныеОбязательства(ТекстыЗапроса)
	
	ИмяТаблицы = "ТЧОценочныеОбязательства";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТДанныеДокументов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.МестоВСтруктуреПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ТабличнаяЧасть.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения,
	|	ТабличнаяЧасть.СуммаРезерва КАК СуммаРезерва,
	|	ТабличнаяЧасть.СуммаРезерваНУ КАК СуммаРезерваНУ,
	|	ТабличнаяЧасть.СуммаРезерваСтраховыхВзносов КАК СуммаРезерваСтраховыхВзносов,
	|	ТабличнаяЧасть.СуммаРезерваСтраховыхВзносовНУ КАК СуммаРезерваСтраховыхВзносовНУ,
	|	ТабличнаяЧасть.СуммаРезерваФССНесчастныеСлучаи КАК СуммаРезерваФССНесчастныеСлучаи,
	|	ТабличнаяЧасть.СуммаРезерваФССНесчастныеСлучаиНУ КАК СуммаРезерваФССНесчастныеСлучаиНУ,
	|	ТабличнаяЧасть.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ТЧОценочныеОбязательства
	|ИЗ
	|	Документ.РезервыПоОплатеТруда.ОценочныеОбязательства КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументов КАК ДанныеДокументов
	|		ПО ТабличнаяЧасть.Ссылка = ДанныеДокументов.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТОценочныеОбязательства";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаТЧОценочныеОбязательства(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Резерв по вознаграждениям
	"ВЫБРАТЬ
	|	1 КАК ВидНачисления,
	|	ДанныеДокументов.Организация КАК Организация,
	|	РасшифровкаОбязательств.Подразделение КАК Подразделение,
	|	РасшифровкаОбязательств.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасшифровкаОбязательств.СуммаРезерва КАК Сумма,
	|	ВЫРАЗИТЬ(РасшифровкаОбязательств.СуммаРезерва * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31, 2)) КАК СуммаУпр,
	|	РасшифровкаОбязательств.СуммаРезерваНУ КАК СуммаНУ,
	|	РасшифровкаОбязательств.СпособОтражения КАК СпособОтражения,
	|	ДанныеДокументов.ВидРезервов КАК ВидРезервов,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПоВознаграждениям) КАК ХозяйственнаяОперацияСписание,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоВознаграждениям) КАК НастройкаХозяйственнойОперации,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПоВознаграждениям) КАК НастройкаХозяйственнойОперацииСписание,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям) КАК ТипОбязательства,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям) КАК СтатьяАктивовПассивов,
	|	ДанныеДокументов.ВидРезервов КАК АналитикаСтатьиАктивовПассивов,
	|	РасшифровкаОбязательств.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента КАК ВидОперацииДокумента,
	|	РасшифровкаОбязательств.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокументов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВТОценочныеОбязательства
	|ИЗ
	|	ТЧОценочныеОбязательства КАК РасшифровкаОбязательств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументов КАК ДанныеДокументов
	|		ПО РасшифровкаОбязательств.Ссылка = ДанныеДокументов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Резерв по страховым взносам
	|ВЫБРАТЬ
	|	2,
	|	ДанныеДокументов.Организация,
	|	РасшифровкаОбязательств.Подразделение,
	|	РасшифровкаОбязательств.ФизическоеЛицо,
	|	РасшифровкаОбязательств.СуммаРезерваСтраховыхВзносов,
	|	ВЫРАЗИТЬ(РасшифровкаОбязательств.СуммаРезерваСтраховыхВзносов * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31, 2)),
	|	РасшифровкаОбязательств.СуммаРезерваСтраховыхВзносовНУ,
	|	РасшифровкаОбязательств.СпособОтражения,
	|	ДанныеДокументов.ВидРезервов,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам),
	|	ДанныеДокументов.ВидРезервов,
	|	РасшифровкаОбязательств.ИдентификаторСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента,
	|	РасшифровкаОбязательств.НомерСтроки,
	|	ДанныеДокументов.Ссылка
	|
	|ИЗ
	|	ТЧОценочныеОбязательства КАК РасшифровкаОбязательств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументов КАК ДанныеДокументов
	|		ПО РасшифровкаОбязательств.Ссылка = ДанныеДокументов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Резерв по взносам на НС и ПЗ
	|ВЫБРАТЬ
	|	3,
	|	ДанныеДокументов.Организация,
	|	РасшифровкаОбязательств.Подразделение,
	|	РасшифровкаОбязательств.ФизическоеЛицо,
	|	РасшифровкаОбязательств.СуммаРезерваФССНесчастныеСлучаи,
	|	ВЫРАЗИТЬ(РасшифровкаОбязательств.СуммаРезерваФССНесчастныеСлучаи * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31, 2)),
	|	РасшифровкаОбязательств.СуммаРезерваФССНесчастныеСлучаиНУ,
	|	РасшифровкаОбязательств.СпособОтражения,
	|	ДанныеДокументов.ВидРезервов,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам),
	|	ДанныеДокументов.ВидРезервов,
	|	РасшифровкаОбязательств.ИдентификаторСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента,
	|	РасшифровкаОбязательств.НомерСтроки,
	|	ДанныеДокументов.Ссылка
	|
	|ИЗ
	|	ТЧОценочныеОбязательства КАК РасшифровкаОбязательств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументов КАК ДанныеДокументов
	|		ПО РасшифровкаОбязательств.Ссылка = ДанныеДокументов.Ссылка";
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	КонецЕсли;
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТРасходыДокумента(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТРасходыДокумента";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Начисление резерва
	"ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления КАК ВидНачисления,
	|	ОценочныеОбязательства.Организация КАК Организация,
	|	ОценочныеОбязательства.Подразделение КАК Подразделение,
	|
	|	ОценочныеОбязательства.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов КАК АналитикаСтатьиАктивовПассивов,
	|
	|	ОценочныеОбязательства.СпособОтражения КАК СпособОтражения,
	|
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаУпр > 0 ТОГДА
	|			ОценочныеОбязательства.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.Сумма > 0 ТОГДА
	|			ОценочныеОбязательства.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаНУ > 0 ТОГДА
	|			ОценочныеОбязательства.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|
	|	ОценочныеОбязательства.СчетУчета КАК СчетУчета,
	|	ОценочныеОбязательства.ВидРезервов КАК ВидРезервов,
	|
	|	ОценочныеОбязательства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|
	|	ОценочныеОбязательства.ТипОбязательства КАК ТипОбязательства,
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ) КАК ПериодРегистрации,
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента КАК ВидОперации,
	|	ДанныеДокументов.Ссылка
	|
	|ПОМЕСТИТЬ ВТРасходыДокумента
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|		ПО ДанныеДокументов.Ссылка = ОценочныеОбязательства.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументов.ВидОперацииДокумента В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
	|	И (ОценочныеОбязательства.Сумма > 0 ИЛИ ОценочныеОбязательства.СуммаНУ > 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание, корректировка резерва
	|ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления,
	|	ОценочныеОбязательства.Организация,
	|	ОценочныеОбязательства.Подразделение,
	|
	|	ОценочныеОбязательства.СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов,
	|
	|	ОценочныеОбязательства.СпособОтражения,
	|
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.СуммаУпр
	|		ИНАЧЕ ОценочныеОбязательства.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.Сумма
	|		ИНАЧЕ ОценочныеОбязательства.Сумма
	|	КОНЕЦ,
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.СуммаНУ
	|		ИНАЧЕ ОценочныеОбязательства.СуммаНУ
	|	КОНЕЦ,
	|
	|	ОценочныеОбязательства.СчетУчета,
	|	ОценочныеОбязательства.ВидРезервов,
	|
	|	ОценочныеОбязательства.ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации,
	|
	|	ОценочныеОбязательства.ТипОбязательства,
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ),
	|	ОценочныеОбязательства.ИдентификаторСтроки,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента,
	|	ДанныеДокументов.Ссылка
	|
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|		ПО ДанныеДокументов.Ссылка = ОценочныеОбязательства.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументов.ВидОперацииДокумента В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка))
	|	И (ОценочныеОбязательства.Сумма <> 0
	|		ИЛИ ОценочныеОбязательства.СуммаНУ <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТДоходыДокумента(ТекстыЗапроса)

	ИмяТаблицы = "ВТДоходыДокумента";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления КАК ВидНачисления,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ОценочныеОбязательства.Подразделение КАК Подразделение,
	|	ОценочныеОбязательства.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов КАК АналитикаСтатьиАктивовПассивов,
	|	ДанныеДокументов.СтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеДокументов.АналитикаСтатьиДоходов КАК АналитикаДоходов,
	|
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаУпр < 0
	|			ТОГДА -ОценочныеОбязательства.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.Сумма < 0
	|			ТОГДА -ОценочныеОбязательства.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаНУ < 0
	|			ТОГДА -ОценочныеОбязательства.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|
	|	ОценочныеОбязательства.СчетУчета КАК СчетУчета,
	|	ОценочныеОбязательства.ВидРезервов КАК ВидРезервов,
	|
	|	ОценочныеОбязательства.ХозяйственнаяОперацияСписание КАК ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперацииСписание КАК НастройкаХозяйственнойОперации,
	|	
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ) КАК ПериодРегистрации,
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокументов.ВидОперацииДокумента КАК ВидОперации,
	|	ДанныеДокументов.Ссылка
	|
	|ПОМЕСТИТЬ ВТДоходыДокумента
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|		ПО ДанныеДокументов.Ссылка = ОценочныеОбязательства.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументов.ВидОперацииДокумента В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
	|	И (ОценочныеОбязательства.Сумма < 0 ИЛИ ОценочныеОбязательства.СуммаНУ < 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТРасходыРаспределяемые(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТРасходыРаспределяемые";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Начисления резервов на оплату труда и взносов со способом отражения на производство
	"ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления КАК ВидНачисления,
	|	ОценочныеОбязательства.Организация КАК Организация,
	|	ОценочныеОбязательства.Подразделение КАК Подразделение,
	|	ОценочныеОбязательства.ФизическоеЛицо КАК ФизическоеЛицо,
	|
	|	ОценочныеОбязательства.СпособОтражения КАК СпособОтражения,
	|	ПорядокОтражения.БазаРаспределенияПоСдельнымРаботам КАК БазаРаспределения,
	|	ПорядокОтражения.ПринятиеКНалоговомуУчетуПоСдельнымРаботам КАК ПринимаютсяКНалоговомуУчету,
	|	ВЫБОР ОценочныеОбязательства.ТипОбязательства
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам)
	|			ТОГДА ПорядокОтражения.ОтдельнаяСтатьяКалькуляцииВзносов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ)
	|			ТОГДА ПорядокОтражения.ОтдельнаяСтатьяКалькуляцииВзносов
	|		ИНАЧЕ ПорядокОтражения.ОтдельнаяСтатьяКалькуляции
	|	КОНЕЦ КАК ОтдельнаяСтатьяКалькуляции,
	|	ВЫБОР ОценочныеОбязательства.ТипОбязательства
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам)
	|			ТОГДА ПорядокОтражения.СтатьяКалькуляцииВзносов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ)
	|			ТОГДА ПорядокОтражения.СтатьяКалькуляцииВзносов
	|		ИНАЧЕ ПорядокОтражения.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляции,
	|
	|	ОценочныеОбязательства.ТипОбязательства КАК ТипОбязательства,
	|	ОценочныеОбязательства.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов КАК АналитикаСтатьиАктивовПассивов,
	|
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаУпр < 0 ТОГДА 0
	|		ИНАЧЕ ОценочныеОбязательства.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.Сумма < 0 ТОГДА 0
	|		ИНАЧЕ ОценочныеОбязательства.Сумма
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаНУ < 0 ТОГДА 0
	|		ИНАЧЕ ОценочныеОбязательства.СуммаНУ
	|	КОНЕЦ КАК СуммаНУ,
	|
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	ОценочныеОбязательства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ОценочныеОбязательства.Ссылка
	|
	|ПОМЕСТИТЬ ВТРасходыРаспределяемые
	|ИЗ
	|	ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК ПорядокОтражения
	|	ПО ОценочныеОбязательства.СпособОтражения = ПорядокОтражения.Ссылка
	|		И ПорядокОтражения.ОплатаСдельныхРабот
	|
	|ГДЕ
	|	&ВидОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
	|	И (ОценочныеОбязательства.Сумма > 0 ИЛИ ОценочныеОбязательства.СуммаНУ > 0)
	|	И НЕ (ОценочныеОбязательства.ВидНачисления = 3
	|		И ПорядокОтражения.РежимУчетаВзносовНаТравматизм = 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления,
	|	ОценочныеОбязательства.Организация,
	|	ОценочныеОбязательства.Подразделение,
	|	ОценочныеОбязательства.ФизическоеЛицо,
	|
	|	ОценочныеОбязательства.СпособОтражения,
	|	ПорядокОтражения.БазаРаспределенияПоСдельнымРаботам,
	|	ПорядокОтражения.ПринятиеКНалоговомуУчетуПоСдельнымРаботам,
	|	ВЫБОР ОценочныеОбязательства.ТипОбязательства
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам)
	|			ТОГДА ПорядокОтражения.ОтдельнаяСтатьяКалькуляцииВзносов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ)
	|			ТОГДА ПорядокОтражения.ОтдельнаяСтатьяКалькуляцииВзносов
	|		ИНАЧЕ ПорядокОтражения.ОтдельнаяСтатьяКалькуляции
	|	КОНЕЦ,
	|	ВЫБОР ОценочныеОбязательства.ТипОбязательства
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам)
	|			ТОГДА ПорядокОтражения.СтатьяКалькуляцииВзносов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ)
	|			ТОГДА ПорядокОтражения.СтатьяКалькуляцииВзносов
	|		ИНАЧЕ ПорядокОтражения.СтатьяКалькуляции
	|	КОНЕЦ,
	|
	|	ОценочныеОбязательства.ТипОбязательства,
	|	ОценочныеОбязательства.СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов,
	|
	|	ОценочныеОбязательства.СуммаУпр,
	|	ОценочныеОбязательства.Сумма,
	|	ОценочныеОбязательства.СуммаНУ,
	|
	|	ОценочныеОбязательства.ИдентификаторСтроки,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации,
	|	ОценочныеОбязательства.ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.Ссылка
	|
	|ИЗ
	|	ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК ПорядокОтражения
	|	ПО ОценочныеОбязательства.СпособОтражения = ПорядокОтражения.Ссылка
	|		И ПорядокОтражения.ОплатаСдельныхРабот
	|
	|ГДЕ
	|	&ВидОперации НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
	|	И (ОценочныеОбязательства.Сумма <> 0 ИЛИ ОценочныеОбязательства.СуммаНУ <> 0)
	|	И НЕ (ОценочныеОбязательства.ВидНачисления = 3
	|		И ПорядокОтражения.РежимУчетаВзносовНаТравматизм = 1)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТВыработкаСотрудников(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТВыработкаСотрудников";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВыработкаСотрудников.ВидНачисления,
	|	ВыработкаСотрудников.Организация,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.ФизическоеЛицо,
	|
	|	ВыработкаСотрудников.ТипОбязательства,
	|	ВыработкаСотрудников.СтатьяАктивовПассивов,
	|	ВыработкаСотрудников.АналитикаСтатьиАктивовПассивов,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет,
	|	ВыработкаСотрудников.ВидФондаВзносов,
	|
	|	ВыработкаСотрудников.ПринимаютсяКНалоговомуУчету,
	|	ВыработкаСотрудников.ОтражениеПрочихРабот,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.Этап,
	|
	|	ВыработкаСотрудников.РезультатУпр КАК СуммаУпр,
	|	ВыработкаСотрудников.РезультатРегл КАК СуммаРегл,
	|	ВыработкаСотрудников.РезультатНУ КАК СуммаНУ,
	|
	|	ВыработкаСотрудников.НастройкаХозяйственнойОперации,
	|	ВыработкаСотрудников.ХозяйственнаяОперация,
	|	ВыработкаСотрудников.ИдентификаторСтроки,
	|	ВыработкаСотрудников.Ссылка
	|
	|ПОМЕСТИТЬ ВТВыработкаСотрудников
	|ИЗ
	|	&ТаблицаВыработкаСотрудников КАК ВыработкаСотрудников";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТСписокСпособовОтражения(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТСписокСпособовОтражения";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаТЧОценочныеОбязательства(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ СпособОтражения
	|ПОМЕСТИТЬ ВТСписокСпособовОтражения
	|ИЗ ТЧОценочныеОбязательства";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТНастройкиСпособовОтражения(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТНастройкиСпособовОтражения";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТСписокСпособовОтражения(ТекстыЗапроса);
	
	ТекстЗапроса = Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов(ИмяТаблицы);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТПорядокОтражения(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТПорядокОтражения";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТНастройкиСпособовОтражения(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Настройки отражения оплаты труда
	"ВЫБРАТЬ
	|	1 КАК ВидНачисления,
	|	СпособыОтражения.СпособОтражения,
	|	СпособыОтражения.НаправлениеДеятельности,
	|
	|	СпособыОтражения.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособыОтражения.ВариантРаспределения КАК ВариантРаспределения,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(СпособыОтражения.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).ВидЦенностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидЦенностиНДС,
	|	СпособыОтражения.ПринятиеКНалоговомуУчету КАК ПринимаютсяКНалоговомуУчету,
	|	СпособыОтражения.АналитикаРасходов КАК АналитикаРасходов,
	|	СпособыОтражения.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.НастройкаСчетовУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	КОНЕЦ КАК НастройкаСчетовУчета,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ КАК Счет,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто1
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто3
	|
	|ПОМЕСТИТЬ ВТПорядокОтражения
	|ИЗ
	|	ВТНастройкиСпособовОтражения КАК СпособыОтражения
	|ГДЕ
	|	НЕ СпособыОтражения.ОплатаСдельныхРабот
	|	И НЕ СпособыОтражения.ОблагаетсяЕНВД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Настройки отражения страховых взносов
	|ВЫБРАТЬ
	|	2,
	|	СпособыОтражения.СпособОтражения,
	|	СпособыОтражения.НаправлениеДеятельности,
	|
	|	СпособыОтражения.СтатьяРасходовВзносов,
	|	СпособыОтражения.ВариантРаспределенияВзносов,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходовВзносов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(СпособыОтражения.СтатьяРасходовВзносов КАК ПланВидовХарактеристик.СтатьиРасходов).ВидЦенностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	СпособыОтражения.ПринятиеКНалоговомуУчетуВзносов,
	|	СпособыОтражения.АналитикаРасходовВзносов,
	|	СпособыОтражения.АналитикаАктивовПассивовВзносов,
	|
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.НастройкаСчетовУчетаВзносов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.СчетУчетаВзносов
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто1Взносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто2Взносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто3Взносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ИЗ
	|	ВТНастройкиСпособовОтражения КАК СпособыОтражения
	|ГДЕ
	|	НЕ СпособыОтражения.ОплатаСдельныхРабот
	|	И НЕ СпособыОтражения.ОблагаетсяЕНВД
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Настройки отражения страховых взносов от несчастных случаев
	|ВЫБРАТЬ
	|	3,
	|	СпособыОтражения.СпособОтражения,
	|	СпособыОтражения.НаправлениеДеятельности,
	|
	|	СпособыОтражения.СтатьяРасходовТравматизм,
	|	СпособыОтражения.ВариантРаспределенияТравматизм,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходовТравматизм ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(СпособыОтражения.СтатьяРасходовТравматизм КАК ПланВидовХарактеристик.СтатьиРасходов).ВидЦенностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	СпособыОтражения.ПринятиеКНалоговомуУчетуТравматизм,
	|	СпособыОтражения.АналитикаРасходовТравматизм,
	|	СпособыОтражения.АналитикаАктивовПассивовТравматизм,
	|
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА СпособыОтражения.СчетУчетаТравматизм
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто1Травматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто2Травматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА СпособыОтражения.Субконто3Травматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ИЗ
	|	ВТНастройкиСпособовОтражения КАК СпособыОтражения
	|ГДЕ
	|	(НЕ СпособыОтражения.ОплатаСдельныхРабот
	|		ИЛИ СпособыОтражения.РежимУчетаВзносовНаТравматизм = 1)
	|	И НЕ СпособыОтражения.ОблагаетсяЕНВД";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТРасходыПостатейные";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТПорядокОтражения(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Начисление резерва
	"ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления КАК ВидНачисления,
	|	ОценочныеОбязательства.Организация КАК Организация,
	|	ОценочныеОбязательства.Подразделение КАК Подразделение,
	|
	|	ПорядокОтражения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПорядокОтражения.СтатьяРасходов КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов ТОГДА
	|			ВЫБОР ПорядокОтражения.АналитикаРасходов
	|				КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Подразделение
	|				КОГДА ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) ТОГДА ОценочныеОбязательства.ФизическоеЛицо
	|				КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Организация
	|				ИНАЧЕ ПорядокОтражения.АналитикаРасходов
	|			КОНЕЦ
	|		КОГДА ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА
	|			ВЫБОР ПорядокОтражения.АналитикаАктивовПассивов
	|				КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Подразделение
	|				КОГДА ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) ТОГДА ОценочныеОбязательства.ФизическоеЛицо
	|				КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Организация
	|				ИНАЧЕ ПорядокОтражения.АналитикаАктивовПассивов
	|			КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаСтатьиРасходов,
	|	ПорядокОтражения.ПринимаютсяКНалоговомуУчету КАК ПринимаютсяКНалоговомуУчету,
	|
	|	ОценочныеОбязательства.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов КАК АналитикаСтатьиАктивовПассивов,
	|
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаУпр > 0 ТОГДА
	|			ОценочныеОбязательства.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.Сумма > 0 ТОГДА
	|			ОценочныеОбязательства.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ОценочныеОбязательства.СуммаНУ > 0 ТОГДА
	|			ОценочныеОбязательства.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|
	|	ОценочныеОбязательства.СчетУчета КАК СчетУчета,
	|	ОценочныеОбязательства.ВидРезервов КАК ВидРезервов,
	|
	|	ПорядокОтражения.Счет КАК СчетРасходов,
	|	ПорядокОтражения.Субконто1 КАК Субконто1,
	|	ПорядокОтражения.Субконто2 КАК Субконто2,
	|	ПорядокОтражения.Субконто3 КАК Субконто3,
	|
	|	ОценочныеОбязательства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ) КАК ПериодРегистрации,
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокументов.Ссылка
	|
	|ПОМЕСТИТЬ ВТРасходыПостатейные
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|		ПО ДанныеДокументов.Ссылка = ОценочныеОбязательства.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокОтражения КАК ПорядокОтражения
	|		ПО ОценочныеОбязательства.ВидНачисления = ПорядокОтражения.ВидНачисления
	|			И ОценочныеОбязательства.СпособОтражения = ПорядокОтражения.СпособОтражения
	|
	|ГДЕ
	|	ДанныеДокументов.ВидОперацииДокумента В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Начисление),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация))
	|	И (ОценочныеОбязательства.Сумма > 0 ИЛИ ОценочныеОбязательства.СуммаНУ > 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание, корректировка резерва
	|ВЫБРАТЬ
	|	ОценочныеОбязательства.ВидНачисления,
	|	ОценочныеОбязательства.Организация,
	|	ОценочныеОбязательства.Подразделение,
	|
	|	ПорядокОтражения.НаправлениеДеятельности,
	|	ПорядокОтражения.СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов ТОГДА
	|			ВЫБОР ПорядокОтражения.АналитикаРасходов
	|				КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Подразделение
	|				КОГДА ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) ТОГДА ОценочныеОбязательства.ФизическоеЛицо
	|				КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Организация
	|				ИНАЧЕ ПорядокОтражения.АналитикаРасходов
	|			КОНЕЦ
	|		КОГДА ПорядокОтражения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов ТОГДА
	|			ВЫБОР ПорядокОтражения.АналитикаАктивовПассивов
	|				КОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Подразделение
	|				КОГДА ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) ТОГДА ОценочныеОбязательства.ФизическоеЛицо
	|				КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА ОценочныеОбязательства.Организация
	|				ИНАЧЕ ПорядокОтражения.АналитикаАктивовПассивов
	|			КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ ,
	|	ПорядокОтражения.ПринимаютсяКНалоговомуУчету,
	|
	|	ОценочныеОбязательства.СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов,
	|
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.СуммаУпр
	|		ИНАЧЕ ОценочныеОбязательства.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.Сумма
	|		ИНАЧЕ ОценочныеОбязательства.Сумма
	|	КОНЕЦ,
	|	ВЫБОР ДанныеДокументов.ВидОперацииДокумента
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание)
	|			ТОГДА -ОценочныеОбязательства.СуммаНУ
	|		ИНАЧЕ ОценочныеОбязательства.СуммаНУ
	|	КОНЕЦ,
	|
	|	ОценочныеОбязательства.СчетУчета,
	|	ОценочныеОбязательства.ВидРезервов,
	|
	|	ПорядокОтражения.Счет,
	|	ПорядокОтражения.Субконто1,
	|	ПорядокОтражения.Субконто2,
	|	ПорядокОтражения.Субконто3,
	|
	|	ОценочныеОбязательства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОценочныеОбязательства.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|
	|	КОНЕЦПЕРИОДА(ДанныеДокументов.МесяцНачисления, МЕСЯЦ),
	|	ОценочныеОбязательства.ИдентификаторСтроки,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ДанныеДокументов.Ссылка
	|
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|		ПО ДанныеДокументов.Ссылка = ОценочныеОбязательства.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокОтражения КАК ПорядокОтражения
	|		ПО ОценочныеОбязательства.ВидНачисления = ПорядокОтражения.ВидНачисления
	|			И ОценочныеОбязательства.СпособОтражения = ПорядокОтражения.СпособОтражения
	|
	|ГДЕ
	|	ДанныеДокументов.ВидОперацииДокумента В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Списание),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Корректировка))
	|	И (ОценочныеОбязательства.Сумма <> 0
	|		ИЛИ ОценочныеОбязательства.СуммаНУ <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТИсходныеПрочиеРасходы(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТИсходныеПрочиеРасходы";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Расходы на формирование резервов относим на статью расходов, как указано в способе отражения
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Период                                         КАК Период,
	|	&Организация                                    КАК Организация,
	|	ПостатейныеРасходы.Подразделение                КАК Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов               КАК СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов      КАК АналитикаРасходов,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	СУММА(ПостатейныеРасходы.СуммаУпр)              КАК СуммаСНДС,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)              КАК СуммаБезНДС,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)              КАК СуммаБезНДСУпр,
	|	СУММА(ПостатейныеРасходы.СуммаРегл)             КАК СуммаСНДСРегл,
	|	СУММА(ПостатейныеРасходы.СуммаРегл)             КАК СуммаБезНДСРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                          КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                          КАК ВременнаяРазница,
	|	
	|	ПостатейныеРасходы.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ПостатейныеРасходы.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВТИсходныеПрочиеРасходы
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов,
	|	ПостатейныеРасходы.ИдентификаторСтроки,
	|	ПостатейныеРасходы.ХозяйственнаяОперация,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТПрочиеРасходы(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТПрочиеРасходы";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТИсходныеПрочиеРасходы(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры


Процедура ДобавитьТекстЗапросаВТИсходныеПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТИсходныеПартииПрочихРасходов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	ПостатейныеРасходы.Подразделение               КАК Подразделение,
	|	ПостатейныеРасходы.СтатьяРасходов              КАК СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов     КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                   КАК АналитикаАктивовПассивов,
	|	ПостатейныеРасходы.Ссылка                      КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО                                   КАК АналитикаУчетаПартий,
	|	ПостатейныеРасходы.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                   КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                   КАК ВидДеятельностиНДС,
	|
	|	СУММА(ПостатейныеРасходы.СуммаУпр)             КАК Стоимость,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)             КАК СтоимостьБезНДС,
	|	0                                              КАК НДСУпр,
	|	СУММА(ПостатейныеРасходы.СуммаРегл)            КАК СтоимостьРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0 КОНЕЦ)                             КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0 КОНЕЦ)                             КАК ВременнаяРазница,
	|	0                                              КАК НДСРегл,
	|	
	|	ПостатейныеРасходы.ИдентификаторСтроки         КАК ИдентификаторФинЗаписи,
	|	ПостатейныеРасходы.ХозяйственнаяОперация       КАК ХозяйственнаяОперация,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВТИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.ХозяйственнаяОперация,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации,
	|	ПостатейныеРасходы.ИдентификаторСтроки,
	|	ПостатейныеРасходы.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТПартииПрочихРасходов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТИсходныеПартииПрочихРасходов(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

#КонецОбласти

Функция ТекстЗапросаТрудозатратыНезавершенногоПроизводства(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТрудозатратыНезавершенногоПроизводства";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТВыработкаСотрудников(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	&Организация                                  КАК Организация,
	|	ВыработкаСотрудников.Подразделение            КАК Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства       КАК ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция       КАК КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап                     КАК Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции        КАК СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот                 КАК ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции          КАК ГруппаПродукции,
	|	ВыработкаСотрудников.ВидФондаВзносов          КАК ВидФондаВзносов,
	|	СУММА(ВыработкаСотрудников.СуммаУпр)          КАК Стоимость,
	|	СУММА(ВыработкаСотрудников.СуммаРегл)         КАК СтоимостьРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВыработкаСотрудников.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ВыработкаСотрудников.СуммаРегл - ВыработкаСотрудников.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                        КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ВыработкаСотрудников.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ВыработкаСотрудников.СуммаРегл - ВыработкаСотрудников.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                        КАК ВременнаяРазница,
	|	
	|	ВыработкаСотрудников.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|	ВыработкаСотрудников.НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВТВыработкаСотрудников КАК ВыработкаСотрудников
	|
	|ГДЕ
	|	НЕ ВыработкаСотрудников.ОтражениеПрочихРабот
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ВидФондаВзносов,
	|	ВыработкаСотрудников.ИдентификаторСтроки,
	|	ВыработкаСотрудников.НастройкаХозяйственнойОперации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОтражениеЗарплатыВФинансовомУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеЗарплатыВФинансовомУчете";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТВыработкаСотрудников(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВыработкаСотрудников.Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидРабот КАК ВидРабот,
	|	ВыработкаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВыработкаСотрудников.ГруппаПродукции КАК ГруппаПродукции,
	|	ВыработкаСотрудников.ТипОбязательства КАК ТипНалога,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет КАК ВидПлатежаВГосБюджет,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВыработкаСотрудников.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ВыработкаСотрудников.СуммаРегл - ВыработкаСотрудников.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ВыработкаСотрудников.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ВыработкаСотрудников.СуммаРегл - ВыработкаСотрудников.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВременнаяРазница
	|
	|ИЗ
	|	ВТВыработкаСотрудников КАК ВыработкаСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Организация,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ФизическоеЛицо,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ТипОбязательства";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеДоходы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТДоходыДокумента(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПрочиеДоходы.Организация               КАК Организация,
	|	ПрочиеДоходы.Подразделение             КАК Подразделение,
	|	ПрочиеДоходы.СтатьяДоходов             КАК СтатьяДоходов,
	|	ПрочиеДоходы.АналитикаДоходов          КАК АналитикаДоходов,
	|
	|	СУММА(ПрочиеДоходы.СуммаУпр)           КАК Сумма,
	|	
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПрочиеДоходы.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                 КАК СуммаУпр,
	|	СУММА(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ПрочиеДоходы.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                 КАК СуммаРегл,
	|	
	|	ПрочиеДоходы.ИдентификаторСтроки       КАК ИдентификаторФинЗаписи,
	|	ПрочиеДоходы.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	ПрочиеДоходы.НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВТДоходыДокумента КАК ПрочиеДоходы
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И ПрочиеДоходы.СуммаРегл > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеДоходы.Организация,
	|	ПрочиеДоходы.Подразделение,
	|	ПрочиеДоходы.СтатьяДоходов,
	|	ПрочиеДоходы.АналитикаДоходов,
	|	ПрочиеДоходы.ИдентификаторСтроки,
	|	ПрочиеДоходы.ХозяйственнаяОперация,
	|	ПрочиеДоходы.НастройкаХозяйственнойОперации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТПрочиеРасходы(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТОценочныеОбязательства(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТПрочиеРасходы(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТПартииПрочихРасходов(ТекстыЗапроса);
	
	ТекстЗапроса = 
	// Накопленный резерв по предопределенным статьям "Резервы по вознаграждениям/страховым взносам"
	"ВЫБРАТЬ
	|	&Период                                               КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                КАК ВидДвижения,
	|	&Организация                                          КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                          КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                                          КАК НаправлениеДеятельности,
	|	ОценочныеОбязательства.СтатьяАктивовПассивов          КАК Статья,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов КАК Аналитика,
	|	СУММА(ОценочныеОбязательства.СуммаУпр)                КАК Сумма
	|
	|ИЗ
	|	ВТОценочныеОбязательства КАК ОценочныеОбязательства
	|
	|СГРУППИРОВАТЬ ПО
	|	ОценочныеОбязательства.СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.АналитикаСтатьиАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расходы на формирование резервов относим на статью активов/пассивов, как указано в способе отражения
	|ВЫБРАТЬ
	|	&Период                                     КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Организация                                КАК Организация,
	|	ПостатейныеРасходы.Подразделение            КАК Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов           КАК Статья,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов  КАК Аналитика,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)          КАК Сумма
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТПартииПрочихРасходов(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТВыработкаСотрудников(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТДоходыДокумента(ТекстыЗапроса);

	ТекстЗапроса = 
	// Накопленный резерв по предопределенным статьям "Резервы по вознаграждениям/страховым взносам"
	"ВЫБРАТЬ
	|	&Период                                           КАК Период,
	|	ПостатейныеРасходы.Организация                    КАК Организация,
	|	ПостатейныеРасходы.Подразделение                  КАК Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов          КАК Статья,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СУММА(ПостатейныеРасходы.СуммаРегл)               КАК СуммаРегл,
	|	0                                                 КАК ПостояннаяРазница,
	|	СУММА(ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ) КАК ВременнаяРазница,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК СуммаУпр,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК СуммаСНДС,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК СуммаБезНДС,
	|	
	|	ПостатейныеРасходы.ИдентификаторСтроки            КАК ИдентификаторФинЗаписи,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Организация,
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации,
	|	ПостатейныеРасходы.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Накопленный резерв по предопределенным статьям "Резервы по вознаграждениям/страховым взносам"
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВыработкаСотрудников.Организация                    КАК Организация,
	|	ВыработкаСотрудников.Подразделение                  КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяАктивовПассивов          КАК Статья,
	|	ВыработкаСотрудников.АналитикаСтатьиАктивовПассивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СУММА(ВыработкаСотрудников.СуммаРегл)               КАК СуммаРегл,
	|	0                                                   КАК ПостояннаяРазница,
	|	СУММА(ВыработкаСотрудников.СуммаРегл - ВыработкаСотрудников.СуммаНУ) КАК ВременнаяРазница,
	|	СУММА(ВыработкаСотрудников.СуммаУпр)                КАК СуммаУпр,
	|	СУММА(ВыработкаСотрудников.СуммаУпр)                КАК СуммаСНДС,
	|	СУММА(ВыработкаСотрудников.СуммаУпр)                КАК СуммаБезНДС,
	|
	|	ВыработкаСотрудников.ИдентификаторСтроки            КАК ИдентификаторФинЗаписи,
	|	ВыработкаСотрудников.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВТВыработкаСотрудников КАК ВыработкаСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Организация,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.СтатьяАктивовПассивов,
	|	ВыработкаСотрудников.АналитикаСтатьиАктивовПассивов,
	|	ВыработкаСотрудников.НастройкаХозяйственнойОперации,
	|	ВыработкаСотрудников.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расходы на формирование резервов относим на статью активов/пассивов, как указано в способе отражения
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ПостатейныеРасходы.Организация             КАК Организация,
	|	ПостатейныеРасходы.Подразделение           КАК Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов          КАК Статья,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	СУММА(ПостатейныеРасходы.СуммаРегл)        КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                     КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ПостатейныеРасходы.ПринимаютсяКНалоговомуУчету
	|			ТОГДА ПостатейныеРасходы.СуммаРегл - ПостатейныеРасходы.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                     КАК ВременнаяРазница,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)         КАК СуммаУпр,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)         КАК СуммаСНДС,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)         КАК СуммаБезНДС,
	|	
	|	ПостатейныеРасходы.ИдентификаторСтроки     КАК ИдентификаторФинЗаписи,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Организация,
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов,
	|	ПостатейныеРасходы.НастройкаХозяйственнойОперации,
	|	ПостатейныеРасходы.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание резерва по предопределенным статьям "Резервы по вознаграждениям/страховым взносам"
	|ВЫБРАТЬ
	|	&Период                                     КАК Период,
	|	ПрочиеДоходы.Организация                    КАК Организация,
	|	ПрочиеДоходы.Подразделение                  КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                                КАК НаправлениеДеятельности,
	|	ПрочиеДоходы.СтатьяАктивовПассивов          КАК Статья,
	|	ПрочиеДоходы.АналитикаСтатьиАктивовПассивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	СУММА(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ПрочиеДоходы.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК СуммаРегл,
	|	0                                           КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ПрочиеДоходы.СуммаРегл - ПрочиеДоходы.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК ВременнаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПрочиеДоходы.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК СуммаУпр,
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПрочиеДоходы.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК СуммаСНДС,
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПрочиеДоходы.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК СуммаБезНДС,
	|	
	|	ПрочиеДоходы.ИдентификаторСтроки            КАК ИдентификаторФинЗаписи,
	|	ПрочиеДоходы.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВТДоходыДокумента КАК ПрочиеДоходы
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И ПрочиеДоходы.СуммаРегл > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеДоходы.Организация,
	|	ПрочиеДоходы.Подразделение,
	|	ПрочиеДоходы.СтатьяАктивовПассивов,
	|	ПрочиеДоходы.АналитикаСтатьиАктивовПассивов,
	|	ПрочиеДоходы.ИдентификаторСтроки,
	|	ПрочиеДоходы.НастройкаХозяйственнойОперации
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасходыПостатейные(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Статьи активов/пассивов "Резервы по вознаграждениям/страховым взносам" - статьи расходов, как указано в способе отражения
	"ВЫБРАТЬ
	|	&Период                                           КАК Период,
	|	МАКСИМУМ(ПостатейныеРасходы.НомерСтроки)          КАК НомерСтроки,
	|	ПостатейныеРасходы.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|
	|	ПостатейныеРасходы.Организация                    КАК Организация,
	|
	|	ПостатейныеРасходы.Подразделение                  КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                                      КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов          КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ПостатейныеРасходы.Подразделение                  КАК КорПодразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности        КАК КорНаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов                 КАК КорСтатья,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов        КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаАктивовПассивов,
	|
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК Сумма,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК СуммаУпр,
	|	СУММА(ПостатейныеРасходы.СуммаРегл)               КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СуммаРегл <> 0
	|	И ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Организация,
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.ХозяйственнаяОперация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Статьи активов/пассивов "Резервы по вознаграждениям/страховым взносам" - статьи активов/пассивов, как указано в способе отражения
	|ВЫБРАТЬ
	|	&Период                                           КАК Период,
	|	МАКСИМУМ(ПостатейныеРасходы.НомерСтроки)          КАК НомерСтроки,
	|	ПостатейныеРасходы.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|
	|	ПостатейныеРасходы.Организация                    КАК Организация,
	|
	|	ПостатейныеРасходы.Подразделение                  КАК Подразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов          КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ПостатейныеРасходы.Подразделение                  КАК КорПодразделение,
	|	ПостатейныеРасходы.НаправлениеДеятельности        КАК КорНаправлениеДеятельности,
	|	ПостатейныеРасходы.СтатьяРасходов                 КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов        КАК КорАналитикаАктивовПассивов,
	|
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК Сумма,
	|	СУММА(ПостатейныеРасходы.СуммаУпр)                КАК СуммаУпр,
	|	СУММА(ПостатейныеРасходы.СуммаРегл)               КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыПостатейные КАК ПостатейныеРасходы
	|
	|ГДЕ
	|	ПостатейныеРасходы.СуммаРегл <> 0
	|	И ПостатейныеРасходы.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПостатейныеРасходы.Организация,
	|	ПостатейныеРасходы.Подразделение,
	|	ПостатейныеРасходы.СтатьяРасходов,
	|	ПостатейныеРасходы.АналитикаСтатьиРасходов,
	|	ПостатейныеРасходы.СтатьяАктивовПассивов,
	|	ПостатейныеРасходы.АналитикаСтатьиАктивовПассивов,
	|	ПостатейныеРасходы.НаправлениеДеятельности,
	|	ПостатейныеРасходы.ХозяйственнаяОперация
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период           КАК Период,
	|	&Организация      КАК Организация,
	|	&Период           КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозОперацияДляРеестра КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	&Ответственный КАК Ответственный,
	|	НЕОПРЕДЕЛЕНО КАК Автор,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	0 КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ЗакрытиеМесяца

Процедура СформироватьДокументыНачисленияРезервовПоОплатеТруда(Организации, Период)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Организация
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам.Обороты(
	|			&НачалоПериода, &КонецПериода, , Организация В (&Организации)
	|		) КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеРезервов.Ссылка КАК Ссылка,
	|	НачислениеРезервов.Организация КАК Организация,
	|	НачислениеРезервов.Резерв КАК Резерв,
	|	НачислениеРезервов.Проведен КАК Проведен
	|ИЗ
	|	Документ.РезервыПоОплатеТруда КАК НачислениеРезервов
	|ГДЕ
	|	НЕ НачислениеРезервов.ПометкаУдаления
	|	И (НачислениеРезервов.Организация В (&Организации) 
	|		ИЛИ &ПоВсемОрганизациям)
	|	И НАЧАЛОПЕРИОДА(НачислениеРезервов.МесяцНачисления, МЕСЯЦ) = &НачалоПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачислениеРезервов.Ссылка КАК Ссылка,
	|	НачислениеРезервов.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Резерв,
	|	НачислениеРезервов.Проведен КАК Проведен
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК НачислениеРезервов
	|ГДЕ
	|	НачислениеРезервов.Проведен
	|	И (НачислениеРезервов.Организация В (&Организации)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И НАЧАЛОПЕРИОДА(НачислениеРезервов.ПериодРегистрации, МЕСЯЦ) = &НачалоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Проведен УБЫВ");
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организации));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	
	Результат = Запрос.ВыполнитьПакет();
	НачислениеЗарплаты    = Результат[0].Выбрать();
	СуществующиеДокументы = Результат[1].Выбрать();
	
	ДействующиеНастройки = НастройкиДействующихРезервов(Организации, Период);
	Для Каждого НастройкиОрганизации Из ДействующиеНастройки Цикл
		
		Организация = НастройкиОрганизации.Ключ;
		
		НачислениеЗарплаты.Сбросить();
		Если НЕ НачислениеЗарплаты.НайтиСледующий(Организация, "Организация") Тогда
			// По организации отсутствует начисление зарплаты, начислять резерв не требуется
			Продолжить;
		КонецЕсли;
		
		Для Каждого НастройкиРезерва Из НастройкиОрганизации.Значение Цикл
			
			Резерв = НастройкиРезерва.Ключ;
			Настройки = НастройкиРезерва.Значение;
			
			Если Не Настройки.ФормироватьРезервБУ Тогда
				Продолжить;
			КонецЕсли;
			
			СуществующиеДокументы.Сбросить();
			Если НЕ СуществующиеДокументы.НайтиСледующий(Новый Структура("Организация, Резерв", Организация, Резерв)) Тогда
				// Существующий документ не найден - создаем новый
				ДокументОбъект = Документы.РезервыПоОплатеТруда.СоздатьДокумент();
			ИначеЕсли НЕ СуществующиеДокументы.Проведен Тогда
				// Документ найден, но не проведен - используем его чтобы не множить
				ДокументОбъект = СуществующиеДокументы.Ссылка.ПолучитьОбъект();
			Иначе Продолжить;
			КонецЕсли;
			
			ДокументОбъект.Дата            = КонецМесяца(Период);
			ДокументОбъект.МесяцНачисления = НачалоМесяца(Период);
			ДокументОбъект.Организация     = Организация;
			ДокументОбъект.Резерв          = Резерв;
			ДокументОбъект.ВидОперации     = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление;
			
			ОписаниеДокумента = Новый Структура("Организация, Подразделение, ВидОперации, Дата, МесяцНачисления, Резерв, Сотрудники");
			ЗаполнитьЗначенияСвойств(ОписаниеДокумента, ДокументОбъект);
			
			ОписаниеДокумента.Вставить("ДокументСсылка",                      ДокументОбъект.Ссылка);
			ОписаниеДокумента.Вставить("Начисления",                          ДокументОбъект.Начисления.ВыгрузитьКолонки());
			ОписаниеДокумента.Вставить("Взносы",                              ДокументОбъект.Взносы.ВыгрузитьКолонки());
			ОписаниеДокумента.Вставить("РасчетРезервов",                      ДокументОбъект.РасчетРезервов.ВыгрузитьКолонки());
			ОписаниеДокумента.Вставить("ОценочныеОбязательстваПоСотрудникам", ДокументОбъект.ОценочныеОбязательстваПоСотрудникам.ВыгрузитьКолонки());
			ОписаниеДокумента.Вставить("ОценочныеОбязательства",              ДокументОбъект.ОценочныеОбязательства.ВыгрузитьКолонки());
			ОписаниеДокумента.Вставить("ФизическиеЛица",                      ДокументОбъект.ФизическиеЛица.ВыгрузитьКолонки());
			
			ДополнительныеПараметры = РезервыПоОплатеТруда.ДополнительныеПараметрыЗаполненияТаблицДокумента();
			ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ОписаниеДокумента);
			
			ДанныеЗаполнения = РезервыПоОплатеТрудаРасширенный.ДанныеДляЗаполненияТаблицДокумента(ОписаниеДокумента,
				ОписаниеДокумента.Организация, ОписаниеДокумента.Резерв, ОписаниеДокумента.МесяцНачисления, ДополнительныеПараметры);
			ДанныеЗаполнения.Начисления.Колонки.ИдентификаторСтроки.Имя = "ИдентификаторСтрокиВидаРасчета";
			
			ДокументОбъект.Начисления.Загрузить(ДанныеЗаполнения.Начисления);
			ДокументОбъект.Взносы.Загрузить(ДанныеЗаполнения.Взносы);
			ДокументОбъект.РасчетРезервов.Загрузить(ДанныеЗаполнения.РасчетРезервов);
			ДокументОбъект.ОценочныеОбязательстваПоСотрудникам.Загрузить(ДанныеЗаполнения.ОценочныеОбязательстваПоСотрудникам);
			ДокументОбъект.ОценочныеОбязательства.Загрузить(ДанныеЗаполнения.ОценочныеОбязательства);
			
			Для Каждого СтрокаТаблицыИсточника Из ДанныеЗаполнения.Начисления Цикл
				Для Каждого СтрокаТаблицыПоказателей Из СтрокаТаблицыИсточника.Показатели Цикл
					СтрокаПоказателей = ДокументОбъект.Показатели.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаПоказателей, СтрокаТаблицыПоказателей);
					ЗаполнитьЗначенияСвойств(СтрокаПоказателей, СтрокаТаблицыИсточника, "ИдентификаторСтрокиВидаРасчета");
				КонецЦикла;
			КонецЦикла;
			
			ФизическиеЛицаСотрудников = КадровыйУчет.ФизическиеЛицаСотрудников(ОбщегоНазначения.ВыгрузитьКолонку(ДанныеЗаполнения.Начисления, "Сотрудник", Истина));
			Для Каждого ЗаписьМассива Из ФизическиеЛицаСотрудников Цикл
				НоваяСтрока = ОписаниеДокумента.ФизическиеЛица.Добавить();
				НоваяСтрока.ФизическоеЛицо = ЗаписьМассива;
			КонецЦикла;
			ОписаниеДокумента.ФизическиеЛица.Свернуть("ФизическоеЛицо");
			
			ДокументОбъект.ФизическиеЛица.Загрузить(ОписаниеДокумента.ФизическиеЛица);
			
			СтатьиДоходовИРасходов = СтатьиДоходовИРасходовПоУмолчанию(
				ДокументОбъект.Организация, ДокументОбъект.ВидОперации, ДокументОбъект.Резерв);
			ЗаполнитьЗначенияСвойств(ДокументОбъект, СтатьиДоходовИРасходов);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			
			Если ДокументОбъект.ПроверитьЗаполнение() Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкиДействующихРезервов(Организации, Период)
	
	ДействующиеНастройки = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период",      НачалоМесяца(Период));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.Организация КАК Организация,
	|	Настройки.Резерв КАК Резерв,
	|	Настройки.НачалоПериода КАК НачалоПериода,
	|	Настройки.КонецПериода КАК КонецПериода
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК Настройки
	|
	|ГДЕ
	|	НЕ Настройки.ПометкаУдаления
	|	И Настройки.Организация В (&Организации)
	|	И &Период МЕЖДУ НАЧАЛОПЕРИОДА(Настройки.НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(Настройки.КонецПериода, МЕСЯЦ)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ДействующиеНастройки.Получить(Выборка.Организация) = Неопределено Тогда
			ДействующиеНастройки.Вставить(Выборка.Организация, Новый Соответствие);
		КонецЕсли;
		
		ДействующиеНастройки[Выборка.Организация].Вставить(Выборка.Резерв,
			РезервыПоОплатеТруда.НастройкаРезервов(Выборка.Организация, Выборка.Резерв, Период));
		
	КонецЦикла;
	
	Возврат ДействующиеНастройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
