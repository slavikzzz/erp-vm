////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Ввод остатков взаиморасчетов по аренде".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	#Область РасчетыПоДоговорамАрендыСальдоДебетовое
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Расчеты по договорам аренды (сальдо дебетовое)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.АрендныеОбязательстваОстатокРегл КАК Сумма,
	|	Операция.АрендныеОбязательстваОстатокУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Контрагент КАК СубконтоДт1,
	|	Операция.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Операция.АрендныеОбязательстваОстаток КАК ВалютнаяСуммаДт,      // В валюте договора
	|	0 КАК КоличествоДт,
	|	Операция.АрендныеОбязательстваОстатокРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Арендные обязательства"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Операция.АрендныеОбязательстваСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.АрендныеОбязательстваОстатокРегл <> 0
	|		ИЛИ Операция.АрендныеОбязательстваОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.ВыкупнаяСтоимостьОстатокРегл КАК Сумма,
	|	Операция.ВыкупнаяСтоимостьОстатокУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыкупнаяСтоимость) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Операция.ВыкупнаяСтоимостьОстаток КАК ВалютнаяСуммаДт, // В валюте договора
	|	0 КАК КоличествоДт,
	|	Операция.ВыкупнаяСтоимостьОстатокРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Выкупная стоимость"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Операция.ВыкупнаяСтоимостьСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.ВыкупнаяСтоимостьОстатокРегл <> 0
	|		ИЛИ Операция.ВыкупнаяСтоимостьОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.НДСОстатокРегл КАК Сумма,
	|	Операция.НДСОстатокУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Операция.НДСОстаток КАК ВалютнаяСуммаДт, // В валюте договора
	|	0 КАК КоличествоДт,
	|	Операция.НДСОстатокРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""НДС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Операция.НДССальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.НДСОстатокРегл <> 0
	|		ИЛИ Операция.НДСОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.ПроцентыОстатокРегл КАК Сумма,
	|	Операция.ПроцентыОстатокУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Операция.ПроцентыОстаток КАК ВалютнаяСуммаДт,  // В валюте договора
	|	0 КАК КоличествоДт,
	|	Операция.ПроцентыОстатокРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Проценты"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Операция.ПроцентыСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.ПроцентыОстатокРегл <> 0
	|		ИЛИ Операция.ПроцентыОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.УслугаПоАрендеОстатокРегл КАК Сумма,
	|	Операция.УслугаПоАрендеОстатокУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.УслугаПоАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Операция.УслугаПоАрендеОстаток КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Операция.УслугаПоАрендеОстатокРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Услуги по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Операция.УслугаПоАрендеСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.УслугаПоАрендеОстатокРегл <> 0
	|		ИЛИ Операция.УслугаПоАрендеОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	Строки.СуммаУпр КАК СуммаУУ,
	|	
	|	ВЫБОР Строки.ТипПлатежа
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбеспечительныйПлатеж)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыкупнаяСтоимость)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.УслугаПоАренде)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	Строки.РасчетныйДокумент КАК СубконтоДт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаДт, // В валюте договора
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	ВЫБОР Строки.ТипПлатежа
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж)
	|			ТОГДА ""Обеспечительный платеж""
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс)
	|			ТОГДА ""Авансирование выкупной стоимости""
	|		ИНАЧЕ ""Аванс""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде.АвансыПоДоговорамАренды КАК Строки
	|		ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	(Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Строки.СуммаРегл <> 0
	|		ИЛИ Строки.СуммаУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операция.Договор.Подразделение КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетДт,
	|	Операция.Договор КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СуммыБалансировкиВР.БалансировкаВР КАК СуммаНУДт, // Предыдущие начисления минус предыдущие проценты.
	|	0 КАК СуммаПРДт,
	|	-СуммыБалансировкиВР.БалансировкаВР КАК СуммаВРДт,  
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Балансировка ВР по начислениям до ввода остатков"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыБалансировкиВР КАК СуммыБалансировкиВР
	|		ПО СуммыБалансировкиВР.Ссылка = ДокументыКОтражению.Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область РасчетыПоДоговорамАрендыСальдоКредитовое
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Расчеты по договорам аренды (сальдо кредитовое)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.АрендныеОбязательстваОстатокРегл КАК Сумма,
	|	Операция.АрендныеОбязательстваОстатокУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.Контрагент КАК СубконтоКт1,
	|	Операция.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Операция.АрендныеОбязательстваОстаток КАК ВалютнаяСуммаКт, // В валюте договора
	|	0 КАК КоличествоКт,
	|	Операция.АрендныеОбязательстваОстатокРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Арендные обязательства"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ Операция.АрендныеОбязательстваСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.АрендныеОбязательстваОстатокРегл <> 0
	|		ИЛИ Операция.АрендныеОбязательстваОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.ВыкупнаяСтоимостьОстатокРегл КАК Сумма,
	|	Операция.ВыкупнаяСтоимостьОстатокУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыкупнаяСтоимость) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.Договор КАК СубконтоКт1,
	|	Операция.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Операция.ВыкупнаяСтоимостьОстаток КАК ВалютнаяСуммаКт, // В валюте договора
	|	0 КАК КоличествоКт,
	|	Операция.ВыкупнаяСтоимостьОстатокРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Выкупная стоимость"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ Операция.ВыкупнаяСтоимостьСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.ВыкупнаяСтоимостьОстатокРегл <> 0
	|		ИЛИ Операция.ВыкупнаяСтоимостьОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.НДСОстатокРегл КАК Сумма,
	|	Операция.НДСОстатокУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.Договор КАК СубконтоКт1,
	|	Операция.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Операция.НДСОстаток КАК ВалютнаяСуммаКт, // В валюте договора
	|	0 КАК КоличествоКт,
	|	Операция.НДСОстатокРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""НДС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ Операция.НДССальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.НДСОстатокРегл <> 0
	|		ИЛИ Операция.НДСОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.ПроцентыОстатокРегл КАК Сумма,
	|	Операция.ПроцентыОстатокУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.Договор КАК СубконтоКт1,
	|	Операция.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Операция.ПроцентыОстаток КАК ВалютнаяСуммаКт, // В валюте договора
	|	0 КАК КоличествоКт,
	|	Операция.ПроцентыОстатокРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Проценты"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ Операция.ПроцентыСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.ПроцентыОстатокРегл <> 0
	|		ИЛИ Операция.ПроцентыОстатокУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Операция.УслугаПоАрендеОстатокРегл КАК Сумма,
	|	Операция.УслугаПоАрендеОстатокУпр КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.УслугаПоАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(Операция.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Операция.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Операция.Договор КАК СубконтоКт1,
	|	Операция.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Операция.УслугаПоАрендеОстаток КАК ВалютнаяСуммаКт, // В валюте договора
	|	0 КАК КоличествоКт,
	|	Операция.УслугаПоАрендеОстатокРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Услуги по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетовПоАренде КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	НЕ Операция.УслугаПоАрендеСальдоДебетовое
	|	И (Операция.ОтражатьВБУиНУ ИЛИ Операция.ОтражатьВУУ)
	|	И (Операция.УслугаПоАрендеОстатокРегл <> 0
	|		ИЛИ Операция.УслугаПоАрендеОстатокУпр <> 0)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	
	//++ Локализация
	
	#Область СуммаБалансировкиВР
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА Документ.Договор.Балансодержатель <> ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендатор) ТОГДА
	|		Документ.АрендныеОбязательстваОстатокРегл - Документ.НДСОстатокРегл
	|	ИНАЧЕ 0
	|	КОНЕЦ - Документ.ПроцентыОстатокРегл КАК БалансировкаВР
	|
	|ПОМЕСТИТЬ СуммыБалансировкиВР
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВводОстатковВзаиморасчетовПоАренде КАК Документ
	|		ПО Документ.Ссылка = ДокументыКОтражению.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстыЗапроса.Добавить("");
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти

#Область ФормаДокумента

Процедура ПриВыполненииКоманды(ИмяКоманды, Форма) Экспорт
	
	Объект = Форма.Объект;
	
	//++ Локализация
	
	Если ИмяКоманды = Форма.Команды.ЗаполнитьПоДаннымОУ.Имя Тогда
		ЗаполнитьПоДаннымОУ(Объект);
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПоДаннымОперативногоУчета

Функция ОстаткиПоТипуОперации(Дата, ХозяйственнаяОперация, Организации, ДополнительныйОтбор = Неопределено, ПараметрыЗаполненияОстатков = Неопределено) Экспорт

	Результат = Неопределено;
	
	//++ Локализация
	
	ДатаОстатков = Новый Граница(Дата, ВидГраницы.Включая);
	
	Результат = ОстаткиВзаиморасчетовПоДоговорамАренды(ДатаОстатков, Организации, ДополнительныйОтбор, ПараметрыЗаполненияОстатков);

	//-- Локализация
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Процедура ИнициализироватьДокументПередЗаполнением(Объект) Экспорт

	//++ Локализация
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Объект.ОтражатьВБУиНУ = Истина;
	КонецЕсли; 
	
	ПараметрыУчетаПоОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Объект.Организация, ТекущаяДатаСеанса());
	Объект.НалогообложениеНДС = ПараметрыУчетаПоОрганизации.ОсновноеНалогообложениеНДСПродажи;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

#Область Локализация

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                       КАК Период,
	|	&Организация                  КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)  КАК ДатаОтражения
	|ГДЕ
	|	&Период >= &ДатаНачалаУчета
	|	И (&ОтражатьВБУиНУ ИЛИ &ОтражатьВУУ)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымОУ(Объект)
	
	КлючевыеПоля = Документы.ВводОстатковВзаиморасчетовПоАренде.КлючевыеПоляРеглУчетаПоТипуОперации(Неопределено);
	
	Отбор = Новый Структура;
	Для Каждого ИмяПоля Из КлючевыеПоля Цикл
		Если ИмяПоля <> "Организация" Тогда
			Отбор.Вставить(ИмяПоля, Объект[ИмяПоля]);
		КонецЕсли;		
	КонецЦикла;

	ПараметрыЗаполненияОстатков = Новый Структура("ОтражатьВБУиНУ,ОтражатьВУУ", Объект.ОтражатьВБУиНУ, Объект.ОтражатьВУУ);

	Результат = ОстаткиВзаиморасчетовПоДоговорамАренды(
					Объект.Дата, 
					Объект.Организация, 
					Отбор,
					ПараметрыЗаполненияОстатков);
					
	Если Результат.Количество() <> 0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, Результат[0]);
	КонецЕсли;
		
КонецПроцедуры

Функция ОстаткиВзаиморасчетовПоДоговорамАренды(ДатаОстатков, МассивОрганизаций, ДопОтбор, ПараметрыЗаполненияОстатков)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ втАналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|
	|{ГДЕ
	|	АналитикаУчетаПоПартнерам.Партнер.* КАК Партнер,
	|	АналитикаУчетаПоПартнерам.Контрагент.* КАК Контрагент}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлючиАналитикиУчетаПоПартнерам.Организация КАК Организация,
	|	КлючиАналитикиУчетаПоПартнерам.Партнер КАК Партнер,
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	Расчеты.Договор КАК Договор,
	|	Расчеты.ТипСуммы КАК ТипЗадолженности,
	|	Расчеты.СуммаОстаток > 0 КАК СальдоДебетовое,
	|	ВЫБОР
	|		КОГДА Расчеты.ТипСуммы В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс))
	|			ТОГДА Расчеты.РасчетныйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументАванса,
	|	ВЫБОР
	|		КОГДА Расчеты.СуммаОстаток < 0
	|			ТОГДА -Расчеты.СуммаОстаток
	|		ИНАЧЕ Расчеты.СуммаОстаток
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Расчеты.СуммаРеглОстаток < 0
	|			ТОГДА -Расчеты.СуммаРеглОстаток
	|		ИНАЧЕ Расчеты.СуммаРеглОстаток
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА Расчеты.СуммаОстаток < 0
	|			ТОГДА -Расчеты.СуммаУпрОстаток
	|		ИНАЧЕ Расчеты.СуммаУпрОстаток
	|	КОНЕЦ КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втРасчетыПоФинансовымИнструментам
	|
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|			&Дата,
	|			Договор ССЫЛКА Справочник.ДоговорыАренды
	|			И АналитикаУчетаПоПартнерам В (
	|						ВЫБРАТЬ 
	|							втАналитикаУчетаПоПартнерам.КлючАналитики 
	|						ИЗ 
	|							втАналитикаУчетаПоПартнерам КАК втАналитикаУчетаПоПартнерам)) КАК Расчеты
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|		ПО КлючиАналитикиУчетаПоПартнерам.Ссылка = Расчеты.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Расчеты.СуммаОстаток <> 0
	|
	|{ГДЕ
	|	Расчеты.Договор.* КАК Договор}
	|;
	|
	|ВЫБРАТЬ
	|	""АвансыПоДоговорамАренды"" КАК ЗаполняемаяТабличнаяЧасть,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&ОтражатьВБУиНУ КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУУ КАК ОтражатьВУпрУчете,
	|	Расчеты.Организация КАК Организация,
	|	Расчеты.Партнер КАК Партнер,
	|	Расчеты.Контрагент КАК Контрагент,
	|	Расчеты.Договор КАК Договор,
	|
	// АрендныеОбязательства
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|				ТОГДА Расчеты.Сумма 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК АрендныеОбязательстваОстаток,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|				ТОГДА Расчеты.СуммаРегл 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК АрендныеОбязательстваОстатокРегл,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|				ТОГДА Расчеты.СуммаУпр 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК АрендныеОбязательстваОстатокУпр,
	|
	|	МАКСИМУМ(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|				ТОГДА Расчеты.СальдоДебетовое 
	|				ИНАЧЕ ЛОЖЬ 
	|			КОНЕЦ) КАК АрендныеОбязательстваСальдоДебетовое,
	|
	// ВыкупнаяСтоимость
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимость)
	|				ТОГДА Расчеты.Сумма 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ВыкупнаяСтоимостьОстаток,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимость)
	|				ТОГДА Расчеты.СуммаРегл 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ВыкупнаяСтоимостьОстатокРегл,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимость)
	|				ТОГДА Расчеты.СуммаУпр 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ВыкупнаяСтоимостьОстатокУпр,
	|
	|	МАКСИМУМ(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимость)
	|				ТОГДА Расчеты.СальдоДебетовое 
	|				ИНАЧЕ ЛОЖЬ 
	|			КОНЕЦ) КАК ВыкупнаяСтоимостьСальдоДебетовое,
	|
	// НДС
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)
	|				ТОГДА Расчеты.Сумма 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК НДСОстаток,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)
	|				ТОГДА Расчеты.СуммаРегл 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК НДСОстатокРегл,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)
	|				ТОГДА Расчеты.СуммаУпр 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК НДСОстатокУпр,
	|
	|	МАКСИМУМ(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)
	|				ТОГДА Расчеты.СальдоДебетовое 
	|				ИНАЧЕ ЛОЖЬ 
	|			КОНЕЦ) КАК НДССальдоДебетовое,
	|
	// Проценты
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|				ТОГДА Расчеты.Сумма 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ПроцентыОстаток,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|				ТОГДА Расчеты.СуммаРегл 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ПроцентыОстатокРегл,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|				ТОГДА Расчеты.СуммаУпр 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК ПроцентыОстатокУпр,
	|
	|	МАКСИМУМ(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|				ТОГДА Расчеты.СальдоДебетовое 
	|				ИНАЧЕ ЛОЖЬ 
	|			КОНЕЦ) КАК ПроцентыСальдоДебетовое,
	|
	// УслугаПоАренде
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде)
	|				ИЛИ Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
	|				ТОГДА Расчеты.Сумма 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК УслугаПоАрендеОстаток,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде)
	|				ИЛИ Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
	|				ТОГДА Расчеты.СуммаРегл 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК УслугаПоАрендеОстатокРегл,
	|
	|	СУММА(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде)
	|				ИЛИ Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
	|				ТОГДА Расчеты.СуммаУпр 
	|				ИНАЧЕ 0 
	|			КОНЕЦ) КАК УслугаПоАрендеОстатокУпр,
	|
	|	МАКСИМУМ(ВЫБОР 
	|			КОГДА Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде)
	|				ИЛИ Расчеты.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
	|				ТОГДА Расчеты.СальдоДебетовое 
	|				ИНАЧЕ ЛОЖЬ 
	|			КОНЕЦ) КАК УслугаПоАрендеСальдоДебетовое
	|ИЗ
	|	втРасчетыПоФинансовымИнструментам КАК Расчеты
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Организация,
	|	Расчеты.Партнер,
	|	Расчеты.Контрагент,
	|	Расчеты.Договор
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.Организация,
	|	Расчеты.Партнер,
	|	Расчеты.Контрагент,
	|	Расчеты.Договор";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Запрос = Новый ПостроительЗапроса(ТекстЗапроса);
	Запрос.Параметры.Вставить("МассивОрганизаций", МассивОрганизаций);
	Запрос.Параметры.Вставить("Дата", ДатаОстатков);
	Запрос.Параметры.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВводОстатковВзаиморасчетовПоДоговорамАренды);
	Запрос.Параметры.Вставить("ОтражатьВБУиНУ", ПараметрыЗаполненияОстатков.ОтражатьВБУиНУ);
	Запрос.Параметры.Вставить("ОтражатьВУУ", ПараметрыЗаполненияОстатков.ОтражатьВУУ);
	
	Если ДопОтбор <> Неопределено Тогда
		Для Каждого УсловиеОтбора Из ДопОтбор Цикл
			ТекущийОтбор = Запрос.Отбор.Добавить(УсловиеОтбора.Ключ);
			ТекущийОтбор.ВидСравнения = ВидСравнения.Равно;
			ТекущийОтбор.Значение = УсловиеОтбора.Значение;
			ТекущийОтбор.Использование = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.Результат.Выгрузить();

КонецФункции

#КонецОбласти

//-- Локализация

#КонецОбласти
