
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация    
	ТекстыЗапросов = Новый Массив;
	
#Область СписаниеНедопоставкиНаПрочиеРасходы  // (Дт 91 :: Кт 60.НП, 15)
	СписаниеНедопоставкиНаПрочиеРасходы = "
	|ВЫБРАТЬ // Списание недопоставки на прочие расходы (Дт 91 :: Кт 60.НП, 15)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ДвиженияПрочиеРасходы.СуммаРегл КАК Сумма,
	|	ДвиженияПрочиеРасходы.СуммаУпр КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ДвиженияПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ДвиженияПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ДвиженияПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	ДвиженияПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	// ДТ - Субконто
	|	ДвиженияПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	ДвиженияПрочиеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ДвиженияПрочиеРасходы.СуммаРегл
	|		- ДвиженияПрочиеРасходы.ПостояннаяРазница 
	|		- ДвиженияПрочиеРасходы.ВременнаяРазница	КАК СуммаНУДт,
	|	ДвиженияПрочиеРасходы.ПостояннаяРазница КАК СуммаПРДт,
	|	ДвиженияПрочиеРасходы.ВременнаяРазница КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки)    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ)    
	|	КОНЕЦ КАК ВидСчетаКт,  
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.ГруппаФинансовогоУчета    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета    
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.НаправлениеДеятельности    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности    
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	// КТ - Субконто
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт1,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Контрагент КАК СубконтоКт2,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаКт,
	|	ДвиженияСебестоимостьТоваров.Количество КАК КоличествоКт,
	|	ДвиженияСебестоимостьТоваров.СтоимостьРегл 
	|		+ ДвиженияСебестоимостьТоваров.ДопРасходыРегл 
	|		- ДвиженияСебестоимостьТоваров.ПостояннаяРазница 
	|		- ДвиженияСебестоимостьТоваров.ВременнаяРазница	КАК СуммаНУКт,
	|	ДвиженияСебестоимостьТоваров.ПостояннаяРазница КАК СуммаПРКт,
	|	ДвиженияСебестоимостьТоваров.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Списание недопоставки на прочие расходы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.СебестоимостьТоваров КАК ДвиженияСебестоимостьТоваров
	|	ПО 
	|		ДвиженияСебестоимостьТоваров.Регистратор = Операция.Ссылка
	|		И ДвиженияСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДвиженияСебестоимостьТоваров.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))
	|		И НЕ ДвиженияСебестоимостьТоваров.Сторно
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ПрочиеРасходы КАК ДвиженияПрочиеРасходы
	|	ПО 
	|		ДвиженияПрочиеРасходы.Регистратор = Операция.Ссылка
	|		И ДвиженияПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДвиженияПрочиеРасходы.ИдентификаторФинЗаписи = ДвиженияСебестоимостьТоваров.ИдентификаторФинЗаписи
	|		И ДвиженияПрочиеРасходы.НастройкаХозяйственнойОперации = ДвиженияСебестоимостьТоваров.НастройкаХозяйственнойОперации
	|		И НЕ ДвиженияПрочиеРасходы.Сторно
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ДвиженияПрочиеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО
	|		ДвиженияПрочиеРасходы.АналитикаРасходов = ОбъектыСтроительства.Ссылка
	|";  
	ТекстыЗапросов.Добавить(СписаниеНедопоставкиНаПрочиеРасходы);
#КонецОбласти        

#Область СписаниеНедопоставкиПрочиеАктивы  // (Дт X :: Кт 60.НП, 15)
	СписаниеНедопоставкиПрочиеАктивы = "
	|ВЫБРАТЬ // Списание недопоставки на прочие активы (Дт X :: Кт 60.НП, 15)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ДвиженияПоПрочимАктивамПассивам.СуммаРегл КАК Сумма,
	|	ДвиженияПоПрочимАктивамПассивам.СуммаУпр КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ДвиженияПоПрочимАктивамПассивам.Подразделение КАК ПодразделениеДт,
	|	ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.СчетУчета КАК СчетДт,
	// ДТ - Субконто
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто1 КАК СубконтоДт1,
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто2 КАК СубконтоДт2,
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто3 КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ДвиженияПоПрочимАктивамПассивам.СуммаРегл
	|		- ДвиженияПоПрочимАктивамПассивам.ПостояннаяРазница 
	|		- ДвиженияПоПрочимАктивамПассивам.ВременнаяРазница КАК СуммаНУДт,
	|	ДвиженияПоПрочимАктивамПассивам.ПостояннаяРазница КАК СуммаПРДт,
	|	ДвиженияПоПрочимАктивамПассивам.ВременнаяРазница КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки)    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ)    
	|	КОНЕЦ КАК ВидСчетаКт,  
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.ГруппаФинансовогоУчета    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета    
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.Подразделение КАК ПодразделениеКт,
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.НаправлениеДеятельности    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности    
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	// КТ - Субконто
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт1,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Контрагент КАК СубконтоКт2,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаКт,
	|	ДвиженияСебестоимостьТоваров.Количество КАК КоличествоКт,
	|	ДвиженияСебестоимостьТоваров.СтоимостьРегл 
	|		+ ДвиженияСебестоимостьТоваров.ДопРасходыРегл 
	|		- ДвиженияСебестоимостьТоваров.ПостояннаяРазница 
	|		- ДвиженияСебестоимостьТоваров.ВременнаяРазница	КАК СуммаНУКт,
	|	ДвиженияСебестоимостьТоваров.ПостояннаяРазница КАК СуммаПРКт,
	|	ДвиженияСебестоимостьТоваров.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Списание недопоставки на прочие активы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.СебестоимостьТоваров КАК ДвиженияСебестоимостьТоваров
	|	ПО 
	|		ДвиженияСебестоимостьТоваров.Регистратор = Операция.Ссылка
	|		И ДвиженияСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДвиженияСебестоимостьТоваров.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))
	|		И НЕ ДвиженияСебестоимостьТоваров.Сторно
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
	|	ПО 
	|		ДвиженияПоПрочимАктивамПассивам.Регистратор = Операция.Ссылка
	|		И ДвиженияПоПрочимАктивамПассивам.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		И ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи = ДвиженияСебестоимостьТоваров.ИдентификаторФинЗаписи
	|		И ДвиженияПоПрочимАктивамПассивам.НастройкаХозяйственнойОперации = ДвиженияСебестоимостьТоваров.НастройкаХозяйственнойОперации
	|		И НЕ ДвиженияПоПрочимАктивамПассивам.Сторно
	|";  
	ТекстыЗапросов.Добавить(СписаниеНедопоставкиПрочиеАктивы);
#КонецОбласти        

#Область СписаниеПерепоставкиНаПрочиеДоходы  // (Дт 60.НП, 15 :: Кт 91)
	СписаниеПерепоставкиНаПрочиеДоходы = "
	|ВЫБРАТЬ // Списание перепоставки на прочие доходы (Дт 60.НП, 15 :: Кт 91)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ДвиженияПрочиеДоходы.СуммаРегл КАК Сумма,
	|	ДвиженияПрочиеДоходы.СуммаУпр КАК СуммаУУ,
	|	
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки)    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ)    
	|	КОНЕЦ КАК ВидСчетаДт,  
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.ГруппаФинансовогоУчета    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета    
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.НаправлениеДеятельности    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности    
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	// ДТ - Субконто
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоДт1,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Контрагент КАК СубконтоДт2,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор КАК СубконтоДт3,
	|
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	- ДвиженияСебестоимостьТоваров.Количество КАК КоличествоДт,
	|	- (ДвиженияСебестоимостьТоваров.СтоимостьРегл 
	|		+ ДвиженияСебестоимостьТоваров.ДопРасходыРегл 
	|		- ДвиженияСебестоимостьТоваров.ПостояннаяРазница 
	|		- ДвиженияСебестоимостьТоваров.ВременнаяРазница)	КАК СуммаНУДт,
	|	- ДвиженияСебестоимостьТоваров.ПостояннаяРазница КАК СуммаПРДт,
	|	- ДвиженияСебестоимостьТоваров.ВременнаяРазница КАК СуммаВРДт,
	|    
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	ДвиженияПрочиеДоходы.СтатьяДоходов КАК АналитикаУчетаКт,
	|	ДвиженияПрочиеДоходы.Подразделение КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, НаправлениеДеятельности, Счет
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ДвиженияПрочиеДоходы.Подразделение КАК ПодразделениеКт,
	|	ДвиженияПрочиеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	// КТ - Субконто
	|	ДвиженияПрочиеДоходы.СтатьяДоходов КАК СубконтоКт1,
	|	ДвиженияПрочиеДоходы.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ДвиженияПрочиеДоходы.СуммаРегл	КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Списание перепоставки на прочие доходы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.СебестоимостьТоваров КАК ДвиженияСебестоимостьТоваров
	|	ПО 
	|		ДвиженияСебестоимостьТоваров.Регистратор = Операция.Ссылка
	|		И ДвиженияСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДвиженияСебестоимостьТоваров.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))
	|		И НЕ ДвиженияСебестоимостьТоваров.Сторно
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ПрочиеДоходы КАК ДвиженияПрочиеДоходы
	|	ПО 
	|		ДвиженияПрочиеДоходы.Регистратор = Операция.Ссылка
	|		И ДвиженияПрочиеДоходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДвиженияПрочиеДоходы.ИдентификаторФинЗаписи = ДвиженияСебестоимостьТоваров.ИдентификаторФинЗаписи
	|		И ДвиженияПрочиеДоходы.НастройкаХозяйственнойОперации = ДвиженияСебестоимостьТоваров.НастройкаХозяйственнойОперации
	|		И НЕ ДвиженияПрочиеДоходы.Сторно
	|";   
	ТекстыЗапросов.Добавить(СписаниеПерепоставкиНаПрочиеДоходы);
#КонецОбласти	

#Область СписаниеПерепоставкиНаПрочиеПассивы  // (Дт 60.НП, 15 :: Кт X)
	СписаниеПерепоставкиНаПрочиеПассивы = "
	|ВЫБРАТЬ // Списание перепоставки на прочие пассивы (Дт 60.НП, 15 :: Кт X)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ДвиженияПоПрочимАктивамПассивам.СуммаРегл КАК Сумма,
	|	ДвиженияПоПрочимАктивамПассивам.СуммаУпр КАК СуммаУУ,
	|	
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки)    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ)    
	|	КОНЕЦ КАК ВидСчетаДт,  
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.ГруппаФинансовогоУчета    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета    
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор.НаправлениеДеятельности    
	|		КОГДА ДвиженияСебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			ТОГДА ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности    
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	// ДТ - Субконто
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоДт1,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Контрагент КАК СубконтоДт2,
	|	ДвиженияСебестоимостьТоваров.АналитикаУчетаНоменклатуры.Договор КАК СубконтоДт3,
	|
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	- ДвиженияСебестоимостьТоваров.Количество КАК КоличествоДт,
	|	- (ДвиженияСебестоимостьТоваров.СтоимостьРегл 
	|		+ ДвиженияСебестоимостьТоваров.ДопРасходыРегл 
	|		- ДвиженияСебестоимостьТоваров.ПостояннаяРазница 
	|		- ДвиженияСебестоимостьТоваров.ВременнаяРазница)	КАК СуммаНУДт,
	|	- ДвиженияСебестоимостьТоваров.ПостояннаяРазница КАК СуммаПРДт,
	|	- ДвиженияСебестоимостьТоваров.ВременнаяРазница КАК СуммаВРДт,
	|    
	// КТ - Вид счета, Аналитика учета, Место учета
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, НаправлениеДеятельности, Счет
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ДвиженияПоПрочимАктивамПассивам.Подразделение КАК ПодразделениеКт,
	|	ДвиженияПоПрочимАктивамПассивам.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.СчетУчета КАК СчетКт,
	// КТ - Субконто
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто1 КАК СубконтоКт1,
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто2 КАК СубконтоКт2,
	|	ДвиженияПоПрочимАктивамПассивам.НастройкаСчетовУчета.Субконто3 КАК СубконтоКт3,
	|	
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ДвиженияПоПрочимАктивамПассивам.СуммаРегл
	|		- ДвиженияПоПрочимАктивамПассивам.ПостояннаяРазница
	|		- ДвиженияПоПрочимАктивамПассивам.ВременнаяРазница КАК СуммаНУКт,
	|	ДвиженияПоПрочимАктивамПассивам.ПостояннаяРазница КАК СуммаПРКт,
	|	ДвиженияПоПрочимАктивамПассивам.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Списание перепоставки на прочие пассивы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.СебестоимостьТоваров КАК ДвиженияСебестоимостьТоваров
	|	ПО 
	|		ДвиженияСебестоимостьТоваров.Регистратор = Операция.Ссылка
	|		И ДвиженияСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДвиженияСебестоимостьТоваров.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))
	|		И НЕ ДвиженияСебестоимостьТоваров.Сторно
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
	|	ПО 
	|		ДвиженияПоПрочимАктивамПассивам.Регистратор = Операция.Ссылка
	|		И ДвиженияПоПрочимАктивамПассивам.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|		И ДвиженияПоПрочимАктивамПассивам.ИдентификаторФинЗаписи = ДвиженияСебестоимостьТоваров.ИдентификаторФинЗаписи
	|		И ДвиженияПоПрочимАктивамПассивам.НастройкаХозяйственнойОперации = ДвиженияСебестоимостьТоваров.НастройкаХозяйственнойОперации
	|		И НЕ ДвиженияПоПрочимАктивамПассивам.Сторно
	|";
	ТекстыЗапросов.Добавить(СписаниеПерепоставкиНаПрочиеПассивы);
#КонецОбласти	

#Область СписаниеНДСНаРасходы  // (Дт 91 :: Кт 19)
	СписаниеНДСНаРасходы = "
	|ВЫБРАТЬ //// Списание НДС на расходы (Дт 91 :: Кт 19.03) @НДСПредъявленный
	|
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА Партии.НДСРегл
	|		ИНАЧЕ -Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА Партии.НДСУпр
	|		ИНАЧЕ -Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Операция.СтатьяРасходов КАК СубконтоДт1,
	|	Операция.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка))
	|			= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ТОГДА ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание НДС на расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Партии КАК Партии
	|	ПО Партии.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО СтатьиРасходов.Ссылка = Операция.СтатьяРасходов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = Операция.АналитикаРасходов
	|		И НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		И ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|		И Партии.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)";
	ТекстыЗапросов.Добавить(СписаниеНДСНаРасходы);
#КонецОбласти	

#Область СписаниеНДСНаПрочиеАктивы  // (Дт Х :: Кт 19)
	СписаниеНДСНаПрочиеАктивы = "
	|ВЫБРАТЬ //// Включение/исключение НДС (Дт 91 :: Кт 19.03) @НДСПредъявленный
	|
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА Партии.НДСРегл
	|		ИНАЧЕ -Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА Партии.НДСУпр
	|		ИНАЧЕ -Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Операция.НастройкаСчетовУчетаАктивов.СчетУчета КАК СчетДт,
	|	Операция.НастройкаСчетовУчетаАктивов.Субконто1 КАК СубконтоДт1,
	|	Операция.НастройкаСчетовУчетаАктивов.Субконто2 КАК СубконтоДт2,
	|	Операция.НастройкаСчетовУчетаАктивов.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание НДС на прочие активы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеРасхожденийПоступлениеПриобретение КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Партии КАК Партии
	|	ПО Партии.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьиАктивовПассивов
	|	ПО СтатьиАктивовПассивов.Ссылка = Операция.СтатьяРасходов
    |
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|		И Партии.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)";
	ТекстыЗапросов.Добавить(СписаниеНДСНаПрочиеАктивы);
#КонецОбласти	

	Возврат СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

// Заполняет массив допустимых наименований входящих документов.
//
// Параметры:
//  МассивНаименований	 - Массив - массив наименования входящих документов.
//
Процедура ДополнитьНаименованияВходящихДокументов(МассивНаименований) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры
//++ Локализация

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти
