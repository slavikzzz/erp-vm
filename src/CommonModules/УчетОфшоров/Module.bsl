#Область ОперацииНДС0

Функция СписокОфшоров(Знач ДатаЗапроса) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаЗапроса) Тогда
		
		ДатаЗапроса = ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Параметры.Вставить("ТаблицаОфшоров", КлассификаторОфшоров());
	Запрос.Параметры.Вставить("ДатаЗапроса", НачалоДня(ДатаЗапроса));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОфшоров.Код КАК Код,
	|	ТаблицаОфшоров.ДатаНачала КАК ДатаНачала,
	|	ТаблицаОфшоров.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ Офшоры
	|ИЗ
	|	&ТаблицаОфшоров КАК ТаблицаОфшоров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Код,
	|	ДатаОкончания,
	|	ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраныМира.Ссылка КАК СтранаРегистрации
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|		ЛЕВОЕ СОЕДИНЕНИЕ Офшоры КАК Офшоры
	|		ПО СтраныМира.Код = Офшоры.Код
	|ГДЕ
	|	(Офшоры.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|				И Офшоры.ДатаНачала <= &ДатаЗапроса
	|			ИЛИ &ДатаЗапроса МЕЖДУ Офшоры.ДатаНачала И Офшоры.ДатаОкончания)
	|	И СтраныМира.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Офшоры";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СтранаРегистрации");
	
КонецФункции

Функция ЯвляетсяОфшором(Знач ДатаЗапроса, Страна) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаЗапроса) Тогда
		ДатаЗапроса = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Параметры.Вставить("ТаблицаОфшоров", КлассификаторОфшоров());
	Запрос.Параметры.Вставить("ДатаЗапроса", НачалоДня(ДатаЗапроса));
	Запрос.Параметры.Вставить("Страна", Страна);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОфшоров.Код КАК Код,
	|	ТаблицаОфшоров.ДатаНачала КАК ДатаНачала,
	|	ТаблицаОфшоров.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ Офшоры
	|ИЗ
	|	&ТаблицаОфшоров КАК ТаблицаОфшоров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Код,
	|	ДатаОкончания,
	|	ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраныМира.Ссылка КАК СтранаРегистрации
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|		ЛЕВОЕ СОЕДИНЕНИЕ Офшоры КАК Офшоры
	|		ПО СтраныМира.Код = Офшоры.Код
	|ГДЕ
	|	(Офшоры.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|				И Офшоры.ДатаНачала <= &ДатаЗапроса
	|			ИЛИ &ДатаЗапроса МЕЖДУ Офшоры.ДатаНачала И Офшоры.ДатаОкончания)
	|	И СтраныМира.Ссылка = &Страна
	|	И СтраныМира.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Офшоры";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область КонтролируемыеСделки

// Возвращает страны-офшоры по отчетному году.
// Формирует список стран, которые действовали на начало отчетного года - на 1 января.
//
// Параметры:
//  ОтчетныйГод - Дата - дата, которая определяет отчетный год.
//
// Возвращаемое значение:
//  - ТаблицаЗначений
//		- Колонки:
//			СтранаРегистрации - СправочникСсылка.СтраныМира
//			ДатаНачалаДействияОфшора - Дата
//			ДатаОкончанияДействияОфшора - Дата
Функция СтраныОфшорыПоОтчетномуГоду(Знач ОтчетныйГод) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	
	Запрос.Параметры.Вставить("ТаблицаОфшоров", КлассификаторОфшоров());
	Запрос.Параметры.Вставить("ОтчетныйГод", НачалоГода(ОтчетныйГод));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОфшоров.Код КАК Код,
	|	ТаблицаОфшоров.ДатаНачала КАК ДатаНачала,
	|	ТаблицаОфшоров.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ Офшоры
	|ИЗ
	|	&ТаблицаОфшоров КАК ТаблицаОфшоров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Код,
	|	ДатаОкончания,
	|	ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраныМира.Ссылка КАК СтранаРегистрации,
	|	Офшоры.ДатаНачала КАК ДатаНачалаДействияОфшора,
	|	Офшоры.ДатаОкончания КАК ДатаОкончанияДействияОфшора
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Офшоры КАК Офшоры
	|		ПО СтраныМира.Код = Офшоры.Код
	|ГДЕ
	|	Офшоры.ДатаНачала <= &ОтчетныйГод
	|	И (Офшоры.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ Офшоры.ДатаОкончания >= &ОтчетныйГод)
	|	И СтраныМира.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Офшоры";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция КлассификаторОфшоров()
	
	МакетОфшоров = ПолучитьОбщийМакет("ПереченьОфшоров");
	КлассификаторXML = МакетОфшоров.ПолучитьТекст();
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(КлассификаторXML);
	Возврат СериализаторXDTO.ПрочитатьXML(Чтение);
	
КонецФункции

#КонецОбласти