
#Область СлужебныеПроцедурыИФункции

#Область ЗаписьВЖурналРегистрации

Процедура ЗаписатьСобытиеНачалоОбмена()

	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Информация,,, КомментарийЖРНачалоОбмена(НастройкиСервиса));

КонецПроцедуры

Функция КомментарийЖРНачалоОбмена(НастройкиСервиса)

	СтрокаПриложение 	= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор приложения';
													|en = 'Application ID'"), НастройкиСервиса.ИдентификаторПриложения);
	СтрокаИС 			= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор информационной системы';
													|en = 'Information system ID'"), НастройкиСервиса.Идентификатор);
	СтрокаСобытие 		= НСтр("ru = 'Начало процесса обмена данными.';
								|en = 'Start data exchange.'");
	Комментарий = СтрШаблон("%1 %2 %3", СтрокаСобытие, СтрокаПриложение, СтрокаИС);
	
	Возврат Комментарий;

КонецФункции

Процедура ЗаписатьСобытиеОкончаниеОбмена(БылиОшибки)

	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Информация,,,КомментарийЖРОкончаниеОбмена(НастройкиСервиса, БылиОшибки));	

КонецПроцедуры

Функция КомментарийЖРОкончаниеОбмена(НастройкиСервиса, БылиОшибки)

	СтрокаПриложение 	= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор приложения';
													|en = 'Application ID'"), НастройкиСервиса.ИдентификаторПриложения);
	СтрокаИС 			= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор информационной системы';
													|en = 'Information system ID'"), НастройкиСервиса.Идентификатор);
	
	СтрокаСобытие = НСтр("ru = 'Окончание процесса обмена данными.';
						|en = 'Finish data exchange.'");
	Если БылиОшибки Тогда
		СтрокаСобытие = СтрШаблон("%1 %2", СтрокаСобытие, НСтр("ru = 'Были ошибки.';
																|en = 'There were errors.'"));
	КонецЕсли;
	Комментарий = СтрШаблон("%1 %2 %3", СтрокаСобытие, СтрокаПриложение, СтрокаИС);
	
	Возврат Комментарий;

КонецФункции

Процедура ЗаписатьСобытиеНачалоПубликацииРЛ()

	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Информация,,, КомментарийЖРНачалоПубликацииРЛ(НастройкиСервиса));

КонецПроцедуры

Функция КомментарийЖРНачалоПубликацииРЛ(НастройкиСервиса)

	СтрокаПриложение 	= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор приложения';
													|en = 'Application ID'"), НастройкиСервиса.ИдентификаторПриложения);
	СтрокаИС 			= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор информационной системы';
													|en = 'Information system ID'"), НастройкиСервиса.Идентификатор);
	СтрокаСобытие 		= НСтр("ru = 'Начало процесса публикации расчетных листков.';
								|en = 'Start payslip publication.'");
	Комментарий = СтрШаблон("%1 %2 %3", СтрокаСобытие, СтрокаПриложение, СтрокаИС);
	
	Возврат Комментарий;

КонецФункции

Процедура ЗаписатьСобытиеОкончаниеПубликацииРЛ(БылиОшибки, КоличествоВыгружено)

	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Информация,,,КомментарийЖРОкончаниеПубликацииРЛ(НастройкиСервиса, БылиОшибки, КоличествоВыгружено));

КонецПроцедуры

Функция КомментарийЖРОкончаниеПубликацииРЛ(НастройкиСервиса, БылиОшибки, КоличествоВыгружено)

	СтрокаПриложение 	= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор приложения';
													|en = 'Application ID'"), НастройкиСервиса.ИдентификаторПриложения);
	СтрокаИС 			= СтрШаблон("%1: %2",  НСтр("ru = 'Идентификатор информационной системы';
													|en = 'Information system ID'"), НастройкиСервиса.Идентификатор);
	
	СтрокаСобытие = НСтр("ru = 'Окончание процесса публикации расчетных листков. Опубликовано: %1.';
						|en = 'Finishing payslip publication. Published: %1.'");
	СтрокаСобытие = СтрШаблон(СтрокаСобытие, КоличествоВыгружено);
	Если БылиОшибки Тогда
		СтрокаСобытие = СтрШаблон("%1 %2", СтрокаСобытие, НСтр("ru = 'Были ошибки.';
																|en = 'There were errors.'"));
	КонецЕсли;
	Комментарий = СтрШаблон("%1 %2 %3", СтрокаСобытие, СтрокаПриложение, СтрокаИС);
	
	Возврат Комментарий;

КонецФункции

Функция ОписаниеТекстаЗапросаДляЖР(ПараметрыОбмена, ИмяМетода, HTTPЗапрос)

	АдресСервера = СтрШаблон("%1://%2", ПараметрыОбмена.СтруктураАдреса.Схема, ПараметрыОбмена.СтруктураАдреса.ИмяСервера);
	ЗапросТекст = СтрШаблон("%1 %2%3%4", ИмяМетода, АдресСервера, HTTPЗапрос.АдресРесурса);
	Для Каждого Заголовок Из HTTPЗапрос.Заголовки Цикл
		ЗначениеЗаголовка = Заголовок.Значение;
		Если Заголовок.Ключ = "Authorization" Тогда
			ЗначениеЗаголовка = "*";
		ИначеЕсли Не ЗначениеЗаполнено(ЗначениеЗаголовка) Тогда
			ЗначениеЗаголовка = "<null>";
		КонецЕсли;
		ЗапросТекст = СтрШаблон("%1%2%3: %4",ЗапросТекст, Символы.ПС, Заголовок.Ключ, ЗначениеЗаголовка);
	КонецЦикла;
	
	Возврат ЗапросТекст;

КонецФункции

Функция ОписаниеОтветаЗапросаДляЖР(HTTPЗапрос, HTTPОтвет, ЗапросФайла = Ложь)

	Если HTTPОтвет = Неопределено Тогда
		ОтветТекст = НСтр("ru = 'Не удалось получить ответ от сервера.';
							|en = 'Cannot get a response from the server.'");
	Иначе
		ОтветТекст = СтрШаблон(НСтр("ru = 'Сервер вернул код состояния: %1';
									|en = 'Server returned status code: %1'"), HTTPОтвет.КодСостояния);
		ПубликацияФайла = (HTTPЗапрос.ПолучитьИмяФайлаТела() <> Неопределено);
		Если Не ПубликацияФайла И Не ЗапросФайла Тогда
			ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Если ЗначениеЗаполнено(ТелоОтвета) Тогда
				ОтветТекст = СтрШаблон("%1%2%2%4", ОтветТекст, Символы.ПС, Символы.ПС, ТелоОтвета);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОтветТекст;

КонецФункции

Процедура ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, HTTPЗапрос)

	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ПараметрыОбмена, ИмяМетода, HTTPЗапрос);
	
	ТелоЗапроса = "";
	ЭтоПередачаФайла = (HTTPЗапрос.ПолучитьИмяФайлаТела() <> Неопределено);
	Если Не ЭтоПередачаФайла Тогда
		ТелоЗапроса = HTTPЗапрос.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ТелоЗапроса);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПротоколЗапрос(), УровеньЖурналаРегистрации.Информация,,,Комментарий);

КонецПроцедуры

Процедура ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, HTTPЗапрос, HTTPОтвет, ЗапросФайла = Ложь)

	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ПараметрыОбмена, ИмяМетода, HTTPЗапрос);
	ОтветТекст  = ОписаниеОтветаЗапросаДляЖР(HTTPЗапрос, HTTPОтвет, ЗапросФайла);
	
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПротоколОтвет(), УровеньЖурналаРегистрации.Информация,,,Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, HTTPЗапрос, HTTPОтвет = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ПараметрыОбмена, ИмяМетода, HTTPЗапрос);
	ОтветТекст  = ОписаниеОтветаЗапросаДляЖР(HTTPЗапрос, HTTPОтвет);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ОтветТекст = СтрШаблон("%1%2%3",ОтветТекст,Символы.ПС,ОписаниеОшибки);
	КонецЕсли;

	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, ОписаниеОшибки) Экспорт

	Если ТипЗнч(ОписаниеОшибки) = Тип("Структура") Тогда
		ОписаниеОшибкиТекст = СформироватьJSON(ОписаниеОшибки);
	Иначе
		ОписаниеОшибкиТекст = ОписаниеОшибки;
	КонецЕсли;
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при загрузке %1
	|Описание ошибки:
	|%2';
	|en = 'An error occurred while importing %1
	|Error details:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, ТипИзменений, ОписаниеОшибкиТекст);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	
КонецПроцедуры

Процедура ЗаписатьПредупреждениеПроверкиВерсийФайлов(ДокументКЭДО, ТекстСообщения)

	ШаблонОписания = НСтр(
	"ru = 'Проверка версии электронного документа:
	|%1';
	|en = 'Check the electronic document version:
	|%1'");
	Комментарий = СтрШаблон(ШаблонОписания, ТекстСообщения);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Предупреждение,, ДокументКЭДО, Комментарий);

КонецПроцедуры

Процедура ЗарегистрироватьОшибкуПубликацииОбъекта(ОбъектПубликации, РесурсСервиса, ОписаниеОшибки) Экспорт

	Если ЗначениеЗаполнено(ОбъектПубликации) Тогда
		ПредставлениеОбъекта = Строка(ОбъектПубликации);
	Иначе
		ПредставлениеОбъекта = НСтр("ru = '<объект не найден>';
									|en = '<object is not found>'");
	КонецЕсли;
	
	ШаблонОписания = НСтр(
	"ru = 'Ошибка публикации: ресурс: %1, объект: %2
	|Описание ошибки:
	|%3';
	|en = 'Publishing error: resource %1, object: %2
	|Error description:
	|%3'");
	ТекстОшибки = СтрШаблон(ШаблонОписания, РесурсСервиса, ПредставлениеОбъекта, ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,, ОбъектПубликации, ТекстОшибки);

КонецПроцедуры

Процедура ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки) Экспорт
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при выгрузке %1
	|Описание ошибки:
	|%2';
	|en = 'An error occurred while exporting %1
	|Error details:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеИзменений, ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВыгрузкиИзменений(ДанныеСсылка, ОписаниеОшибки) Экспорт
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при выгрузке %1
	|Описание ошибки:
	|%2';
	|en = 'An error occurred while exporting %1
	|Error details:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, Строка(ДанныеСсылка), ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,, ДанныеСсылка, ТекстОшибки);
	
КонецПроцедуры

Процедура ЗаписатьПредупреждениеВыгрузкиИзменений(ДанныеСсылка, ОписаниеОшибки) Экспорт
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при выгрузке %1
	|Описание ошибки:
	|%2';
	|en = 'An error occurred while exporting %1
	|Error details:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, Строка(ДанныеСсылка), ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Предупреждение,, ДанныеСсылка, ТекстОшибки);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВыгрузкиФайла(ОписаниеОшибки)
	
	ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеТокенаАутентификации

Функция ТокенАутентификации(ПараметрыОбмена)
	
	УстановитьПривилегированныйРежим(Истина);
	АдресТокена = ИнтеграцияУправлениеПерсоналом.АдресХраненияТокенаПриложения(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		АдресТокена,
		"Токен,СрокГодностиТокена");
	УстановитьПривилегированныйРежим(Ложь);
	
	Токен = Неопределено;
	Если ДанныеВБезопасномХранилище.Токен <> Неопределено
		И ДанныеВБезопасномХранилище.СрокГодностиТокена <> Неопределено Тогда
		
		Если ТекущаяУниверсальнаяДатаВМиллисекундах() > ДанныеВБезопасномХранилище.СрокГодностиТокена Тогда
			Токен = НовыйТокенАутентификации(ПараметрыОбмена).Токен;
		Иначе
			Токен = ДанныеВБезопасномХранилище.Токен;
		КонецЕсли;
		
	Иначе
		Токен = НовыйТокенАутентификации(ПараметрыОбмена).Токен;
	КонецЕсли;
	
	Если Токен = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка авторизации.';
								|en = 'Authorization error.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Возврат Токен;
	
КонецФункции

Функция НовыйТокенАутентификации(ПараметрыОбмена)
	
	Результат = Новый Структура("Токен,НеактуальныеКлючи",,Ложь);
	
	КлючиПриложения = ИнтеграцияУправлениеПерсоналом.КлючиПриложения(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
	Если КлючиПриложения = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Не заданы ключи приложения.';
								|en = 'Application keys not set.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Ответ = НовыйТокен(ПараметрыОбмена, КлючиПриложения);
	ЗаполнитьЗначенияСвойств(Результат, Ответ);
	Если ЗначениеЗаполнено(Ответ.Токен) Тогда
		УстановитьПривилегированныйРежим(Истина);
		АдресТокена = ИнтеграцияУправлениеПерсоналом.АдресХраненияТокенаПриложения(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(АдресТокена,Ответ.Токен, "Токен");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(АдресТокена,Ответ.СрокГодностиТокена, "СрокГодностиТокена");
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция НовыйТокен(ПараметрыОбмена, Ключи)
	
	Результат = Новый Структура("Токен,СрокГодностиТокена,НеактуальныеКлючи",,,Ложь);
	
	СтрокаBase64 = Base64Строка(
	ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон("%1:%2", Ключи.ИдентификаторКлиента, Ключи.СекретКлиента)));
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ПС, "");
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ВК, "");
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", КабинетСотрудникаПовтИсп.ОписаниеКлиентскогоПриложения());
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Authorization", СтрШаблон("Basic %1", СтрокаBase64));
	Запрос = Новый HTTPЗапрос("/auth/oidc/token", Заголовки);
	Запрос.УстановитьТелоИзСтроки("grant_type=client_credentials");
	
	Соединение = ПараметрыОбмена.Соединение;
	Попытка
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Исключение
		ЗаписатьОшибкуВызова(ПараметрыОбмена, "POST", Запрос, ,ОписаниеОшибки());
		Возврат Результат;
	КонецПопытки;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		РезультатЧтенияJSON = ПрочитатьJSON(ЧтениеJSON);
		Если РезультатЧтенияJSON.Свойство("id_token") Тогда
			Токен = РезультатЧтенияJSON["id_token"];
			Результат.СрокГодностиТокена = ТекущаяУниверсальнаяДатаВМиллисекундах() + 3600*100;
			Результат.Токен = Токен;
		Иначе
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2';
				|en = 'Cannot to get authentication token.
				|Server returned code:%1
				|%2'"), Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		КонецЕсли;
		
	ИначеЕсли Ответ.КодСостояния = 400 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
		ОтветОшибка = ОбъектОтвета["error"];
		Если ОтветОшибка <> Неопределено Тогда
			Сообщение = ОтветОшибка["message"];
			Если Сообщение <> Неопределено И СтрНайти(Сообщение, "User name or password is invalid") > 0 Тогда
				Комментарий = НСтр("ru = 'Не удалось получить токен аутентификации. Неправильные ключи.';
									|en = 'Cannot get an authorization token. Invalid keys.'");
				ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
				Результат.НеактуальныеКлючи = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не Результат.НеактуальныеКлючи Тогда
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2';
				|en = 'Cannot to get authentication token.
				|Server returned code:%1
				|%2'"), Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		КонецЕсли;
		
	ИначеЕсли Ответ.КодСостояния = 401 Тогда
		
		Комментарий = НСтр("ru = 'Не удалось получить токен аутентификации. Неправильные ключи.';
							|en = 'Cannot get an authorization token. Invalid keys.'");
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		Результат.НеактуальныеКлючи = Истина;
		
	Иначе
		
		Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2';
				|en = 'Cannot to get authentication token.
				|Server returned code:%1
				|%2'"), Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОписаниеРесурсовAPI

Функция РесурсAPI(ВерсияAPI)
	
	РесурсAPI = "/api";
	
	Если Не ПустаяСтрока(ВерсияAPI) Тогда
		РесурсAPI = СтрШаблон("%1/v%2", РесурсAPI, ВерсияAPI);
	КонецЕсли;
	
	Возврат РесурсAPI;
	
КонецФункции

Функция РесурсВерсияAPI()

	Возврат "/api/apiVersions";

КонецФункции

Функция РесурсФизическиеЛица()
	
	Возврат "/persons";
	
КонецФункции

Функция РесурсОрганизации()
	
	Возврат "/employers";
	
КонецФункции

Функция РесурсСтруктураПредприятия()
	
	Возврат "/divisions";
	
КонецФункции

Функция РесурсДолжности()
	
	Возврат "/positions";
	
КонецФункции

Функция РесурсШтатноеРасписание()
	
	Возврат "/stafflist-positions";
	
КонецФункции

Функция РесурсСотрудники()
	
	Возврат "/employees";
	
КонецФункции

Функция РесурсПрименяемыеВычеты()
	
	Возврат "/tax-deductions";
	
КонецФункции

Функция РесурсСоставныеЧастиЗарплаты()
	
	Возврат "/payslip-component-types";
	
КонецФункции

Функция РесурсРасчетныеЛисты()
	
	Возврат "/payslips";
	
КонецФункции

Функция РесурсСправки2НДФЛ(ВерсияAPI)
	
	Возврат "/forms2NDFL";
	
КонецФункции

// Устаревший ресурс. Для формата ниже 303.
Функция РесурсОтветыНаЗапросыСправок2НДФЛ(ВерсияAPI)
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "/forms2NDFL/responses";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция РесурсСправкиСМестаРаботы()
	
	Возврат "/employment-certificates";
	
КонецФункции

Функция РесурсСправкиОбОстаткеОтпуска()
	
	Возврат "/vacations/balance-reports";
	
КонецФункции

Функция РесурсФайлы()
	
	Возврат "/files";
	
КонецФункции

Функция РесурсФайлыПолучение()
	
	Возврат "/files/{ID}";
	
КонецФункции

Функция РесурсПолучениеВсехИзменений()
	
	Возврат "/data/updates";
	
КонецФункции

Функция РесурсРезультатыСогласования()
	
	Возврат "/agreement-results";
	
КонецФункции

Функция РесурсДокументыНаПодпись()
	
	Возврат "/documents-to-be-signed";
	
КонецФункции

Функция РесурсЗаявлениеНаКомпенсациюОтпуска()
	
	Возврат "/leave-encashment-requests/requests/{ID}";
	
КонецФункции

Функция РесурсЗаявлениеНаУдержаниеДСВ()
	
	Возврат "/contributions/requests/{ID}";
	
КонецФункции

Функция РесурсФизическиеЛицаАктивные()

	Возврат "/persons/active";

КонецФункции

Функция РесурсФизическиеЛицаНеактивные()

	Возврат "/persons/inactive"; 

КонецФункции

Функция РесурсПинг()
	
	Возврат "/ping";
	
КонецФункции

Функция РесурсИнформацияОПриложении()

	Возврат "/application/info";

КонецФункции

Функция РесурсИспользуемыеФункции()

	Возврат "/used-features";

КонецФункции

Функция РесурсНастройкиОтпусков()

	Возврат "/vacations-settings";

КонецФункции

Функция РесурсНастройкиПриложения()

	Возврат "/application/settings";

КонецФункции

Функция РесурсАдминистратор()

	Возврат "/administrators";

КонецФункции

Функция РесурсОзнакомлениеСРасчетнымЛистком()

	Возврат "/payslips/{personID}/{month}/sign";

КонецФункции

Функция РесурсСистемаВзаимодействияНастройки()

	Возврат "/collaboration-system/settings";

КонецФункции

Функция РесурсСистемаВзаимодействияПриложение()

	Возврат "/collaboration-system/application-info";

КонецФункции

Функция РесурсДоступныеФункцииФизическихЛиц(ВерсияAPI)
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "/persons/options";
	Иначе
		Возврат "/office-owners/options";
	КонецЕсли;
	
КонецФункции

Функция РесурсСогласиеНаПрисоединениеККЭДО(ВерсияAPI)
	
	Возврат "/joinToPersonnelEdmСonsent";
	
КонецФункции

Функция РесурсЗаявлениеНаНалоговыеВычеты()
	
	Возврат "/tax-deductions/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросИзменениеЛичныхДанных(ВерсияAPI)
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "/persons/information-change-requests/{ID}";
	Иначе
		Возврат "/office-owners/information-change-requests/{ID}";
	КонецЕсли;
	
КонецФункции

Функция РесурсЗапросСправки2НДФЛ(ВерсияAPI)
	
	Возврат "/forms2NDFL/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросСправкиСРаботы()
	
	Возврат "/employment-certificates/requests/{ID}";
	
КонецФункции

Функция РесурсЗаявлениеНаОтпуск()
	
	Возврат "/vacations/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросСправкиОбОстаткеОтпуска()
	
	Возврат "/vacations/balance-reports/requests/{ID}";
	
КонецФункции

Функция РесурсОтсутствие()
	
	Возврат "/absences/{ID}";
	
КонецФункции

Функция РесурсРезультатСогласования()
	
	Возврат "/agreement-results/{ID}";
	
КонецФункции

Функция РесурсДанныеСбораГрафиковОтпусковПредприятия(ВерсияAPI) Экспорт

	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "";
	Иначе
		Возврат "/vacations-schedule-draft/enterprise";
	КонецЕсли;

КонецФункции

Функция РесурсГрафиковОтпусковПодразделения(ВерсияAPI)
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "";
	Иначе
		Возврат "/vacations-schedule-draft/divisions/{ID}";
	КонецЕсли;
	
КонецФункции

Функция РесурсОграничениеДоступаКРабочимКонтактам(ВерсияAPI)
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияAPI, "1.0") Тогда
		Возврат "";
	Иначе
		Возврат "/office-owners/workContactsAccess";
	КонецЕсли;
	
КонецФункции

Функция РесурсЗапросПравилоСогласования(ВерсияAPI)
	
	Возврат "/agreement-rules/{ID}";
	
КонецФункции

#КонецОбласти

#Область ИспользованиеAPI

Функция НовоеHTTPСоединение(СтруктураURI, Таймаут = 100)
	
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
	КонецЕсли;
	ЗащищенноеСоединение = Неопределено;
	Если ВРег(СтруктураURI.Схема) = "HTTPS" Или ВРег(СтруктураURI.Схема) = "FTPS" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	Соединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт,,, ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	
	Возврат Соединение;
	
КонецФункции

Функция НовоеОписаниеПараметрыОбмена()

	ПараметрыОбмена = Новый Структура;
	ПараметрыОбмена.Вставить("СтруктураАдреса", 	Неопределено);
	ПараметрыОбмена.Вставить("Соединение",			Неопределено);
	ПараметрыОбмена.Вставить("ВестиПротокол",		Ложь);
	ПараметрыОбмена.Вставить("ОбновлениеПубликации",Ложь);
	ПараметрыОбмена.Вставить("ВерсияПриложения",	"");
	ПараметрыОбмена.Вставить("ВерсияDTO",			"");
	ПараметрыОбмена.Вставить("ВерсияAPI",			"");
	ПараметрыОбмена.Вставить("ТипОбъектаРазмерПакета",	Новый Соответствие);
	
	Возврат ПараметрыОбмена;

КонецФункции

Функция ПараметрыОбмена(ВестиПротокол) Экспорт
	
	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	
	АдресПриложения = НастройкиСервиса.АдресПриложения;
	Если ЗначениеЗаполнено(НастройкиСервиса.АдресПриложенияПоИмени) И НастройкиСервиса.АдресПриложенияПоИмениДоступен Тогда
		АдресПриложения = НастройкиСервиса.АдресПриложенияПоИмени;
	КонецЕсли;
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПриложения);
	
	ПараметрыОбмена = НовоеОписаниеПараметрыОбмена();
	ПараметрыОбмена.СтруктураАдреса 	= СтруктураАдреса;
	ПараметрыОбмена.Соединение 			= НовоеHTTPСоединение(СтруктураАдреса);
	ПараметрыОбмена.ВестиПротокол 		= ВестиПротокол Или Константы.РегистрироватьВЖурналеСобытийЗапросы.Получить();
	ПараметрыОбмена.ВерсияПриложения 	= НастройкиСервиса.ВерсияПриложения;
	ПараметрыОбмена.ВерсияDTO 			= СокрЛП(НастройкиСервиса.ВерсияDTO);
	ПараметрыОбмена.ВерсияAPI 			= СокрЛП(НастройкиСервиса.ВерсияAPI);
	ПараметрыОбмена.ТипОбъектаРазмерПакета = ИнтеграцияУправлениеПерсоналомОбмен.ТипОбъектаРазмерПакета(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
	
	Возврат ПараметрыОбмена;
	
КонецФункции

Функция HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса)
	
	РесурсСервиса 			= ПараметрыЗапроса.РесурсСервиса;
	ИмяМетода 				= ПараметрыЗапроса.ИмяМетода;
	СтрокаТела 				= ПараметрыЗапроса.СтрокаТела;
	ОписаниеФайла 			= ПараметрыЗапроса.ОписаниеФайла;
	РазрешенныйКодОтвета 	= ПараметрыЗапроса.РазрешенныйКодОтвета;
	
	Ответ = Неопределено;
	ИспользоватьВерсии = ЗначениеЗаполнено(ПараметрыОбмена.ВерсияDTO);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", КабинетСотрудникаПовтИсп.ОписаниеКлиентскогоПриложения());
	Если ИспользоватьВерсии Тогда
		Заголовки.Вставить("formatVersion", ПараметрыОбмена.ВерсияDTO);
	КонецЕсли;
	
	Токен = ТокенАутентификации(ПараметрыОбмена);
	Если Не ЗначениеЗаполнено(Токен) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	
	ИмяФайла = "";
	Если ЗначениеЗаполнено(ОписаниеФайла) Тогда
		ИмяФайла = ОписаниеФайла.ИмяФайла;
		ТипКонтента = ТипКонтента(ИспользоватьВерсии, ОписаниеФайла.Расширение);
		Заголовки.Вставить("Content-Type", ТипКонтента);
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Заголовки.Вставить("Content-Type", "application/json");
	КонецЕсли;
	
	АдресРесурса = СтрШаблон("/%1%2%3",ПараметрыОбмена.СтруктураАдреса.ПутьНаСервере,РесурсAPI(ПараметрыОбмена.ВерсияAPI),РесурсСервиса);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		Запрос.УстановитьИмяФайлаТела(ИмяФайла);
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Запрос.УстановитьТелоИзСтроки(СтрокаТела);
	КонецЕсли;
	
	Если ПараметрыОбмена.ВестиПротокол Тогда
		ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос);
	КонецЕсли;
	
	Соединение = ПараметрыОбмена.Соединение;
	Попытка
		Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, , ОписаниеОшибки());
		Ответ = Неопределено;
	КонецПопытки;
	
	Если ПараметрыОбмена.ВестиПротокол Тогда
		ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос, Ответ, ПараметрыЗапроса.ЗапросФайла);
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 401 Тогда
		ПараметрыОбмена.Соединение = НовоеHTTPСоединение(ПараметрыОбмена.СтруктураАдреса);
		Токен = ТокенАутентификации(ПараметрыОбмена);
		Если Не ЗначениеЗаполнено(Токен) Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
		Если ПараметрыОбмена.ВестиПротокол Тогда
			ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос);
		КонецЕсли;
		Соединение = ПараметрыОбмена.Соединение;
		Попытка
			Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
		Исключение
			ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, , ОписаниеОшибки());
			Ответ = Неопределено;
		КонецПопытки;
		Если ПараметрыОбмена.ВестиПротокол Тогда
			ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос, Ответ);
		КонецЕсли;
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния >= 300
		И Не (Ответ.КодСостояния = 404 И ИмяМетода = "DELETE")
		И Ответ.КодСостояния <> РазрешенныйКодОтвета Тогда
		ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, Ответ);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция HTTPОтветСервисаЗапросБезВерсионирования(ПараметрыОбмена, РесурсСервиса, ИмяМетода, РазрешенныйКодОтвета = "")
	
	Ответ = Неопределено; 
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", КабинетСотрудникаПовтИсп.ОписаниеКлиентскогоПриложения());
	
	Токен = ТокенАутентификации(ПараметрыОбмена);
	Если Не ЗначениеЗаполнено(Токен) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	
	АдресРесурса = СтрШаблон("/%1%2",ПараметрыОбмена.СтруктураАдреса.ПутьНаСервере, РесурсСервиса);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если ПараметрыОбмена.ВестиПротокол Тогда
		ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос);
	КонецЕсли;
	
	Соединение = ПараметрыОбмена.Соединение;
	Попытка
		Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, , ОписаниеОшибки());
		Ответ = Неопределено;
	КонецПопытки;
	
	Если ПараметрыОбмена.ВестиПротокол Тогда
		ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос, Ответ);
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 401 Тогда
		ПараметрыОбмена.Соединение = НовоеHTTPСоединение(ПараметрыОбмена.СтруктураАдреса);
		
		Токен = ТокенАутентификации(ПараметрыОбмена);
		Если Не ЗначениеЗаполнено(Токен) Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
		Попытка
			Если ПараметрыОбмена.ВестиПротокол Тогда
				ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос);
			КонецЕсли;
			Соединение = ПараметрыОбмена.Соединение;
			Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
		Исключение
			ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, , ОписаниеОшибки());
			Ответ = Неопределено;
		КонецПопытки;
		Если ПараметрыОбмена.ВестиПротокол Тогда
			ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыОбмена, ИмяМетода, Запрос, Ответ);
		КонецЕсли;
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния >= 300 И Ответ.КодСостояния <> РазрешенныйКодОтвета Тогда
		ЗаписатьОшибкуВызова(ПараметрыОбмена, ИмяМетода, Запрос, Ответ);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ТипКонтента(ИспользоватьВерсии, Расширение)

	ТипКонтента = "application/octet-stream";
	Если ИспользоватьВерсии Или КабинетСотрудника.ИспользоватьРасширениеФайлаВЗаголовке() Тогда
		РасширениеФайла = НРег(Расширение);
		Если РасширениеФайла = "jpg"
			Или РасширениеФайла = "jpeg"
			Или РасширениеФайла = "jpe"
			Или РасширениеФайла = "jfif" Тогда
			ТипКонтента = "image/jpeg";
		ИначеЕсли РасширениеФайла = "png" Тогда
			ТипКонтента = "image/png";
		ИначеЕсли РасширениеФайла = "gif" Тогда
			ТипКонтента = "image/gif";
		ИначеЕсли РасширениеФайла = "tiff" Тогда
			ТипКонтента = "image/tiff";
		ИначеЕсли РасширениеФайла = "webp" Тогда
			ТипКонтента = "image/webp";
		ИначеЕсли РасширениеФайла = "bmp" Тогда
			ТипКонтента = "image/vnd.wap.wbmp";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипКонтента;

КонецФункции

Функция ПараметрыЗапроса(РесурсСервиса, ИмяМетода, СтрокаТела = "", ОписаниеФайла = Неопределено)

	ПараметрыЗапроса = Новый Структура("
	|РесурсСервиса,
	|ИмяМетода,
	|СтрокаТела,
	|ОписаниеФайла,
	|РазрешенныйКодОтвета,
	|ЗапросФайла");
	
	ПараметрыЗапроса.РесурсСервиса 	= РесурсСервиса;
	ПараметрыЗапроса.ИмяМетода 		= ИмяМетода;
	ПараметрыЗапроса.СтрокаТела 	= СтрокаТела;
	ПараметрыЗапроса.ОписаниеФайла 	= ОписаниеФайла;
	ПараметрыЗапроса.ЗапросФайла 	= Ложь;
	
	Возврат ПараметрыЗапроса;

КонецФункции

#КонецОбласти

#Область ОписанияОбъектов

#Область ОписанияОбъектовКакМассив

// Описание объекта - form2NDFLRequest.
Функция ОписаниеОбъектаЗапросСправокНДФЛ(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",	"Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	 	"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("purpose", 		"Назначение2НДФЛ", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxYear", 		"НалоговыйПериод", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("proofPeriod",	"КоличествоМесяцев", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("incomePeriodStartDate",	"НачалоПериода", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("incomePeriodEndDate", 	"ОкончаниеПериода", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("obtainingMode",	"ВариантФормированияФайлаОтвета", Тип("ПеречислениеСсылка.ВариантыФормированияФайлаОтветаЗаявкиСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("copiesNumber",	"КоличествоЭкземпляров", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendEmail",		"ОтправлятьEmail", Тип("Булево")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("addressEmail",	"АдресEmail", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",			"Комментарий", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - personalInformationChangeRequest.
Функция ОписаниеОбъектаЗапросНаИзменениеЛичныхДанных(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeName",	"ИзменитьФИО", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("lastName",		"Фамилия", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("firstName",		"Имя", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("patronymic",	"Отчество", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeIdentityDocument", 	"ИзменитьДокумент", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("identityDocument", 			"ДокументУдостоверяющийЛичность", Тип("Структура"), ОписаниеОбъектаДокументУдостоверяющийЛичность(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changePersonalPhone",		"ИзменитьНомерТелефона", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personalPhone",				"ЛичныйНомерТелефона", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeAddress",				"ИзменитьАдрес", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("registrationAddress",		"АдресРегистрации", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("residentialAddress",		"АдресМестаПроживания", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeINN",		"ИзменитьИНН", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("INN",			"ИНН", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeSNILS",	"ИзменитьСНИЛС", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("SNILS",			"СНИЛС", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeEmail",	"ИзменитьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("email",			"Email", Тип("Строка")));
	Иначе
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeName",	"ИзменитьФИО", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("lastName",		"Фамилия", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("firstName",		"Имя", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("patronymic",	"Отчество", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeIdentityDocument", 	"ИзменитьДокумент", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("identityDocument", 			"ДокументУдостоверяющийЛичность", Тип("Структура"), ОписаниеОбъектаДокументУдостоверяющийЛичность(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changePersonalPhone",		"ИзменитьНомерТелефона", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personalPhone",				"ЛичныйНомерТелефона", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeAddress",				"ИзменитьАдрес", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("registrationAddress",		"АдресРегистрации", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("residentialAddress",		"АдресМестаПроживания", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 			"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 					"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 				"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeTaxID",			"ИзменитьИНН", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxID",					"ИНН", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeInsuranceNumber",	"ИзменитьСНИЛС", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("insuranceNumber",		"СНИЛС", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("changeEmail",			"ИзменитьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("email",					"Email", Тип("Строка")));
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - certificateFromEmployerRequest.
Функция ОписаниеОбъектаЗапросСправкиСРаботы(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("purpose",		"НазначениеСправкиСРаботыСтрока", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("obtainingMode",	"ВариантФормированияФайлаОтвета", Тип("ПеречислениеСсылка.ВариантыФормированияФайлаОтветаЗаявкиСотрудника")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("copiesNumber",	"КоличествоЭкземпляров", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendEmail",		"ОтправлятьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("addressEmail",	"АдресEmail", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		Если КабинетСотрудника.ИспользоватьФормат50375() Тогда
			ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("certificateTypeID", "ВидСправки", Тип("Строка")));
		КонецЕсли;
	Иначе
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("purpose",		"НазначениеСправкиСРаботыСтрока", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("obtainingMode",	"ВариантФормированияФайлаОтвета", Тип("ПеречислениеСсылка.ВариантыФормированияФайлаОтветаЗаявкиСотрудника")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("copiesNumber",	"КоличествоЭкземпляров", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendEmail",		"ОтправлятьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("addressEmail",	"АдресEmail", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("certificateTypeID", "ВидСправки", Тип("Строка")));
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - vacationRequest.
Функция ОписаниеОбъектаЗапросНаОтпуск(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	 	"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employeeID",	"Сотрудник", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("startDate",		"ДатаНачала", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("endDate",		"ДатаОкончания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("unpaid",		"ЗаСвойСчет", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("leaveEncashmentDays", 	"КоличествоДнейКомпенсации", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("leaveEncashment", 		"КомпенсироватьОтпуск", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",			"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicDocumentID", "ИдентификаторЭлектронногоДокумента", Тип("Строка")));
	Иначе
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	 	"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employeeID",	"Сотрудник", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("startDate",		"ДатаНачала", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("endDate",		"ДатаОкончания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("unpaid",		"ЗаСвойСчет", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("leaveEncashmentDays", 	"КоличествоДнейКомпенсации", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("leaveEncashment", 		"КомпенсироватьОтпуск", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",			"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicDocumentID", 				"ИдентификаторЭлектронногоДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("oneТimeAdditionalVacationPayment", 	"ЕдиновременнаяВыплата", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("vacationMaterialAid", 				"МатериальнаяПомощь", Тип("Булево")));
	КонецЕсли;
		
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - leaveEncashmentRequest.
Функция ОписаниеОбъектаЗапросНаКомпенсациюОтпуска(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("leaveEncashmentDays",	"КоличествоДнейКомпенсации", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - vacationBalanceRequest.
Функция ОписаниеОбъектаЗапросСправкиОбОстаткеОтпуска(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.2") Тогда
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("obtainingMode",	"ВариантФормированияФайлаОтвета", Тип("ПеречислениеСсылка.ВариантыФормированияФайлаОтветаЗаявкиСотрудника")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("copiesNumber",	"КоличествоЭкземпляров", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendEmail",		"ОтправлятьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("addressEmail",	"АдресEmail", Тип("Строка")));
	Иначе
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("obtainingMode",	"ВариантФормированияФайлаОтвета", Тип("ПеречислениеСсылка.ВариантыФормированияФайлаОтветаЗаявкиСотрудника")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("copiesNumber",	"КоличествоЭкземпляров", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendEmail",		"ОтправлятьEmail", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("addressEmail",	"АдресEmail", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - taxDeductionRequest.
Функция ОписаниеОбъектаЗаявлениеНаНалоговыеВычеты(ВерсияDTO)
	 
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 				"ИдентификаторЗапроса", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 			"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 		"ДатаСоздания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 			"ФизическоеЛицо", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 		"Организация", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("applyMonth", 		"МесяцПрименения", Тип("Дата"), Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personalDeduction", "ЭтоЛичныйВычет", Тип("Булево"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("childTaxDeduction", "ЭтоВычетНаДетей", Тип("Булево"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("childTaxDeductions", 	"ВычетыНаДетей", Тип("ТаблицаЗначений"), ОписаниеОбъектаВычетаНаДетей(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxAuthorityNotices", 	"УведомленияИзНалоговой", Тип("ТаблицаЗначений"), ОписаниеОбъектаУведомленияИзНалоговой(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("educationTaxDeduction", 		"ЭтоВычетНаОбучение", Тип("Булево"), Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("medicalTreatmentTaxDeduction", 	"ЭтоВычетНаЛечение", Тип("Булево"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("realEstateTaxDeduction", 		"ЭтоВычетНаНедвижимость", Тип("Булево"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 						"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 			"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO),Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 					"Комментарий", Тип("Строка"),, Истина));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - childTaxDeductionsRequest.
Функция ОписаниеОбъектаВычетаНаДетей(ВерсияDTO)
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("childSeniority", "СтаршинствоРебенка", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("childDisability", "РебенокИнвалид", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("singleParent", "РодительОдиночка", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("guardian", "ЗаявительОпекун", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("endDate", "ДатаОкончания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

// Описание объекта - taxAuthorityNoticesRequest.
Функция ОписаниеОбъектаУведомленияИзНалоговой(ВерсияDTO)
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("amount", "Размер", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

// Описание объекта - voluntaryInsuranceContributionsRequest.
Функция ОписаниеОбъектаЗапросНаУдержаниеДСВ(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка"),,Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("calculationMethod", "СпособРасчетаУдержанияДСВ", Тип("ПеречислениеСсылка.СпособыРасчетаУдержанийКабинетСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("percentage",	"Процент", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("amount",		"Сумма", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO),Истина));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - absence.
Функция ОписаниеОбъектаУведомлениеОбОтсутствии(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	 	"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("reason", 		"ПричинаОтсутствия", Тип("ПеречислениеСсылка.ПричиныОтсутствийЗаявокКабинетСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("startDate",		"ДатаНачала", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("endDate",		"ДатаОкончания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 	"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("allDay",		"ВесьДень", Тип("Булево")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",			"Комментарий", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicDocumentID", "ИдентификаторЭлектронногоДокумента", Тип("Строка")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - agreementResult.
Функция ОписаниеОбъектРезультатСогласования(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторОбъекта", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"Подписант", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personName",	"ИмяПодписанта", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentID",	"ИдентификаторДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicDocumentID",	"ИдентификаторЭлектронногоДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("result",				"РезультатСогласования", Тип("ПеречислениеСсылка.РезультатыСогласованияБЗК")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicSignature",	"ЭлектроннаяПодпись", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("signatoryRole",			"РольПодписанта", Тип("ПеречислениеСсылка.РолиПодписантовКЭДО")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentVersion",		"ВерсияДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("comment", 				"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("certificate",			"СертификатЭП", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("time",					"ДатаПодписи", Тип("Строка")));
		Если КабинетСотрудника.ИспользоватьФормат503() Тогда
			ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("updateDocument",		"ОбновитьПредставление", Тип("Булево")));
		КонецЕсли;
		
	Иначе
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторОбъекта", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"Подписант", Тип("Строка"),,Истина));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personName",	"ИмяПодписанта", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentID",	"ИдентификаторДокумента", Тип("Строка"),,Истина));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicDocumentID",	"ИдентификаторЭлектронногоДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("result",				"РезультатСогласования", Тип("ПеречислениеСсылка.РезультатыСогласованияБЗК")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("electronicSignature",	"ЭлектроннаяПодпись", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("signatoryRole",			"РольПодписанта", Тип("ПеречислениеСсылка.РолиПодписантовКЭДО")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentVersion",		"ВерсияДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("comment", 				"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("certificate",			"СертификатЭП", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("time",					"ДатаПодписи", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("updateDocument",		"ОбновитьПредставление", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("fileName",				"ИмяФайла", Тип("Строка")));
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - electronicDocument.
Функция ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)

	ОписаниеОбъекта = Новый Массив;
	
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("originalDocument", "ИсходныйДокумент", Тип("Структура"), ОписаниеОбъектаФайл(ВерсияDTO)));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("additionalPresentations", "ПредставленияДокумента", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - file
Функция ОписаниеОбъектаФайл(ВерсияDTO) Экспорт
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("name", "НаименованиеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("extension", "РасширениеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("size", "РазмерФайла", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("ID", "ИдентификаторФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("hash", "ХешСумма", Тип("Строка")));

	Возврат ОписаниеПолей;
	
КонецФункции

// Описание объекта - inputFile, устаревший метод.
Функция ОписаниеОбъектаВходящийФайл()
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("fileID", 	"ИдентификаторФайла", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("name", 		"НаименованиеФайла", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("extension", "РасширениеФайла", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("size", 		"РазмерФайла", Тип("Число"))); 
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - identityDocument.
Функция ОписаниеОбъектаДокументУдостоверяющийЛичность(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("identityDocumentType",	"ДокументВид", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("serias",				"ДокументСерия", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("number",				"ДокументНомер", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("issueDate",				"ДокументДатаВыдачи", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("issuingAuthority",		"ДокументКемВыдан", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("issuingAuthorityID",	"ДокументКодПодразделения", Тип("Строка")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - vacationBalanceReport.
Функция ОписаниеОбъектаСправкаОбОстаткеОтпуска(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", "ИдентификаторЗаявки", Тип("Строка"),,Истина));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachment",  "Вложение", Неопределено, ОписаниеОбъектаВходящийФайл()));
		
	Иначе
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"Заявка", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", 	"ИдентификаторЗаявки", Тип("Строка"),,Истина));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachment", 	"Вложение", Неопределено, ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта  - documentToBeSigned.
Функция ОписаниеОбъектаДокументНаПодпись(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID",							"ИдентификаторДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",					"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("date",							"Дата", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("name",							"НазваниеДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document",						"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",							"Комментарий", Тип("Строка")));
		Если Не КабинетСотрудника.ИспользоватьФормат503() Тогда
			ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("emloyees", 	"ФизическиеЛица", Тип("ТаблицаЗначений")));
		Иначе
			ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employees", "ФизическиеЛица", Тип("ТаблицаЗначений")));
			ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("updatePresentationsPossible",	"ВозможноОбновлениеПредставлений", Тип("Булево")));
		КонецЕсли;
		
	ИначеЕсли КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.2") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID",							"ИдентификаторДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",					"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("date",							"Дата", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("name",							"НазваниеДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document",						"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",							"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employees", 					"ФизическиеЛица", Тип("ТаблицаЗначений")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("updatePresentationsPossible",	"ВозможноОбновлениеПредставлений", Тип("Булево")));
		
	Иначе
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID",							"ИдентификаторДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",					"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("date",							"Дата", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("name",							"НазваниеДокумента", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document",						"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note",							"Комментарий", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employees", 					"ФизическиеЛица", Тип("ТаблицаЗначений")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("updatePresentationsPossible",	"ВозможноОбновлениеПредставлений", Тип("Булево")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendNotification",				"ОтправлятьУведомление", Тип("Булево")));
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - certificateFromEmployer.
Функция ОписаниеОбъектаСправкаСРаботы(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 		"Заявка", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", "ИдентификаторЗаявки", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID","Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachment","Вложение", Неопределено, ОписаниеОбъектаВходящийФайл()));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 	"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 		"Комментарий", Тип("Строка")));
		
	Иначе
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 		"Заявка", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", "ИдентификаторЗаявки", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID","Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 	"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 		"Комментарий", Тип("Строка")));
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - form2NDFL.
Функция ОписаниеОбъектаСправка2НДФЛ(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"Справка2НДФЛ", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", 	"ИдентификаторЗаявки", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxYear", 		"НалоговыйПериод", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("incomeAmount", 	"СуммаДохода", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxAmount", 	"СуммаНалога", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		// не используется с версии 3.0.3.1
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachment",	"Вложение", Неопределено, ОписаниеОбъектаВходящийФайл()));
		
	Иначе
		
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"Справка2НДФЛ", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", 	"ИдентификаторЗаявки", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID",	"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxYear", 		"НалоговыйПериод", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("incomeAmount", 	"СуммаДохода", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("taxAmount", 	"СуммаНалога", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 	"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("document", 		"ЭлектронныйДокумент", Тип("Структура"), ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("note", 			"Комментарий", Тип("Строка")));
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - employeeContributions.
Функция ОписаниеОбъектаПлановыеУдержания(ВерсияDTO) Экспорт

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("contributions", "ПлановыеУдержания", Тип("ТаблицаЗначений"), ОписаниеОбъектаПлановоеУдержание(ВерсияDTO)));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - contribution.
Функция ОписаниеОбъектаПлановоеУдержание(ВерсияDTO) Экспорт

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("presentation", 		"ПредставлениеУдержания", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("expirationDate", 	"ДатаОкончания", Тип("Дата")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("calculationMethod", "СпособРасчета", Тип("ПеречислениеСсылка.СпособыРасчетаУдержанийКабинетСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("percentage", 		"Процент", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("amount", 			"Сумма", Тип("Число")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - applicationInfo.
Функция ОписаниеОбъектаИнформацияОПриложении(ВерсияDTO)

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("id", 					"ИдентификаторПриложения", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("applicationVersion", 	"ВерсияПриложения", Тип("Строка")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - joinToPersonnelEdmConsent
Функция ОписаниеОбъектаСогласиеНаПрисоединениеККЭДО(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.2") Тогда
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 					"Согласие", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 			"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 				"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentToBeSignedID", 	"ИдентификаторДокументаНаПодпись", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 			"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 				"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 			"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
	Иначе
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 					"Согласие", Неопределено));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 			"Организация", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 				"ФизическоеЛицо", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("documentToBeSignedID", 	"ИдентификаторДокументаНаПодпись", Тип("Строка")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("dateCreated", 			"ДатаСоздания", Тип("Дата")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 				"Версия", Тип("Число")));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("attachments", 			"Вложения", Тип("ТаблицаЗначений"), ОписаниеОбъектаФайл(ВерсияDTO)));
		ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("sendNotification",		"ОтправлятьУведомление", Тип("Булево")));
	КонецЕсли;

	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - personOptions.
Функция ОписаниеОбъектаДоступныеФункцииФизическихЛиц(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 			"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personnelEdmUse", 	"ИспользуетКЭДО", Тип("Булево")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - taxDeductionsApplied
Функция ОписаниеПолейПрименяемыеНалоговыеВычеты(ВерсияDTO)
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("employerID", "Организация",Тип("Строка"),,Истина));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID",   "ФизическоеЛицо", Тип("Строка"),,Истина));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("deductions", "ИнформацияОВычетах", Тип("ТаблицаЗначений"), ОписаниеОбъектаИнформацияОВычетах(ВерсияDTO)));
	Возврат ОписаниеПолей;
	
КонецФункции

// Описание объекта - taxDeduction
Функция ОписаниеОбъектаИнформацияОВычетах(ВерсияDTO)
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("startDate", 	"ДатаНачала", Тип("Дата"),,Истина));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("amount", 		"РазмерВычета", Тип("Число"),,Истина));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("endDate", 	"ДатаОкончания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", 		"ОписаниеВычета", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

// Описание объекта - salaryComponent.
Функция ОписаниеОбъектаСоставнаяЧастьЗарплаты(ВерсияDTO)

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 						"Идентификатор", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("section", 					"Группа", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("name", 						"Наименование", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("specificSalaryComponent", 	"Вид", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("priority", 					"Приоритет", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("accrualType", 				"ТипНачисления", Тип("Строка")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - salaryComponentInfo.
Функция ОписаниеОбъектаСтрокаРасчетногоЛистка(ВерсияDTO)

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID", 	"Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employeeID", 	"Сотрудник", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("componentID", 	"СоставнаяЧасть", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("amount", 		"Сумма", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("workplace", 	"ПредставлениеРабочегоМеста", Тип("Строка")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - form2NDFLResponse, используется до версии 3.0.3.1..
Функция ОписаниеОбъектаОтветНаЗапросСправок2НДФЛ()
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestID", "ИдентификаторЗаявки", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("employerID","Организация", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("forms2NDFL", "Справки2НДФЛ", Тип("ТаблицаЗначений"), ОписаниеОбъектаСправка2НДФЛ("")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Описание объекта - agreementRule.
Функция ОписаниеОбъектаПравилоСогласования(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("ID", 			"ИдентификаторПравила", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("version", 		"Версия", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("requestType", 	"ТипЗаявки", Тип("ПеречислениеСсылка.ТипыЗаявокКабинетСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("divisionID", 	"ИдентификаторПодразделения", Новый ОписаниеТипов("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("steps", 		"ШагиПравилаСогласования", Тип("ТаблицаЗначений"), ОписаниеОбъектаШагиПравилаСогласования(ВерсияDTO))); 
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("isDeleted", 	"ПометкаУдаления", Тип("Булево")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - agreementStep.
Функция ОписаниеОбъектаШагиПравилаСогласования(ВерсияDTO)
	
	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("stepNumber", 	"НомерШага", Тип("Число")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("stepType", 		"ТипШага", Тип("ПеречислениеСсылка.ТипыШаговСогласованияКабинетСотрудника")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("signatoryRole", "РольПодписанта", Тип("ПеречислениеСсылка.РолиПодписантовКЭДО")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 		"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("needElectronicSignature", "ТребуемаяПодпись", Тип("ПеречислениеСсылка.ВидыТребуемойПодписиКабинетСотрудника")));
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Описание объекта - workContactsAccess.
Функция ОписаниеОбъектаОграничениеДоступаКРабочимКонтактам(ВерсияDTO) 

	ОписаниеОбъекта = Новый Массив;
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("personID", 	"ФизическоеЛицо", Тип("Строка")));
	ОписаниеОбъекта.Добавить(НовоеОписаниеПоля("access", 	"УровеньДоступа", Тип("ПеречислениеСсылка.УровниДоступаКИнформацииОСотрудниках")));
	
	Возврат ОписаниеОбъекта;

КонецФункции

#КонецОбласти

#Область КонструкторыСоответствияПолей

// Описание полей объекта - employer.
Функция ОписаниеПолейОрганизаций(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",		"Организация");
	СоответствиеПолей.Вставить("name",		"Наименование");
	СоответствиеПолей.Вставить("taxID",		"ИНН");
	СоответствиеПолей.Вставить("isBranch",	"ОбособленноеПодразделение");
	СоответствиеПолей.Вставить("parentOrganizationID", "ГоловнаяОрганизация");
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - division.
Функция ОписаниеПолейСтруктурыПредприятия(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",				"СтруктураПредприятия");
	СоответствиеПолей.Вставить("name",				"Наименование");
	СоответствиеПолей.Вставить("code",				"Код");
	СоответствиеПолей.Вставить("parentDivisionID",	"Родитель");
	СоответствиеПолей.Вставить("headID",			"ФизическоеЛицоРуководителя");
	СоответствиеПолей.Вставить("priority",			"Порядок");
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - position.
Функция ОписаниеПолейДолжностей(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",		"Должность");
	СоответствиеПолей.Вставить("name",		"Наименование");
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - staffListPosition.
Функция ОписаниеПолейШтатногоРасписания(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ПозицияШтатногоРасписания");
	СоответствиеПолей.Вставить("name",			"Наименование");
	СоответствиеПолей.Вставить("employerID",	"Организация");
	СоответствиеПолей.Вставить("divisionID",	"МестоВСтруктуреПредприятия");
	СоответствиеПолей.Вставить("positionID",	"Должность");
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - person.
Функция ОписаниеПолейФизическихЛиц(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		СоответствиеПолей.Вставить("ID",				"ФизическоеЛицо");
		СоответствиеПолей.Вставить("lastName",			"Фамилия");
		СоответствиеПолей.Вставить("firstName",			"Имя");
		СоответствиеПолей.Вставить("patronymic",		"Отчество");
		СоответствиеПолей.Вставить("initials",			"Инициалы");
		СоответствиеПолей.Вставить("gender",			"Пол");
		СоответствиеПолей.Вставить("birthDate",			"ДатаРождения");
		СоответствиеПолей.Вставить("taxID",				"ИНН");
		СоответствиеПолей.Вставить("insuranceNumber",	"СтраховойНомерПФР");
		СоответствиеПолей.Вставить("birthPlace",		"МестоРождения");
		СоответствиеПолей.Вставить("phoneNumber",		"ТелефонРабочийПредставление");
		СоответствиеПолей.Вставить("mobilePhoneNumber",	"ТелефонМобильныйПредставление");
		СоответствиеПолей.Вставить("eMail",				"EMailПредставление");
		СоответствиеПолей.Вставить("identityDocument",	ОписаниеПолейДокументВид(ВерсияDTO));
		СоответствиеПолей.Вставить("registrationAddress", "АдресПоПропискеПредставление");
		СоответствиеПолей.Вставить("residentialAddress",  "АдресМестаПроживанияПредставление");
		СоответствиеПолей.Вставить("workContactsAccess",  "ДоступКонтактнойИнформации");
		СоответствиеПолей.Вставить("picture", 			  ОписаниеПолейФайл(ВерсияDTO));
	Иначе
		СоответствиеПолей.Вставить("ID",				"ФизическоеЛицо");
		СоответствиеПолей.Вставить("lastName",			"Фамилия");
		СоответствиеПолей.Вставить("firstName",			"Имя");
		СоответствиеПолей.Вставить("patronymic",		"Отчество");
		СоответствиеПолей.Вставить("initials",			"Инициалы");
		СоответствиеПолей.Вставить("gender",			"Пол");
		СоответствиеПолей.Вставить("birthDate",			"ДатаРождения");
		СоответствиеПолей.Вставить("taxID",				"ИНН");
		СоответствиеПолей.Вставить("insuranceNumber",	"СтраховойНомерПФР");
		СоответствиеПолей.Вставить("birthPlace",		"МестоРождения");
		СоответствиеПолей.Вставить("phoneNumber",		"ТелефонРабочийПредставление");
		СоответствиеПолей.Вставить("mobilePhoneNumber",	"ТелефонМобильныйПредставление");
		СоответствиеПолей.Вставить("eMail",				"EMailПредставление");
		СоответствиеПолей.Вставить("identityDocument",	ОписаниеПолейДокументВид(ВерсияDTO));
		СоответствиеПолей.Вставить("registrationAddress", "АдресПоПропискеПредставление");
		СоответствиеПолей.Вставить("residentialAddress",  "АдресМестаПроживанияПредставление");
		СоответствиеПолей.Вставить("picture", 			  ОписаниеПолейФайл(ВерсияDTO));
		СоответствиеПолей.Вставить("presentationForms",   ОписаниеПолейСклонений(ВерсияDTO));
	КонецЕсли;
	
	Возврат СоответствиеПолей;

КонецФункции

// Описание полей объекта - presentationForms.
Функция ОписаниеПолейСклонений(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	Если Не КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		СоответствиеПолей.Вставить("nominative",	"Именительный");
		СоответствиеПолей.Вставить("genitive",		"Родительный");
		СоответствиеПолей.Вставить("dative",		"Дательный");
		СоответствиеПолей.Вставить("accusative",	"Винительный");
		СоответствиеПолей.Вставить("instrumental",	"Творительный");
		СоответствиеПолей.Вставить("prepositional",	"Предложный");
	КонецЕсли;
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей - file.
Функция ОписаниеПолейФайл(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		СоответствиеПолей.Вставить("name",		"НаименованиеФайла");
		СоответствиеПолей.Вставить("extension",	"РасширениеФайла");
		СоответствиеПолей.Вставить("fileID",	"ИдентификаторФайла");
	Иначе
		Описание = ОписаниеОбъектаФайл(ВерсияDTO);
		Для каждого ОписаниеПоля Из Описание Цикл
			СоответствиеПолей.Вставить(ОписаниеПоля["ИмяПоляСервиса"], ОписаниеПоля["ИмяПоляКонфигурации"]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - identityDocument.
Функция ОписаниеПолейДокументВид(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	
	Описание = ОписаниеОбъектаДокументУдостоверяющийЛичность(ВерсияDTO);
	Для каждого ОписаниеПоля Из Описание Цикл
		СоответствиеПолей.Вставить(ОписаниеПоля["ИмяПоляСервиса"], ОписаниеПоля["ИмяПоляКонфигурации"]);
	КонецЦикла;
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - employee.
Функция ОписаниеПолейСотрудников(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",					"Сотрудник");
	СоответствиеПолей.Вставить("personID",				"ФизическоеЛицо");
	СоответствиеПолей.Вставить("employmentDate",		"ДатаПриема");
	СоответствиеПолей.Вставить("staffListPositionID",	"ДолжностьПоШтатномуРасписанию");
	СоответствиеПолей.Вставить("employerID",			"Организация");
	СоответствиеПолей.Вставить("divisionID",			"МестоВСтруктуреПредприятия");
	СоответствиеПолей.Вставить("positionID",			"Должность");
	СоответствиеПолей.Вставить("transferDate",			"РабочееМестоПериодРегистрации");
	СоответствиеПолей.Вставить("employmentType",		"ВидЗанятости");
	СоответствиеПолей.Вставить("wageRate",				ОписаниеПолейТарифнойСтавки(ВерсияDTO));
	СоответствиеПолей.Вставить("workScheduleID", 		"ГрафикРаботы");
	СоответствиеПолей.Вставить("dismissed", 			"Уволен");
	СоответствиеПолей.Вставить("hidden", 				"СкрыватьВСписках");
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - wageRate.
Функция ОписаниеПолейТарифнойСтавки(ВерсияDTO)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("value",			"ТарифнаяСтавка");
	СоответствиеПолей.Вставить("presentation",	"ПоказательТарифнойСтавки");
	Возврат СоответствиеПолей;
	
КонецФункции

// Описание полей объекта - collaborationSettings.
Функция ОписаниеПолейНастройкиСистемыВзаимодействия(ВерсияDTO)

	Описание = Новый Соответствие;
	Описание.Вставить("botID", 					"ИдентификаторПользователяСВ");
	Описание.Вставить("botName", 				"ИмяПользователяСВ");
	Описание.Вставить("systemConversationID", 	"ИдентификаторОбсужденияСВ");
	
	Возврат Описание;

КонецФункции

// Описание полей объекта - applicationSettings.
Функция ОписаниеПолейНастройкиПриложения(ВерсияDTO)

	Описание = Новый Соответствие;
	Описание.Вставить("applicationUrl", "АдресПриложения");
	
	Возврат Описание;

КонецФункции

// Описание полей объекта - personExternal.
Функция ОписаниеВнешнееФизическоеЛицо(ВерсияDTO)

	Описание = Новый Соответствие;
	Описание.Вставить("personExternalID", "ФизическоеЛицо");
	
	Возврат Описание;

КонецФункции

// Описание полей объекта - apiVersions.
Функция ОписаниеПолейОбъектаВерсииФормата()

	Описание = Новый Соответствие;
	Описание.Вставить("apiVersion", 	"ВерсияAPI");
	Описание.Вставить("formatVersion", 	"ВерсияDTO");
	
	Возврат Описание;

КонецФункции

// Описание полей объекта - vacationsSettings.
Функция ОписаниеПолейОбъектаНастройкиЗаявокНаОтпуск(ВерсияDTO)

	Описание = Новый Соответствие;
	Описание.Вставить("vacationStartDateControl", 		"КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления");
	Описание.Вставить("daysBeforeVacationStart", 		"КоличествоДнейДоНачалаОтпуска");
	Описание.Вставить("fourteenDaysVacationControl", 	"КонтролироватьНаличиеОтпускаМенее14дней");
	Описание.Вставить("advanceVacationControl", 		"ОграничиватьПредоставлениеОтпускаАвансом");
	Описание.Вставить("advanceVacationDays", 			"МаксимальноеКоличествоДнейОтпускаАвансом");
	Описание.Вставить("workDaysControl", 				"ПроверятьНаличиеРабочихДнейВПериодеОтпуска");
	
	Возврат Описание;

КонецФункции

// Описание полей объекта - usedFeatures.
Функция ОписаниеПолейОбъектаИспользуемаяФункциональность(ВерсияDTO)

	Описание = Новый Соответствие;
	
	Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ВерсияDTO, "1.0") Тогда
		
		Описание.Вставить("salaryInfo", 						"ИнформацияОЗарплате");
		Описание.Вставить("taxDeductionApplication", 			"ЗаявленияНаВычетыНДФЛ");
		Описание.Вставить("absenceInfo", 						"РегистрацияОтсутствий");
		Описание.Вставить("personalReason", 					"ОтсутствиеПоЛичнымОбстоятельствам");
		Описание.Вставить("late", 								"Опоздание");
		Описание.Вставить("vacation", 							"Отпуск");
		Описание.Вставить("illness", 							"Болезнь");
		Описание.Вставить("businessTrip", 						"Командировка");
		Описание.Вставить("studyLeave", 						"УчебныйОтпуск");
		Описание.Вставить("unpaidLeave", 						"ОтпускБезОплаты");
		Описание.Вставить("timeOff", 							"Отгул");
		Описание.Вставить("maternityLeave", 					"ОтпускПоБеременностиИРодам");
		Описание.Вставить("parentalLeave", 						"ОтпускПоУходуЗаРебенком");
		Описание.Вставить("invalidChildCare", 					"УходЗаРебенкомИнвалидом");
		Описание.Вставить("vacationInfo", 						"ИнформацияОбОтпуске");
		Описание.Вставить("vacationScheduleInfo", 				"ГрафикОтпусков");
		Описание.Вставить("form2NDFLRequest", 					"ЗапросСправки2НДФЛ");
		Описание.Вставить("certificateFromEmployerRequest", 	"ЗапросСправкиСМестаРаботы");
		Описание.Вставить("employeeAddress", 					"АдресСотрудника");
		Описание.Вставить("personalContactInfo", 				"ЛичнаяКонтактнаяИнформация");
		Описание.Вставить("personalInformationChangeRequest", 	"ЗапросНаИзменениеЛичнойИнформации");
		Описание.Вставить("fileForPrinterOutput", 				"ДоступноПолучениеФайлаДляПечати");
		Описание.Вставить("documentScan", 						"ДоступноПолучениеСканаОригинала");
		Описание.Вставить("fileWithElectronicSignature", 		"ДоступноПолучениеДокументаСЭП");
		Описание.Вставить("paperForm", 							"ДоступноПолучениеДокументаВБумажномВиде");
		Описание.Вставить("sendCopyToEMail", 					"ДоступноПолучениеКопииНаЭлектроннуюПочту");
		Описание.Вставить("leaveEncashmentRequest",				"ЗаявленияНаКомпенсациюОтпуска");
		Описание.Вставить("voluntaryInsuranceContributions",	"ЗаявленияНаДСВ");
		Описание.Вставить("calculationMethodPercentage",			"СпособРасчетаДСВПроцентом");
		Описание.Вставить("calculationMethodFixedAmount",			"СпособРасчетаДСВСуммой");
		Описание.Вставить("calculationMethodPercentageUpToAmount",	"СпособРасчетаДСВПроцентомНеБолееСуммы");
		Описание.Вставить("positionRequestsInApplication", 		"ЗапросМестаРаботыВЗаявлении");
		Описание.Вставить("extendedInformationChangeRequest",	"РасширенныйЗапросНаИзменениеЛичнойИнформации");
		Описание.Вставить("correspondence", 					"ИспользуютсяОбсуждения");
		Описание.Вставить("companyInfo", 						"РазделКомпания");
		Описание.Вставить("certificatesInfo", 					"РазделСправки");
		Описание.Вставить("documentsToBeSignedInfo", 			"РазделДокументы");
		Описание.Вставить("joinToPersonnelEdmPaper", 			"ПолучениеСогласияКЭДОНаБумаге");
		Описание.Вставить("joinToPersonnelEdmElectronicSignature", 	"ПолучениеСогласияКЭДОУНЭП");
		Если КабинетСотрудника.ИспользоватьФормат50366() Тогда
			Описание.Вставить("personnelEdm", "ИспользуетсяКЭДО");
		КонецЕсли;
		
	Иначе
		
		Описание.Вставить("salaryInfo", 						"ИнформацияОЗарплате");
		Описание.Вставить("taxDeductionApplication", 			"ЗаявленияНаВычетыНДФЛ");
		Описание.Вставить("absenceInfo", 						"РегистрацияОтсутствий");
		Описание.Вставить("personalReason", 					"ОтсутствиеПоЛичнымОбстоятельствам");
		Описание.Вставить("late", 								"Опоздание");
		Описание.Вставить("vacation", 							"Отпуск");
		Описание.Вставить("illness", 							"Болезнь");
		Описание.Вставить("businessTrip", 						"Командировка");
		Описание.Вставить("studyLeave", 						"УчебныйОтпуск");
		Описание.Вставить("unpaidLeave", 						"ОтпускБезОплаты");
		Описание.Вставить("timeOff", 							"Отгул");
		Описание.Вставить("maternityLeave", 					"ОтпускПоБеременностиИРодам");
		Описание.Вставить("parentalLeave", 						"ОтпускПоУходуЗаРебенком");
		Описание.Вставить("invalidChildCare", 					"УходЗаРебенкомИнвалидом");
		Описание.Вставить("vacationInfo", 						"ИнформацияОбОтпуске");
		Описание.Вставить("vacationScheduleInfo", 				"ГрафикОтпусков");
		Описание.Вставить("form2NDFLRequest", 					"ЗапросСправки2НДФЛ");
		Описание.Вставить("certificateFromEmployerRequest", 	"ЗапросСправкиСМестаРаботы");
		Описание.Вставить("employeeAddress", 					"АдресСотрудника");
		Описание.Вставить("personalContactInfo", 				"ЛичнаяКонтактнаяИнформация");
		Описание.Вставить("personalInformationChangeRequest", 	"ЗапросНаИзменениеЛичнойИнформации");
		Описание.Вставить("fileForPrinterOutput", 				"ДоступноПолучениеФайлаДляПечати");
		Описание.Вставить("documentScan", 						"ДоступноПолучениеСканаОригинала");
		Описание.Вставить("fileWithElectronicSignature", 		"ДоступноПолучениеДокументаСЭП");
		Описание.Вставить("paperForm", 							"ДоступноПолучениеДокументаВБумажномВиде");
		Описание.Вставить("sendCopyToEMail", 					"ДоступноПолучениеКопииНаЭлектроннуюПочту");
		Описание.Вставить("leaveEncashmentRequest",				"ЗаявленияНаКомпенсациюОтпуска");
		Описание.Вставить("voluntaryInsuranceContributions",	"ЗаявленияНаДСВ");
		Описание.Вставить("calculationMethodPercentage",			"СпособРасчетаДСВПроцентом");
		Описание.Вставить("calculationMethodFixedAmount",			"СпособРасчетаДСВСуммой");
		Описание.Вставить("calculationMethodPercentageUpToAmount",	"СпособРасчетаДСВПроцентомНеБолееСуммы");
		Описание.Вставить("positionRequestsInApplication", 		"ЗапросМестаРаботыВЗаявлении");
		Описание.Вставить("extendedInformationChangeRequest",	"РасширенныйЗапросНаИзменениеЛичнойИнформации");
		Описание.Вставить("correspondence", 					"ИспользуютсяОбсуждения");
		Описание.Вставить("companyInfo", 						"РазделКомпания");
		Описание.Вставить("certificatesInfo", 					"РазделСправки");
		Описание.Вставить("documentsToBeSignedInfo", 			"РазделДокументы");
		Описание.Вставить("joinToPersonnelEdmPaper", 			"ПолучениеСогласияКЭДОНаБумаге");
		Описание.Вставить("joinToPersonnelEdmElectronicSignature", 	"ПолучениеСогласияКЭДОУНЭП");
		Описание.Вставить("personnelEdm", 						"ИспользуетсяКЭДО");
		Описание.Вставить("oneТimeAdditionalVacationPayment",	"ЕдиновременнаяВыплатаКОтпуску");
		Описание.Вставить("vacationMaterialAid",				"МатериальнаяПомощьКОтпуску");
		Описание.Вставить("extendedApprovalRoles", 				"РасширенныеРолиСогласования");
		Описание.Вставить("certificateAboutVacationBalanceRequest", "ЗапросСправкиОстаткиОтпусков");
		
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// Описание объекта salaryInfo
Функция ОписаниеИнформацияОЗарплате()

	Описание = Новый Структура("
	|personID,
	|month,
	|isFirstHalf,
	|components,
	|documentsToBeSigned");
	
	Возврат Описание;

КонецФункции

#КонецОбласти

#Область МетодыПреобразованияОбъектов

// Возвращает описание объекта на основании описания объекта сервиса.
//
// 	Параметры:
// 		ОписаниеОбъекта - содержит описание полей объекта сервиса.
//
// Возвращаемое значение:
// 		Структура - свойства структуры ИмяПоляКонфигурации описания объекта.
//
Функция ОписаниеОбъекта(ОписаниеОбъекта) Экспорт

	ОписаниеПолей = Новый Массив;
	Для каждого ОписаниеПоля Из ОписаниеОбъекта Цикл
		ОписаниеПолей.Добавить(ОписаниеПоля["ИмяПоляКонфигурации"]); 
	КонецЦикла;
	
	Возврат Новый Структура(СтрСоединить(ОписаниеПолей,","));

КонецФункции

// Преобразование структуры ОписаниеОбъекта в структуру с именами полей сервиса.
//
// 	Параметры:
// 		ОписаниеОбъекта - Структура - имена свойств ИмяПоляКонфигурации описания объекта,
// 		ОписаниеОбъектаСервиса - Массив - содержит описание полей объекта сервиса.
//
// Возвращаемое значение:
// 		Структура - имена свойств ИмяПоляСервиса  описания объекта.
//
Функция ОбъектСервисаПоОписанию(ОписаниеОбъекта, ОписаниеОбъектаСервиса) Экспорт

	Результат = Новый Структура;
	Для каждого ОписаниеПоля Из ОписаниеОбъектаСервиса Цикл
		ЗначениеПоля = Неопределено;
		ОписаниеОбъекта.Свойство(ОписаниеПоля["ИмяПоляКонфигурации"], ЗначениеПоля);
		Если ЗначениеПоля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Результат.Вставить(ОписаниеПоля["ИмяПоляСервиса"], ЗначениеПоля);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Возвращает объект сервиса file
Функция ОбъектСервисаФайл(ИдентификаторФайла, ИмяФайла, РасширениеФайла, РазмерФайла, ВерсияDTO, ВерсияФайла = Неопределено)

	ОписаниеОбъектаФайл = ОписаниеОбъектаФайл(ВерсияDTO);
	
	ОписаниеФайла = ОписаниеОбъекта(ОписаниеОбъектаФайл);
	ОписаниеФайла.НаименованиеФайла 	= ИмяФайла;
	ОписаниеФайла.РасширениеФайла 		= РасширениеФайла;
	ОписаниеФайла.РазмерФайла 			= РазмерФайла;
	ОписаниеФайла.ИдентификаторФайла 	= ИдентификаторФайла;
	ОписаниеФайла.ХешСумма 				= ВерсияФайла;
	
	Возврат ОбъектСервисаПоОписанию(ОписаниеФайла, ОписаниеОбъектаФайл);

КонецФункции

Функция НовоеОписаниеПоля(ИмяПоляСервиса, ИмяПоляКонфигурации, ТипПоля, ОписаниеПолей = Неопределено, ОбязательноеПоле = Ложь) Экспорт
	
	ОписаниеПоля = Новый Соответствие;
	ОписаниеПоля.Вставить("ИмяПоляСервиса", ИмяПоляСервиса);
	ОписаниеПоля.Вставить("ИмяПоляКонфигурации", ИмяПоляКонфигурации);
	ОписаниеПоля.Вставить("ТипПоля", ТипПоля);
	ОписаниеПоля.Вставить("ОписаниеПолей", ОписаниеПолей);
	ОписаниеПоля.Вставить("ОбязательноеПоле", ОбязательноеПоле);
	Возврат ОписаниеПоля;
	
КонецФункции

// Возвращает структуру с описание объекта на основании  описания полей.
//
// Параметры:
// 		ОписаниеПолейОбъекта -  Соответствие - описание полей объекта
//
// Возвращаемое значение;
// 		Структура
//
Функция НовоеОписаниеОбъекта(ОписаниеПолейОбъекта)

	Описание = Новый Структура;
	Для каждого ЭлементКоллекции Из ОписаниеПолейОбъекта Цикл
		Описание.Вставить(ЭлементКоллекции.Значение);
	КонецЦикла;
	
	Возврат Описание;

КонецФункции

// Возвращает соответствие заполненное на основании описания объекта.
//
// Параметры:
// 		ОписаниеОбъекта - Структура - 
// 		ОписаниеПолейОбъекта - Соответствие - описание полей, ключ - имя поля сервиса, значение - имя в конфигурации.
//
// Возвращаемое значение:
// 		Соответствие
//
Функция ОбъектСервисаПоОписаниюПолей(ОписаниеОбъекта, ОписаниеПолейОбъекта)

	ОбъектСервиса = Новый Соответствие;
	Для каждого ОписаниеПоля Из ОписаниеПолейОбъекта Цикл
		ОбъектСервиса.Вставить(ОписаниеПоля.Ключ, ОписаниеОбъекта[ОписаниеПоля.Значение]);
	КонецЦикла;
	
	Возврат ОбъектСервиса;

КонецФункции

// Возвращает структуру заполненную на основании объекта сервиса.
//
// Параметры:
// 		ОбъектОтвета - Соответствие
// 		ОписаниеПолейОбъекта - Соответствие - описание полей, ключ - имя поля сервиса, значение - имя в конфигурации.
//
// Возвращаемое значение:
// 		Соответствие
//
Функция ОписаниеОбъектаИзОбъектаОтвета(ОбъектОтвета, ОписаниеПолейОбъекта)

	ОписаниеОбъекта = Новый Структура;
	Для каждого ОписаниеПоля Из ОписаниеПолейОбъекта Цикл
		ОписаниеОбъекта.Вставить(ОписаниеПоля.Значение, ОбъектОтвета[ОписаниеПоля.Ключ]);
	КонецЦикла;
	
	Возврат ОписаниеОбъекта;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПреобразованиеЗагруженныхОбъектов

// Преобразование Соответствия ОбъектСервиса в структуру с именами полей ИБ.
//
// 	Параметры:
// 		ОбъектСервиса - Соответствие - объект загруженный из сервиса,
// 		ОписаниеПолей - Массив - содержит описание полей объекта.
//
// Возвращаемое значение:
// 		Структура - имена свойств ИмяПоляКонфигурации.
//
Функция СтруктураИзОбъектаСервиса(ОбъектСервиса, ОписаниеПолей)

	Объекты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектСервиса);
	Таблица = ТаблицаИзМассиваОбъектов(Объекты, ОписаниеПолей);
	
	ОписаниеОбъекта = Новый Структура;
	Для каждого Колонка Из Таблица.Колонки Цикл
		ИмяКолонки = Колонка.Имя;
		ОписаниеОбъекта.Вставить(ИмяКолонки, Таблица[0][ИмяКолонки]);
	КонецЦикла;
	
	Возврат ОписаниеОбъекта;

КонецФункции

// Преобразование массива объектов сервиса в таблицу значений с именами полей ИБ.
//
// 	Параметры:
// 		Объекты - Массив - элемент массива это соответствие описывающее объект загруженный из сервиса,
// 		ОписаниеПолей - Массив - содержит описание полей объекта.
//
// Возвращаемое значение:
// 		ТаблицаЗначений - имена колонок ИмяПоляКонфигурации.
//
Функция ТаблицаИзМассиваОбъектов(Объекты, ОписаниеПолей) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПолей);
	Для Каждого ОписаниеОбъекта Из Объекты Цикл
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьСтрокуТаблицыПоОписанию(НоваяСтрока, ОписаниеОбъекта, ОписаниеПолей);
	КонецЦикла;
	
	Возврат Таблица;

КонецФункции

Функция МассивОписанийИзМассиваОбъектов(Объекты, ОписаниеПолей)
	
	МассивОписаний = Новый Массив;
	Для Каждого ОписаниеОбъекта Из Объекты Цикл
		МассивОписаний.Добавить(СтруктураИзЗначенияПоляСервиса(ОписаниеОбъекта, ОписаниеПолей));
	КонецЦикла;
	
	Возврат МассивОписаний;

КонецФункции

Процедура ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПолей)
	
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		ТипПоля = ОписаниеПоля["ТипПоля"];
		Если ТипПоля = Неопределено Тогда
			ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПоля["ОписаниеПолей"]);
		Иначе
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипПоля);
			Таблица.Колонки.Добавить(ОписаниеПоля["ИмяПоляКонфигурации"], Новый ОписаниеТипов(МассивТипов));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуТаблицыПоОписанию(СтрокаТаблицы, ОписаниеОбъекта, ОписаниеПолей)
	
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		ЗначениеПоляСервиса = ОписаниеОбъекта.Получить(ОписаниеПоля["ИмяПоляСервиса"]);
		Если ЗначениеПоляСервиса = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			ЗаполнитьСтрокуТаблицыПоОписанию(СтрокаТаблицы, ОписаниеОбъекта, ОписаниеПоля["ОписаниеПолей"]);
		Иначе
			СтрокаТаблицы[ОписаниеПоля["ИмяПоляКонфигурации"]] = ЗначениеИзЗначенияПоляСервиса(ЗначениеПоляСервиса, ОписаниеПоля);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеИзЗначенияПоляСервиса(ЗначениеПоляСервиса, ОписаниеПоля)
	
	Значение = ЗначениеПоляСервиса;
	Если ОписаниеПоля["ТипПоля"] = Тип("ТаблицаЗначений") Тогда
		Значение = ТаблицаИзМассиваОбъектов(Значение, ОписаниеПоля["ОписаниеПолей"]);
	ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("Структура") Тогда
		Значение = СтруктураИзЗначенияПоляСервиса(Значение, ОписаниеПоля["ОписаниеПолей"]);
	ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("Массив") Тогда
		Значение = МассивОписанийИзМассиваОбъектов(Значение, ОписаниеПоля["ОписаниеПолей"]);
	Иначе
		ИмяПоля = ОписаниеПоля["ИмяПоляКонфигурации"];
		Если ИмяПоля = "Назначение2НДФЛ" Тогда
			Значение = НазначениеСправкиНДФЛСервиса(Значение);
		ИначеЕсли ИмяПоля = "ВариантФормированияФайлаОтвета" Тогда
			Значение = ВариантФормированияФайлаОтветаСервиса(Значение);
		ИначеЕсли ИмяПоля = "РезультатСогласования" Тогда
			Значение = ЗначениеРезультатСогласования(Значение);
		ИначеЕсли ИмяПоля = "ПричинаОтсутствия" Тогда
			Значение = ПричинаОтсутствияСервиса(Значение);
		ИначеЕсли ИмяПоля = "СпособРасчетаУдержанияДСВ" Тогда
			Значение = СпособРасчетаУдержанияДСВ(Значение);
		ИначеЕсли ИмяПоля = "РольПодписанта" Тогда
			Значение = РольПодписантаСервиса(Значение);
		ИначеЕсли ИмяПоля = "ТипЗаявки" Тогда
			Значение = ТипЗаявкиСервиса(Значение);
		ИначеЕсли ИмяПоля = "ТипШага" Тогда
			Значение = ТипШагаСогласованияСервиса(Значение);
		ИначеЕсли ИмяПоля = "ТребуемаяПодпись" Тогда
			Значение = ВидТребуемойПодписиСервиса(Значение);
		Иначе
			Значение = КабинетСотрудникаВнутренний.ЗначениеИзЗначенияПоляСервиса(ЗначениеПоляСервиса, ИмяПоля);	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция СтруктураИзЗначенияПоляСервиса(ЗначениеПоляСервиса, ОписаниеПолей)

	ОписаниеОбъекта = Новый Структура;
	Для каждого ОписаниеПоля Из ОписаниеПолей Цикл
		
		ЗначениеПоля = ЗначениеПоляСервиса.Получить(ОписаниеПоля["ИмяПоляСервиса"]);
		Если ЗначениеПоля = Неопределено Тогда
			Значение = Неопределено;
		ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("ТаблицаЗначений") Тогда
			Значение = ТаблицаИзМассиваОбъектов(ЗначениеПоля, ОписаниеПоля["ОписаниеПолей"]);
		ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("Структура") Тогда
			Значение = СтруктураИзЗначенияПоляСервиса(ЗначениеПоля, ОписаниеПоля["ОписаниеПолей"]);
		Иначе
			Значение = ЗначениеИзЗначенияПоляСервиса(ЗначениеПоля, ОписаниеПоля);
		КонецЕсли;
		
		ОписаниеОбъекта.Вставить(ОписаниеПоля["ИмяПоляКонфигурации"], Значение);
	
	КонецЦикла;
	
	Возврат ОписаниеОбъекта;

КонецФункции

Функция НазначениеСправкиНДФЛСервиса(Назначение)
	
	Результат = Назначение;
	Если Назначение = "incomeProof" Тогда
		Результат = "ПодтверждениеДоходов";
	ИначеЕсли Назначение = "declare" Тогда
		Результат = "ДекларированиеДоходов";
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ВариантФормированияФайлаОтветаСервиса(Вариант)
	
	ВариантыФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника;
	Результат = Неопределено;
	
	Если Вариант = "fileForPrinterOutput" Тогда
		Результат = ВариантыФормированияФайлаОтвета.ФайлДляВыводаНаПринтер;
	ИначеЕсли Вариант = "scanWithSignatureAndStamp" Тогда
		Результат = ВариантыФормированияФайлаОтвета.СканСПодписьюИПечатью;
	ИначеЕсли Вариант = "paperForm" Тогда
		Результат = ВариантыФормированияФайлаОтвета.ВБумажномВиде;
	ИначеЕсли Вариант = "fileWithElectronicSignature" Тогда
		Результат = ВариантыФормированияФайлаОтвета.ФайлСЭП;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеРезультатСогласования(ЗначениеПоля)

	Результат = ЗначениеПоля;
	Если ЗначениеПоля = "agreed" Тогда
		Результат = Перечисления.РезультатыСогласованияБЗК.Согласовано;
	ИначеЕсли ЗначениеПоля = "denied" Тогда
		Результат = Перечисления.РезультатыСогласованияБЗК.Отклонено;
	КонецЕсли;
	Возврат Результат;

КонецФункции

Функция СоответствиеЗначенийРольПодписанта()

	Описание = Новый Соответствие;
	Описание.Вставить("employee", 				Перечисления.РолиПодписантовКЭДО.Сотрудник);
	Описание.Вставить("directManager", 			Перечисления.РолиПодписантовКЭДО.НепосредственныйРуководитель);
	Описание.Вставить("executor", 				Перечисления.РолиПодписантовКЭДО.Исполнитель);
	Описание.Вставить("employer", 				Перечисления.РолиПодписантовКЭДО.Организация);
	Описание.Вставить("allWorkspaceManager",	Перечисления.РолиПодписантовКЭДО.РуководителиПоВсемМестамРаботы);
	Описание.Вставить("superiorManager",		Перечисления.РолиПодписантовКЭДО.ВышестоящийРуководитель);
	Описание.Вставить("teamLeader",				Перечисления.РолиПодписантовКЭДО.НеформальныйРуководитель);
	Описание.Вставить("additionalWorkspaceManager",	Перечисления.РолиПодписантовКЭДО.РуководительПоДополнительнымМестамРаботы);
	
	Возврат Описание;

КонецФункции

Функция РольПодписантаСервиса(ЗначениеПоля)

	СоответствиеЗначений = СоответствиеЗначенийРольПодписанта();
	Результат = СоответствиеЗначений[ЗначениеПоля];
	Если Результат = Неопределено Тогда
		Результат = ЗначениеПоля;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ПричинаОтсутствияСервиса(ПричинаОтсутствия)
	
	Результат = ПричинаОтсутствия;
	Если ПричинаОтсутствия = "late" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Опоздание;
	ИначеЕсли ПричинаОтсутствия = "illness" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Болезнь;
	ИначеЕсли ПричинаОтсутствия = "vacation" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск;
	ИначеЕсли ПричинаОтсутствия = "studyLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск;
	ИначеЕсли ПричинаОтсутствия = "unpaidLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет;
	ИначеЕсли ПричинаОтсутствия = "maternityLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам;
	ИначеЕсли ПричинаОтсутствия = "parentalLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоУходуЗаРебенком;
	ИначеЕсли ПричинаОтсутствия = "timeOff" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул;
	ИначеЕсли ПричинаОтсутствия = "businessTrip" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка;
	ИначеЕсли ПричинаОтсутствия = "personalReason" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела;
	ИначеЕсли ПричинаОтсутствия = "invalidChildCare" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СпособРасчетаУдержанияДСВ(СпособРасчета)

	Результат = СпособРасчета;
	Если СпособРасчета = "percentage" Тогда
		Результат = Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.Процентом; 
	ИначеЕсли СпособРасчета = "fixedAmount" Тогда
		Результат = Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.ФиксированнойСуммой;
	ИначеЕсли СпособРасчета = "percentageUpToAmount" Тогда
		Результат =  Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.ПроцентомНеБолееСуммы;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ТипЗаявкиСервиса(ЗначениеПоля)
	
	Результат = ЗначениеПоля;
	Если ЗначениеПоля = "forms2NDFLRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ;
	ИначеЕсли ЗначениеПоля = "leaveEncashmentRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска;
	ИначеЕсли ЗначениеПоля = "personalInformationChangeRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных;
	ИначеЕсли ЗначениеПоля = "voluntaryInsuranceContributionsRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ;
	ИначеЕсли ЗначениеПоля = "certificatesFromEmployerRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы;
	ИначеЕсли ЗначениеПоля = "vacationRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск;
	ИначеЕсли ЗначениеПоля = "vacationBalanceRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска;
	ИначеЕсли ЗначениеПоля = "taxDeductionRequests" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты;
	ИначеЕсли ЗначениеПоля = "absences" Тогда
		Результат = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТипШагаСогласованияСервиса(ЗначениеПоля)
	
	Результат = Неопределено;
	
	Если ЗначениеПоля = "all" Тогда 
		Результат = Перечисления.ТипыШаговСогласованияКабинетСотрудника.ВсеСогласующие;
	ИначеЕсли ЗначениеПоля = "anybody" Тогда
		Результат = Перечисления.ТипыШаговСогласованияКабинетСотрудника.ЛюбойИзСогласующих;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидТребуемойПодписиСервиса(ЗначениеПоля)
	
	Результат = Неопределено;
	
	Если ЗначениеПоля = "notRequired" Тогда
		Результат = Перечисления.ВидыТребуемойПодписиКабинетСотрудника.НеТребуется;
	ИначеЕсли ЗначениеПоля = "unqualifiedSignature" Тогда
		Результат = Перечисления.ВидыТребуемойПодписиКабинетСотрудника.УНЭП
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеДанныхИБ

#Область ФункцииПолученияДанных

Функция ДанныеФизическихЛиц(ПараметрыОбмена, МассивОтбора)
	
	ТаблицаДанных = СведенияФизическихЛиц(МассивОтбора, ПараметрыОбмена.ВерсияDTO);
	Фотографии = КабинетСотрудникаВнутренний.ФотографииФизическихЛиц(ТаблицаДанных.ВыгрузитьКолонку("ФизическоеЛицоСсылка"));

	Если Не КабинетСотрудника.ВерсионированиеИспользуется() Тогда
		НастройкиФункциональности = РегистрыСведений.ИспользуемаяФункциональностьСервисаКабинетСотрудника.Настройки();
		УровеньДоступаКИ = УровеньДоступаКИнформацииОСотрудниках(НастройкиФункциональности.УровеньДоступаКИ);
		ТаблицаДанных.Колонки.Добавить("ДоступКонтактнойИнформации", Новый ОписаниеТипов("Строка"));
		ТаблицаДанных.ЗаполнитьЗначения(УровеньДоступаКИ, "ДоступКонтактнойИнформации");
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТаблицаДанных", ТаблицаДанных);
	Результат.Вставить("Фотографии", 	Фотографии);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДолжностей(ПараметрыОбмена, МассивОтбора)
	
	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	СоответствиеПолей = ОписаниеПолейДолжностей(ВерсияDTO);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтбора", МассивОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Должности.Ссылка КАК Должность,
	|	Должности.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Должности КАК Должности
	|ГДЕ
	|	Должности.Ссылка В(&СписокОтбора)";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить(); 
	
	ТаблицаДанных.Колонки.Добавить("ДолжностьИдентификатор", Новый ОписаниеТипов("Строка"));
	ДолжностьИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Должность", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	
	Для Каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.ДолжностьИдентификатор = ДолжностьИдентификатор[СтрокаТЗ.Должность];
	КонецЦикла;
		
	ТаблицаДанных.Колонки.Удалить("Должность");
	ТаблицаДанных.Колонки.ДолжностьИдентификатор.Имя = "Должность";	
	
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);

КонецФункции

Функция ДанныеОрганизаций(ПараметрыОбмена, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейОрганизаций(ПараметрыОбмена.ВерсияDTO);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтбора", МассивОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	Организации.Наименование КАК Наименование,
	|	Организации.ИНН КАК ИНН,
	|	Организации.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&СписокОтбора)";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ТаблицаДанных.Колонки.Добавить("ОрганизацияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ГоловнаяОрганизацияИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Организации = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "Организация", Истина);
	ГоловныеОрганизации = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "ГоловнаяОрганизация", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Организации, ГоловныеОрганизации, Истина);
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
	ОбщегоНазначенияБЗККлиентСервер.УдалитьПустыеЗначенияИзМассива(Организации);
	ОрганизацияИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Организации, ТипОбъекта);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.ОрганизацияИдентификатор 			= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		СтрокаТЗ.ГоловнаяОрганизацияИдентификатор 	= ОрганизацияИдентификатор[СтрокаТЗ.ГоловнаяОрганизация];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("Организация");
	ТаблицаДанных.Колонки.ОрганизацияИдентификатор.Имя = "Организация";
	ТаблицаДанных.Колонки.Удалить("ГоловнаяОрганизация");
	ТаблицаДанных.Колонки.ГоловнаяОрганизацияИдентификатор.Имя = "ГоловнаяОрганизация";
	
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеСтруктурыПредприятия(ПараметрыОбмена, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейСтруктурыПредприятия(ПараметрыОбмена.ВерсияDTO);
	ТаблицаДанных = КабинетСотрудникаВнутренний.ДанныеСтруктурыПредприятия(МассивОтбора);
	
	ТаблицаДанных.Колонки.Добавить("СтруктураПредприятияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("РодительИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицоРуководителяИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Подразделения = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "СтруктураПредприятия", Истина);
	ПодразделенияРодитель = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "Родитель", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Подразделения, ПодразделенияРодитель, Истина);
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение; 
	ОбщегоНазначенияБЗККлиентСервер.УдалитьПустыеЗначенияИзМассива(Подразделения);
	ПодразделениеИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Подразделения, ТипОбъекта);
	
	РуководительИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицоРуководителя", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.СтруктураПредприятияИдентификатор 			= ПодразделениеИдентификатор[СтрокаТЗ.СтруктураПредприятия];
		СтрокаТЗ.РодительИдентификатор 						= ПодразделениеИдентификатор[СтрокаТЗ.Родитель];
		СтрокаТЗ.ФизическоеЛицоРуководителяИдентификатор 	= РуководительИдентификатор[СтрокаТЗ.ФизическоеЛицоРуководителя];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("СтруктураПредприятия");
	ТаблицаДанных.Колонки.СтруктураПредприятияИдентификатор.Имя = "СтруктураПредприятия";
	ТаблицаДанных.Колонки.Удалить("Родитель");
	ТаблицаДанных.Колонки.РодительИдентификатор.Имя = "Родитель";
	ТаблицаДанных.Колонки.Удалить("ФизическоеЛицоРуководителя");
	ТаблицаДанных.Колонки.ФизическоеЛицоРуководителяИдентификатор.Имя = "ФизическоеЛицоРуководителя";
	
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеСтруктурыЮридическихЛиц(ПараметрыОбмена, Организации, ПодразделенияОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Подразделения", ПодразделенияОрганизаций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	""0000"" КАК Код,
	|	Организации.Наименование КАК Наименование,
	|	НЕОПРЕДЕЛЕНО КАК РодительПодразделения,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицоРуководителя,
	|	"""" КАК Порядок,
	|	Организации.Ссылка КАК Организация,
	|	ИСТИНА КАК ЭтоОрганизация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&Организации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Подразделения.Ссылка,
	|	Подразделения.Код,
	|	Подразделения.Наименование,
	|	Подразделения.Родитель,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	Подразделения.РеквизитДопУпорядочиванияИерархического,
	|	Подразделения.Владелец,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК Подразделения
	|ГДЕ
	|	Подразделения.Ссылка В(&Подразделения)";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаДанных) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ТаблицаДанных.Колонки.Добавить("СтруктураПредприятия");
	ТаблицаДанных.Колонки.Добавить("Родитель");
	
	ПубликуемыеФизическиеЛица = КабинетСотрудника.ПубликуемыеФизическиеЛица();
	
	ОрганизацииРуководители = Новый Соответствие;
	Организации = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "Организация", Истина);
	СведенияОРуководителях = ИнтеграцияУправлениеПерсоналом.СведенияОРуководителяхОрганизаций(Организации);
	Для Каждого Организация Из Организации Цикл
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Продолжить;
		КонецЕсли;
		ДанныеРуководителя = СведенияОРуководителях[Организация];
		Если ПубликуемыеФизическиеЛица[ДанныеРуководителя.Руководитель] <> Неопределено Тогда
			ОрганизацииРуководители.Вставить(Организация, ДанныеРуководителя.Руководитель);
		КонецЕсли;
	КонецЦикла;
	
	ПодразделенияРуководители = ИнтеграцияУправлениеПерсоналом.РуководителиПодразделенийОрганизаций(ПодразделенияОрганизаций, ПубликуемыеФизическиеЛица);
	
	ПорядокОрганизаций = ПорядокОрганизаций();
	Для Каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		ПорядокОрганизации = ПорядокОрганизаций[СтрокаТЗ.Организация];
		ПорядокПодразделения = СтрокаТЗ.Порядок;
		Если ЗначениеЗаполнено(ПорядокОрганизации) Тогда
			СтрокаТЗ.Порядок = ПорядокОрганизации + ПорядокПодразделения;
		КонецЕсли;
		
		СтрокаТЗ.Родитель = СтрокаТЗ.РодительПодразделения;
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Родитель) Тогда
			СтрокаТЗ.Родитель = СтрокаТЗ.Организация;
		КонецЕсли;
		СтрокаТЗ.СтруктураПредприятия = СтрокаТЗ.Ссылка;
		
		СтрокаТЗ.ФизическоеЛицоРуководителя = ПодразделенияРуководители[СтрокаТЗ.Ссылка];
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ФизическоеЛицоРуководителя) Или ПубликуемыеФизическиеЛица[СтрокаТЗ.ФизическоеЛицоРуководителя] = Неопределено Тогда
			СтрокаТЗ.ФизическоеЛицоРуководителя = ОрганизацииРуководители[СтрокаТЗ.Организация];
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Добавить("СтруктураПредприятияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("РодительИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицоРуководителяИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Подразделения = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "СтруктураПредприятия", Истина);
	ПодразделенияРодитель = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "Родитель", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Подразделения, ПодразделенияРодитель, Истина);
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	ОбщегоНазначенияБЗККлиентСервер.УдалитьПустыеЗначенияИзМассива(Подразделения);
	ПодразделениеИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Подразделения, ТипОбъекта);
	
	РуководительИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицоРуководителя", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.СтруктураПредприятияИдентификатор 			= ПодразделениеИдентификатор[СтрокаТЗ.СтруктураПредприятия];
		СтрокаТЗ.РодительИдентификатор 						= ПодразделениеИдентификатор[СтрокаТЗ.Родитель];
		СтрокаТЗ.ФизическоеЛицоРуководителяИдентификатор 	= РуководительИдентификатор[СтрокаТЗ.ФизическоеЛицоРуководителя];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("СтруктураПредприятия");
	ТаблицаДанных.Колонки.СтруктураПредприятияИдентификатор.Имя = "СтруктураПредприятия";
	ТаблицаДанных.Колонки.Удалить("Родитель");
	ТаблицаДанных.Колонки.РодительИдентификатор.Имя = "Родитель";
	ТаблицаДанных.Колонки.Удалить("ФизическоеЛицоРуководителя");
	ТаблицаДанных.Колонки.ФизическоеЛицоРуководителяИдентификатор.Имя = "ФизическоеЛицоРуководителя";
	
	СоответствиеПолей = ОписаниеПолейСтруктурыПредприятия(ПараметрыОбмена.ВерсияDTO);
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеШтатногоРасписания(ПараметрыОбмена, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейШтатногоРасписания(ПараметрыОбмена.ВерсияDTO);
	ТаблицаДанных = КабинетСотрудникаВнутренний.ДанныеШтатногоРасписания(МассивОтбора);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ОшибкиЗаполнения = Новый Соответствие;
	СтрокиКУдалению = Новый Массив;
	ТаблицаДанных.Колонки.МестоВСтруктуреПредприятия.Имя = "Подразделение";
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл 
		ДанныеДляПроверки = ИнтеграцияУправлениеПерсоналом.ДанныеДляПроверкиПозицииШтатногоРасписания(СтрокаТЗ);
		РезультатПроверки = ИнтеграцияУправлениеПерсоналом.РезультатПроверкиДанныхПозицииШтатногоРасписания(ДанныеДляПроверки);
		Если ЗначениеЗаполнено(РезультатПроверки[Приложение].БлокирующиеОшибки) Тогда
			ОшибкиОбъекта = ИнтеграцияУправлениеПерсоналом.ОшибкиЗаполненияОбъектаДляПриложения(РезультатПроверки, Приложение);
			ОшибкиЗаполнения.Вставить(СтрокаТЗ.ПозицияШтатногоРасписания, ОшибкиОбъекта);
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	ТаблицаДанных.Колонки.Подразделение.Имя = "МестоВСтруктуреПредприятия";
	
	Для каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ТаблицаДанных.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Добавить("ПозицияШтатногоРасписанияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ОрганизацияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("МестоВСтруктуреПредприятияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ДолжностьИдентификатор", Новый ОписаниеТипов("Строка"));
	
	ПозицияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ПозицияШтатногоРасписания", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	ПодразделениеИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "МестоВСтруктуреПредприятия", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	ДолжностьИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Должность", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.ПозицияШтатногоРасписанияИдентификатор 	= ПозицияИдентификатор[СтрокаТЗ.ПозицияШтатногоРасписания];
		СтрокаТЗ.ОрганизацияИдентификатор 					= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		СтрокаТЗ.МестоВСтруктуреПредприятияИдентификатор 	= ПодразделениеИдентификатор[СтрокаТЗ.МестоВСтруктуреПредприятия];
		СтрокаТЗ.ДолжностьИдентификатор 					= ДолжностьИдентификатор[СтрокаТЗ.Должность];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("ПозицияШтатногоРасписания");
	ТаблицаДанных.Колонки.ПозицияШтатногоРасписанияИдентификатор.Имя = "ПозицияШтатногоРасписания";
	ТаблицаДанных.Колонки.Удалить("Организация");
	ТаблицаДанных.Колонки.ОрганизацияИдентификатор.Имя = "Организация";
	ТаблицаДанных.Колонки.Удалить("МестоВСтруктуреПредприятия");
	ТаблицаДанных.Колонки.МестоВСтруктуреПредприятияИдентификатор.Имя = "МестоВСтруктуреПредприятия";
	ТаблицаДанных.Колонки.Удалить("Должность");
	ТаблицаДанных.Колонки.ДолжностьИдентификатор.Имя = "Должность";
	
	Если ЗначениеЗаполнено(ОшибкиЗаполнения) Тогда
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
		Попытка
			ИнтеграцияУправлениеПерсоналом.ПроверитьЗарегистрироватьБлокирующиеОшибкиЗаполнения(Приложение, ОшибкиЗаполнения, ТипОбъекта);
		Исключение
			// Не обрабатываем исключение при неудачной записи результатов проверки.
		КонецПопытки;
	КонецЕсли;
	
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);

КонецФункции

Функция ДанныеСотрудников(ПараметрыОбмена, МассивОтбора)
	
	ДанныеСотрудников = КабинетСотрудникаВнутренний.ДанныеСотрудниковДляПубликации(ПараметрыОбмена, МассивОтбора);
	Если ДанныеСотрудников.Колонки.Найти("Подразделение") <> Неопределено Тогда
		ДанныеСотрудников.Колонки.Удалить("Подразделение")
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	Позиции = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеСотрудников, "ДолжностьПоШтатномуРасписанию", Истина);
	ПубликуемыеПозиции = ИнтеграцияУправлениеПерсоналом.ПозицииПравилВыгрузки(Приложение, Позиции);
	
	ФизическиеЛица = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеСотрудников, "ФизическоеЛицо", Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Исключения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Исключения.Публикуется КАК Публикуется
	|ИЗ
	|	РегистрСведений.ИсключенияИзПравилПубликацииУправлениеПерсоналом КАК Исключения
	|ГДЕ
	|	Исключения.ФизическоеЛицо В(&ФизическиеЛица)
	|	И Исключения.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника)";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	ФизическиеЛицаИсключения = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ФизическиеЛицаИсключения.Вставить(Выборка.ФизическоеЛицо, Выборка.Публикуется);
	КонецЦикла; 
	
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ВыгруженныеФизическиеЛица = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъектаФизическоеЛицо, ФизическиеЛица);
	ФизическиеЛицаВыгружались = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ВыгруженныеФизическиеЛица);

	ПроверяемыеОбъекты = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеОбъекты, ФизическиеЛица);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеОбъекты, Позиции);
	ОшибкиЗаполненияОбъектов = ИнтеграцияУправлениеПерсоналом.ОшибкиЗаполненияОбъектов(Приложение, ПроверяемыеОбъекты);
	
	Отбор = Новый Структура("БлокирующаяОшибка,ТипОбъекта", Истина);
	Отбор.ТипОбъекта = ТипОбъектаФизическоеЛицо;
	ОшибкиФизическихЛиц = ОшибкиЗаполненияОбъектов.Скопировать(Отбор);
	Отбор.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
	ОшибкиПозиций = ОшибкиЗаполненияОбъектов.Скопировать(Отбор); 
	
	ОшибкиФизическихЛиц.Индексы.Добавить("Ссылка");
	ОшибкиПозиций.Индексы.Добавить("Ссылка");
	Отбор = Новый Структура("Ссылка");
	
	СоответствиеПолей = ОписаниеПолейСотрудников(ПараметрыОбмена.ВерсияDTO);
	ВыбираемыеПоля = ВыбираемыеПоля(СоответствиеПолей);
	ТаблицаДанных = Новый ТаблицаЗначений;
	Для каждого ИмяПоля Из ВыбираемыеПоля Цикл
		ТаблицаДанных.Колонки.Добавить(ИмяПоля);
	КонецЦикла;
	
	ОшибкиЗаполнения = Новый Соответствие; 
	ДанныеСотрудников.Колонки.МестоВСтруктуреПредприятия.Имя = "Подразделение";
	СчОбъектов = 1;
	Для каждого СтрокаТЗ Из ДанныеСотрудников Цикл
		
		Если СчОбъектов = 500 Тогда
			Прервать;
		КонецЕсли;
		
		Если ФизическиеЛицаВыгружались[СтрокаТЗ.ФизическоеЛицо] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеДляПроверки = ИнтеграцияУправлениеПерсоналом.ДанныеДляПроверкиСотрудника(СтрокаТЗ);
		
		Отбор.Ссылка = СтрокаТЗ.ФизическоеЛицо;
		ОшибкиФизическогоЛица = ОшибкиФизическихЛиц.НайтиСтроки(Отбор);
		
		Отбор.Ссылка = СтрокаТЗ.ДолжностьПоШтатномуРасписанию;
		ОшибкиПозиции = ОшибкиПозиций.НайтиСтроки(Отбор);
		
		РезультатПроверки = ИнтеграцияУправлениеПерсоналом.РезультатПроверкиДанныхСотрудника(ДанныеДляПроверки, ОшибкиФизическогоЛица, ОшибкиПозиции);
		Если ЗначениеЗаполнено(РезультатПроверки[Приложение].БлокирующиеОшибки) Тогда
			ОшибкиОбъекта = ИнтеграцияУправлениеПерсоналом.ОшибкиЗаполненияОбъектаДляПриложения(РезультатПроверки, Приложение);
			ОшибкиЗаполнения.Вставить(СтрокаТЗ.Сотрудник, ОшибкиОбъекта);
			Продолжить;
		КонецЕсли;
		
		ИсключаяСвойства = "";
		Уволен = Ложь;
		Если СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
			ИсключаяСвойства = "ТарифнаяСтавка,ПоказательТарифнойСтавки";
			Уволен = Истина;
		КонецЕсли;
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,, ИсключаяСвойства);
		НоваяСтрока.Уволен = Уволен;
		НоваяСтрока.МестоВСтруктуреПредприятия = СтрокаТЗ.Подразделение;
		СкрыватьВСписках = Ложь;
		Если ФизическиеЛицаИсключения[СтрокаТЗ.ФизическоеЛицо] = Истина Тогда
			СкрыватьВСписках = Ложь;
		ИначеЕсли ФизическиеЛицаИсключения[СтрокаТЗ.ФизическоеЛицо] = Ложь Тогда
			СкрыватьВСписках = Истина;
		Иначе
			СкрыватьВСписках = ?(ПубликуемыеПозиции[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] = Неопределено, Истина, Ложь);
		КонецЕсли;
		НоваяСтрока.СкрыватьВСписках = СкрыватьВСписках;
		
		СчОбъектов = СчОбъектов + 1;
		
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Добавить("СотрудникИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицоИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ДолжностьПоШтатномуРасписаниюИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ОрганизацияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("МестоВСтруктуреПредприятияИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ДолжностьИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ГрафикРаботыИдентификатор", Новый ОписаниеТипов("Строка"));
	
	СотрудникИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Сотрудник", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник);
	ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ПозицияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ДолжностьПоШтатномуРасписанию", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	ПодразделениеИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "МестоВСтруктуреПредприятия", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	ДолжностьИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Должность", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	ГрафикРаботыИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ГрафикРаботы", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикРаботы);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.СотрудникИдентификатор 					= СотрудникИдентификатор[СтрокаТЗ.Сотрудник];
		СтрокаТЗ.ФизическоеЛицоИдентификатор 				= ФизическоеЛицоИдентификатор[СтрокаТЗ.ФизическоеЛицо];
		СтрокаТЗ.ДолжностьПоШтатномуРасписаниюИдентификатор = ПозицияИдентификатор[СтрокаТЗ.ДолжностьПоШтатномуРасписанию];
		СтрокаТЗ.ОрганизацияИдентификатор 					= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		СтрокаТЗ.МестоВСтруктуреПредприятияИдентификатор 	= ПодразделениеИдентификатор[СтрокаТЗ.МестоВСтруктуреПредприятия];
		СтрокаТЗ.ДолжностьИдентификатор 					= ДолжностьИдентификатор[СтрокаТЗ.Должность];
		СтрокаТЗ.ГрафикРаботыИдентификатор 					= ГрафикРаботыИдентификатор[СтрокаТЗ.ГрафикРаботы];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("Сотрудник");
	ТаблицаДанных.Колонки.СотрудникИдентификатор.Имя = "Сотрудник";
	ТаблицаДанных.Колонки.Удалить("ФизическоеЛицо");
	ТаблицаДанных.Колонки.ФизическоеЛицоИдентификатор.Имя = "ФизическоеЛицо";
	ТаблицаДанных.Колонки.Удалить("ДолжностьПоШтатномуРасписанию");
	ТаблицаДанных.Колонки.ДолжностьПоШтатномуРасписаниюИдентификатор.Имя = "ДолжностьПоШтатномуРасписанию";
	ТаблицаДанных.Колонки.Удалить("Организация");
	ТаблицаДанных.Колонки.ОрганизацияИдентификатор.Имя = "Организация";
	ТаблицаДанных.Колонки.Удалить("МестоВСтруктуреПредприятия");
	ТаблицаДанных.Колонки.МестоВСтруктуреПредприятияИдентификатор.Имя = "МестоВСтруктуреПредприятия";
	ТаблицаДанных.Колонки.Удалить("Должность");
	ТаблицаДанных.Колонки.ДолжностьИдентификатор.Имя = "Должность";
	ТаблицаДанных.Колонки.Удалить("ГрафикРаботы");
	ТаблицаДанных.Колонки.ГрафикРаботыИдентификатор.Имя = "ГрафикРаботы";
	
	Если ЗначениеЗаполнено(ОшибкиЗаполнения) Тогда
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
		Попытка
			ИнтеграцияУправлениеПерсоналом.ПроверитьЗарегистрироватьБлокирующиеОшибкиЗаполнения(Приложение, ОшибкиЗаполнения, ТипОбъекта);
		Исключение
			// Не обрабатываем исключение при неудачной записи результатов проверки.
		КонецПопытки;
	КонецЕсли;
	
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеОВычетах(ПараметрыОбмена, МассивОтбора)
	
	ТаблицаДанных = ПрименяемыеВычеты(МассивОтбора, ТекущаяДатаСеанса());
	ФизическиеЛицаСВычетами  = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "ФизическоеЛицо", Истина);
	
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицоИдентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ОрганизацияИдентификатор", Новый ОписаниеТипов("Строка"));
	
	ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация); 
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.ФизическоеЛицоИдентификатор 	= ФизическоеЛицоИдентификатор[СтрокаТЗ.ФизическоеЛицо];
		СтрокаТЗ.ОрганизацияИдентификатор 		= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Удалить("ФизическоеЛицо");
	ТаблицаДанных.Колонки.ФизическоеЛицоИдентификатор.Имя = "ФизическоеЛицо";
	ТаблицаДанных.Колонки.Удалить("Организация");
	ТаблицаДанных.Колонки.ОрганизацияИдентификатор.Имя = "Организация";
	
	ОписаниеПолей = ОписаниеПолейПрименяемыеНалоговыеВычеты(ПараметрыОбмена.ВерсияDTO);
	
	Результат = Новый Структура("Данные,ФизическиеЛицаБезВычетов");
	Результат.Данные = МассивИзТаблицы(ТаблицаДанных, ОписаниеПолей);
	Результат.ФизическиеЛицаБезВычетов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(МассивОтбора, ФизическиеЛицаСВычетами);
	
	Возврат Результат;
	
КонецФункции

Функция ПрименяемыеВычеты(СписокФизическихЛиц, ДатаАктуальности)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерВычетовНДФЛСрезПоследних.КодВычета КАК КодВычета,
	|	РазмерВычетовНДФЛСрезПоследних.Размер КАК Размер
	|ИЗ
	|	РегистрСведений.РазмерВычетовНДФЛ.СрезПоследних(&ДатаАктуальности, ) КАК РазмерВычетовНДФЛСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	РазмерыВычетов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		РазмерыВычетов.Вставить(Выборка.КодВычета, Выборка.Размер);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(ДатаАктуальности));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецГода(ДатаАктуальности));
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИмущественныеВычетыНДФЛ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТРегистраторыИмущественныхВычетов
	|ИЗ
	|	Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК ИмущественныеВычетыНДФЛ
	|ГДЕ
	|	ИмущественныеВычетыНДФЛ.Проведен
	|	И ИмущественныеВычетыНДФЛ.ПрименятьВычетыС МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИмущественныеВычетыНДФЛ.Сотрудник В(&СписокФизическихЛиц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации) КАК МесяцРегистрации,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация КАК Организация
	|ПОМЕСТИТЬ ВТПоследнийМесяцВычетовНаДетей
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
	|ГДЕ
	|	СтандартныеВычетыНаДетейНДФЛ.ДействуетДо >= &ДатаНачала
	|	И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо В(&СписокФизическихЛиц)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МесяцРегистрации,
	|	Организация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МАКСИМУМ(СтандартныеВычетыФизическихЛицНДФЛ.Период) КАК Период,
	|	СтандартныеВычетыФизическихЛицНДФЛ.Регистратор.Организация КАК Организация
	|ПОМЕСТИТЬ ВТПоследнийМесяцВычетов
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ КАК СтандартныеВычетыФизическихЛицНДФЛ
	|
	|СГРУППИРОВАТЬ ПО
	|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо,
	|	СтандартныеВычетыФизическихЛицНДФЛ.Регистратор.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмущественныеВычетыНДФЛ.Организация КАК Организация,
	|	ИмущественныеВычетыНДФЛ.Сотрудник КАК ФизическоеЛицо,
	|	ИмущественныеВычетыНДФЛ.ПрименятьВычетыС КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ИмущественныеВычетыНДФЛ.ПрименятьВычетыС, ГОД) КАК ДатаОкончания,
	|	ИмущественныеВычетыНДФЛ.РасходыНаСтроительствоПриобретение КАК РасходыНаСтроительствоПриобретение,
	|	ИмущественныеВычетыНДФЛ.ПроцентыПоКредитам КАК ПроцентыПоКредитам,
	|	ИмущественныеВычетыНДФЛ.ПроцентыПриПерекредитовании КАК ПроцентыПриПерекредитовании,
	|	ИмущественныеВычетыНДФЛ.РасходыНаСвоеОбучение КАК РасходыНаСвоеОбучение,
	|	ИмущественныеВычетыНДФЛ.РасходыНаОбучениеДетей КАК РасходыНаОбучениеДетей,
	|	ИмущественныеВычетыНДФЛ.РасходыНаЛечение КАК РасходыНаЛечение,
	|	ИмущественныеВычетыНДФЛ.СтраховыеВзносыНаМедУслуги КАК СтраховыеВзносыНаМедУслуги,
	|	ИмущественныеВычетыНДФЛ.РасходыНаДорогостоящееЛечение КАК РасходыНаДорогостоящееЛечение,
	|	ИмущественныеВычетыНДФЛ.ВзносыНаДобровольноеСтрахованиеЖизни КАК ВзносыНаДобровольноеСтрахованиеЖизни
	|ИЗ
	|	ВТРегистраторыИмущественныхВычетов КАК Регистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК ИмущественныеВычетыНДФЛ
	|		ПО Регистраторы.Ссылка = ИмущественныеВычетыНДФЛ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация КАК Организация,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации КАК ДатаНачала,
	|	СтандартныеВычетыНаДетейНДФЛ.ДействуетДо КАК ДатаОкончания,
	|	СтандартныеВычетыНаДетейНДФЛ.КодВычета КАК КодВычета,
	|	СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей КАК КоличествоДетей,
	|	ВидыВычетовНДФЛ.ПолноеНаименование КАК ПолноеНаименование
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследнийМесяцВычетовНаДетей КАК ПоследнийМесяцВычетовНаДетей
	|		ПО СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации = ПоследнийМесяцВычетовНаДетей.МесяцРегистрации
	|			И СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация = ПоследнийМесяцВычетовНаДетей.Организация
	|			И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо = ПоследнийМесяцВычетовНаДетей.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыВычетовНДФЛ КАК ВидыВычетовНДФЛ
	|		ПО СтандартныеВычетыНаДетейНДФЛ.КодВычета = ВидыВычетовНДФЛ.Ссылка
	|ГДЕ
	|	СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтандартныеВычетыНаДетейНДФЛ.КодВычета.Код,
	|	ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтандартныеВычетыФизическихЛицНДФЛ.Регистратор.Организация КАК Организация,
	|	СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыФизическихЛицНДФЛ.Период КАК ДатаНачала,
	|	СтандартныеВычетыФизическихЛицНДФЛ.КодВычетаЛичный КАК КодВычета,
	|	ВидыВычетовНДФЛ.ПолноеНаименование КАК ПолноеНаименование
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ КАК СтандартныеВычетыФизическихЛицНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследнийМесяцВычетов КАК ВТПоследнийМесяцВычетов
	|		ПО СтандартныеВычетыФизическихЛицНДФЛ.Период = ВТПоследнийМесяцВычетов.Период
	|			И СтандартныеВычетыФизическихЛицНДФЛ.ФизическоеЛицо = ВТПоследнийМесяцВычетов.ФизическоеЛицо
	|			И СтандартныеВычетыФизическихЛицНДФЛ.Регистратор.Организация = ВТПоследнийМесяцВычетов.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыВычетовНДФЛ КАК ВидыВычетовНДФЛ
	|		ПО СтандартныеВычетыФизическихЛицНДФЛ.КодВычетаЛичный = ВидыВычетовНДФЛ.Ссылка";
	УстановитьПривилегированныйРежим(Истина);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("РазмерВычета", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10)));
	Таблица.Колонки.Добавить("ОписаниеВычета", Новый ОписаниеТипов("Строка"));
	
	Выборка = РезультатыЗапроса[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.РасходыНаСтроительствоПриобретение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаСтроительствоПриобретение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на жилье';
												|en = 'Accommodation expenses'");
		КонецЕсли;
		
		Если Выборка.ПроцентыПоКредитам > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ПроцентыПоКредитам;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Проценты по кредитам';
												|en = 'Credit interest'");
		КонецЕсли;
		
		Если Выборка.ПроцентыПриПерекредитовании > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ПроцентыПриПерекредитовании;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Проценты при перекредитовании';
												|en = 'Refinancing interest'");
		КонецЕсли;
		
		Если Выборка.РасходыНаСвоеОбучение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаСвоеОбучение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на свое обучение';
												|en = 'Training expenses'");
		КонецЕсли;
		
		Если Выборка.РасходыНаОбучениеДетей > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаОбучениеДетей;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на обучение детей';
												|en = 'Children''s education expenses'");
		КонецЕсли;
		
		Если Выборка.РасходыНаЛечение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаЛечение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на лечение';
												|en = 'Treatment expenses'");
		КонецЕсли;
		
		Если Выборка.СтраховыеВзносыНаМедУслуги > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.СтраховыеВзносыНаМедУслуги;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Страховые взносы на медицинские услуги';
												|en = 'Insurance contributions for medical services'");
		КонецЕсли;
		
		Если Выборка.РасходыНаДорогостоящееЛечение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаДорогостоящееЛечение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на дорогостоящее лечение';
												|en = 'Expensive treatment expenses'");
		КонецЕсли;
		
		Если Выборка.ВзносыНаДобровольноеСтрахованиеЖизни > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ВзносыНаДобровольноеСтрахованиеЖизни;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Взносы на добровольное страхование жизни';
												|en = 'Voluntary life insurance contributions'");
		КонецЕсли;
	КонецЦикла;
		
	Выборка = РезультатыЗапроса[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		РазмерВычета = РазмерыВычетов[Выборка.КодВычета];
		ТекстРубли = НСтр("ru = 'р';
							|en = 'r'");
		ОписаниеВычета = СтрШаблон("%1 %2. %3", РазмерВычета, ТекстРубли, Выборка.ПолноеНаименование);
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.РазмерВычета = РазмерВычета;
		НоваяСтрока.ОписаниеВычета = ОписаниеВычета;
	КонецЦикла;
	
	Выборка = РезультатыЗапроса[5].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		РазмерВычета = РазмерыВычетов[Выборка.КодВычета];
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.РазмерВычета = РазмерВычета;
		НоваяСтрока.ОписаниеВычета = Выборка.ПолноеНаименование;
	КонецЦикла;
	
	Результат = Таблица.Скопировать(, "Организация, ФизическоеЛицо");
	Результат.Свернуть("Организация, ФизическоеЛицо");
	Результат.Колонки.Добавить("ИнформацияОВычетах", Новый ОписаниеТипов("ТаблицаЗначений"));
	Для Каждого СтрокаТаблицы  Из Результат Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("Организация", СтрокаТаблицы.Организация);
		Отбор.Вставить("ФизическоеЛицо", СтрокаТаблицы.ФизическоеЛицо);
		НайденныеСтроки = Таблица.НайтиСтроки(Отбор);
		ТаблицаИнформацияОВычетах = НоваяТаблицаИнформацияОВычетах();
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаИнформацияОВычетах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
		КонецЦикла;
		СтрокаТаблицы.ИнформацияОВычетах = ТаблицаИнформацияОВычетах;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаИнформацияОВычетах()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("РазмерВычета", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("ОписаниеВычета", Новый ОписаниеТипов("Строка"));
	Возврат Результат;
	
КонецФункции

Функция СведенияФизическихЛиц(МассивОтбора, ВерсияDTO)
	
	СоответствиеПолей = ОписаниеПолейФизическихЛиц(ВерсияDTO);
	ВыбираемыеПоля = ВыбираемыеПоля(СоответствиеПолей);
	ВыбираемыеПоля.Добавить("ФИОПолные");
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ВыбираемыеПоля, "ТелефонМобильныйПредставление");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ВыбираемыеПоля, "EMailПредставление");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ВыбираемыеПоля, "ДоступКонтактнойИнформации");
	
	ПоляФайл = ВыбираемыеПоля(ОписаниеПолейФайл(ВерсияDTO));
	Для каждого ИмяПоля Из ПоляФайл Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ВыбираемыеПоля, ИмяПоля);
	КонецЦикла;
	
	ПоляСклонений = ВыбираемыеПоля(ОписаниеПолейСклонений(ВерсияDTO));
	Для Каждого ИмяПоля Из ПоляСклонений Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ВыбираемыеПоля, ИмяПоля);
	КонецЦикла;
	
	КадровыеДанные = СтрСоединить(ВыбираемыеПоля, ", ");
	ТаблицаДанных = КадровыйУчет.КадровыеДанныеФизическихЛиц(Ложь, МассивОтбора, КадровыеДанные, ТекущаяДатаСеанса());
	
	КонтактнаяИнформация = ИнтеграцияУправлениеПерсоналом.КонтактнаяИнформацияФизическихЛиц(МассивОтбора);
	
	ТаблицаДанных.Колонки.Добавить("ТелефонМобильныйПредставление", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("EMailПредставление", 			Новый ОписаниеТипов("Строка"));
	
	Для каждого ИмяПоля Из ПоляФайл Цикл
		ТаблицаДанных.Колонки.Добавить(ИмяПоля, Новый ОписаниеТипов("Строка"));
	КонецЦикла;
	Для Каждого ИмяПоля Из ПоляСклонений Цикл
		ТаблицаДанных.Колонки.Добавить(ИмяПоля, Новый ОписаниеТипов("Строка"));
	КонецЦикла;
	
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицоИдентификатор", Новый ОписаниеТипов("Строка"));
	ПубличныеИдентификаторы = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	ПараметрыСклонения.ЭтоФИО = Истина;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ОшибкиЗаполнения = Новый Соответствие;
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		ДанныеДляПроверки = ИнтеграцияУправлениеПерсоналом.ДанныеДляПроверкиФизическогоЛица(СтрокаТЗ, Неопределено, КонтактнаяИнформация);
		РезультатПроверки = ИнтеграцияУправлениеПерсоналом.РезультатПроверкиДанныхФизическогоЛица(ДанныеДляПроверки, Истина);
		Если ЗначениеЗаполнено(РезультатПроверки[Приложение].БлокирующиеОшибки) Тогда
			ОшибкиОбъекта = ИнтеграцияУправлениеПерсоналом.ОшибкиЗаполненияОбъектаДляПриложения(РезультатПроверки, Приложение);
			ОшибкиЗаполнения.Вставить(СтрокаТЗ.ФизическоеЛицо, ОшибкиОбъекта);
			СтрокиКУдалению.Добавить(СтрокаТЗ);
			Продолжить;
		КонецЕсли;
		
		СтрокаТЗ.ТелефонМобильныйПредставление = КонтактнаяИнформация.МобильныеТелефоны[СтрокаТЗ.ФизическоеЛицо];
		СтрокаТЗ.EMailПредставление = КонтактнаяИнформация.АдресаПочты[СтрокаТЗ.ФизическоеЛицо];
		СтрокаТЗ.МестоРождения = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(СтрокаТЗ.МестоРождения);
		Если ЗначениеЗаполнено(СтрокаТЗ.ТелефонРабочийПредставление) И СтрДлина(СтрокаТЗ.ТелефонРабочийПредставление) > 40 Тогда
			СтрокаТЗ.ТелефонРабочийПредставление = "";
		КонецЕсли;
		ПараметрыСклонения.Пол = ?(СтрокаТЗ.Пол = Перечисления.ПолФизическогоЛица.Мужской,1,2);
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, СклонениеПредставленийОбъектов.ДанныеСклонения(СтрокаТЗ.ФИОПолные, ПараметрыСклонения));
		СтрокаТЗ.ФизическоеЛицоИдентификатор = ПубличныеИдентификаторы[СтрокаТЗ.ФизическоеЛицо];
	КонецЦикла;
	
	ТаблицаДанных.Колонки.ФизическоеЛицо.Имя = "ФизическоеЛицоСсылка";
	ТаблицаДанных.Колонки.ФизическоеЛицоИдентификатор.Имя = "ФизическоеЛицо";
	
	Для каждого СтрокаТЗ Из СтрокиКУдалению Цикл
		ТаблицаДанных.Удалить(СтрокаТЗ);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОшибкиЗаполнения) Тогда
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
		Попытка
			ИнтеграцияУправлениеПерсоналом.ПроверитьЗарегистрироватьБлокирующиеОшибкиЗаполнения(Приложение, ОшибкиЗаполнения, ТипОбъекта);
		Исключение
			// Не обрабатываем исключение при неудачной записи результатов проверки.
		КонецПопытки;
	КонецЕсли;
	
	Возврат ТаблицаДанных;

КонецФункции

Функция СсылкаПубличныйИдентификатор(ТаблицаДанных, ИмяКолонки, ТипОбъекта) Экспорт

	Ссылки = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, ИмяКолонки, Истина);
	ОбщегоНазначенияБЗККлиентСервер.УдалитьПустыеЗначенияИзМассива(Ссылки);
	Возврат ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Ссылки, ТипОбъекта);

КонецФункции

#КонецОбласти

#Область ПолучениеДанныхРасчетныхЛистков

Функция ДанныеРасчетныхЛистов(ПараметрыВыгрузки, ВерсияDTO)
	
	Организация 			= ПараметрыВыгрузки.Организация;
	Месяц 					= ПараметрыВыгрузки.Месяц;
	ПерваяПоловинаМесяца 	= ПараметрыВыгрузки.ПерваяПоловинаМесяца;
	СписокФизическихЛиц 	= ПараметрыВыгрузки.СписокФизическихЛиц;
	ПовторнаяПубликация 	= ПараметрыВыгрузки.ПовторнаяПубликация;
	
	Данные = ИнтеграцияУправлениеПерсоналомСлужебный.ДанныеРасчетныхЛистовДляПубликации(ПараметрыВыгрузки);
	Если Не ЗначениеЗаполнено(Данные.ДанныеРасчетныхЛистков) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеРасчетныхЛистов 			= Данные.ДанныеРасчетныхЛистков; 
	ВидыМестВыплатыРегистраторов 	= Данные.ВидыМестВыплатыРегистраторов;
	ОписаниеСоставныхЧастейЗарплаты = Данные.ОписаниеСоставныхЧастейЗарплаты;
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(СписокФизическихЛиц, ТипОбъекта);
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
	Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	ОрганизацииИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Организации, ТипОбъекта);
	
	ДанныеРасчетныхЛистов   = Данные.ДанныеРасчетныхЛистков; 
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
	СотрудникИдентификатор = СсылкаПубличныйИдентификатор(ДанныеРасчетныхЛистов, "Сотрудник", ТипОбъекта);
	
	ОписаниеОбъектаСтрокаРасчетногоЛистка = ОписаниеОбъектаСтрокаРасчетногоЛистка(ВерсияDTO);
	
	ДанныеРасчетныхЛистов.Индексы.Добавить("ФизическоеЛицо");
	Отбор = Новый Структура("ФизическоеЛицо");
	
	СоставныеЧастиЗарплаты = Новый Соответствие;
	ИнформацияОЗарплате = Новый Соответствие;
	Для каждого ФизическоеЛицо Из СписокФизическихЛиц Цикл
		
		Отбор.ФизическоеЛицо = ФизическоеЛицо;
		СтрокиДанных = ДанныеРасчетныхЛистов.НайтиСтроки(Отбор);
		
		Компоненты = Новый Массив;
		Для каждого СтрокаДанных Из СтрокиДанных Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаДанных.Сотрудник)
				Или Не ЗначениеЗаполнено(СтрокаДанных.ВидРасчета) И Не ЗначениеЗаполнено(СтрокаДанных.Группа) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо") Тогда
				Если СтрокаДанных.Сумма < 0 Тогда
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ДолгСотрудникаНаНачало"];
				Иначе
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ДолгОрганизацииНаНачало"];
				КонецЕсли;
			ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
				Если СтрокаДанных.Сумма < 0 Тогда
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ДолгСотрудникаНаКонец"];
				Иначе
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ДолгОрганизацииНаКонец"];
				КонецЕсли;
			ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено") Тогда
				Если ВидыМестВыплатыРегистраторов[СтрокаДанных.РегистраторВыплаты] = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект Тогда
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ВыплатаНаКарту"];
				ИначеЕсли ВидыМестВыплатыРегистраторов[СтрокаДанных.РегистраторВыплаты] = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет Тогда
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ВыплатаНаСчет"];
				Иначе
					СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты["ВыплатаНаличными"];
				КонецЕсли;
			ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено")
				Или СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано")
				Или СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно")
				Или СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы") Тогда
				СоставнаяЧасть = ОписаниеСоставныхЧастейЗарплаты[СтрокаДанных.ВидРасчета];
			Иначе
				Продолжить;
			КонецЕсли;
			
			СоставныеЧастиЗарплаты.Вставить(СоставнаяЧасть.Идентификатор, СоставнаяЧасть);
			
			СтрокаРасчетногоЛистка = ОписаниеОбъекта(ОписаниеОбъектаСтрокаРасчетногоЛистка);
			СтрокаРасчетногоЛистка.Организация 		= ОрганизацииИдентификатор[Организация];
			СтрокаРасчетногоЛистка.Сотрудник 		= СотрудникИдентификатор[СтрокаДанных.Сотрудник];
			СтрокаРасчетногоЛистка.СоставнаяЧасть 	= СоставнаяЧасть.Идентификатор;
			СтрокаРасчетногоЛистка.Сумма 			= ?(СтрокаДанных.Сумма = Неопределено, 0, Число(СтрокаДанных.Сумма));
			СтрокаРасчетногоЛистка.ПредставлениеРабочегоМеста = СтрокаДанных.ПредставлениеРабочегоМеста;
			ОбъектСтрокаРасчетногоЛистка = ОбъектСервисаПоОписанию(СтрокаРасчетногоЛистка, ОписаниеОбъектаСтрокаРасчетногоЛистка);
			
			Компоненты.Добавить(ОбъектСтрокаРасчетногоЛистка);
			
		КонецЦикла;
		
		Если Компоненты.Количество() = 0 И Не ПовторнаяПубликация Тогда
			Продолжить;
		КонецЕсли;
		
		ОбъектИнформацияОЗарплате = ОписаниеИнформацияОЗарплате();
		ОбъектИнформацияОЗарплате.personID 		= ФизическоеЛицоИдентификатор[ФизическоеЛицо];
		ОбъектИнформацияОЗарплате.month 		= Месяц;
		ОбъектИнформацияОЗарплате.isFirstHalf 	= ПерваяПоловинаМесяца;
		ОбъектИнформацияОЗарплате.components 	= Компоненты;
		ИнформацияОЗарплате.Вставить(ФизическоеЛицо, ОбъектИнформацияОЗарплате);
	
	КонецЦикла;
	
	СоставныеЧастиЗарплатыДляПубликации = Новый Массив;
	ОписаниеОбъектаСоставнаяЧастьЗарплаты = ОписаниеОбъектаСоставнаяЧастьЗарплаты(ВерсияDTO);
	Для каждого ЭлементКоллекции Из СоставныеЧастиЗарплаты Цикл
		ОбъектСоставнаяЧастьЗарплаты = ОбъектСервисаПоОписанию(ЭлементКоллекции.Значение, ОписаниеОбъектаСоставнаяЧастьЗарплаты);
		СоставныеЧастиЗарплатыДляПубликации.Добавить(ОбъектСоставнаяЧастьЗарплаты);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("СоставныеЧастиЗарплаты", 	СоставныеЧастиЗарплатыДляПубликации);
	Результат.Вставить("ИнформацияОЗарплате", 		ИнформацияОЗарплате);
	Результат.Вставить("РасчетныеЛистыДокументы", 	Данные.РасчетныеЛистыДокументы);
	Результат.Вставить("ФизическиеЛицаИменаФайлов", Данные.ФизическиеЛицаИменаФайлов);
	Результат.Вставить("ДокументыКЭДО", 			Данные.ДокументыКЭДО);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПорядокОрганизаций() Экспорт
	
	Результат = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организации.Наименование";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Счетчик = 1;
		Пока Выборка.Следующий() Цикл
			Порядок = ДобавитьЛидирующиеНули(Формат(Счетчик, "ЧГ=0"), 3);
			Результат.Вставить(Выборка.Организация, Порядок);
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьЛидирующиеНули(ИсходнаяСтрока, ДлинаСтроки)
	
	ТекстПолный = ИсходнаяСтрока;
	Пока СтрДлина(ТекстПолный) < ДлинаСтроки Цикл
		ТекстПолный = "0" + ТекстПолный;
	КонецЦикла;
	
	Возврат ТекстПолный;
	
КонецФункции

#КонецОбласти

#Область Обмен

Функция РезультатВыполненияОбмена(ВестиПротокол) Экспорт
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ВидСобытия = Перечисления.ВидыСобытийОбменаУправлениеПерсоналом.Обмен;
	СобытиеОбмена = РегистрыСведений.СобытияОбменаУправлениеПерсоналом.СобытиеОбмена(Приложение, ВидСобытия);
	СобытиеОбмена.ДатаНачала = ТекущаяДатаСеанса();
	
	ЗаписатьСобытиеНачалоОбмена();
	
	БылиОшибки = Ложь;
	Попытка
		БылиОшибки = ИнтеграцияУправлениеПерсоналом.ОбработатьБудущиеСобытия(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
	Исключение
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Попытка
		
		НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
		
		Если Не Константы.СервисКабинетСотрудникаВЛокальнойСети.Получить() 
			И Не НастройкиСервиса.АдресПриложенияПоИмениДоступен И ЗначениеЗаполнено(НастройкиСервиса.АдресПриложенияПоИмени) Тогда
			ПроверитьДоступностьАдресаПоИмени(ВестиПротокол);
		КонецЕсли;
		
		ПараметрыОбмена = ПараметрыОбмена(ВестиПротокол);
		
		ПроверитьВерсиюФорматаОбмена(ПараметрыОбмена);
		ИнтеграцияУправлениеПерсоналом.ПроверитьРуководителейОрганизаций(БылиОшибки);
		ПроверитьОбновитьНастройкиФункциональностиСервиса(ПараметрыОбмена);
		ОпубликоватьНастройкиЗаявокНаОтпуск(ПараметрыОбмена);
		ОпубликоватьУдалениеДанных(ПараметрыОбмена, БылиОшибки);
		ОпубликоватьУдалениеДокументов(ПараметрыОбмена, БылиОшибки);
		ЗагрузитьНезагруженныеОбъекты(ПараметрыОбмена, БылиОшибки);
		ЗагрузитьИзмененияИзСервиса(ПараметрыОбмена, БылиОшибки);
		ОпубликоватьИзмененияЗаявок(ПараметрыОбмена, БылиОшибки);
		ОпубликоватьИзмененияДокументов(ПараметрыОбмена, БылиОшибки);
		
		ОпубликоватьИзменения(ПараметрыОбмена, БылиОшибки);
		
		КабинетСотрудника.ВыполнитьОбработчикиОбмена(ПараметрыОбмена);
		
	Исключение
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ЗаписатьСобытиеОкончаниеОбмена(БылиОшибки);
	
	СобытиеОбмена.ДатаОкончания = ТекущаяДатаСеанса();
	СобытиеОбмена.БылиОшибки 	= БылиОшибки;
	РегистрыСведений.СобытияОбменаУправлениеПерсоналом.ЗаписатьСобытиеОбмена(СобытиеОбмена);
	
	Если Не БылиОшибки Тогда
		КадровыйЭДО.ОбновитьСостоянияДокументовКЭДО();
	КонецЕсли;
	
	Возврат БылиОшибки;
	
КонецФункции

Функция РезультатПубликацииПослеПодключения() Экспорт
	
	БылиОшибки = Ложь;
	Попытка
		
		ПараметрыОбмена = ПараметрыОбмена(Ложь);
		ПараметрыОбмена.ОбновлениеПубликации = Истина;
		
		ПроверитьВерсиюФорматаОбмена(ПараметрыОбмена);
		
		ИнтеграцияУправлениеПерсоналом.ПроверитьРуководителейОрганизаций(БылиОшибки);
		ОпубликоватьИзменения(ПараметрыОбмена, БылиОшибки);
		
	Исключение
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат БылиОшибки;

КонецФункции

#Область ПубликацияУдаленияОбъектов

Процедура ОпубликоватьУдалениеДанных(ПараметрыОбмена, БылиОшибки)
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипыОбъектов", ТипыОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыДляУдаления.Ссылка КАК ОбъектДляУдаления,
	|	ТИПЗНАЧЕНИЯ(ОбъектыДляУдаления.Ссылка) КАК ТипДанных,
	|	ОбъектыДляУдаления.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ОбъектыДляУдаления
	|ГДЕ
	|	ОбъектыДляУдаления.ВыгружатьУдаление
	|	И НЕ ОбъектыДляУдаления.ТипОбъекта В (&ТипыОбъектов)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Данные = РезультатЗапроса.Выгрузить();
	ТипыОбъектов = ОбщегоНазначения.ВыгрузитьКолонку(Данные, "ТипОбъекта", Истина);
	
	ТипыДанных = ТипыПубликуемыхДанных();
	РесурсСервисаПоТипуДанных = Новый Соответствие;
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["ФизическиеЛица"], 					РесурсФизическиеЛица());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Организации"], 						РесурсОрганизации());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Должности"], 						РесурсДолжности());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Сотрудники"], 						РесурсСотрудники());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["ШтатноеРасписание"], 				РесурсШтатноеРасписание());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["СтруктураПредприятия"], 				РесурсСтруктураПредприятия());
	
	УдаленныеОбъекты = Новый ТаблицаЗначений;
	УдаленныеОбъекты.Колонки.Добавить("Ссылка");
	УдаленныеОбъекты.Колонки.Добавить("ТипОбъекта");
	Отбор = Новый Структура("ТипОбъекта");
	Для каждого ТипОбъекта Из ТипыОбъектов Цикл
		
		Отбор.ТипОбъекта = ТипОбъекта;
		НайденныеСтроки = Данные.Скопировать(Отбор);
		СсылкаПубличныйИдентификатор = СсылкаПубличныйИдентификатор(НайденныеСтроки, "ОбъектДляУдаления", ТипОбъекта);
		РесурсСервиса = РесурсСервисаПоТипуДанных[НайденныеСтроки[0].ТипДанных];
		Если РесурсСервиса <> Неопределено Тогда
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				Идентификатор = СсылкаПубличныйИдентификатор[СтрокаТЗ.ОбъектДляУдаления];
				Если РезультатУдаленияОбъектаИзСервиса(ПараметрыОбмена, РесурсСервиса, Идентификатор) Тогда
					НоваяСтрока = УдаленныеОбъекты.Добавить();
					НоваяСтрока.Ссылка 		= СтрокаТЗ.ОбъектДляУдаления;
					НоваяСтрока.ТипОбъекта 	= ТипОбъекта;
				Иначе
					БылиОшибки = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = УдаленныеОбъекты;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать(); 
		
		Для каждого СтрокаТЗ Из УдаленныеОбъекты Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

Процедура ОпубликоватьУдалениеДокументов(ПараметрыОбмена, БылиОшибки)
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипыОбъектов", ТипыОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзмененияДляОбмена.Ссылка КАК Ссылка,
	|	ИзмененияДляОбмена.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ИзмененияДляОбмена
	|ГДЕ
	|	ИзмененияДляОбмена.ВыгружатьУдаление
	|	И ИзмененияДляОбмена.ТипОбъекта В(&ТипыОбъектов)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Данные = РезультатЗапроса.Выгрузить();
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО;
	ТаблицаСогласий = Данные.Скопировать(Новый Структура("ТипОбъекта", ТипОбъекта));
	СогласиеИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаСогласий, "Ссылка", ТипОбъекта);
	
	УдаленныеДокументы = Новый ТаблицаЗначений;
	УдаленныеДокументы.Колонки.Добавить("Ссылка");
	УдаленныеДокументы.Колонки.Добавить("ТипОбъекта");
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РесурсСервиса = РесурсСервисаПоТипуОбъекта(Выборка.ТипОбъекта, ПараметрыОбмена);
		Если Не ЗначениеЗаполнено(РесурсСервиса) Тогда
			ЗаполнитьЗначенияСвойств(УдаленныеДокументы.Добавить(), Выборка);
			Продолжить;
		КонецЕсли;
		
		ИдентификаторДокумента = "";
		Если Выборка.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись Тогда
			ИдентификаторДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Ссылка, "ИдентификаторДокумента");
		Иначе
			ИдентификаторДокумента = СогласиеИдентификатор[Выборка.Ссылка];
		КонецЕсли;
		
		Если РезультатУдаленияОбъектаИзСервиса(ПараметрыОбмена, РесурсСервиса, СокрЛП(ИдентификаторДокумента)) Тогда
			ЗаполнитьЗначенияСвойств(УдаленныеДокументы.Добавить(), Выборка);
			Если Выборка.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись Тогда
				КадровыйЭДО.ЗарегистрироватьУдалениеПубликацииДокументаКЭДО(Выборка.Ссылка, ТекущаяДатаСеанса());
			КонецЕсли;
		Иначе
			БылиОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из УдаленныеДокументы Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаТЗ.Ссылка);
			ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", СтрокаТЗ.ТипОбъекта);
			Блокировка.Заблокировать();
			
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка = СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта = СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.Удалить();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			КабинетСотрудника.ЗаписатьОшибкуРегистрацииИзменений(СтрокаТЗ.Ссылка, ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РезультатУдаленияОбъектаИзСервиса(ПараметрыОбмена, Знач РесурсСервиса, ИдентификаторОбъекта)
	
	РесурсСервиса = СтрШаблон("%1/%2", РесурсСервиса , ИдентификаторОбъекта);
	
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "DELETE"));
	Если Ответ = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли Ответ.КодСостояния = 200 Или Ответ.КодСостояния = 204 Или Ответ.КодСостояния = 404 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция РесурсСервисаПоТипуОбъекта(ТипОбъекта, ПараметрыОбмена)

	РесурсСервиса = Неопределено;
	Если Не ЗначениеЗаполнено(ТипОбъекта) Тогда
		РесурсСервиса = РесурсДокументыНаПодпись();
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись Тогда
		РесурсСервиса = РесурсДокументыНаПодпись();
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО Тогда
		РесурсСервиса = РесурсСогласиеНаПрисоединениеККЭДО(ПараметрыОбмена.ВерсияAPI);
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикОтпусковПредприятия Тогда
		РесурсСервиса = РесурсДанныеСбораГрафиковОтпусковПредприятия(ПараметрыОбмена.ВерсияAPI);
	КонецЕсли;
	
	Возврат РесурсСервиса;

КонецФункции

#КонецОбласти

#Область ПовторнаяЗагрузкаОбъектов

Процедура ЗагрузитьНезагруженныеОбъекты(ПараметрыОбмена, БылиОшибки)
	
	Если Не КабинетСотрудника.ПовторнаяЗагрузкаОбъектовДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Идентификатор КАК ИдентификаторОбъекта,
	|	Таблица.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.НезагруженныеОбъектыУправлениеПерсоналом КАК Таблица
	|ГДЕ
	|	Таблица.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТипОбъектаРесурс 		= ТипОбъектаРесурсСервиса(ПараметрыОбмена);
	ТипОбъектаИмяОбъекта 	= ТипОбъектаИмяОбъекта();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РесурсСервиса = ТипОбъектаРесурс[Выборка.ТипОбъекта];
		Если Не ЗначениеЗаполнено(РесурсСервиса) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Выборка.ИдентификаторОбъекта) Тогда
			ЗарегистрироватьНезагруженныйИдентификатор(Выборка.ИдентификаторОбъекта, Выборка.ТипОбъекта, Истина);
			Продолжить;
		КонецЕсли;
		
		Ответ = ЗагрузитьОбъектИзСервиса(ПараметрыОбмена, РесурсСервиса, СокрЛП(Выборка.ИдентификаторОбъекта));
		Если Ответ.БылиОшибки Тогда
			БылиОшибки = Истина;
		ИначеЕсли ЗначениеЗаполнено(Ответ.ПолученныеИзменения) Тогда
			ПолученныеИзменения = Новый Соответствие;
			ПолученныеИзменения.Вставить(ТипОбъектаИмяОбъекта[Выборка.ТипОбъекта], Ответ.ПолученныеИзменения);
			Результат = ОбработатьИзмененияИзСервиса(ПараметрыОбмена, ПолученныеИзменения, Истина);
			Если Не Результат.БылиОшибки Тогда
				ЗарегистрироватьНезагруженныйИдентификатор(Выборка.ИдентификаторОбъекта, Выборка.ТипОбъекта, Истина)
			КонецЕсли;
			БылиОшибки = БылиОшибки Или Результат.БылиОшибки;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ТипОбъектаРесурсСервиса(ПараметрыОбмена)
	
	ПеречислениеМенеджер = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	
	РесурсСтатусов = Новый Соответствие;
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗапросСправки2НДФЛ, РесурсЗапросСправки2НДФЛ(ПараметрыОбмена.ВерсияAPI));
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗапросСправкиСМестаРаботы, РесурсЗапросСправкиСРаботы());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаОстаткиОтпусков, РесурсЗапросСправкиОбОстаткеОтпуска());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаИзменениеЛичныхДанных, РесурсЗапросИзменениеЛичныхДанных(ПараметрыОбмена.ВерсияAPI));
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаНаОтпуск, РесурсЗаявлениеНаОтпуск());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.Отсутствие, РесурсОтсутствие());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаНалоговыйВычет, РесурсЗаявлениеНаНалоговыеВычеты());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаНаКомпенсациюОтпуска, РесурсЗаявлениеНаКомпенсациюОтпуска());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ЗаявкаДСВ, РесурсЗаявлениеНаУдержаниеДСВ());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.РезультатСогласования, РесурсРезультатСогласования());
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ГрафикОтпусковПодразделения, РесурсГрафиковОтпусковПодразделения(ПараметрыОбмена.ВерсияAPI));
	РесурсСтатусов.Вставить(ПеречислениеМенеджер.ПравилоСогласования, РесурсЗапросПравилоСогласования(ПараметрыОбмена.ВерсияAPI));
	
	Возврат РесурсСтатусов;

КонецФункции

Функция ТипОбъектаИмяОбъекта()
	
	ПеречислениеМенеджер = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	
	ТипОбъектаИмяОбъекта = Новый Соответствие;
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗапросСправки2НДФЛ, "forms2NDFLRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаНаОтпуск, "vacationRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаНалоговыйВычет, "taxDeductionRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаИзменениеЛичныхДанных, "personalInformationChangeRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.Отсутствие, "absences");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаОстаткиОтпусков, "vacationBalanceRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗапросСправкиСМестаРаботы, "certificatesFromEmployerRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаНаКомпенсациюОтпуска, "leaveEncashmentRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ЗаявкаДСВ, "voluntaryInsuranceContributionsRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.РезультатСогласования, "agreementResults");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ГрафикОтпусковПодразделения, "divisionVacationsScheduleRequests");
	ТипОбъектаИмяОбъекта.Вставить(ПеречислениеМенеджер.ПравилоСогласования, "agreementRules");
	
	Возврат ТипОбъектаИмяОбъекта;

КонецФункции

Функция ЗагрузитьОбъектИзСервиса(ПараметрыОбмена, РесурсСервиса, ИдентификаторОбъекта)
	
	Результат = Новый Структура("БылиОшибки,ПолученныеИзменения", Ложь);
	РесурсСервиса = СтрЗаменить(РесурсСервиса, "{ID}", ИдентификаторОбъекта);
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	Если Ответ = Неопределено Тогда
		Результат.БылиОшибки = Истина;
	ИначеЕсли Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ИменаСвойствСервисаСоЗначениямиДата = ИменаСвойствСервисаСоЗначениямиДата();
		Результат.ПолученныеИзменения = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСервисаСоЗначениямиДата, ФорматДатыJSON.ISO, "ВосстановлениеJSON", ОбщегоНазначения.ОбщийМодуль("КабинетСотрудникаМенеджерОбмена"),, ИменаСвойствСервисаСоЗначениямиДата);	
	ИначеЕсли Ответ.КодСостояния = 404 Тогда
		// объект не найден
	Иначе
		Результат.БылиОшибки = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗарегистрироватьНезагруженныйИдентификатор(ИдентификаторОбъекта, ТипОбъекта, ЗарегистрироватьУдаление = Ложь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НезагруженныеОбъектыУправлениеПерсоналом");
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", ИдентификаторОбъекта);
		ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъекта);
		ЭлементБлокировки.УстановитьЗначение("Приложение", Приложение);
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений.НезагруженныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Идентификатор 	= ИдентификаторОбъекта;
		МенеджерЗаписи.Приложение 		= Приложение;
		МенеджерЗаписи.ТипОбъекта 		= ТипОбъекта;
		Если ЗарегистрироватьУдаление Тогда
			МенеджерЗаписи.Удалить();
		Иначе
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = НСтр(
		"ru = 'Ошибка регистрации незагруженных объектов %1, %2
		|Описание ошибки:
		|%3';
		|en = 'An error occurred when registering non imported objects %1, %2
		|Error details:
		|%3'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ИдентификаторОбъекта, ТипОбъекта, ОписаниеОшибки());
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзменений

Процедура ЗагрузитьИзмененияИзСервиса(ПараметрыОбмена, БылиОшибки)
	
	Лимит = 100;
	Результат = НовыйРезультатЗагрузкиИзменений();
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	ПервыйЗапрос = Истина;
	Пока ПервыйЗапрос Или Не Результат.БылиОшибки И Результат.КоличествоОбъектов = Лимит Цикл
		
		ПервыйЗапрос = Ложь;
		Результат.КоличествоОбъектов = 0;
		
		ВерсияИзменений = РегистрыСведений.ВерсииИзмененийУправлениеПерсоналом.ВерсияИзменений(Приложение);
		Ответ = ИзмененияИзСервиса(ПараметрыОбмена, ВерсияИзменений, Лимит);
		Если Ответ.БылиОшибки Тогда
			Результат.БылиОшибки = Истина;
		ИначеЕсли ЗначениеЗаполнено(Ответ.ПолученныеИзменения) Тогда
			Результат = ОбработатьИзмененияИзСервиса(ПараметрыОбмена, Ответ.ПолученныеИзменения);
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатОбработки = РезультатОбработкиНеобработанныхРезультатовСогласования();
	Результат.БылиОшибки = Результат.БылиОшибки Или РезультатОбработки.БылиОшибки;
	
	БылиОшибки = БылиОшибки Или Результат.БылиОшибки;
	
КонецПроцедуры

Функция ИзмененияИзСервиса(ПараметрыОбмена, ВерсияИзменений, Лимит)
	
	Результат = Новый Структура("БылиОшибки,ПолученныеИзменения", Ложь);
	
	Версия = Формат(ВерсияИзменений + 1, "ЧН=0; ЧГ=0");
	РесурсСервиса = РесурсПолучениеВсехИзменений();
	РесурсСервиса = СтрШаблон("%1?version=%2&limit=%3", РесурсСервиса, Версия, Лимит);
	
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	Если Ответ = Неопределено Тогда
		Результат.БылиОшибки = Истина;
	ИначеЕсли Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ИменаСвойствСервисаСоЗначениямиДата = ИменаСвойствСервисаСоЗначениямиДата();
		Результат.ПолученныеИзменения = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСервисаСоЗначениямиДата, ФорматДатыJSON.ISO, "ВосстановлениеJSON", ОбщегоНазначения.ОбщийМодуль("КабинетСотрудникаМенеджерОбмена"),, ИменаСвойствСервисаСоЗначениямиДата);	
	ИначеЕсли Ответ.КодСостояния = 204 Тогда
		// нет изменений
	Иначе
		Результат.БылиОшибки = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ОбработатьИзмененияИзСервиса(ПараметрыОбмена, ПолученныеИзменения, ПовторнаяЗагрузка = Ложь)

	РезультатОбработки = НовыйРезультатЗагрузкиИзменений();
	
	ВерсияИзменений = ЗагрузитьЗаявки(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки);
	ВерсияИзменений = ЗагрузитьРезультатыСогласования(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки, ВерсияИзменений);
	ВерсияИзменений = ЗагрузитьПравилаСогласования(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки, ВерсияИзменений);
	ВерсияИзменений = КабинетСотрудникаВнутренний.ЗагрузитьГрафикиОтпусков(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки, ВерсияИзменений);
	
	Если Не ПовторнаяЗагрузка И ВерсияИзменений <> 0 Тогда
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		РегистрыСведений.ВерсииИзмененийУправлениеПерсоналом.ЗаписатьВерсиюИзменений(Приложение, ВерсияИзменений);
	КонецЕсли;
	
	Возврат РезультатОбработки;

КонецФункции

Функция ЗагрузитьЗаявки(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки)

	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	
	ТаблицаЗаявок = Новый ТаблицаЗначений;
	ТаблицаЗаявок.Колонки.Добавить("Объект");
	ТаблицаЗаявок.Колонки.Добавить("ВерсияИзменений", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаЗаявок.Колонки.Добавить("ТипЗаявки", 			Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗаявокКабинетСотрудника"));
	ТаблицаЗаявок.Колонки.Добавить("ПричинаОтсутствия", 	Новый ОписаниеТипов("ПеречислениеСсылка.ПричиныОтсутствийЗаявокКабинетСотрудника"));
	ТаблицаЗаявок.Колонки.Добавить("ИдентификаторЗаявки", 	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ТаблицаЗаявок.Колонки.Добавить("ФизическоеЛицо", 		Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаЗаявок.Колонки.Добавить("ОшибкаКонвертации");
	
	ТипыОбрабатываемыхЗаявок = КабинетСотрудникаВнутренний.ТипыОбрабатываемыхЗаявок();
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["forms2NDFLRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросСправокНДФЛ(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["vacationRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросНаОтпуск(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["taxDeductionRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗаявлениеНаНалоговыеВычеты(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["personalInformationChangeRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросНаИзменениеЛичныхДанных(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["absences"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаУведомлениеОбОтсутствии(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["vacationBalanceRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросСправкиОбОстаткеОтпуска(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["certificatesFromEmployerRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросСправкиСРаботы(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["leaveEncashmentRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросНаКомпенсациюОтпуска(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ) <> Неопределено Тогда
		МассивОбъектов = ПолученныеИзменения["voluntaryInsuranceContributionsRequests"];
		Если ЗначениеЗаполнено(МассивОбъектов) Тогда
			ТаблицаЗаявкиСервиса = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаЗапросНаУдержаниеДСВ(ВерсияDTO));
			ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаЗаявок, ТаблицаЗаявкиСервиса, Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ);
		КонецЕсли;
	КонецЕсли;
		
	СписокФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗаявок, "ФизическоеЛицо", Истина);
	// в таблицу будет добавлена колонка "ИмяТаблицы"
	ПараметрыБизнесПроцессов = БизнесПроцессыЗаявокСотрудников.ПараметрыСозданияБизнесПроцессовПоЗаявкамСервиса(СписокФизическихЛиц, ТаблицаЗаявок);
	
	ТипЗаявкиТипОбъекта = ИнтеграцияКабинетСотрудника.ТипЗаявкиТипОбъекта();

	ТаблицаЗаявок.Сортировать("ВерсияИзменений");
	СвойстваОтветовПоЗаявкам = "ВариантФормированияФайлаОтвета,КоличествоЭкземпляров,ОтправлятьEmail,АдресEmail";
	ВерсияИзменений = 0;
	Для каждого СтрокаТЗ Из ТаблицаЗаявок Цикл
		
		ОписаниеЗаявки = СтрокаТЗ.Объект;
		
		ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗаявки;
		ВерсияИзменений = СтрокаТЗ.ВерсияИзменений;
		
		// проверка повторной регистрации заявки
		РезультатРегистрацииЗаявки = ПараметрыБизнесПроцессов.ЗарегистрированныеЗаявки[ИдентификаторЗаявки];
		Если РезультатРегистрацииЗаявки <> Неопределено Тогда
			// заявка с таким идентификатором уже зарегистрирована
			ЗаявкаЗарегистрирована = Истина;
			Если ТипЗнч(РезультатРегистрацииЗаявки) = Тип("Массив") Тогда
				// есть заявки с дополнительной идентификацией
				Если ЗначениеЗаполнено(ОписаниеЗаявки.ИдентификаторЭлектронногоДокумента)
					И РезультатРегистрацииЗаявки.Найти(ОписаниеЗаявки.ИдентификаторЭлектронногоДокумента) = Неопределено Тогда
					ЗаявкаЗарегистрирована = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если ЗаявкаЗарегистрирована Тогда
				ВерсияИзменений = СтрокаТЗ.ВерсияИзменений;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.ОшибкаКонвертации) Тогда
			РезультатОбработки.БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(СтрокаТЗ.ТипЗаявки, СтрокаТЗ.ОшибкаКонвертации);
			ЗарегистрироватьНезагруженныйИдентификатор(ИдентификаторЗаявки, ТипЗаявкиТипОбъекта[СтрокаТЗ.ТипЗаявки]);
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			ИмяТаблицы = СтрокаТЗ.ИмяТаблицы;
			ОписаниеБизнесПроцесса = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыБизнесПроцессов.ОписанияБизнесПроцессов[ИмяТаблицы]);
			
			ОписаниеБизнесПроцесса.ТипЗаявкиКабинетСотрудника 	= СтрокаТЗ.ТипЗаявки;
			ОписаниеБизнесПроцесса.ИдентификаторЗаявки 			= ИдентификаторЗаявки;
			
			ФайлыЗаявки = Новый Массив;
			Если ОписаниеЗаявки.Свойство("Вложения") И ЗначениеЗаполнено(ОписаниеЗаявки.Вложения) Тогда
				ФайлыЗаявки = ФайлыЗаявки(ПараметрыОбмена, ОписаниеЗаявки.Вложения);
			КонецЕсли;
			ОписаниеБизнесПроцесса.Вложения = ФайлыЗаявки;
			
			ЭлектронныйДокумент = Неопределено;
			Если ОписаниеЗаявки.Свойство("ЭлектронныйДокумент") И ЗначениеЗаполнено(ОписаниеЗаявки.ЭлектронныйДокумент) Тогда
				ЭлектронныйДокумент = ФайлЗаявки(ПараметрыОбмена, ОписаниеЗаявки.ЭлектронныйДокумент);
			КонецЕсли;
			ОписаниеБизнесПроцесса.ЭлектронныйДокумент = ЭлектронныйДокумент;
			
			Если СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ Тогда
				
				ЗаполнитьЗначенияСвойств(ОписаниеБизнесПроцесса, ОписаниеЗаявки, СвойстваОтветовПоЗаявкам);
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				Если ОписаниеЗаявки.Назначение2НДФЛ = "ПодтверждениеДоходов" Тогда
					ОписаниеБизнесПроцесса.НачалоПериода = ОписаниеЗаявки.НачалоПериода;
					ОписаниеБизнесПроцесса.ОкончаниеПериода = ОписаниеЗаявки.ОкончаниеПериода;
					ОписаниеБизнесПроцесса.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно;
				Иначе
					ОписаниеБизнесПроцесса.НалоговыйПериод = ОписаниеЗаявки.НалоговыйПериод;
					ОписаниеБизнесПроцесса.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.ВРазрезеКодовОКАТО;
				КонецЕсли;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				Если ОписаниеЗаявки.ИзменитьФИО Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.Фамилия) Тогда
						ОписаниеБизнесПроцесса.Фамилия = ОписаниеЗаявки.Фамилия;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеЗаявки.Имя) Тогда
						ОписаниеБизнесПроцесса.Имя = ОписаниеЗаявки.Имя;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеЗаявки.Отчество) Тогда
						ОписаниеБизнесПроцесса.Отчество = ОписаниеЗаявки.Отчество;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьДокумент Тогда
					ОписаниеДокумента = ОписаниеЗаявки.ДокументУдостоверяющийЛичность;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументВид) Тогда
						ВидДокумента = ВидыДокументовСервиса(ОписаниеДокумента.ДокументВид); 
						ОписаниеБизнесПроцесса.ДокументВид = ?(ВидДокумента.Ссылка <> Неопределено, ВидДокумента.Ссылка, ВидДокумента.ТекстовоеПредставление);
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументСерия) Тогда
						ОписаниеБизнесПроцесса.ДокументСерия = ОписаниеДокумента.ДокументСерия; 
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументНомер) Тогда
						ОписаниеБизнесПроцесса.ДокументНомер = ОписаниеДокумента.ДокументНомер;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументДатаВыдачи) Тогда
						ОписаниеБизнесПроцесса.ДокументДатаВыдачи = ОписаниеДокумента.ДокументДатаВыдачи;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументКемВыдан) Тогда
						ОписаниеБизнесПроцесса.ДокументКемВыдан = ОписаниеДокумента.ДокументКемВыдан;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеДокумента.ДокументКодПодразделения) Тогда
						ОписаниеБизнесПроцесса.ДокументКодПодразделения = ОписаниеДокумента.ДокументКодПодразделения;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьНомерТелефона Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.ЛичныйНомерТелефона) Тогда
						ОписаниеБизнесПроцесса.НомерМобильногоТелефона = ОписаниеЗаявки.ЛичныйНомерТелефона;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьАдрес Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.АдресРегистрации) Тогда
						ОписаниеБизнесПроцесса.АдресРегистрации = ОписаниеЗаявки.АдресРегистрации;
					КонецЕсли;
					Если ЗначениеЗаполнено(ОписаниеЗаявки.АдресМестаПроживания) Тогда
						ОписаниеБизнесПроцесса.АдресМестаПроживания = ОписаниеЗаявки.АдресМестаПроживания;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьEmail Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.Email) Тогда
						ОписаниеБизнесПроцесса.Email = ОписаниеЗаявки.Email;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьИНН Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.ИНН) Тогда
						ОписаниеБизнесПроцесса.ИНН = ОписаниеЗаявки.ИНН;
					КонецЕсли;
				КонецЕсли;
				
				Если ОписаниеЗаявки.ИзменитьСНИЛС Тогда
					Если ЗначениеЗаполнено(ОписаниеЗаявки.СНИЛС) Тогда
						ОписаниеБизнесПроцесса.СНИЛС = ОписаниеЗаявки.СНИЛС;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы Тогда
				
				ЗаполнитьЗначенияСвойств(ОписаниеБизнесПроцесса, ОписаниеЗаявки, СвойстваОтветовПоЗаявкам);
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				ОписаниеБизнесПроцесса.НазначениеСправки = ОписаниеЗаявки.НазначениеСправкиСРаботыСтрока;
				
				ВидСправки = Неопределено;
				Если ОписаниеЗаявки.Свойство("ВидСправки", ВидСправки) Тогда
					ОписаниеБизнесПроцесса.ВидСправки = ВидСправки;
				КонецЕсли;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.Сотрудник 			 = ОписаниеЗаявки.Сотрудник;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				ОписаниеБизнесПроцесса.ИдентификаторЭлектронногоДокумента = ОписаниеЗаявки.ИдентификаторЭлектронногоДокумента;
				
				ОписаниеБизнесПроцесса.ЗаСвойСчет 			= ОписаниеЗаявки.ЗаСвойСчет;
				ОписаниеБизнесПроцесса.ДатаНачалаОтпуска 	= ОписаниеЗаявки.ДатаНачала;
				ОписаниеБизнесПроцесса.ДатаОкончанияОтпуска = ОписаниеЗаявки.ДатаОкончания;
				
				Если ОписаниеЗаявки.КомпенсироватьОтпуск Тогда
					ОписаниеБизнесПроцесса.КоличествоДнейКомпенсации = ОписаниеЗаявки.КоличествоДнейКомпенсации;
				КонецЕсли;
				
				// ВерсияDTO 1.0 и выше.
				МатериальнаяПомощь = Неопределено;
				Если ОписаниеЗаявки.Свойство("МатериальнаяПомощь", МатериальнаяПомощь) Тогда
					ОписаниеБизнесПроцесса.МатериальнаяПомощь = МатериальнаяПомощь;
				КонецЕсли;
				ЕдиновременнаяВыплата = Неопределено;
				Если ОписаниеЗаявки.Свойство("ЕдиновременнаяВыплата", ЕдиновременнаяВыплата) Тогда
					ОписаниеБизнесПроцесса.ЕдиновременнаяВыплата = ЕдиновременнаяВыплата;
				КонецЕсли;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				ОписаниеБизнесПроцесса.КоличествоДнейКомпенсации = ОписаниеЗаявки.КоличествоДнейКомпенсации;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска Тогда
				
				ЗаполнитьЗначенияСвойств(ОписаниеБизнесПроцесса, ОписаниеЗаявки, СвойстваОтветовПоЗаявкам);
				ОписаниеБизнесПроцесса.ФизическоеЛицо  = ОписаниеЗаявки.ФизическоеЛицо;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				ОписаниеБизнесПроцесса.ЛичныйВычет 			= ОписаниеЗаявки.ЭтоЛичныйВычет;
				ОписаниеБизнесПроцесса.ВычетНаДетей 		= ОписаниеЗаявки.ЭтоВычетНаДетей;
				ОписаниеБизнесПроцесса.ВычетНаНедвижимость 	= ОписаниеЗаявки.ЭтоВычетНаНедвижимость;
				ОписаниеБизнесПроцесса.ВычетНаЛечение 		= ОписаниеЗаявки.ЭтоВычетНаЛечение;
				ОписаниеБизнесПроцесса.ВычетНаОбучение 		= ОписаниеЗаявки.ЭтоВычетНаОбучение;
				
				Если ОписаниеЗаявки.ЭтоВычетНаДетей Тогда
					Для каждого ВычетНаРебенка Из ОписаниеЗаявки.ВычетыНаДетей Цикл
						НовыйВычет = ОписаниеБизнесПроцесса.Вычеты.Добавить();
						НовыйВычет.ДействуетДо = ВычетНаРебенка.ДатаОкончания - 86400;
						НовыйВычет.ВидВычета = ОпределитьВидВычетаНаРебенка(ВычетНаРебенка);
					КонецЦикла;
				КонецЕсли;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				
				ОписаниеБизнесПроцесса.СпособРасчета = ОписаниеЗаявки.СпособРасчетаУдержанияДСВ;
				ОписаниеБизнесПроцесса.РазмерУдержанияПроцент 	= ОписаниеЗаявки.Процент;
				ОписаниеБизнесПроцесса.РазмерУдержанияСумма 	= ОписаниеЗаявки.Сумма;
				
			ИначеЕсли СтрокаТЗ.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия Тогда
				
				ОписаниеБизнесПроцесса.Организация 			 = ОписаниеЗаявки.Организация;
				ОписаниеБизнесПроцесса.ФизическоеЛицо 		 = ОписаниеЗаявки.ФизическоеЛицо;
				ОписаниеБизнесПроцесса.КомментарийСотрудника = ОписаниеЗаявки.Комментарий;
				ОписаниеБизнесПроцесса.ИдентификаторЭлектронногоДокумента = ОписаниеЗаявки.ИдентификаторЭлектронногоДокумента;
				
				Если СтрокаТЗ.ПричинаОтсутствия =  Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск Тогда
					
					ОписаниеБизнесПроцесса.ДатаНачалаОтпуска 	= ОписаниеЗаявки.ДатаНачала;
					ОписаниеБизнесПроцесса.ДатаОкончанияОтпуска = ОписаниеЗаявки.ДатаОкончания;
					ОписаниеБизнесПроцесса.УчебныйОтпуск 		= Истина;
					
				ИначеЕсли СтрокаТЗ.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Болезнь
					Или СтрокаТЗ.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам Тогда
					
					Если СтрокаТЗ.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам Тогда
						ОписаниеБизнесПроцесса.ПоБеременности = Истина;
					КонецЕсли;
					
					ОписаниеБизнесПроцесса.ДатаНачалаОтсутствия 	= ОписаниеЗаявки.ДатаНачала;
					ОписаниеБизнесПроцесса.ДатаОкончанияОтсутствия 	= ОписаниеЗаявки.ДатаОкончания;
					
				Иначе
					
					ОписаниеБизнесПроцесса.ПричинаОтсутствия 		= СтрокаТЗ.ПричинаОтсутствия;
					ОписаниеБизнесПроцесса.ДатаНачалаОтсутствия 	= ОписаниеЗаявки.ДатаНачала;
					ОписаниеБизнесПроцесса.ДатаОкончанияОтсутствия 	= ОписаниеЗаявки.ДатаОкончания;
					ОписаниеБизнесПроцесса.ВесьДень 				= ОписаниеЗаявки.ВесьДень;
					
				КонецЕсли;
				
			КонецЕсли;
			
			БизнесПроцессыЗаявокСотрудников.СоздатьБизнесПроцессПоЗаявке(ИмяТаблицы, ОписаниеБизнесПроцесса, ПараметрыБизнесПроцессов);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			РезультатОбработки.БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(СтрокаТЗ.ТипЗаявки, ОписаниеОшибки());
			ЗарегистрироватьНезагруженныйИдентификатор(ИдентификаторЗаявки, ТипЗаявкиТипОбъекта[СтрокаТЗ.ТипЗаявки]);
			
		КонецПопытки;
		
	КонецЦикла;
	
	РезультатОбработки.КоличествоОбъектов = РезультатОбработки.КоличествоОбъектов + ТаблицаЗаявок.Количество();
	
	Возврат ВерсияИзменений;
	
КонецФункции

Функция ЗагрузитьРезультатыСогласования(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки, ВерсияИзменений)

	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	
	ТаблицаРезультатовСогласований = Новый ТаблицаЗначений;
	ТаблицаРезультатовСогласований.Колонки.Добавить("Объект");
	ТаблицаРезультатовСогласований.Колонки.Добавить("ВерсияИзменений", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаРезультатовСогласований.Колонки.Добавить("ИдентификаторДокумента", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(72)));
	ТаблицаРезультатовСогласований.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ТаблицаРезультатовСогласований.Колонки.Добавить("ОшибкаКонвертации");
	
	МассивОбъектов = ПолученныеИзменения["agreementResults"];
	Если ЗначениеЗаполнено(МассивОбъектов) Тогда
		ТаблицаОбъектов = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектРезультатСогласования(ВерсияDTO));
		ЗаполнитьТаблицуДанныхПоРезультатамСогласования(ТаблицаРезультатовСогласований, ТаблицаОбъектов);
	КонецЕсли;
	
	Если ТаблицаРезультатовСогласований.Количество() = 0 Тогда
		Возврат ВерсияИзменений;
	КонецЕсли;
	
	Идентификаторы = ТаблицаРезультатовСогласований.ВыгрузитьКолонку("ИдентификаторДокумента");
	// получаем таблицу с существующими документами по идентификаторам 
	ДокументыКЭДО = ДокументыКЭДОПоИдентификатору(Идентификаторы);
	ДокументыКЭДОРасчетныеЛистки = ДокументыКЭДОРасчетныеЛисткиДляСогласования(ДокументыКЭДО);
	
	ДокументыОтбор = ДокументыКЭДО.ВыгрузитьКолонку("Ссылка");
	ДокументыКЭДОСогласия = КадровыйЭДО.ДокументыКЭДОСогласиеНаПрисоединениеККЭДО(ДокументыОтбор);
	
	ИдентификаторыДокументовПовторнойПубликации = Новый Массив;
	НеобработанныеОбъекты = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТаблицаРезультатовСогласований Цикл
		
		ВерсияИзменений = ?(ВерсияИзменений < СтрокаТЗ.ВерсияИзменений, СтрокаТЗ.ВерсияИзменений, ВерсияИзменений);
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТЗДокументКЭДО = ДокументыКЭДО.Найти(СтрокаТЗ.ИдентификаторДокумента, "ИдентификаторДокумента");
		Если СтрокаТЗДокументКЭДО = Неопределено Тогда
			ШаблонОписания = НСтр("ru = 'Не найден документ с идентификатором %1.';
									|en = 'A document with the %1 ID is not found.'");
			Описание = СтрШаблон(ШаблонОписания, СтрокаТЗ.ИдентификаторДокумента);
			НеобработанныеОбъекты.Вставить(СтрокаТЗ.Идентификатор, Новый Структура("Объект,Описание",СтрокаТЗ.Объект,Описание));
			Продолжить;
		КонецЕсли;
		
		РезультатСогласования = СтрокаТЗ.Объект;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.ОшибкаКонвертации) Тогда
			РезультатОбработки.БылиОшибки = Истина;
			ТипИзменений = НСтр("ru = 'Результат согласования';
								|en = 'Approval result'");
			ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, СтрокаТЗ.ОшибкаКонвертации);
			ЗарегистрироватьНезагруженныйИдентификатор(РезультатСогласования.ИдентификаторОбъекта, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
			Продолжить;
		КонецЕсли;
		
		Если Не ЭлектронныйДокументСоответствуетОтвету(СтрокаТЗДокументКЭДО, РезультатСогласования) Тогда
			// Электронный документ не соответствует тому, который получен в результате согласования,
			// не обрабатываем.
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			ВыполняетсяБлокировка = Ложь;
			ЗагрузитьРезультатСогласования(ИспользуетсяКадровыйЭДО, РезультатСогласования, СтрокаТЗДокументКЭДО,
				ДокументыКЭДОРасчетныеЛистки, ДокументыКЭДОСогласия, ИдентификаторыДокументовПовторнойПубликации);
				
			ЗафиксироватьТранзакцию();
			
		Исключение
			// не удалось обработать результат согласования
			ОтменитьТранзакцию();
			// Если ошибка блокировки, не регистрируем ошибку обработки,
			// результат согласования будет сохранен как не обработанный.
			Если ВыполняетсяБлокировка Тогда
				НеобработанныеОбъекты.Вставить(СтрокаТЗ.Идентификатор, СтрокаТЗ.Объект);
			Иначе
				РезультатОбработки.БылиОшибки = Истина;
				ТипИзменений = НСтр("ru = 'Результат согласования';
									|en = 'Approval result'");
				ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, ОписаниеОшибки());
				ЗарегистрироватьНезагруженныйИдентификатор(РезультатСогласования.ИдентификаторОбъекта, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
			КонецЕсли;
			
		КонецПопытки;
	
	КонецЦикла;
	
	КабинетСотрудника.РегистрацияПовторнойПубликацииДокументовКЭДО(ИдентификаторыДокументовПовторнойПубликации);
	
	// Сохранение необработанных результатов согласования текущего пакета изменений.
	Для каждого ЭлементКоллекции Из НеобработанныеОбъекты Цикл
		ОписаниеОбъекта = ЭлементКоллекции.Значение;
		Объект 		= ОписаниеОбъекта.Объект;
		Описание 	= ОписаниеОбъекта.Описание;
		МенеджерЗаписи = РегистрыСведений.НеобработанныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Приложение 		= Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		МенеджерЗаписи.ТипОбъекта 		= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования;
		МенеджерЗаписи.Идентификатор 	= ЭлементКоллекции.Ключ;
		МенеджерЗаписи.Объект 			= Новый ХранилищеЗначения(Объект);
		МенеджерЗаписи.Описание 		= Описание;
		МенеджерЗаписи.ДатаРегистрации 	= ТекущаяДатаСеанса();
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
	РезультатОбработки.КоличествоОбъектов = РезультатОбработки.КоличествоОбъектов + ТаблицаРезультатовСогласований.Количество();
	
	Возврат ВерсияИзменений;

КонецФункции

Функция РезультатОбработкиНеобработанныхРезультатовСогласования()

	РезультатОбработки = НовыйРезультатЗагрузкиИзменений();
	
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	
	НеобработанныеРезультаты = НеобработанныеРезультаты();
	Идентификаторы = НеобработанныеРезультаты.ВыгрузитьКолонку("ИдентификаторДокумента");
	// получаем таблицу с существующими документами по идентификаторам 
	ДокументыКЭДО = ДокументыКЭДОПоИдентификатору(Идентификаторы);
	ДокументыКЭДОРасчетныеЛистки = ДокументыКЭДОРасчетныеЛисткиДляСогласования(ДокументыКЭДО);
	
	ДокументыОтбор = ДокументыКЭДО.ВыгрузитьКолонку("Ссылка");
	ДокументыКЭДОСогласия = КадровыйЭДО.ДокументыКЭДОСогласиеНаПрисоединениеККЭДО(ДокументыОтбор);
	
	ИдентификаторыДокументовПовторнойПубликации = Новый Массив;
	ОбработанныеОбъекты = Новый Массив;
	Для каждого СтрокаТЗ Из НеобработанныеРезультаты Цикл
		
		РезультатСогласования = СтрокаТЗ.РезультатСогласования.Получить();
		Если Не ЗначениеЗаполнено(РезультатСогласования) Тогда
			// снимаем с регистрации
			ОбработанныеОбъекты.Добавить(СтрокаТЗ.Идентификатор);
		КонецЕсли;
		
		СтрокаТЗДокументКЭДО = ДокументыКЭДО.Найти(СтрокаТЗ.ИдентификаторДокумента, "ИдентификаторДокумента");
		Если СтрокаТЗДокументКЭДО = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЭлектронныйДокументСоответствуетОтвету(СтрокаТЗДокументКЭДО, РезультатСогласования) Тогда
			// Электронный документ не соответствует тому, который получен в результате согласования,
			// не обрабатываем.
			ОбработанныеОбъекты.Добавить(СтрокаТЗ.Идентификатор);
			Продолжить;
		КонецЕсли;
		
		ОбъектОбработан = Истина;
		
		НачатьТранзакцию();
		Попытка
			
			ВыполняетсяБлокировка = Ложь;
			ЗагрузитьРезультатСогласования(ИспользуетсяКадровыйЭДО, РезультатСогласования, СтрокаТЗДокументКЭДО,
				ДокументыКЭДОРасчетныеЛистки, ДокументыКЭДОСогласия, ИдентификаторыДокументовПовторнойПубликации);
				
			ЗафиксироватьТранзакцию();
			
		Исключение
			// не удалось обработать результат согласования
			ОтменитьТранзакцию();
			Если ВыполняетсяБлокировка Тогда
				ОбъектОбработан = Ложь;
			Иначе	
				РезультатОбработки.БылиОшибки = Истина;
				ТипИзменений = НСтр("ru = 'Результат согласования';
									|en = 'Approval result'");
				ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, ОписаниеОшибки());
				ЗарегистрироватьНезагруженныйИдентификатор(РезультатСогласования.ИдентификаторОбъекта, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
			КонецЕсли;
		КонецПопытки;
		
		Если ОбъектОбработан Тогда
			ОбработанныеОбъекты.Добавить(СтрокаТЗ.Идентификатор)
		КонецЕсли;
	
	КонецЦикла;
	
	КабинетСотрудника.РегистрацияПовторнойПубликацииДокументовКЭДО(ИдентификаторыДокументовПовторнойПубликации);
	Для каждого Идентификатор Из ОбработанныеОбъекты Цикл
		МенеджерЗаписи = РегистрыСведений.НеобработанныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Идентификатор 	= Идентификатор;
		МенеджерЗаписи.ТипОбъекта 		= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования;
		МенеджерЗаписи.Приложение 		= Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
	Возврат РезультатОбработки;

КонецФункции

Процедура ЗагрузитьРезультатСогласования(ИспользуетсяКадровыйЭДО, РезультатСогласования, СтрокаТЗДокументКЭДО, ДокументыКЭДОРасчетныеЛистки, ДокументыКЭДОСогласия, ИдентификаторыДокументовПовторнойПубликации)

	РолиПодписантов = РолиПодписантовРезультатаСогласования(РезультатСогласования);
	Если Перечисления.РолиПодписантовКЭДО.ЭтоПодписьРуководителя(РолиПодписантов) Тогда
		
		// Обработка записи ЭП
		Если ИспользуетсяКадровыйЭДО И ЗначениеЗаполнено(РезультатСогласования.ЭлектроннаяПодпись) Тогда
			СохранитьЭлектроннуюПодписьРезультатаСогласования(СтрокаТЗДокументКЭДО.ЭлектронныйДокумент, РезультатСогласования)
		КонецЕсли;
		
	ИначеЕсли Перечисления.РолиПодписантовКЭДО.ЭтоПодписьСотрудника(РолиПодписантов) Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодписиДокументовКЭДО");
		ЭлементБлокировки.УстановитьЗначение("Объект", СтрокаТЗДокументКЭДО.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", РезультатСогласования.Подписант);
		
		СтрокаТЗРасчетныйЛисток = ДокументыКЭДОРасчетныеЛистки.Найти(СтрокаТЗДокументКЭДО.Ссылка, "ДокументКадровогоЭДО");
		Если ЗначениеЗаполнено(СтрокаТЗРасчетныйЛисток) Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасчетныеЛисткиКабинетСотрудника");
			ЭлементБлокировки.УстановитьЗначение("Организация", 	СтрокаТЗРасчетныйЛисток.Организация);
			ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо",	СтрокаТЗРасчетныйЛисток.ФизическоеЛицо);
			ЭлементБлокировки.УстановитьЗначение("Месяц", 			СтрокаТЗРасчетныйЛисток.Месяц);
			ЭлементБлокировки.УстановитьЗначение("ПерваяПоловинаМесяца", СтрокаТЗРасчетныйЛисток.ПерваяПоловинаМесяца);
		КонецЕсли;
		
		СтрокаТЗСогласие = ДокументыКЭДОСогласия.Найти(СтрокаТЗДокументКЭДО.Ссылка, "ДокументКадровогоЭДО");
		Если ЗначениеЗаполнено(СтрокаТЗСогласие) Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеСогласияНаПрисоединениеККЭДО");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаТЗСогласие.Ссылка);
		КонецЕсли;
	
		ВыполняетсяБлокировка = Истина;
		Блокировка.Заблокировать();
		ВыполняетсяБлокировка = Ложь;
		
		Если ЗначениеЗаполнено(СтрокаТЗРасчетныйЛисток) Тогда
			Запись = РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТЗРасчетныйЛисток, "Организация,ФизическоеЛицо,Месяц,ПерваяПоловинаМесяца");
			Запись.Прочитать();
			Запись.СостояниеПубликации = Перечисления.СостоянияРасчетныхЛистковКабинетСотрудника.СотрудникОзнакомился;
			Запись.Записать();
		КонецЕсли;
		
		// Обработка записи ЭП
		Отпечаток = "";
		Если ИспользуетсяКадровыйЭДО И ЗначениеЗаполнено(РезультатСогласования.ЭлектроннаяПодпись) Тогда
			СохранитьЭлектроннуюПодписьРезультатаСогласования(СтрокаТЗДокументКЭДО.ЭлектронныйДокумент, РезультатСогласования, Отпечаток);
			ОбновитьПредставление = Неопределено;
			Если РезультатСогласования.Свойство("ОбновитьПредставление", ОбновитьПредставление) И ОбновитьПредставление Тогда
				ИдентификаторыДокументовПовторнойПубликации.Добавить(СтрокаТЗДокументКЭДО.ИдентификаторДокумента);
			КонецЕсли;
		КонецЕсли;
		
		Запись = РегистрыСведений.ПодписиДокументовКЭДО.СоздатьМенеджерЗаписи();
		Запись.Объект = СтрокаТЗДокументКЭДО.Ссылка;
		Запись.ФизическоеЛицо = РезультатСогласования.Подписант;
		Запись.Отпечаток = Отпечаток;
		Запись.ДатаПодписи = РезультатСогласования.ДатаПодписи;
		Запись.РезультатСогласования = РезультатСогласования.РезультатСогласования;
		Запись.Записать();
		
		Если ЗначениеЗаполнено(СтрокаТЗСогласие) Тогда
			Состояние = ?(РезультатСогласования.РезультатСогласования = Перечисления.РезультатыСогласованияБЗК.Согласовано,
						Перечисления.СостоянияСогласийНаПрисоединениеККЭДО.Согласие,
						Перечисления.СостоянияСогласийНаПрисоединениеККЭДО.Отказ);
			Подтвержден = ЗначениеЗаполнено(Отпечаток);
			РегистрыСведений.СостояниеСогласияНаПрисоединениеККЭДО.УстановитьСостояние(СтрокаТЗСогласие.Ссылка, Состояние, Подтвержден);
		КонецЕсли;
		
	КонецЕсли;
	
	БизнесПроцессыЗаявокСотрудников.ЗарегистрироватьРезультатСогласованияЗаявки(СтрокаТЗДокументКЭДО.Документ, РезультатСогласования);

КонецПроцедуры

Функция ПубличныйИдентификаторСсылка(ТаблицаДанных, ИмяКолонки, ТипОбъекта) Экспорт
	
	ИдентификаторСсылка = Новый Соответствие;
	Если Не ЗначениеЗаполнено(ТаблицаДанных) Тогда
		Возврат ИдентификаторСсылка;
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, ИмяКолонки, Истина);
	ОбщегоНазначенияБЗККлиентСервер.УдалитьПустыеЗначенияИзМассива(Идентификаторы);
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		Возврат ИдентификаторСсылка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубличныеИдентификаторы.Ссылка КАК Ссылка,
	|	ПубличныеИдентификаторы.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом КАК ПубличныеИдентификаторы
	|ГДЕ
	|	ПубличныеИдентификаторы.ТипОбъекта = &ТипОбъекта
	|	И ПубличныеИдентификаторы.Идентификатор В(&Идентификаторы)";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ИдентификаторСсылка.Вставить(Выборка.Идентификатор, Выборка.Ссылка);
	КонецЦикла;
	
	Если Идентификаторы.Количество() <> ИдентификаторСсылка.Количество() Тогда
		ИдентификаторыБезСсылок = Новый Массив;
		Для каждого Идентификатор Из Идентификаторы Цикл
			Если ИдентификаторСсылка[Идентификатор] = Неопределено Тогда
				ИдентификаторыБезСсылок.Добавить(Идентификатор);
			КонецЕсли;
		КонецЦикла;
		ДополнитьПубличныеИдентификаторы(ИдентификаторыБезСсылок, ИдентификаторСсылка, ТипОбъекта);
	КонецЕсли;
	
	Возврат ИдентификаторСсылка;

КонецФункции

Процедура ДополнитьПубличныеИдентификаторы(ИдентификаторыБезСсылок, ИдентификаторСсылка, ТипОбъекта)

	ТипОбъектаТипЗначения = ИнтеграцияУправлениеПерсоналом.ТипОбъектаТипЗначенияДляПубличныхИдентификаторов();
	
	Тип = ТипОбъектаТипЗначения[ТипОбъекта];
	Если Тип = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверятьОрганизацию = ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц()
		И ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	
	МенеджерОбъекта = ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(Тип);
	ИмяТаблицы = Метаданные.НайтиПоТипу(Тип).ПолноеИмя();
	
	ТаблицаОбъектов = Новый ТаблицаЗначений;
	ТаблицаОбъектов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип)));
	ТаблицаОбъектов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	
	Для каждого Идентификатор Из ИдентификаторыБезСсылок Цикл
		НоваяСтрока = ТаблицаОбъектов.Добавить();
		НоваяСтрока.Ссылка 			= МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор));
		НоваяСтрока.Идентификатор 	= Идентификатор;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОбъектов);
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТТаблицаОбъектов
	|ИЗ
	|	&ТаблицаОбъектов КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	ТаблицаОбъектов.Идентификатор КАК Идентификатор,
	|	&ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	ВТТаблицаОбъектов КАК ТаблицаОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ИмяТаблицы КАК Таблица
	|		ПО ТаблицаОбъектов.Ссылка = Таблица.Ссылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаДляРегистрации = Таблица.СкопироватьКолонки();
	ИдентификаторыОрганизаций = Новый Массив;
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			ИдентификаторСсылка.Вставить(СтрокаТЗ.Идентификатор, СтрокаТЗ.Ссылка);
			ЗаполнитьЗначенияСвойств(ТаблицаДляРегистрации.Добавить(), СтрокаТЗ);
		ИначеЕсли ПроверятьОрганизацию Тогда
			ИдентификаторыОрганизаций.Добавить(СтрокаТЗ.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИдентификаторыОрганизаций) Тогда
		Запрос.УстановитьПараметр("ИдентификаторыОрганизаций", ИдентификаторыОрганизаций);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Идентификаторы.Организация КАК Ссылка,
		|	Идентификаторы.Идентификатор КАК Идентификатор,
		|	&ТипОбъекта КАК ТипОбъекта
		|ИЗ
		|	РегистрСведений.ИдентификаторыОрганизацийКабинетСотрудника КАК Идентификаторы
		|ГДЕ
		|	Идентификаторы.Идентификатор В(&ИдентификаторыОрганизаций)";
		Таблица = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из Таблица Цикл
			Если ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
				ИдентификаторСсылка.Вставить(СтрокаТЗ.Идентификатор, СтрокаТЗ.Ссылка);
				ЗаполнитьЗначенияСвойств(ТаблицаДляРегистрации.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом");
		ЭлементБлокировки.ИсточникДанных = ТаблицаДляРегистрации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаДляРегистрации Цикл
			МенеджерЗаписи = РегистрыСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;

КонецПроцедуры

Процедура ЗаполнитьТаблицуДанныхПоЗаявкам(ТаблицаДанных, ТаблицаЗаявок, ТипЗаявки)

	ЗаполнятьПричинуОтсутствия = (ТаблицаЗаявок.Колонки.Найти("ПричинаОтсутствия") <> Неопределено);
	
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ИдентификаторФизическоеЛицо = ПубличныйИдентификаторСсылка(ТаблицаЗаявок, "ФизическоеЛицо", ТипОбъектаФизическоеЛицо);
	
	ЕстьОрганизация = (ТаблицаЗаявок.Колонки.Найти("Организация") <> Неопределено);
	ИдентификаторОрганизация = Новый Соответствие;
	Если ЕстьОрганизация Тогда
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
		ИдентификаторОрганизация = ПубличныйИдентификаторСсылка(ТаблицаЗаявок, "Организация", ТипОбъекта);
	КонецЕсли;
	
	ЕстьСотрудник = (ТаблицаЗаявок.Колонки.Найти("Сотрудник") <> Неопределено);
	ИдентификаторСотрудник = Новый Соответствие;
	Если ЕстьСотрудник Тогда
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
		ИдентификаторСотрудник = ПубличныйИдентификаторСсылка(ТаблицаЗаявок, "Сотрудник", ТипОбъекта);
	КонецЕсли;
	
	КонвертироватьВидСправки = Ложь;
	ИдентификаторВидСправки = Новый Соответствие;
	Если ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы Тогда
		КонвертироватьВидСправки = (ТаблицаЗаявок.Колонки.Найти("ВидСправки") <> Неопределено);
		Если КонвертироватьВидСправки Тогда
			ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВидПредоставляемойСотрудникамСправки;
			ИдентификаторВидСправки = ПубличныйИдентификаторСсылка(ТаблицаЗаявок, "ВидСправки", ТипОбъекта);
		КонецЕсли;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ТаблицаЗаявок Цикл
		
		Объект = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТЗ);
		НоваяСтрока = ТаблицаДанных.Добавить();
		
		ФизическоеЛицо = ИдентификаторФизическоеЛицо[СтрокаТЗ.ФизическоеЛицо];
		Объект.Вставить("ФизическоеЛицо", ФизическоеЛицо);
		Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			ОшибкиКонвертацииСсылки = Новый Структура();
			ОшибкиКонвертацииСсылки.Вставить("ФизическоеЛицо", 	СтрокаТЗ.ФизическоеЛицо);
			НоваяСтрока.ОшибкаКонвертации = ОшибкиКонвертацииСсылки;
		КонецЕсли;
		
		Если КонвертироватьВидСправки Тогда
			ВидСправки = ИдентификаторВидСправки[Объект.ВидСправки];
			Объект.Вставить("ВидСправки", ВидСправки);
			Если ЗначениеЗаполнено(Объект.ВидСправки) И Не ЗначениеЗаполнено(ВидСправки) Тогда
				Если Не ЗначениеЗаполнено(НоваяСтрока.ОшибкаКонвертации) Тогда
					НоваяСтрока.ОшибкаКонвертации = Новый Структура();
				КонецЕсли;
				НоваяСтрока.ОшибкаКонвертации.Вставить("ВидСправки", Объект.ВидСправки);
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьОрганизация Тогда
			Организация = ИдентификаторОрганизация[Объект.Организация];
			Объект.Вставить("Организация", Организация);
			Если ЗначениеЗаполнено(Объект.Организация) И Не ЗначениеЗаполнено(Организация) Тогда
				Если Не ЗначениеЗаполнено(НоваяСтрока.ОшибкаКонвертации) Тогда
					НоваяСтрока.ОшибкаКонвертации = Новый Структура();
				КонецЕсли;
				НоваяСтрока.ОшибкаКонвертации.Вставить("Организация", Объект.Организация);
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьСотрудник Тогда
			Сотрудник = ИдентификаторСотрудник[Объект.Сотрудник];
			Объект.Вставить("Сотрудник", Сотрудник);
			Если ЗначениеЗаполнено(Объект.Сотрудник) И Не ЗначениеЗаполнено(Сотрудник) Тогда
				Если Не ЗначениеЗаполнено(НоваяСтрока.ОшибкаКонвертации) Тогда
					НоваяСтрока.ОшибкаКонвертации = Новый Структура();
				КонецЕсли;
				НоваяСтрока.ОшибкаКонвертации.Вставить("Сотрудник", Объект.Сотрудник);
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Объект 				= Объект;
		НоваяСтрока.ТипЗаявки 			= ТипЗаявки;
		Если ЗаполнятьПричинуОтсутствия Тогда
			НоваяСтрока.ПричинаОтсутствия = СтрокаТЗ.ПричинаОтсутствия;
		КонецЕсли;
		НоваяСтрока.ВерсияИзменений 	= СтрокаТЗ.Версия;
		НоваяСтрока.ФизическоеЛицо 		= ФизическоеЛицо;
		НоваяСтрока.ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗапроса; 
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьТаблицуДанныхПоРезультатамСогласования(ТаблицаДанных, ТаблицаОбъектов)
	
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ИдентификаторПодписан = ПубличныйИдентификаторСсылка(ТаблицаОбъектов, "Подписант", ТипОбъектаФизическоеЛицо);
	
	Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
		Объект = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТЗ);
		Подписант = ИдентификаторПодписан[СтрокаТЗ.Подписант];
		Объект.Вставить("Подписант", Подписант);
		НоваяСтрока = ТаблицаДанных.Добавить();
		Если ЗначениеЗаполнено(СтрокаТЗ.Подписант) И Не ЗначениеЗаполнено(Подписант) Тогда
			ОшибкиКонвертацииСсылки = Новый Структура();
			ОшибкиКонвертацииСсылки.Вставить("Подписант", 	СтрокаТЗ.Подписант);
			НоваяСтрока.ОшибкаКонвертации = ОшибкиКонвертацииСсылки;
		КонецЕсли;
		НоваяСтрока.Объект 			= Объект;
		НоваяСтрока.ВерсияИзменений = СтрокаТЗ.Версия;
		НоваяСтрока.Идентификатор = СтрокаТЗ.ИдентификаторОбъекта;
		Если ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторЭлектронногоДокумента) Тогда
			НоваяСтрока.ИдентификаторДокумента = СтрокаТЗ.ИдентификаторЭлектронногоДокумента;
		Иначе
			НоваяСтрока.ИдентификаторДокумента = СтрокаТЗ.ИдентификаторДокумента;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ФайлыЗаявки(ПараметрыОбмена, ТаблицаВложений) Экспорт
	
	ФайлыЗаявки = Новый Массив;
	
	ПараметрыЗапроса = ПараметрыЗапроса("", "GET");
	ПараметрыЗапроса.ЗапросФайла = Истина;
	
	ШаблонРесурса = РесурсФайлыПолучение();
	Для Каждого СтрокаТаблицы Из ТаблицаВложений Цикл
		
		РесурсСервиса = СтрЗаменить(ШаблонРесурса,"{ID}",СтрокаТаблицы.ИдентификаторФайла);
		ПараметрыЗапроса.РесурсСервиса = РесурсСервиса;
		Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса);
		Если Ответ = Неопределено Или Не Ответ.КодСостояния = 200 Тогда
			// вызывающий метод обрабатывает исключение
			ВызватьИсключение НСтр("ru = 'Произошла ошибка при загрузке файлов из сервиса. Подробности в журнале регистрации.';
									|en = 'An error occurred while importing files from the service. See event log for details.'");
		КонецЕсли;
		
		ОписаниеФайла = Новый Структура("ИмяФайла,РасширениеФайла,ДвоичныеДанные,ХешСумма");
		ОписаниеФайла.ИмяФайла = СтрЗаменить(СтрокаТаблицы.НаименованиеФайла, "." + СтрокаТаблицы.РасширениеФайла, "");
		ОписаниеФайла.РасширениеФайла = СтрЗаменить(СтрокаТаблицы.РасширениеФайла, ".", "");
		ОписаниеФайла.ХешСумма = СтрокаТаблицы.ХешСумма;
		ОписаниеФайла.ДвоичныеДанные = Ответ.ПолучитьТелоКакДвоичныеДанные();
		
		ФайлыЗаявки.Добавить(ОписаниеФайла);
		
	КонецЦикла;
	
	Возврат ФайлыЗаявки;
	
КонецФункции

Функция ФайлЗаявки(ПараметрыОбмена, ЭлектронныйДокумент) Экспорт
	
	ИсходныйДокумент = ЭлектронныйДокумент.ИсходныйДокумент;
	
	НаименованиеФайла 	= ЗаменитьЗапрещенныеСимволыВИмениФайла(ИсходныйДокумент.НаименованиеФайла);
	РасширениеФайла 	= ИсходныйДокумент.РасширениеФайла;
	ИдентификаторФайла 	= ИсходныйДокумент.ИдентификаторФайла;
	ХешСумма 			= ИсходныйДокумент.ХешСумма;
	
	ШаблонРесурса = РесурсФайлыПолучение();
	РесурсСервиса = СтрЗаменить(ШаблонРесурса,"{ID}",ИдентификаторФайла);
	ПараметрыЗапроса = ПараметрыЗапроса(РесурсСервиса, "GET");
	ПараметрыЗапроса.ЗапросФайла = Истина;
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса);
	Если Ответ = Неопределено Или Не Ответ.КодСостояния = 200 Тогда
		// вызывающий метод обрабатывает исключение
		ВызватьИсключение НСтр("ru = 'Произошла ошибка при загрузке файлов из сервиса. Подробности в журнале регистрации.';
								|en = 'An error occurred while importing files from the service. See event log for details.'");
	КонецЕсли;
	
	ОписаниеФайла = Новый Структура("ИмяФайла,РасширениеФайла,ДвоичныеДанные,ХешСумма,ОригиналВMXL");
	ОписаниеФайла.ИмяФайла 			= НаименованиеФайла;
	ОписаниеФайла.РасширениеФайла 	= РасширениеФайла;
	ОписаниеФайла.ХешСумма 			= ХешСумма;
	ОписаниеФайла.ДвоичныеДанные = Ответ.ПолучитьТелоКакДвоичныеДанные();
	
	Если ЭлектронныйДокумент.Свойство("ОригиналВMXL")
		И ЭлектронныйДокумент.ОригиналВMXL <> Неопределено Тогда
		
		ИдентификаторОригиналаФайла = ЭлектронныйДокумент.ОригиналВMXL.ИдентификаторФайла;
		Если ЗначениеЗаполнено(ИдентификаторОригиналаФайла) Тогда
			ШаблонРесурса = РесурсФайлыПолучение();
			РесурсСервиса = СтрЗаменить(ШаблонРесурса,"{ID}",ИдентификаторОригиналаФайла);
			ПараметрыЗапроса = ПараметрыЗапроса(РесурсСервиса, "GET");
			ПараметрыЗапроса.ЗапросФайла = Истина;
			Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса);
			Если Ответ = Неопределено Или Не Ответ.КодСостояния = 200 Тогда
				// вызывающий метод обрабатывает исключение
				ВызватьИсключение НСтр("ru = 'Произошла ошибка при загрузке файлов из сервиса. Подробности в журнале регистрации.';
										|en = 'An error occurred while importing files from the service. See event log for details.'");
			КонецЕсли;
		КонецЕсли;
		ОписаниеФайла.ОригиналВMXL = Ответ.ПолучитьТелоКакДвоичныеДанные();
		
	КонецЕсли;
	
	Возврат ОписаниеФайла;
	
КонецФункции

Функция ЗаменитьЗапрещенныеСимволыВИмениФайла(ИсходнаяСтрока, ЗаменятьНа = "_")
	
	ЗапрещенныеСимволы = СтрРазделить("@,\,/,:,*,?,"",<,>,|,+", ",");
	
	СтрокаПослеЗамены = СокрЛП(ИсходнаяСтрока);
	
	Для Каждого ЗапрещенныйСимвол Из ЗапрещенныеСимволы Цикл
		СтрокаПослеЗамены = СтрЗаменить(СтрокаПослеЗамены, ЗапрещенныйСимвол, ЗаменятьНа);
	КонецЦикла;
	
	Если СтрЗаканчиваетсяНа(СтрокаПослеЗамены, ".") Тогда
		СтрокаПослеЗамены = Лев(СтрокаПослеЗамены, СтрДлина(СтрокаПослеЗамены) - 1) + ЗаменятьНа;
	КонецЕсли;
	
	Возврат СтрокаПослеЗамены;
	
КонецФункции

Функция НовыйРезультатЗагрузкиИзменений()

	Возврат Новый Структура("БылиОшибки,КоличествоОбъектов", Ложь, 0);

КонецФункции

Функция ОпределитьВидВычетаНаРебенка(ВычетНаРебенка)
	
	ТекстВычета = НСтр("ru = 'Вычет на';
						|en = 'Deduction for'");
	
	СтаршинствоРебенка = "первого";
	Если ВычетНаРебенка.СтаршинствоРебенка = 1 Тогда
		СтаршинствоРебенка = НСтр("ru = 'первого';
									|en = 'the first'");
	ИначеЕсли ВычетНаРебенка.СтаршинствоРебенка = 2 Тогда
		СтаршинствоРебенка =  НСтр("ru = 'второго';
									|en = 'the second'");
	ИначеЕсли ВычетНаРебенка.СтаршинствоРебенка = 3 Тогда
		СтаршинствоРебенка =  НСтр("ru = 'третьего или последующего';
									|en = 'the third and next'");
	КонецЕсли;
			
	Возврат (ТекстВычета + " " + СтаршинствоРебенка + " " + НСтр("ru = 'ребенка';
																|en = 'child'") 
						 + ?(ВычетНаРебенка.РебенокИнвалид, НСтр("ru = '-инвалида';
																|en = 'disabled'"), "")
						 + " " + ?(ВычетНаРебенка.ЗаявительОпекун, НСтр("ru = 'опекуну';
																		|en = 'to guardian'"), НСтр("ru = 'родителю';
																								|en = 'to parent'")) 
						 + ?(ВычетНаРебенка.РодительОдиночка, " " + НСтр("ru = 'в двойном размере';
																		|en = 'in double amount'"), ""));
КонецФункции

Функция ВосстановлениеJSON(Знач Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	Исключение
		Результат = Дата(1,1,1);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ИменаСвойствСервисаСоЗначениямиДата()
	
	Результат = Новый Массив;
	Результат.Добавить("dateCreated");
	Результат.Добавить("issueDate");
	Результат.Добавить("startDate");
	Результат.Добавить("endDate");
	Результат.Добавить("applyMonth");
	Результат.Добавить("incomePeriodStartDate");
	Результат.Добавить("incomePeriodEndDate");
	Результат.Добавить("time");
	Возврат Результат;
	
КонецФункции

Функция ВидыДокументовСервиса(ВидДокумента)
	
	ВидыДокументовФизическихЛиц = Справочники.ВидыДокументовФизическихЛиц;

	Результат = Новый Структура;
	
	Если ВидДокумента = "passport" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.ПаспортРФ);
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Паспорт гражданина РФ';
															|en = 'RF citizen passport'"));
	ИначеЕсли ВидДокумента = "foreignPassport" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.НайтиПоКоду(10));
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Иностранный паспорт';
															|en = 'Foreign passport'"));
	ИначеЕсли ВидДокумента = "residenceCard" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.НайтиПоКоду(12));
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Вид на жительство';
															|en = 'Lawful permanent residence'"));
	ИначеЕсли ВидДокумента = "identityCard" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.НайтиПоКоду(14));
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Временное удостоверение личности гражданина РФ';
															|en = 'Temporary identity card of the Russian Federation citizen'"));
	ИначеЕсли ВидДокумента = "militaryIdentityCard" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.УдостоверениеОфицера);
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Удостоверение личности офицера';
															|en = 'Officer identity card'"));
	ИначеЕсли ВидДокумента = "residencePermission" Тогда
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.НайтиПоКоду(15));
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Разрешение на временное проживание в Российской Федерации';
															|en = 'Permit for temporary residence in the Russian Federation'"));
	Иначе
		Результат.Вставить("Ссылка", ВидыДокументовФизическихЛиц.НайтиПоКоду(91));
		Результат.Вставить("ТекстовоеПредставление", НСтр("ru = 'Иной документ';
															|en = 'Other document'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДокументыКЭДОРасчетныеЛисткиДляСогласования(ТаблицаДокументыКЭДО)

	ДокументыКЭДО = ТаблицаДокументыКЭДО.ВыгрузитьКолонку("Ссылка");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыКЭДО", ДокументыКЭДО);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетныеЛисткиКабинетСотрудника.Организация КАК Организация,
	|	РасчетныеЛисткиКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетныеЛисткиКабинетСотрудника.Месяц КАК Месяц,
	|	РасчетныеЛисткиКабинетСотрудника.ПерваяПоловинаМесяца КАК ПерваяПоловинаМесяца,
	|	РасчетныеЛисткиКабинетСотрудника.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|ИЗ
	|	РегистрСведений.РасчетныеЛисткиКабинетСотрудника КАК РасчетныеЛисткиКабинетСотрудника
	|ГДЕ
	|	РасчетныеЛисткиКабинетСотрудника.ДокументКадровогоЭДО В(&ДокументыКЭДО)";
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ЭлектронныйДокументСоответствуетОтвету(ДанныеДокументаКЭДО, РезультатСогласования)
	
	ЭлектронныйДокумент = ДанныеДокументаКЭДО.ЭлектронныйДокумент;
	ДокументКЭДО = ДанныеДокументаКЭДО.Ссылка;
	
	Если Не ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
		ТекстСообщения = НСтр("ru = 'Нет электронного документа.';
								|en = 'There is no electronic document.'");
		ЗаписатьПредупреждениеПроверкиВерсийФайлов(ДокументКЭДО, ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатСогласования.ВерсияДокумента) Тогда
		Возврат Истина;
	КонецЕсли;
		
	ДвоичныеДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(ЭлектронныйДокумент, Ложь);
	Если ДвоичныеДанныеФайла = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка получения данных электронного документа.';
								|en = 'An error occurred when receiving the electronic document data.'");
		ЗаписатьПредупреждениеПроверкиВерсийФайлов(ДокументКЭДО, ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанныеФайла);
	ВерсииСовпадают = (ВерсияФайла = ВРег(РезультатСогласования.ВерсияДокумента));
	Если Не ВерсииСовпадают Тогда
		ТекстСообщения = НСтр("ru = 'Версии файлов различаются.';
								|en = 'The file versions are different.'");
		ЗаписатьПредупреждениеПроверкиВерсийФайлов(ДокументКЭДО, ТекстСообщения);
	КонецЕсли;
	
	Возврат ВерсииСовпадают;

КонецФункции

Функция НеобработанныеРезультаты()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НеобработанныеРезультаты.Идентификатор КАК Идентификатор,
	|	НеобработанныеРезультаты.Объект КАК РезультатСогласования
	|ИЗ
	|	РегистрСведений.НеобработанныеОбъектыУправлениеПерсоналом КАК НеобработанныеРезультаты
	|ГДЕ
	|	НеобработанныеРезультаты.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника)
	|	И НеобработанныеРезультаты.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования)";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Таблица.Колонки.Добавить("ИдентификаторДокумента");
	Для каждого СтрокаТЗ Из Таблица Цикл
		
		Объект = СтрокаТЗ.РезультатСогласования.Получить();
		Если Не ЗначениеЗаполнено(Объект) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТЗ.ИдентификаторДокумента = Объект.ИдентификаторДокумента;
		Если ЗначениеЗаполнено(Объект.ИдентификаторЭлектронногоДокумента) Тогда
			СтрокаТЗ.ИдентификаторДокумента = Объект.ИдентификаторЭлектронногоДокумента;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Таблица;

КонецФункции

Функция ДокументыКЭДОПоИдентификатору(Идентификаторы)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторыДокументов", Идентификаторы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументКадровогоЭДО.Ссылка КАК Ссылка,
	|	ДокументКадровогоЭДО.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ДокументКадровогоЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ДокументКадровогоЭДО.ОснованиеДокумента КАК Документ
	|ИЗ
	|	Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|ГДЕ
	|	ДокументКадровогоЭДО.ИдентификаторДокумента В(&ИдентификаторыДокументов)
	|	И НЕ ДокументКадровогоЭДО.ПометкаУдаления";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ЗагрузитьПравилаСогласования(ПараметрыОбмена, ПолученныеИзменения, РезультатОбработки, ВерсияИзменений)

	ТаблицаПравилСогласования = Новый ТаблицаЗначений;
	ТаблицаПравилСогласования.Колонки.Добавить("Объект");
	ТаблицаПравилСогласования.Колонки.Добавить("ВерсияИзменений", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ТаблицаПравилСогласования.Колонки.Добавить("ИдентификаторПравила", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ТаблицаПравилСогласования.Колонки.Добавить("ИдентификаторПодразделения", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ТаблицаПравилСогласования.Колонки.Добавить("ОшибкаКонвертации");
	
	МассивОбъектов = ПолученныеИзменения["agreementRules"];
	Если ЗначениеЗаполнено(МассивОбъектов) Тогда
		ТаблицаОбъектов = ТаблицаИзМассиваОбъектов(МассивОбъектов, ОписаниеОбъектаПравилоСогласования(ПараметрыОбмена.ВерсияDTO));
		ЗаполнитьТаблицуДанныхПоПравиламСогласования(ТаблицаПравилСогласования, ТаблицаОбъектов);
	КонецЕсли;
	
	Если ТаблицаПравилСогласования.Количество() = 0 Тогда
		Возврат ВерсияИзменений;
	КонецЕсли;
	
	Идентификаторы = ТаблицаПравилСогласования.ВыгрузитьКолонку("ИдентификаторПравила");
	ИдентификаторПравилоСогласования = ПравилаСогласованияПоИдентификаторам(Идентификаторы); 
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	ИдентификаторПодразделение = ПубличныйИдентификаторСсылка(ТаблицаПравилСогласования, "ИдентификаторПодразделения", ТипОбъекта);
	
	Для Каждого СтрокаТЗ Из ТаблицаПравилСогласования Цикл
		
		ВерсияИзменений = ?(ВерсияИзменений < СтрокаТЗ.ВерсияИзменений, СтрокаТЗ.ВерсияИзменений, ВерсияИзменений);
		ОписаниеПравила = СтрокаТЗ.Объект;
		ПравилоСогласования = ИдентификаторПравилоСогласования[СтрокаТЗ.ИдентификаторПравила];
		Если ПравилоСогласования = Неопределено И ОписаниеПравила.ПометкаУдаления = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.ОшибкаКонвертации) Тогда
			РезультатОбработки.БылиОшибки = Истина;
			ТипИзменений = НСтр("ru = 'Правило согласования';
								|en = 'Approval rule'");
			ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, СтрокаТЗ.ОшибкаКонвертации);
			ЗарегистрироватьНезагруженныйИдентификатор(СтрокаТЗ.ИдентификаторПравила, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПравилоСогласования);
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			ВыполняетсяБлокировка = Ложь;
			
			Если ПравилоСогласования = Неопределено Тогда
				ПравилоОбъект = Справочники.ПравилаСогласованияЗаявокКабинетСотрудника.СоздатьЭлемент();
			Иначе
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.ПравилаСогласованияЗаявокКабинетСотрудника");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ПравилоСогласования);
				
				ВыполняетсяБлокировка = Истина;
				Блокировка.Заблокировать();
				ВыполняетсяБлокировка = Ложь;
				
				ПравилоОбъект = ПравилоСогласования.ПолучитьОбъект();
				
			КонецЕсли;
			
			Если ОписаниеПравила.ПометкаУдаления = Истина Тогда
				ПравилоОбъект.ПометкаУдаления = Истина;
			Иначе
				ПравилоОбъект.ТипЗаявки = ОписаниеПравила.ТипЗаявки;
				Если Не ПустаяСтрока(ОписаниеПравила.ИдентификаторПодразделения) Тогда
					ПравилоОбъект.Подразделение = ИдентификаторПодразделение[ОписаниеПравила.ИдентификаторПодразделения];
				КонецЕсли;
				ПравилоОбъект.ФизическоеЛицо = ОписаниеПравила.ФизическоеЛицо;
				ПравилоОбъект.ИдентификаторПравила = ОписаниеПравила.ИдентификаторПравила;
				ПравилоОбъект.ШагиПравилаСогласования.Очистить();
				Для Каждого ШагПравила Из ОписаниеПравила.ШагиПравилаСогласования Цикл
					НоваяСтрока = ПравилоОбъект.ШагиПравилаСогласования.Добавить();
					НоваяСтрока.ШагСогласования 	= ШагПравила.НомерШага;
					НоваяСтрока.ТипШага 			= ШагПравила.ТипШага;
					НоваяСтрока.РольПодписанта 	= ШагПравила.РольПодписанта;
					НоваяСтрока.Подписант 		= ШагПравила.ФизическоеЛицо;
					НоваяСтрока.ТребуемаяПодпись 	= ШагПравила.ТребуемаяПодпись;
				КонецЦикла;
			КонецЕсли;
			ПравилоОбъект.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			// не удалось обработать правило согласования
			ОтменитьТранзакцию();
			Если Не ВыполняетсяБлокировка Тогда
				РезультатОбработки.БылиОшибки = Истина;
				ТипИзменений = НСтр("ru = 'Правило согласования';
									|en = 'Approval rule'");
				ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, ОписаниеОшибки());
			КонецЕсли;
			ЗарегистрироватьНезагруженныйИдентификатор(СтрокаТЗ.ИдентификаторПравила, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПравилоСогласования);
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ВерсияИзменений;
	
КонецФункции

Процедура ЗаполнитьТаблицуДанныхПоПравиламСогласования(ТаблицаДанных, ТаблицаОбъектов)
	
	ТаблицаФизическихЛиц = Новый ТаблицаЗначений;
	ТаблицаФизическихЛиц.Колонки.Добавить("ФизическоеЛицо");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОбъектов, ТаблицаФизическихЛиц);
	Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
		Если ЗначениеЗаполнено(СтрокаТЗ.ШагиПравилаСогласования) Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СтрокаТЗ.ШагиПравилаСогласования, ТаблицаФизическихЛиц);
		КонецЕсли;
	КонецЦикла;
	
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ИдентификаторФизическоеЛицо = ПубличныйИдентификаторСсылка(ТаблицаФизическихЛиц, "ФизическоеЛицо", ТипОбъектаФизическоеЛицо);
	
	Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		
		Объект = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТЗ);
		Если ЗначениеЗаполнено(СтрокаТЗ.ФизическоеЛицо) Тогда
			ФизическоеЛицо = ИдентификаторФизическоеЛицо[СтрокаТЗ.ФизическоеЛицо];
			Объект.Вставить("ФизическоеЛицо", ФизическоеЛицо);
			Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
				ОшибкиКонвертацииСсылки = Новый Структура();
				ОшибкиКонвертацииСсылки.Вставить("ФизическоеЛицо", 	СтрокаТЗ.ФизическоеЛицо);
				НоваяСтрока.ОшибкаКонвертации = ОшибкиКонвертацииСсылки;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ШагиПравилаСогласования) Тогда
			Объект.ШагиПравилаСогласования.Колонки.ФизическоеЛицо.Имя = "ФизическоеЛицоИдентификатор";
			Объект.ШагиПравилаСогласования.Колонки.Добавить("ФизическоеЛицо");
		КонецЕсли;
		
		Для каждого ШагПравила Из Объект.ШагиПравилаСогласования Цикл
			Если ЗначениеЗаполнено(ШагПравила.ФизическоеЛицоИдентификатор) Тогда
				ФизическоеЛицо = ИдентификаторФизическоеЛицо[ШагПравила.ФизическоеЛицоИдентификатор];
				ШагПравила.ФизическоеЛицо = ФизическоеЛицо;
				Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
					Если Не ЗначениеЗаполнено(НоваяСтрока.ОшибкаКонвертации) Тогда
						НоваяСтрока.ОшибкаКонвертации = Новый Структура();
					КонецЕсли;
					НоваяСтрока.ОшибкаКонвертации.Вставить("ШагПравила", ШагПравила.ФизическоеЛицоИдентификатор);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		НоваяСтрока.Объект = Объект;
		НоваяСтрока.ВерсияИзменений = СтрокаТЗ.Версия;
		НоваяСтрока.ИдентификаторПравила 		= СтрокаТЗ.ИдентификаторПравила;
		НоваяСтрока.ИдентификаторПодразделения 	= СтрокаТЗ.ИдентификаторПодразделения;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПравилаСогласованияПоИдентификаторам(Идентификаторы)

	ЗагруженныеПравила = Новый Соответствие;
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		Возврат ЗагруженныеПравила;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаСогласования.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ПравилаСогласования.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПравилаСогласованияЗаявокКабинетСотрудника КАК ПравилаСогласования
	|ГДЕ
	|	ПравилаСогласования.ИдентификаторПравила В(&Идентификаторы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗагруженныеПравила.Вставить(Выборка.ИдентификаторПравила, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ЗагруженныеПравила;

КонецФункции

#КонецОбласти

#Область ПубликацияИзмененийОбъектов

#Область ФункцииПреобразованияПеречисленийВЗначенияСервиса

Функция ПолФизическогоЛица(Пол)
	
	Результат = Неопределено;
	
	Если Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской") Тогда
		Результат = "male";
	ИначеЕсли Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский") Тогда
		Результат = "female";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидЗанятостиСотрудника(ВидЗанятости)
	
	Результат = Неопределено;
	
	Если ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы Тогда
		Результат = "mainWork";
	ИначеЕсли ВидЗанятости = Перечисления.ВидыЗанятости.Совместительство Тогда
		Результат = "extraWorkExternal";
	ИначеЕсли ВидЗанятости = Перечисления.ВидыЗанятости.ВнутреннееСовместительство Тогда
		Результат = "extraWorkInternal";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидДокументаФизическогоЛица(ВидДокумента)
	
	Результат = "other";
	
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		КодМВД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "КодМВД");
		Если КодМВД = "21" Тогда
			Результат = "passport";
		ИначеЕсли КодМВД = "10" Тогда
			Результат = "foreignPassport";
		ИначеЕсли КодМВД = "12" Тогда
			Результат = "residenceCard";
		ИначеЕсли КодМВД = "14" Тогда
			Результат = "identityCard";
		ИначеЕсли КодМВД = "04" Тогда
			Результат = "militaryIdentityCard";
		ИначеЕсли КодМВД = "15" Тогда
			Результат = "residencePermission";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СпособРасчетаУдержания(СпособРасчета)

	Результат = Неопределено;
	
	Если СпособРасчета = Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.Процентом Тогда
		Результат = "percentage";
	ИначеЕсли СпособРасчета = Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.ФиксированнойСуммой Тогда
		Результат = "fixedAmount";
	ИначеЕсли СпособРасчета = Перечисления.СпособыРасчетаУдержанийКабинетСотрудника.ПроцентомНеБолееСуммы Тогда
		Результат = "percentageUpToAmount";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция УровеньДоступаКИнформацииОСотрудниках(УровеньДоступа)

	Результат = "grantEveryone";
	Если УровеньДоступа = Перечисления.УровниДоступаКИнформацииОСотрудниках.НетОграничений Тогда
		Результат = "grantEveryone";
	ИначеЕсли УровеньДоступа = Перечисления.УровниДоступаКИнформацииОСотрудниках.Руководители Тогда
		Результат = "grantDivisionHeads";
	ИначеЕсли УровеньДоступа = Перечисления.УровниДоступаКИнформацииОСотрудниках.СотрудникиПодразделения Тогда
		Результат = "grantPeers";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатСогласования(РезультатСогласования)
	
	Результат = Неопределено;
	Если РезультатСогласования = Перечисления.РезультатыСогласованияБЗК.Согласовано Тогда
		Результат = "agreed";
	ИначеЕсли РезультатСогласования = Перечисления.РезультатыСогласованияБЗК.Отклонено Тогда
		Результат = "denied";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РольПодписанта(Роль)
	
	Результат = Неопределено;
	Если Роль = Перечисления.РолиПодписантовКЭДО.Сотрудник Тогда
		Результат = "employee";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.НепосредственныйРуководитель Тогда
		Результат = "directManager";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.Исполнитель Тогда
		Результат = "executor";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.Организация Тогда
		Результат = "employer";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.ВышестоящийРуководитель Тогда
		Результат = "superiorManager";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.НеформальныйРуководитель Тогда
		Результат = "teamLeader";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.РуководителиПоВсемМестамРаботы Тогда
		Результат = "allWorkspaceManager";
	ИначеЕсли Роль = Перечисления.РолиПодписантовКЭДО.РуководительПоДополнительнымМестамРаботы Тогда
		Результат = "additionalWorkspaceManager";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПубликацияОбъектовПоТипам

Функция РезультатВыгрузкиФизическихЛиц(ПараметрыОбмена, Список)
	
	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	Результат = НовыйРезультатВыгрузки();
	ФизическиеЛицаДляВыгрузки = ОграниченныйСписокФизическихЛиц(Список);
	ДанныеФизическихЛиц = ДанныеФизическихЛиц(ПараметрыОбмена, ФизическиеЛицаДляВыгрузки);
	
	ТаблицаДанных = ДанныеФизическихЛиц.ТаблицаДанных;
	
	Если ЗначениеЗаполнено(ДанныеФизическихЛиц.Фотографии) Тогда
		Результат.БылиОшибки = ВыгрузитьФотографииВСервис(ПараметрыОбмена, ТаблицаДанных, ДанныеФизическихЛиц.Фотографии);
	КонецЕсли;
	
	СоответствиеПолей = ОписаниеПолейФизическихЛиц(ВерсияDTO);
	Данные = ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	РазмерПакета = РазмерПакета(ПараметрыОбмена, ТипОбъекта);

	РесурсСервиса = РесурсФизическиеЛица();
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
	Результат.БылиОшибки = Результат.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиДоступныхФункцииФизическихЛиц(ПараметрыОбмена, ФизическиеЛица)
	
	Результат = НовыйРезультатВыгрузки();
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетСогласийНаПрисоединениеККЭДО") Или Не КабинетСотрудника.ИспользоватьФормат503() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	
	ФизическиеЛицаДляОбработки = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъектаФизическоеЛицо, ФизическиеЛица);
	Если Не ЗначениеЗаполнено(ФизическиеЛицаДляОбработки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПубличныеИдентификаторы = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(ФизическиеЛицаДляОбработки, ТипОбъектаФизическоеЛицо);

	ФизическиеЛицаПрисоединенныеККЭДО = КадровыйЭДО.ФизическиеЛицаПрисоединенныеККЭДО(ФизическиеЛицаДляОбработки);
	ИспользуетКЭДО = Новый Соответствие;
	Для каждого СтрокаТЗ Из ФизическиеЛицаПрисоединенныеККЭДО Цикл
		ИспользуетКЭДО.Вставить(СтрокаТЗ.ФизическоеЛицо, СтрокаТЗ.ИспользуетКЭДО);
	КонецЦикла;
	
	ОписаниеОбъектаДоступныеФункцииФизическихЛиц = ОписаниеОбъектаДоступныеФункцииФизическихЛиц(ПараметрыОбмена.ВерсияDTO);
	Данные = Новый Массив;
	Для каждого ФизическоеЛицо Из ФизическиеЛицаДляОбработки Цикл
		ОписаниеДоступныеФункции =  ОписаниеОбъекта(ОписаниеОбъектаДоступныеФункцииФизическихЛиц);
		ОписаниеДоступныеФункции.ФизическоеЛицо = ПубличныеИдентификаторы[ФизическоеЛицо];
		ОписаниеДоступныеФункции.ИспользуетКЭДО = ?(ИспользуетКЭДО[ФизическоеЛицо] = Неопределено,Ложь,ИспользуетКЭДО[ФизическоеЛицо]);
		ОбъектДоступныеФункции = ОбъектСервисаПоОписанию(ОписаниеДоступныеФункции, ОписаниеОбъектаДоступныеФункцииФизическихЛиц); 
		Данные.Добавить(ОбъектДоступныеФункции);
	КонецЦикла;
	
	РесурсСервиса = РесурсДоступныеФункцииФизическихЛиц(ПараметрыОбмена.ВерсияAPI);
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДоступныеФункцииФизическогоЛица);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "personID", РазмерПакета);
	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъектаФизическоеЛицо, РесурсСервиса, Ложь);
	
	Возврат Результат;

КонецФункции

Функция РезультатВыгрузкиОграничениеДоступаКРабочимКонтактам(ПараметрыОбмена, ФизическиеЛица)
	
	Результат = НовыйРезультатВыгрузки();
	
	Если Не КабинетСотрудника.ВерсионированиеИспользуется() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ТипОбъектаФизическоеЛицо = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	
	ФизическиеЛицаДляОбработки = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъектаФизическоеЛицо, ФизическиеЛица);
	Если Не ЗначениеЗаполнено(ФизическиеЛицаДляОбработки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(ФизическиеЛицаДляОбработки, ТипОбъектаФизическоеЛицо);
	
	НастройкиФункциональности = РегистрыСведений.ИспользуемаяФункциональностьСервисаКабинетСотрудника.Настройки();
	ОписаниеОбъектаОграничениеДоступаКРабочимКонтактам = ОписаниеОбъектаОграничениеДоступаКРабочимКонтактам(ПараметрыОбмена.ВерсияDTO);
	Данные = Новый Массив;
	Для каждого ФизическоеЛицо Из ФизическиеЛицаДляОбработки Цикл
		ОписаниеОграничениеДоступа =  ОписаниеОбъекта(ОписаниеОбъектаОграничениеДоступаКРабочимКонтактам);
		ОписаниеОграничениеДоступа.ФизическоеЛицо = ФизическоеЛицоИдентификатор[ФизическоеЛицо];
		ОписаниеОграничениеДоступа.УровеньДоступа = НастройкиФункциональности.УровеньДоступаКИ;
		ОбъектОграничениеДоступа = ОбъектСервисаПоОписанию(ОписаниеОграничениеДоступа, ОписаниеОбъектаОграничениеДоступаКРабочимКонтактам); 
		Данные.Добавить(ОбъектОграничениеДоступа);
	КонецЦикла;
	
	РесурсСервиса = РесурсОграничениеДоступаКРабочимКонтактам(ПараметрыОбмена.ВерсияAPI); 
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "personID", РазмерПакета);
	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъектаФизическоеЛицо, РесурсСервиса, Ложь);
	
	Возврат Результат;

КонецФункции

Функция РезультатВыгрузкиОрганизаций(ПараметрыОбмена, Список)
	
	Результат = НовыйРезультатВыгрузки();
	РесурсСервиса = РесурсОрганизации();
	Данные = ДанныеОрганизаций(ПараметрыОбмена, Список);
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);

	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиСтруктурыПредприятия(ПараметрыОбмена, Список)
	
	Результат = НовыйРезультатВыгрузки();
	РесурсСервиса = РесурсСтруктураПредприятия();
	Данные = ДанныеСтруктурыПредприятия(ПараметрыОбмена, Список); 
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);

	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиСтруктурыЮридическихЛиц(ПараметрыОбмена, Организации, ПодразделенияОрганизаций)
	
	Результат = НовыйРезультатВыгрузки();
	РесурсСервиса = РесурсСтруктураПредприятия();
	Данные = ДанныеСтруктурыЮридическихЛиц(ПараметрыОбмена,  Организации, ПодразделенияОрганизаций);
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);

	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиДолжностей(ПараметрыОбмена, Список)
	
	Результат = НовыйРезультатВыгрузки();
	РесурсСервиса = РесурсДолжности();
	Данные = ДанныеДолжностей(ПараметрыОбмена, Список);
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность;
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);

	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиШтатногоРасписания(ПараметрыОбмена, Список)

	Результат = НовыйРезультатВыгрузки();
	Данные = ДанныеШтатногоРасписания(ПараметрыОбмена, Список);
	
	Если ЗначениеЗаполнено(Данные) Тогда
		РесурсСервиса = РесурсШтатноеРасписание();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
		
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
		ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);
		
		Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиСотрудников(ПараметрыОбмена, ТаблицаДляВыгрузки)
	
	Результат = НовыйРезультатВыгрузки();
	ОтменитьПубликацию = Новый Массив;
	
	Отбор = Новый Структура("УсловноПубликуется", Истина);
	УсловноПубликуемыеСотрудники = ТаблицаДляВыгрузки.НайтиСтроки(Отбор);
	ВыгружаютсяУсловно = Новый Массив;
	Для каждого СтрокаТЗ Из УсловноПубликуемыеСотрудники Цикл
		ВыгружаютсяУсловно.Добавить(СтрокаТЗ.ПредметПубликации);
	КонецЦикла;
	Если ВыгружаютсяУсловно.Количество() > 0 Тогда
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
		ВыгруженныеОбъекты = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъекта, ВыгружаютсяУсловно);
		ОбъектыВыгружались = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ВыгруженныеОбъекты);
		УдаляемыеСтроки = Новый Массив;
		Для каждого СтрокаТЗ Из УсловноПубликуемыеСотрудники Цикл
			Если ОбъектыВыгружались[СтрокаТЗ.ПредметПубликации] = Неопределено Тогда
				УдаляемыеСтроки.Добавить(СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
		Для каждого СтрокаТЗ Из УдаляемыеСтроки Цикл
			ТаблицаДляВыгрузки.Удалить(СтрокаТЗ);
		КонецЦикла;
	КонецЕсли;
	
	Данные = ДанныеСотрудников(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
	
	Если ЗначениеЗаполнено(Данные) Тогда

		РесурсСервиса = РесурсСотрудники();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, "ID", РазмерПакета);
		
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
		ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Истина);
		
		Для каждого Сотрудник Из ОтменитьПубликацию Цикл
			Результат.Выгружено.Добавить(Сотрудник);
		КонецЦикла;
		
		Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиПрименяемыхВычетов(ПараметрыОбмена, Список)
	
	Результат = НовыйРезультатВыгрузки();
	
	РесурсСервиса = РесурсПрименяемыеВычеты();
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ФизическиеЛицаКОбработке = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъекта, Список);
	
	СведенияОВычетах = ДанныеОВычетах(ПараметрыОбмена, ФизическиеЛицаКОбработке);
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, СведенияОВычетах.Данные, "personID", РазмерПакета);
	
	ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, РесурсСервиса, Ложь);
	
	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	
	Результат.Вставить("ФизическиеЛицаБезВычетов", СведенияОВычетах.ФизическиеЛицаБезВычетов);
	
	
	Возврат Результат;
	
КонецФункции

Функция СоответствиеНаоборот(ИсходноеСоответствие)
	
	НовоеСоответствие = Новый Соответствие;
	Для каждого КлючИЗначение Из ИсходноеСоответствие Цикл
		НовоеСоответствие.Вставить(КлючИЗначение.Значение, КлючИЗначение.Ключ);
	КонецЦикла;
	
	Возврат НовоеСоответствие;

КонецФункции

Процедура АктивироватьФизическихЛицПоРезультатуВыгрузки(ПараметрыОбмена, РезультатВыгрузки)

	Если Не КабинетСотрудника.ИспользоватьФормат503() Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВыгрузки.Выгружено.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(РезультатВыгрузки.Выгружено, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	
	Данные = Новый Массив;
	Для каждого ФизическоеЛицо Из РезультатВыгрузки.Выгружено Цикл
		Данные.Добавить(ФизическоеЛицоИдентификатор[ФизическоеЛицо]);
	КонецЦикла;
	
	РесурсСервиса = РесурсФизическиеЛицаАктивные();
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные);
	
	Если Результат.БылиОшибки Тогда
		РезультатВыгрузки.БылиОшибки = Истина;
		Если ЗначениеЗаполнено(Результат.Выгружено) Тогда
			РезультатВыгрузки.Выгружено = Результат.Выгружено;
		Иначе
			РезультатВыгрузки.Выгружено.Очистить();
		КонецЕсли;
		Для Каждого Ошибка Из Результат.НеВыгружено Цикл
			ЗарегистрироватьОшибкуПубликацииОбъекта(Ошибка.Ключ, РесурсСервиса, Ошибка.Значение);
			РезультатВыгрузки.НеВыгружено.Вставить(Ошибка.Ключ, Ошибка.Значение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыгрузитьФотографииВСервис(ПараметрыОбмена, ТаблицаДанных, Фотографии)
	
	БылиОшибки = Ложь;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		
		ХранилищеВложения = Фотографии[СтрокаТаблицы.ФизическоеЛицоСсылка];
		Если Не ЗначениеЗаполнено(ХранилищеВложения) Тогда
			Продолжить;
		КонецЕсли;
		
		Фотография = ХранилищеВложения.Получить();
		Если ТипЗнч(Фотография) <> Тип("ДвоичныеДанные") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не КабинетСотрудника.РазмерФайлаСоответствуетТребованиям(Фотография) Тогда
			ТекстСообщения = НСтр("ru = 'Размер фотографии превышает 5Мб: %1.';
									|en = 'The photo size exceeds 5 MB: %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(СтрокаТаблицы.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		Расширение = КабинетСотрудника.РасширениеФотографии(Фотография);
		Если Расширение = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Неизвестный формат картинки фотографии: %1.';
									|en = 'Unknown picture format: %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(СтрокаТаблицы.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		Картинка = Новый Картинка(Фотография);
		ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, Картинка, Расширение);
		Если ИдентификаторФайла = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка публикации файла фотографии: %1.';
									|en = 'Photo file publication error: %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(СтрокаТаблицы.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		ПостфиксИмени = НСтр("ru = 'Фотография';
							|en = 'Photo'");
		НаименованиеФайла = СтрШаблон("%1 %2.%3",Строка(СтрокаТаблицы.ФизическоеЛицоСсылка), ПостфиксИмени, Расширение);
		
		ОписаниеФайла = Новый Структура("ИдентификаторФайла,НаименованиеФайла,РасширениеФайла,РазмерФайла");
		
		ОписаниеФайла.ИдентификаторФайла 	= ИдентификаторФайла;
		ОписаниеФайла.НаименованиеФайла 	= НаименованиеФайла;
		ОписаниеФайла.РасширениеФайла 		= Расширение;
		// преобразуем в строку, т.к. поле в таблице имеет тип Строка
		ОписаниеФайла.РазмерФайла 			= Формат(Фотография.Размер(), "ЧГ=0"); 
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ОписаниеФайла);
		
	КонецЦикла;
	
	Возврат БылиОшибки;
	
КонецФункции

Функция ОграниченныйСписокФизическихЛиц(ФизическиеЛица)
	
	Если ФизическиеЛица.Количество() <= 500 Тогда
		Возврат ФизическиеЛица;
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ВыгруженныеФизическиеЛица = ИнтеграцияУправлениеПерсоналом.ОбъектыВыгруженныеВПриложение(Приложение, ТипОбъекта, ФизическиеЛица);
	// Получим физических лиц, которые еще не выгружались
	ФизическиеЛицаДляВыгрузки = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ФизическиеЛица, ВыгруженныеФизическиеЛица);
	
	Если ФизическиеЛицаДляВыгрузки.Количество() < 500 Тогда
		Добавить = 500 - ФизическиеЛицаДляВыгрузки.Количество();
		Для каждого ФизическоеЛицо Из ВыгруженныеФизическиеЛица Цикл
			Если Добавить = 0 Тогда
				Прервать;
			КонецЕсли;
			ФизическиеЛицаДляВыгрузки.Добавить(ФизическоеЛицо);
			Добавить = Добавить - 1;
		КонецЦикла;
	Иначе
		ФизическиеЛицаДляВыгрузки = ОбщегоНазначенияБЗККлиентСервер.СрезМассива(ФизическиеЛицаДляВыгрузки, 0, 499);
	КонецЕсли;
	
	Возврат ФизическиеЛицаДляВыгрузки;

КонецФункции

#КонецОбласти

Процедура ОпубликоватьИзменения(ПараметрыОбмена, БылиОшибки)
	
	ОпубликоватьИзмененияПубликуемыхОбъектов(ПараметрыОбмена, БылиОшибки);
	ОпубликоватьПрочиеИзменения(ПараметрыОбмена, БылиОшибки);
	
КонецПроцедуры

Процедура ОпубликоватьИзмененияПубликуемыхОбъектов(ПараметрыОбмена, БылиОшибки)
	
	ПубликоватьСтруктуруЮридическихЛиц = ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц();
		
	// Таблица с данными зарегистрированными к отправке.
	// Колонки таблицы: ПредметПубликации, ВерсияДанных, Публикуется, ТипДанных, ТипОбъекта.
	ТаблицаИзменений = ИзмененияДляПубликации();
	Если ТаблицаИзменений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОбработано = НоваяТаблицаОбработано();
	ОтменитьРегистрацию = ТаблицаИзменений.СкопироватьКолонки("ПредметПубликации,ТипОбъекта,ВерсияДанных");
	
	ТаблицаИзменений.Индексы.Добавить("ТипОбъекта,Публикуется");
	Отбор = Новый Структура("ТипОбъекта,Публикуется");

	Результат = НовыйРезультатВыгрузки();
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиФизическихЛиц(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		АктивироватьФизическихЛицПоРезультатуВыгрузки(ПараметрыОбмена, РезультатВыгрузки);
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДоступныеФункцииФизическогоЛица;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиДоступныхФункцииФизическихЛиц(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиОграничениеДоступаКРабочимКонтактам(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиОрганизаций(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		Если ПубликоватьСтруктуруЮридическихЛиц Тогда
			ОтборПоТипу = Новый Структура("ТипДанных");
			ОтборПоТипу.ТипДанных = Тип("СправочникСсылка.Организации");
			Организации = ТаблицаДляВыгрузки.Скопировать(ОтборПоТипу).ВыгрузитьКолонку("ПредметПубликации");
			ОтборПоТипу.ТипДанных = Тип("СправочникСсылка.ПодразделенияОрганизаций");
			ПодразделенияОрганизаций = ТаблицаДляВыгрузки.Скопировать(ОтборПоТипу).ВыгрузитьКолонку("ПредметПубликации");
			РезультатВыгрузки = РезультатВыгрузкиСтруктурыЮридическихЛиц(ПараметрыОбмена, Организации, ПодразделенияОрганизаций);
			ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
		Иначе
			РезультатВыгрузки = РезультатВыгрузкиСтруктурыПредприятия(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
			ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
		КонецЕсли;
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиДолжностей(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиШтатногоРасписания(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник;
	Отбор.ТипОбъекта = ТипОбъекта;
	Отбор.Публикуется = Истина;
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиСотрудников(ПараметрыОбмена, ТаблицаДляВыгрузки);
		ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	Отбор.Публикуется = Ложь;
	ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
	
	КабинетСотрудникаВнутренний.ОпубликоватьЗарегистрированныеИзменения(ПараметрыОбмена, ТаблицаОбработано, ТаблицаИзменений, Результат);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ОтменитьРегистрацию.Колонки.ПредметПубликации.Имя = "Ссылка";
	ТаблицаИзменений.Колонки.ПредметПубликации.Имя = "Ссылка";
	Попытка
		ИнтеграцияУправлениеПерсоналомОбмен.ОтменитьРегистрациюИзменений(Приложение, ТаблицаОбработано, ТаблицаИзменений, ОтменитьРегистрацию);
		ИнтеграцияУправлениеПерсоналомОбмен.ОтменитьРегистрациюУсловноПубликуемыхОбъектов(Приложение);
	Исключение
		БылиОшибки = Истина;
	КонецПопытки;
	
	БылиОшибки = БылиОшибки Или Результат.БылиОшибки;
	
КонецПроцедуры

Процедура ОпубликоватьПрочиеИзменения(ПараметрыОбмена, БылиОшибки)
	
	Результат = НовыйРезультатВыгрузки();
	
	ТаблицаИзменений = ИзмененияВычетыНДФЛ();
	Если ЗначениеЗаполнено(ТаблицаИзменений) Тогда
		
		ТаблицаОбработано = НоваяТаблицаОбработано();
		ОтменитьРегистрацию = ТаблицаИзменений.СкопироватьКолонки("ФизическоеЛицо,ТипОбъекта,ВерсияДанных");
		
		ТаблицаИзменений.Индексы.Добавить("Публикуется");
		Отбор = Новый Структура("Публикуется");
		
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ;
		Отбор.Публикуется = Истина;
		ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
		Если ТаблицаДляВыгрузки.Количество()>0 Тогда
			РезультатВыгрузки = РезультатВыгрузкиПрименяемыхВычетов(ПараметрыОбмена, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ФизическоеЛицо"));
			// Физические лица по которым заказали публикацию вычетов, но они не имеют вычетов.
			ФизическиеЛицаБезВычетов = РезультатВыгрузки.ФизическиеЛицаБезВычетов;
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатВыгрузки.Выгружено, РезультатВыгрузки.ФизическиеЛицаБезВычетов);
			ИнтеграцияУправлениеПерсоналомОбмен.ДополнитьТаблицуРезультатамиВыгрузки(ТаблицаОбработано, РезультатВыгрузки, ТипОбъекта);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
		КонецЕсли;
		Отбор.Публикуется = Ложь;
		ТаблицаОтменитьВыгрузку = ТаблицаИзменений.Скопировать(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтменитьВыгрузку, ОтменитьРегистрацию);
		
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		ОтменитьРегистрацию.Колонки.ФизическоеЛицо.Имя = "Ссылка";
		ТаблицаИзменений.Колонки.ФизическоеЛицо.Имя = "Ссылка";
		Попытка
			ИнтеграцияУправлениеПерсоналомОбмен.ОтменитьРегистрациюИзменений(Приложение, ТаблицаОбработано, ТаблицаИзменений, ОтменитьРегистрацию);
		Исключение
			БылиОшибки = Истина;
		КонецПопытки;
	
		БылиОшибки = БылиОшибки Или Результат.БылиОшибки;
		
	КонецЕсли;
	
	КабинетСотрудникаВнутренний.ОпубликоватьПрочиеИзменения(ПараметрыОбмена, БылиОшибки);
	
КонецПроцедуры

Функция ИзмененияДляПубликации()

	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикРаботы);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикОтпусковПредприятия);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВидПредоставляемойСотрудникамСправки);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикОтпусков);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаработанныеПраваНаОтпуск);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДоступныеФункцииФизическогоЛица);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипыОбъектов", ТипыОбъектов);
	Запрос.УстановитьПараметр("Приложение", Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВсеИзменения.Ссылка КАК ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных КАК ВерсияДанных,
	|	ВсеИзменения.ТипОбъекта КАК ТипОбъекта
	|ПОМЕСТИТЬ ВТВсеИзменения
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ВсеИзменения
	|ГДЕ
	|	ВсеИзменения.ТипОбъекта В(&ТипыОбъектов)
	|	И НЕ ВсеИзменения.ВыгружатьУдаление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации КАК ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ПубликуемыеОбъекты.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется,
	|	ЕСТЬNULL(ПубликуемыеОбъекты.УсловноВыгружается, ЛОЖЬ) КАК УсловноПубликуется,
	|	ВсеИзменения.ТипОбъекта КАК ТипОбъекта
	|ПОМЕСТИТЬ ВТИзмененияПрочихОбъектов
	|ИЗ
	|	ВТВсеИзменения КАК ВсеИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО ВсеИзменения.ПредметПубликации = ПубликуемыеОбъекты.Ссылка
	|ГДЕ
	|	НЕ ВсеИзменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо)";
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТВсеИзменения") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Изменения.ПредметПубликации КАК ПредметПубликации,
	|	Изменения.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ПубликуемыеОбъекты.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется,
	|	ЛОЖЬ КАК УсловноПубликуется,
	|	ТИПЗНАЧЕНИЯ(Изменения.ПредметПубликации) КАК ТипДанных,
	|	NULL КАК ФизическоеЛицо,
	|	Изменения.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	ВТВсеИзменения КАК Изменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО Изменения.ПредметПубликации = ПубликуемыеОбъекты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом КАК ОшибкиЗаполнения
	|		ПО Изменения.ПредметПубликации = ОшибкиЗаполнения.Ссылка
	|			И (ОшибкиЗаполнения.БлокирующаяОшибка)
	|			И (ОшибкиЗаполнения.Приложение = &Приложение)
	|ГДЕ
	|	Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо)
	|	И ОшибкиЗаполнения.БлокирующаяОшибка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных,
	|	ВсеИзменения.Публикуется,
	|	ЛОЖЬ,
	|	ТИПЗНАЧЕНИЯ(ВсеИзменения.ПредметПубликации),
	|	NULL,
	|	ВсеИзменения.ТипОбъекта
	|ИЗ
	|	ВТИзмененияПрочихОбъектов КАК ВсеИзменения
	|ГДЕ
	|	НЕ ВсеИзменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных,
	|	ВсеИзменения.Публикуется,
	|	ВсеИзменения.УсловноПубликуется,
	|	ТИПЗНАЧЕНИЯ(ВсеИзменения.ПредметПубликации),
	|	Сотрудники.ФизическоеЛицо,
	|	ВсеИзменения.ТипОбъекта
	|ИЗ
	|	ВТИзмененияПрочихОбъектов КАК ВсеИзменения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом КАК ОшибкиЗаполнения
	|			ПО Сотрудники.ФизическоеЛицо = ОшибкиЗаполнения.Ссылка
	|				И (ОшибкиЗаполнения.БлокирующаяОшибка)
	|		ПО ВсеИзменения.ПредметПубликации = Сотрудники.Ссылка
	|ГДЕ
	|	ВсеИзменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник)
	|	И (ВсеИзменения.УсловноПубликуется
	|			ИЛИ ОшибкиЗаполнения.БлокирующаяОшибка ЕСТЬ NULL)";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ИзмененияВычетыНДФЛ()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Вычеты.Ссылка КАК ФизическоеЛицо,
	|	Вычеты.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ФизическиеЛица.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется,
	|	Вычеты.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Вычеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ФизическиеЛица
	|		ПО Вычеты.Ссылка = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом КАК ОшибкиЗаполнения
	|		ПО Вычеты.Ссылка = ОшибкиЗаполнения.Ссылка
	|			И (ОшибкиЗаполнения.БлокирующаяОшибка)
	|ГДЕ
	|	Вычеты.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ)
	|	И ОшибкиЗаполнения.БлокирующаяОшибка ЕСТЬ NULL
	|	И НЕ Вычеты.ВыгружатьУдаление";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

Функция ТипыПубликуемыхДанных()

	ОписаниеТиповДанных = Новый Соответствие;

	ОписаниеТиповДанных.Вставить("ФизическиеЛица", 					Тип("СправочникСсылка.ФизическиеЛица"));
	ОписаниеТиповДанных.Вставить("Организации", 					Тип("СправочникСсылка.Организации"));
	ОписаниеТиповДанных.Вставить("Должности", 						Тип("СправочникСсылка.Должности"));
	ОписаниеТиповДанных.Вставить("Сотрудники", 						Тип("СправочникСсылка.Сотрудники"));
	ОписаниеТиповДанных.Вставить("ПодразделенияОрганизаций", 		Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	ОписаниеТиповДанных.Вставить("ШтатноеРасписание", 	 			Тип("СправочникСсылка.ШтатноеРасписание"));
	ОписаниеТиповДанных.Вставить("СтруктураПредприятия", 			Тип("СправочникСсылка.СтруктураПредприятия"));
	
	Возврат ОписаниеТиповДанных;
	
КонецФункции

Функция НовыйРезультатВыгрузки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", Новый Массив);
	Результат.Вставить("НеВыгружено", Новый Соответствие);
	Результат.Вставить("БылиОшибки", Ложь);
	Результат.Вставить("ОтменитьРегистрацию", Новый Массив);
	Возврат Результат;
	
КонецФункции

Функция СформироватьJSON(Значение) Экспорт
	
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
	НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON( ,Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, Значение, НастройкиСериализацииJSON, "ПреобразованиеJSON", ОбщегоНазначения.ОбщийМодуль("КабинетСотрудникаМенеджерОбмена"));
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция ПреобразованиеJSON(Знач Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Результат = Неопределено;

	Если ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
		Результат = ПолФизическогоЛица(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ВидыЗанятости") Тогда
		Результат = ВидЗанятостиСотрудника(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ВидыДокументовФизическихЛиц") Тогда
		Результат = ВидДокументаФизическогоЛица(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.СпособыРасчетаУдержанийКабинетСотрудника") Тогда
		Результат = СпособРасчетаУдержания(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.УровниДоступаКИнформацииОСотрудниках") Тогда
		Результат = УровеньДоступаКИнформацииОСотрудниках(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.РезультатыСогласованияБЗК") Тогда
		Результат = РезультатСогласования(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.РолиПодписантовКЭДО") Тогда
		Результат = РольПодписанта(Значение);
	ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Если Не Значение.Пустая() Тогда
			Результат = Строка(Значение.УникальныйИдентификатор());
		КонецЕсли;
	Иначе
		Результат = КабинетСотрудникаВнутренний.ПреобразованиеJSON(Свойство, Значение, ДополнительныеПараметры, Отказ);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, Данные, ПолеКлюча = "", РазмерПакета = 100) Экспорт
	
	РезультатВыгрузки = НовыйРезультатВыгрузки();
	
	Пакеты = Новый Массив;
	Если Данные.Количество() > РазмерПакета Тогда
		ДанныеПакета = Новый Массив;
		СчПакета = 0;
		Для Ид=0 По Данные.ВГраница() Цикл
			Если СчПакета = РазмерПакета Тогда
				Пакеты.Добавить(ДанныеПакета);
				СчПакета = 0;
				ДанныеПакета = Новый Массив;
			КонецЕсли;
			СчПакета = СчПакета + 1;
			ДанныеПакета.Добавить(Данные[Ид]);
		КонецЦикла;
		Пакеты.Добавить(ДанныеПакета);
	Иначе
		Пакеты.Добавить(Данные);
	КонецЕсли;
	
	Для каждого ДанныеПакета Из Пакеты Цикл
		
		СтрокаТела = СформироватьJSON(ДанныеПакета);
		Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
		
		ЕстьПолеКлюча = Не ПустаяСтрока(ПолеКлюча);
		Если Ответ = Неопределено Тогда
			РезультатВыгрузки.БылиОшибки = Истина;
		ИначеЕсли Ответ.КодСостояния = 200 Или Ответ.КодСостояния = 204 Тогда
			Для Каждого ЭлементПакета Из ДанныеПакета Цикл
				Если ЕстьПолеКлюча Тогда
					ЭлементКоллекции = ЭлементПакета[ПолеКлюча];
				Иначе
					ЭлементКоллекции = ЭлементПакета;
				КонецЕсли;
				РезультатВыгрузки.Выгружено.Добавить(ЭлементКоллекции);
			КонецЦикла;
		ИначеЕсли Ответ.КодСостояния = 400 Тогда
			
			РезультатВыгрузки.БылиОшибки = Истина;
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
			
			Попытка
				ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			Исключение
				КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
				РезультатВыгрузки.БылиОшибки = Истина;
				Продолжить;
			КонецПопытки;
			
			Результат = ОбъектОтвета["result"];
			Если Результат <> Неопределено Тогда
				Для Каждого ЭлементРезультат Из Результат Цикл
					ЭлементПакета = ДанныеПакета[Число(ЭлементРезультат["position"]) - 1];
					Если ЕстьПолеКлюча Тогда
						ЭлементКоллекции = ЭлементПакета[ПолеКлюча];
					Иначе
						ЭлементКоллекции = ЭлементПакета;
					КонецЕсли;
					РезультатВыгрузки.Выгружено.Добавить(ЭлементКоллекции);
				КонецЦикла;
			КонецЕсли;
			
			КоличествоЭлементов = ДанныеПакета.Количество();
			Ошибки = ОбъектОтвета["errors"];
			Если Ошибки <> Неопределено Тогда
				Для Каждого ЭлементОшибки Из Ошибки Цикл
					ПозицияВКоллекции = ЭлементОшибки["position"];
					Если ПозицияВКоллекции <> Неопределено Тогда
						НомерПозиции = Число(ПозицияВКоллекции);
						Если НомерПозиции <= КоличествоЭлементов Тогда
							ЭлементПакета = ДанныеПакета[НомерПозиции - 1];
							Если ЕстьПолеКлюча Тогда
								ЭлементКоллекции = ЭлементПакета[ПолеКлюча];
							Иначе
								ЭлементКоллекции = ЭлементПакета;
							КонецЕсли;
							РезультатВыгрузки.НеВыгружено.Вставить(ЭлементКоллекции, ОписаниеОшибкиВыгрузки(ЭлементОшибки));
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			РезультатВыгрузки.БылиОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ОписаниеОшибкиВыгрузки(ОбъектОшибка)
	
	Попытка
		Результат = НСтр("ru = 'Тип ошибки: %1
						|Код ошибки: %2
						|Описание: %3
						|Подробно:';
						|en = 'Error type: %1
						|Error code: %2
						|Details: %3
						|More:'");
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Результат, ОбъектОшибка["error"]["type"], ОбъектОшибка["error"]["code"], ОбъектОшибка["error"]["description"]);
	
		Для Каждого КлючЗначение Из ОбъектОшибка["error"]["value"] Цикл
			Результат = Результат + Символы.ПС + КлючЗначение.Ключ + ": " + КлючЗначение.Значение;
		КонецЦикла;
	Исключение
		Результат = НСтр("ru = 'Неизвестное описание ошибки.';
						|en = 'Unknown error details.'");
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПубликацияИзмененийЗаявок

Процедура ОпубликоватьИзмененияЗаявок(ПараметрыОбмена, БылиОшибки)

	РезультатОбработки = НовыйРезультатВыгрузки();
	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросСправки2НДФЛ);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросСправкиСМестаРаботы);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаИзменениеЛичныхДанных);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНалоговыйВычет);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНаОтпуск);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНаКомпенсациюОтпуска);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаОстаткиОтпусков);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Отсутствие);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаДСВ);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТипыОбъектов", ТипыОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзмененияДляСервиса.Ссылка КАК ПредметПубликации,
	|	ИзмененияДляСервиса.ВерсияДанных КАК ВерсияДанных,
	|	ТИПЗНАЧЕНИЯ(ИзмененияДляСервиса.Ссылка) КАК ТипДанных,
	|	ИзмененияДляСервиса.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ИзмененияДляСервиса
	|ГДЕ
	|	ИзмененияДляСервиса.ТипОбъекта В(&ТипыОбъектов)
	|	И НЕ ИзмененияДляСервиса.ВыгружатьУдаление
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипДанных";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ТаблицаИзменений = РезультатЗапроса.Выгрузить();
	
	ТаблицаИзменений.Индексы.Добавить("ТипДанных");
	Отбор = Новый Структура("ТипДанных");
	ТипыДанных = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "ТипДанных", Истина);
	
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	ПубликоватьРезультатСогласования = КабинетСотрудника.ИспользоватьФормат303();
	РесурсСтатусовСервиса = РесурсСтатусовСервиса(ПараметрыОбмена);
	
	// Заявки с ошибками заполнения, например пустой идентификатор заявки,
	// снимаем с регистрации изменений после выгрузки данных.
	ОтменитьРегистрацию = ТаблицаИзменений.СкопироватьКолонки("ПредметПубликации,ТипОбъекта,ВерсияДанных");
	
	// Заявки к выгрузке, не участвующие в кадровом ЭДО.
	// Ключ - ТипыЗаявокКабинетСотрудника,
	// Значение - массив объектов к выгрузке.
	ЗаявкиКВыгрузке = Новый Соответствие;
	
	// Заявки по которым были ошибки при обработке, до публикации результатов.
	// Ключ - ссылка на заявку,
	// Значение - описание причины не обработки заявки.
	НеОбработанныеЗаявки = Новый Соответствие;
	
	// Содержит все заявки полученный как изменения для публикации.
	// Ключ - ИдентификаторЗаявки - идентификатор в сервисе,
	// Значение - ссылка на заявку.
	ИдентификаторЗаявка = Новый Соответствие;
	
	// Результаты согласования для выгрузки.
	// Ключ - ИдентификаторЗаявки - идентификатор в сервисе,
	// Значение - массив объектов РезультатСогласования.
	ЗаявкаРезультатыСогласования = Новый Соответствие;
	
	// Заявки для выгрузки по "строму" формату.
	// Ключ - ИдентификаторЗаявки - идентификатор в сервисе,
	// Значение - Структура, ресурс сервиса и СтатусПубликации.
	СтатусыЗаявокКПубликации = Новый Соответствие;
	
	// Таблица с документами кадрового ЭДО по заявкам, содержит колонки
	// Ссылка - ссылка на документ КЭДО,
	// Документ - ссылка на бизнес-процесс - Заявку,
	// ЭлектронныйДокумент - ссылка на присоединенный файл.
	ДокументыКЭДОПоЗаявкам = ДокументыКЭДОПоПолюДокумент(ТаблицаИзменений.ВыгрузитьКолонку("ПредметПубликации"));
	ДокументыКЭДОПоЗаявкам.Индексы.Добавить("Документ");
	
	ТипЗаявкиТипОбъекта = ИнтеграцияКабинетСотрудника.ТипЗаявкиТипОбъекта();
	
	// Результаты согласования для заполнения идентификаторов.
	ВсеРезультатыСогласования = Новый Массив;
	
	ОписаниеОбъектРезультатСогласования = ОписаниеОбъектРезультатСогласования(ВерсияDTO);
	Для каждого ТипДанных Из ТипыДанных Цикл
		
		Если ТипДанных = Тип("Неопределено") Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.ТипДанных = ТипДанных;
		ТаблицаПоТипуДанных = ТаблицаИзменений.Скопировать(Отбор);
		ПубликуемыеЗаявки = ТаблицаПоТипуДанных.ВыгрузитьКолонку("ПредметПубликации");
		
		// Таблица с данными заявок, содержит колонки
		// Заявка - ссылка на бизнес-процесс,
		// ТипЗаявкиКабинетСотрудника - ПеречислениеСсылка.ТипыЗаявокКабинетСотрудника,
		// ИдентификаторЗаявки - строка, идентификатор заявки из сервиса,
		// СостояниеЗаявки - ПеречислениеСсылка.СостоянияЗаявокКабинетСотрудника
		// ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
		// Комментарий - Строка - ответ по заявке
		// ЗаявкаПодписывается - Булево - признак того, что заявка подписывается
		// ВариантФормированияФайлаОтвета - вариант формирования ответа
		// Исполнитель - Пользователь или Роль исполнителя.
		ТаблицаЗаявок = БизнесПроцессыЗаявокСотрудников.ТаблицаЗаявокСотрудника(ПубликуемыеЗаявки, ТипДанных);
		
		ФизическоеЛицоИдентификатор = Новый Соответствие;
		Если ЗначениеЗаполнено(ТаблицаЗаявок) И ТаблицаЗаявок[0].ТипЗаявкиКабинетСотрудника = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска Тогда
			ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаЗаявок, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
		КонецЕсли;
		
		// Таблица содержит вложения по заявкам не участвующим в кадровом ЭДО.
		ТаблицаВложенийЗаявок = БизнесПроцессыЗаявокСотрудников.ТаблицаФайловОтветаЗаявокСотрудника(ПубликуемыеЗаявки, ТипДанных);
		ОтборЗаявок = Новый Структура("Заявка");
		
		Для каждого СтрокаТЗ Из ТаблицаЗаявок Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторЗаявки) Тогда
				РезультатОбработки.БылиОшибки = Истина;
				НоваяСтрока = ОтменитьРегистрацию.Добавить();
				НоваяСтрока.ПредметПубликации = СтрокаТЗ.Заявка;
				НоваяСтрока.ТипОбъекта = ТипЗаявкиТипОбъекта[СтрокаТЗ.ТипЗаявкиКабинетСотрудника];
				ОписаниеОшибки = НСтр("ru = 'Не заполнен идентификатор заявки.';
										|en = 'The request ID is not filled in.'");
				ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки);
				Продолжить;
			КонецЕсли;
			
			ТипЗаявкиСервиса = СтрокаТЗ.ТипЗаявкиКабинетСотрудника;
			ИдентификаторЗаявка.Вставить(СтрокаТЗ.ИдентификаторЗаявки, СтрокаТЗ.Заявка);
			
			ЭлектронныйДокумент = Неопределено;
			ИдентификаторДокумента = Неопределено;
			
			// Проверяем, что у заявки есть документ кадрового ЭДО,
			// документ может отсутствовать, если еще не выполнена обработка обновления.
			СтрокаТаблицы = ДокументыКЭДОПоЗаявкам.Найти(СтрокаТЗ.Заявка, "Документ");
			Если СтрокаТаблицы = Неопределено Тогда
				ОписаниеОшибки = НСтр("ru = 'Не найден документ кадрового ЭДО.';
										|en = 'The HR EDI document is not found.'");
				НеОбработанныеЗаявки.Вставить(СтрокаТЗ.Заявка, ОписаниеОшибки);
				ЗаписатьПредупреждениеВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки);
				Продолжить;
			Иначе
				ЭлектронныйДокумент = СтрокаТаблицы.ЭлектронныйДокумент;
				ИдентификаторДокумента = СтрокаТаблицы.ИдентификаторДокумента;
				// Электронный документ есть в заявках, полученных из сервиса с использованием ЭП.
				ТребуетсяЭлектроннаяПодпись = СтрокаТЗ.ЗаявкаПодписывается И ИспользуетсяКадровыйЭДО И ЗначениеЗаполнено(ЭлектронныйДокумент);
			КонецЕсли;
			
			РезультатВыполнения = "";
			ЗаявкаИсполнена = Ложь;
			Если СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена Тогда
				РезультатВыполнения = Перечисления.РезультатыСогласованияБЗК.Согласовано;
				ЗаявкаИсполнена = Истина;
			ИначеЕсли СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Отказ Тогда
				РезультатВыполнения = Перечисления.РезультатыСогласованияБЗК.Отклонено;
			Иначе
				РезультатОбработки.БылиОшибки = Истина;
				ОписаниеОшибки = НСтр("ru = 'Не установлен статус выполнения заявки.';
										|en = 'Request execution status is not set.'");
				НеОбработанныеЗаявки.Вставить(СтрокаТЗ.Заявка, ОписаниеОшибки);
				ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки);
				Продолжить;
			КонецЕсли;
			
			// Обработка заявок не участвующих в кадровом ЭДО.
			Если ЗаявкаИсполнена И ТипЗаявкиСервиса = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска Тогда
				
				ОбъектСправка = ОбъектСправкаОбОстаткеОтпуска(ПараметрыОбмена, СтрокаТЗ, ТаблицаВложенийЗаявок, НеОбработанныеЗаявки, ФизическоеЛицоИдентификатор);
				Если ОбъектСправка <> Неопределено Тогда
					ЗаявкиПоТипу = ЗаявкиКВыгрузке[ТипЗаявкиСервиса];
					Если ЗаявкиПоТипу = Неопределено Тогда
						ЗаявкиПоТипу = Новый Массив;
					КонецЕсли;
					ЗаявкиПоТипу.Добавить(ОбъектСправка);
					ЗаявкиКВыгрузке.Вставить(ТипЗаявкиСервиса, ЗаявкиПоТипу);
				КонецЕсли;
				
			КонецЕсли;
			
			// Комментарий передаем, если отказ по заявке
			// или это не заявки на справку с места работы или 2-НДФЛ,
			// для этих справок комментарий публикуется со справкой.
			Комментарий = "";
			Если Не ЗаявкаИсполнена
				Или ТипЗаявкиСервиса <> Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ И ТипЗаявкиСервиса <> Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы 
				Или СтрокаТЗ.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ВБумажномВиде Тогда
				Комментарий = СтрокаТЗ.Комментарий;
			КонецЕсли;
			
			Если ПубликоватьРезультатСогласования Тогда
				
				РезультатыСогласования = Новый Массив;
				
				Если ТребуетсяЭлектроннаяПодпись Тогда
					
					Ответ = РезультатыСогласованияЗаявкиСПодписью(
								СтрокаТЗ.ИдентификаторЗаявки,
								РезультатВыполнения,
								ЭлектронныйДокумент,
								ИдентификаторДокумента,
								Комментарий,
								ВерсияDTO);
					
					Если Ответ.ОписаниеОшибки <> Неопределено Тогда
						РезультатОбработки.БылиОшибки = Истина;
						НеОбработанныеЗаявки.Вставить(СтрокаТЗ.Заявка, Ответ.ОписаниеОшибки);
						Продолжить;
					ИначеЕсли Ответ.РезультатыСогласования <> Неопределено Тогда
						РезультатыСогласования = Ответ.РезультатыСогласования;
					КонецЕсли;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(РезультатыСогласования) Тогда
					
					// не требуется ЭП, или у заявки нет ЭП
					
					Исполнитель = СтрокаТЗ.Исполнитель;
					Подписант = "";
					ИмяПодписанта = "";
					Если ЗначениеЗаполнено(Исполнитель) 
						И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Пользователи") 
						И Исполнитель <> Пользователи.СсылкаНеуказанногоПользователя(Ложь) Тогда
						Подписант = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Исполнитель, "ФизическоеЛицо");
						Если ЗначениеЗаполнено(Подписант) И КабинетСотрудника.ФизическоеЛицоПубликуется(Подписант) Тогда
							ИмяПодписанта = Строка(Подписант);
						Иначе
							Подписант = "";
							ИмяПодписанта = Строка(Исполнитель);
						КонецЕсли;
					КонецЕсли;
					
					РезультатСогласования = ОписаниеОбъекта(ОписаниеОбъектРезультатСогласования);
					РезультатСогласования.Подписант 				= Подписант;
					РезультатСогласования.ИмяПодписанта 			= ИмяПодписанта;
					РезультатСогласования.ИдентификаторДокумента 	= СтрокаТЗ.ИдентификаторЗаявки;
					РезультатСогласования.РезультатСогласования 	= РезультатВыполнения;
					РезультатСогласования.Комментарий 				= Комментарий;
					РезультатСогласования.РольПодписанта 			= Перечисления.РолиПодписантовКЭДО.Исполнитель;
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатСогласования, "ИмяФайла")
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТЗ, "ИмяФайла") Тогда
						
						РезультатСогласования.ИмяФайла 				= СтрокаТЗ.ИмяФайла;
					КонецЕсли;
					
					ДобавитьВКоллекциюРезультатСогласования(РезультатыСогласования, РезультатСогласования, СтрокаТЗ, ОписаниеОбъектРезультатСогласования);
					
				КонецЕсли;
				
				ЗаявкаРезультатыСогласования.Вставить(СтрокаТЗ.ИдентификаторЗаявки, РезультатыСогласования);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеРезультатыСогласования, РезультатыСогласования);
				
			Иначе
				
				РесурсСервиса = РесурсСтатусовСервиса[ТипЗаявкиСервиса];
				Если ЗаявкаИсполнена Тогда
					СтатусПубликации = "completed";
				Иначе
					СтатусПубликации = "rejected";
				КонецЕсли;
				
				ПараметрыСтатуса = Новый Структура;
				ПараметрыСтатуса.Вставить("РесурсСервиса", РесурсСервиса);
				ПараметрыСтатуса.Вставить("СтатусПубликации", СтатусПубликации);
				
				СтатусыЗаявокКПубликации.Вставить(СтрокаТЗ.ИдентификаторЗаявки, ПараметрыСтатуса);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования);
	
	// Идентификаторы заявок по которым были ошибки выгрузки.
	НеВыгруженныеЗаявки = Новый Массив;
	
	// Публикация заявок не участвующие в кадровом ЭДО.
	Если ЗаявкиКВыгрузке.Количество() > 0 Тогда
		
		РесурсЗаявокСервиса = РесурсЗаявокСервисаБезКЭДО();
		Для каждого ЭлементКоллекции Из ЗаявкиКВыгрузке Цикл
		
			ТипЗаявкиСервиса = ЭлементКоллекции.Ключ;
			ЗаявкиПоТипу = ЭлементКоллекции.Значение;
			РесурсСервиса = РесурсЗаявокСервиса[ТипЗаявкиСервиса];
			РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СправкаОстаткиОтпусков);
			РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				ЗаявкиПоТипу,
				"requestID", РазмерПакета);
				
			РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
			
			Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
				// Не найденные заявки с таким идентификатором, регистрируем как успешно выгруженные.
				РегистрироватьКаКВыгружена = (ЗначениеЗаполнено(Ошибка.Значение) И СтрНайти(Ошибка.Значение, "objectNotFound") > 0);
				НеВыгруженныеЗаявки.Добавить(Ошибка.Ключ);
				Заявка = ИдентификаторЗаявка[Ошибка.Ключ];
				Если Заявка <> Неопределено Тогда
					Если РегистрироватьКаКВыгружена Тогда
						РезультатОбработки.Выгружено.Добавить(Заявка);
					Иначе	
						РезультатОбработки.НеВыгружено.Вставить(Заявка, Ошибка.Значение);
					КонецЕсли;
				КонецЕсли;
				ЗарегистрироватьОшибкуПубликацииОбъекта(Заявка, РесурсСервиса, Ошибка.Значение);
			КонецЦикла;
		
		КонецЦикла;
		
	КонецЕсли;
	
	// Публикация результатов согласования по заявкам.
	Если ПубликоватьРезультатСогласования Тогда
		
		// вычеркнем заявки, которые не выгрузились на предыдущем шаге
		Для каждого ИдентификаторЗаявки Из НеВыгруженныеЗаявки Цикл
			ЗаявкаРезультатыСогласования.Удалить(ИдентификаторЗаявки);
		КонецЦикла;
		
		РезультатыСогласования = Новый Массив;
		Для каждого ЭлементКоллекции Из ЗаявкаРезультатыСогласования Цикл
			Для каждого РезультатСогласования Из ЭлементКоллекции.Значение Цикл
				РезультатыСогласования.Добавить(РезультатСогласования);
			КонецЦикла;
		КонецЦикла;
		
		РесурсСервиса = РесурсРезультатыСогласования();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, РезультатыСогласования, "documentID", РазмерПакета);
			
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для каждого Идентификатор Из РезультатВыгрузки.Выгружено Цикл
			ОпубликованнаяЗаявка = ИдентификаторЗаявка[Идентификатор];
			Если ОпубликованнаяЗаявка <> Неопределено Тогда
				РезультатОбработки.Выгружено.Добавить(ОпубликованнаяЗаявка);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			// Не найденные заявки с таким идентификатором, регистрируем как успешно выгруженные.
			РегистрироватьКаКВыгружена = (ЗначениеЗаполнено(Ошибка.Значение) И СтрНайти(Ошибка.Значение, "objectNotFound") > 0);
			Заявка = ИдентификаторЗаявка[Ошибка.Ключ];
			Если Заявка <> Неопределено Тогда
				Если РегистрироватьКаКВыгружена Тогда
					РезультатОбработки.Выгружено.Добавить(Заявка);
				Иначе	
					РезультатОбработки.НеВыгружено.Вставить(Заявка, Ошибка.Значение);
				КонецЕсли;
			КонецЕсли;
			ЗарегистрироватьОшибкуПубликацииОбъекта(Заявка, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	Иначе
		
		// вычеркнем заявки, которые не выгрузились на предыдущем шаге
		Для каждого ИдентификаторЗаявки Из НеВыгруженныеЗаявки Цикл
			СтатусыЗаявокКПубликации.Удалить(ИдентификаторЗаявки);
		КонецЦикла;
		
		Для каждого ЭлементКоллекции Из СтатусыЗаявокКПубликации Цикл
		
			ИдентификаторЗаявки = ЭлементКоллекции.Ключ;
			СтатусПубликации = ЭлементКоллекции.Значение.СтатусПубликации;
			РесурсСервиса = ЭлементКоллекции.Значение.РесурсСервиса;
			
			Заявка = ИдентификаторЗаявка[ИдентификаторЗаявки];
			Если ОпубликоватьСтатусЗаявки(ПараметрыОбмена, РесурсСервиса, ИдентификаторЗаявки, СтатусПубликации) Тогда
				ОпубликованнаяЗаявка = ИдентификаторЗаявка[ИдентификаторЗаявки];
				Если Заявка <> Неопределено Тогда
					РезультатОбработки.Выгружено.Добавить(ОпубликованнаяЗаявка);
				КонецЕсли;
			Иначе
				РезультатОбработки.БылиОшибки = Истина;
				ОписаниеОшибки = НСтр("ru = 'Ошибка обновления статуса заявки.';
										|en = 'Request status update error.'");
				ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки);
				Если Заявка <> Неопределено Тогда
					РезультатОбработки.НеВыгружено.Вставить(Заявка, ОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(РезультатОбработки.НеВыгружено, НеОбработанныеЗаявки);
	РезультатОбработки.ОтменитьРегистрацию = ОтменитьРегистрацию;
	ОтменитьРегистрациюИзмененийЗаявокПоРезультатамПубликации(ТаблицаИзменений, РезультатОбработки);
	
	БылиОшибки = БылиОшибки Или РезультатОбработки.БылиОшибки;
	
КонецПроцедуры

Процедура ОтменитьРегистрациюИзмененийЗаявокПоРезультатамПубликации(ТаблицаИзменений, РезультатВыгрузки)

	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ТаблицаИзменений;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ПредметПубликации");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
		Запрос.УстановитьПараметр("Выгружено", РезультатВыгрузки.Выгружено);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИзменений.ПредметПубликации КАК ПредметПубликации,
		|	ТаблицаИзменений.ВерсияДанных КАК ВерсияДанных,
		|	ТаблицаИзменений.ТипОбъекта КАК ТипОбъекта
		|ПОМЕСТИТЬ ВТОтменитьИзменения
		|ИЗ
		|	&ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	ТаблицаИзменений.ПредметПубликации В(&Выгружено)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтменитьИзменения.ПредметПубликации КАК ПредметПубликации,
		|	ОтменитьИзменения.ТипОбъекта КАК ТипОбъекта
		|ИЗ
		|	ВТОтменитьИзменения КАК ОтменитьИзменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ИзмененияДляСервиса
		|		ПО ОтменитьИзменения.ПредметПубликации = ИзмененияДляСервиса.Ссылка
		|			И ОтменитьИзменения.ВерсияДанных = ИзмененияДляСервиса.ВерсияДанных
		|			И ОтменитьИзменения.ТипОбъекта = ИзмененияДляСервиса.ТипОбъекта";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Выборка.ПредметПубликации;
			МенеджерЗаписи.ТипОбъекта 	= Выборка.ТипОбъекта;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		Для Каждого СтрокаТЗ Из РезультатВыгрузки.ОтменитьРегистрацию Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.ПредметПубликации;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ОписаниеИзменений = НСтр("ru = 'Заявки сотрудников';
								|en = 'Employee requests'");
		ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки());
		РезультатВыгрузки.БылиОшибки = Истина;
	КонецПопытки;

КонецПроцедуры

Функция ОпубликоватьСтатусЗаявки(ПараметрыОбмена, РесурсЗаявкиСервиса, ИдентификаторЗаявки, Статус)
	
	РесурсСервиса = СтрЗаменить(РесурсЗаявкиСервиса,"{ID}",ИдентификаторЗаявки);
	РесурсСервиса = СтрШаблон("%1/status?status=%2",РесурсСервиса,Статус);
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT"));
	Если Ответ <> Неопределено И (Ответ.КодСостояния = 404 Или Ответ.КодСостояния = 200) Тогда
		СтатусУстановлен = Истина;
	Иначе
		СтатусУстановлен = Ложь;
	КонецЕсли;
	
	Возврат СтатусУстановлен;
	
КонецФункции

Функция ОбъектСправкаОбОстаткеОтпуска(ПараметрыОбмена, СтрокаТЗ, ТаблицаВложенийЗаявок, НеОбработанныеЗаявки, ФизическоеЛицоИдентификатор)

	ОбъектСправка = Неопределено;
	
	СтрокаВложений = ТаблицаВложенийЗаявок.Найти(СтрокаТЗ.Заявка, "Заявка");
	Если СтрокаВложений <> Неопределено Тогда
		
		Попытка
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(СтрокаВложений.ФайлЗаявки, РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла());
		Исключение
			ОписаниеОшибки = НСтр("ru = 'Ошибка получения данных файла.';
									|en = 'File data receiving error.'");
			НеОбработанныеЗаявки.Вставить(СтрокаТЗ.Заявка, ОписаниеОшибки);
			ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки());
			Возврат ОбъектСправка;
		КонецПопытки;
		
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		
		ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанныеФайла, ДанныеФайла.Расширение);
		Если ИдентификаторФайла = Неопределено Тогда
			ОписаниеОшибки = НСтр("ru = 'Ошибка публикации файла.';
									|en = 'File publishing error.'");
			НеОбработанныеЗаявки.Вставить(СтрокаТЗ.Заявка, ОписаниеОшибки);
			ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.Заявка, ОписаниеОшибки);
			Возврат ОбъектСправка;
		КонецЕсли;
		
		Если КабинетСотрудника.ВерсияФорматаМеньшеВерсии(ПараметрыОбмена.ВерсияDTO, "1.0") Тогда
			
			ОписаниеОбъектаВходящийФайл = ОписаниеОбъектаВходящийФайл();
			ОписаниеФайла = ОписаниеОбъекта(ОписаниеОбъектаВходящийФайл);
			ОписаниеФайла.НаименованиеФайла 	= ДанныеФайла.ИмяФайла;
			ОписаниеФайла.РасширениеФайла 		= ДанныеФайла.Расширение;
			ОписаниеФайла.РазмерФайла 			= ДанныеФайла.Размер;
			ОписаниеФайла.ИдентификаторФайла 	= ИдентификаторФайла;
			ОбъектФайл = ОбъектСервисаПоОписанию(ОписаниеФайла, ОписаниеОбъектаВходящийФайл);
			
			ОписаниеОбъектаСправкаОбОстаткеОтпуска = ОписаниеОбъектаСправкаОбОстаткеОтпуска(ПараметрыОбмена.ВерсияDTO);
			ОписаниеСправка = ОписаниеОбъекта(ОписаниеОбъектаСправкаОбОстаткеОтпуска);
			ОписаниеСправка.ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗаявки;
			ОписаниеСправка.Вложение = ОбъектФайл;
			ОбъектСправка = ОбъектСервисаПоОписанию(ОписаниеСправка, ОписаниеОбъектаСправкаОбОстаткеОтпуска);
			
		Иначе
			
			ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанныеФайла);
			ИмяФайлаБезРасширения = ИмяФайлаБезРасширения(ДанныеФайла.ИмяФайла, ДанныеФайла.Расширение);
			ОбъектФайл = ОбъектСервисаФайл(ИдентификаторФайла, ИмяФайлаБезРасширения, ДанныеФайла.Расширение, ДанныеФайла.Размер, ПараметрыОбмена.ВерсияDTO, ВерсияФайла);
			
			ОписаниеОбъектаСправкаОбОстаткеОтпуска = ОписаниеОбъектаСправкаОбОстаткеОтпуска(ПараметрыОбмена.ВерсияDTO);
			ОписаниеСправка = ОписаниеОбъекта(ОписаниеОбъектаСправкаОбОстаткеОтпуска);
			ОписаниеСправка.Заявка 				= СтрокаТЗ.Заявка;
			ОписаниеСправка.ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗаявки;
			ОписаниеСправка.ФизическоеЛицо 		= ФизическоеЛицоИдентификатор[СтрокаТЗ.ФизическоеЛицо];
			ОписаниеСправка.Вложение 			= ОбъектФайл;
			ОбъектСправка = ОбъектСервисаПоОписанию(ОписаниеСправка, ОписаниеОбъектаСправкаОбОстаткеОтпуска);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбъектСправка;

КонецФункции

Функция РезультатыСогласованияЗаявкиСПодписью(ИдентификаторЗаявки, РезультатВыполнения, ПрисоединенныйФайл, ИдентификаторДокумента, Комментарий, ВерсияDTO)

	Результат = Новый Структура("РезультатыСогласования,ОписаниеОшибки");
	
	Попытка
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл, РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла());
	Исключение
		ЗаписатьОшибкуВыгрузкиИзменений(ПрисоединенныйФайл, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОписаниеОшибки = НСтр("ru = 'Ошибка получения файла.';
								|en = 'File receiving error.'");
		Результат.ОписаниеОшибки = ОписаниеОшибки;
		Возврат Результат;
	КонецПопытки;
	
	РезультатыСогласования = Новый Массив;
	
	Если ДанныеФайла.ПодписанЭП Тогда
		
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанныеФайла);
		
		ОписаниеОбъектРезультатСогласования = ОписаниеОбъектРезультатСогласования(ВерсияDTO);
		ПодписиДокумента = ЭлектроннаяПодписьКЭДО.УстановленныеПодписи(ПрисоединенныйФайл);
		Для каждого ПодписьДокумента Из ПодписиДокумента Цикл
			
			Подписант = "";
			ИмяПодписанта = "";
			Если ЗначениеЗаполнено(ПодписьДокумента.УстановившийПодпись) Тогда
				Подписант = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодписьДокумента.УстановившийПодпись, "ФизическоеЛицо");
				Если ЗначениеЗаполнено(Подписант) И КабинетСотрудника.ФизическоеЛицоПубликуется(Подписант) Тогда
					ИмяПодписанта = Строка(Подписант);
				Иначе
					Подписант = "";
				КонецЕсли;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Подписант) И ЗначениеЗаполнено(ПодписьДокумента.КомуВыданСертификат) Тогда
				ИмяПодписанта = ПодписьДокумента.КомуВыданСертификат;
			КонецЕсли;
			
			РезультатСогласования = ОписаниеОбъекта(ОписаниеОбъектРезультатСогласования);
			РезультатСогласования.Подписант 	= Подписант;
			РезультатСогласования.ИмяПодписанта = ИмяПодписанта;
			РезультатСогласования.ИдентификаторДокумента = ИдентификаторЗаявки;
			РезультатСогласования.РезультатСогласования = РезультатВыполнения;
			РезультатСогласования.ЭлектроннаяПодпись 	= Base64Строка(ПодписьДокумента.Подпись);
			РезультатСогласования.ВерсияДокумента 		= ВерсияФайла;
			РезультатСогласования.Комментарий 			= Комментарий;
			РезультатСогласования.ДатаПодписи			= ОбщегоНазначения.ПредставлениеЛокальнойДатыСоСмещением(ПодписьДокумента.ДатаПодписи);
			РезультатСогласования.ИдентификаторЭлектронногоДокумента = ИдентификаторДокумента;
			РезультатСогласования.РольПодписанта 		= Перечисления.РолиПодписантовКЭДО.Исполнитель;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатСогласования, "ИмяФайла") Тогда
				РезультатСогласования.ИмяФайла 				= ПодписьДокумента.ИмяФайла;
			КонецЕсли;
			
			ДобавитьВКоллекциюРезультатСогласования(РезультатыСогласования, РезультатСогласования, ПодписьДокумента, ОписаниеОбъектРезультатСогласования);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если РезультатыСогласования.Количество() > 0 Тогда
		Результат.РезультатыСогласования = РезультатыСогласования;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция РесурсСтатусовСервиса(ПараметрыОбмена)

	РесурсСтатусов = Новый Соответствие;
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ, РесурсЗапросСправки2НДФЛ(ПараметрыОбмена.ВерсияAPI));
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы, РесурсЗапросСправкиСРаботы());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска, РесурсЗапросСправкиОбОстаткеОтпуска());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных, РесурсЗапросИзменениеЛичныхДанных(ПараметрыОбмена.ВерсияAPI));
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск, РесурсЗаявлениеНаОтпуск());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия, РесурсОтсутствие());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты, РесурсЗаявлениеНаНалоговыеВычеты());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска, РесурсЗаявлениеНаКомпенсациюОтпуска());
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ, РесурсЗаявлениеНаУдержаниеДСВ());
	
	Возврат РесурсСтатусов;

КонецФункции

Функция РесурсЗаявокСервисаБезКЭДО()

	РесурсСтатусов = Новый Соответствие;
	РесурсСтатусов.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска, РесурсСправкиОбОстаткеОтпуска());
	
	Возврат РесурсСтатусов;

КонецФункции

Функция ДокументыКЭДОПоПолюДокумент(СписокДокументов)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документы", СписокДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументКадровогоЭДО.Ссылка КАК Ссылка,
	|	ДокументКадровогоЭДО.ОснованиеДокумента КАК Документ,
	|	ДокументКадровогоЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ВЫБОР
	|		КОГДА ДокументКадровогоЭДО.ИдентификаторЗаявкиКабинетСотрудника <> ДокументКадровогоЭДО.ИдентификаторДокумента
	|			ТОГДА ДокументКадровогоЭДО.ИдентификаторДокумента
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИдентификаторДокумента
	|ИЗ
	|	Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|ГДЕ
	|	ДокументКадровогоЭДО.ОснованиеДокумента В(&Документы)
	|	И НЕ ДокументКадровогоЭДО.ПометкаУдаления
	|	И ДокументКадровогоЭДО.КатегорияДокумента = ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовКадровогоЭДО.ЗаявлениеСотрудника)";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования)

	ФизическиеЛица = Новый Массив;
	МЧД = Новый Массив;
	Для каждого Объект Из ВсеРезультатыСогласования Цикл
		Если ЗначениеЗаполнено(Объект.personID) Тогда
			ФизическиеЛица.Добавить(Объект.personID);
		КонецЕсли;
		Если Объект.Свойство("powerOfAttorney") И ЗначениеЗаполнено(Объект.powerOfAttorney) Тогда
			МЧД.Добавить(Объект.powerOfAttorney);
		КонецЕсли;
	КонецЦикла;
	
	ФизическоеЛицоИдентификатор = Новый Соответствие;
	Если ЗначениеЗаполнено(ФизическиеЛица) Тогда
		Ссылки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФизическиеЛица);
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
		ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Ссылки, ТипОбъекта);
	КонецЕсли;
	
	МЧДИдентификатор = Новый Соответствие;
	Если ЗначениеЗаполнено(МЧД) Тогда
		Ссылки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МЧД);
		ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МашиночитаемаяДоверенность;
		МЧДИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Ссылки, ТипОбъекта);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФизическиеЛица) Или ЗначениеЗаполнено(МЧД) Тогда
		Для каждого Объект Из ВсеРезультатыСогласования Цикл
			Если ЗначениеЗаполнено(Объект.personID) Тогда
				Объект.personID = ФизическоеЛицоИдентификатор[Объект.personID];
			КонецЕсли;
			Если Объект.Свойство("powerOfAttorney") И ЗначениеЗаполнено(Объект.powerOfAttorney) Тогда
				Объект.powerOfAttorney = МЧДИдентификатор[Объект.powerOfAttorney];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПубликацияИзмененийДокументовКЭДО

Функция НовыйРезультатВыгрузкиДокументов() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", 			Новый Массив);
	Результат.Вставить("НеВыгружено", 			Новый Массив);
	Результат.Вставить("ОтменитьРегистрацию", 	Новый Массив);
	Результат.Вставить("БылиОшибки", Ложь);
	Возврат Результат;
	
КонецФункции

Процедура ОпубликоватьИзмененияДокументов(ПараметрыОбмена, БылиОшибки)

	Если ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника") Тогда
		
		Попытка
			ИнтеграцияУправлениеПерсоналомСлужебный.ЗарегистрироватьДокументыНаПодпись();
		Исключение
			ШаблонОписания = НСтр(
			"ru = 'Произошла ошибка при регистрации изменений документов на подпись
			|Описание ошибки:
			|%1';
			|en = 'An error occurred upon registering the changes of the documents to sign
			|Error description:
			|%1'");
			ТекстОшибки = СтрШаблон(ШаблонОписания, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		КонецПопытки;
		
	КонецЕсли;
	
	РезультатОбработки = НовыйРезультатВыгрузкиДокументов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 500
	|	Изменения.Ссылка КАК ПубликуемыйДокумент,
	|	Изменения.ВерсияДанных КАК ВерсияДанных,
	|	ДокументКадровогоЭДО.ОснованиеДокумента КАК Документ,
	|	ДокументКадровогоЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ДокументКадровогоЭДО.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ТИПЗНАЧЕНИЯ(ДокументКадровогоЭДО.ОснованиеДокумента) КАК ТипДанных,
	|	ВЫБОР
	|		КОГДА ДокументКадровогоЭДО.ПометкаУдаления
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДокументКадровогоЭДО.КатегорияДокумента = ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовКадровогоЭДО.ЗаявлениеСотрудника)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется,
	|	Изменения.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|		ПО Изменения.Ссылка = ДокументКадровогоЭДО.Ссылка
	|ГДЕ
	|	Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись)
	|	И НЕ Изменения.ВыгружатьУдаление";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВсеИзменения = РезультатЗапроса.Выгрузить();
	
	Отбор = Новый Структура("Публикуется");
	Отбор.Публикуется = Ложь;
	ОтменаРегистрации = ВсеИзменения.НайтиСтроки(Отбор);
	Для каждого СтрокаТЗ Из ОтменаРегистрации Цикл
		РезультатОбработки.ОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент)
	КонецЦикла;
	
	Отбор.Публикуется = Истина;
	ТаблицаИзменений = ВсеИзменения.Скопировать(Отбор);
	ТипыДанных = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "ТипДанных", Истина);
	ТаблицаИзменений.Индексы.Добавить("ТипДанных");
	Отбор = Новый Структура("ТипДанных");
	
	ДокументыКПубликации = Новый Массив;
	НеобработанныеДокументы = Новый Массив;
	
	ТипДанныхЗаявкаСправкаСРаботы = БизнесПроцессыЗаявокСотрудников.ТипЗаявкаСотрудникаСправкаСМестаРаботы();
	
	ТаблицаДокументовНаПодпись = ТаблицаИзменений.СкопироватьКолонки();
	
	Для каждого ТипДанных Из ТипыДанных Цикл
		
		Отбор.ТипДанных = ТипДанных;
		ТаблицаПоТипуДанных = ТаблицаИзменений.Скопировать(Отбор);
		
		Если ТипДанных = Тип("ДокументСсылка.СправкаНДФЛ") Тогда
			РезультатВыгрузки = РезультатВыгрузки2НДФЛ(ПараметрыОбмена, ТаблицаПоТипуДанных);
			ДополнитьРезультатОбработки(РезультатОбработки, РезультатВыгрузки);
		ИначеЕсли ТипДанных = ТипДанныхЗаявкаСправкаСРаботы Тогда
			РезультатВыгрузки = РезультатВыгрузкиСправокСРаботы(ПараметрыОбмена, ТаблицаПоТипуДанных);
			ДополнитьРезультатОбработки(РезультатОбработки, РезультатВыгрузки);
		ИначеЕсли ТипДанных = Тип("ДокументСсылка.СогласиеНаПрисоединениеККЭДО") Тогда
			РезультатВыгрузки = РезультатВыгрузкиСогласийКЭДО(ПараметрыОбмена, ТаблицаПоТипуДанных);
			ДополнитьРезультатОбработки(РезультатОбработки, РезультатВыгрузки);
		Иначе
			
			Если Не КадровыйЭДО.ЭтоТипОбъектаСПечатнымиФормами(ТипДанных) Тогда
				Для каждого СтрокаТЗ Из ТаблицаПоТипуДанных Цикл
					РезультатОбработки.ОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				КонецЦикла;
				Продолжить;
			КонецЕсли;
			
			Для каждого СтрокаТЗ Из ТаблицаПоТипуДанных Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаДокументовНаПодпись.Добавить(), СтрокаТЗ);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатВыгрузки = РезультатВыгрузкиДокументовНаПодпись(ПараметрыОбмена, ТаблицаДокументовНаПодпись);
	ДополнитьРезультатОбработки(РезультатОбработки, РезультатВыгрузки);
	
	ОтменитьРегистрациюИзмененийДокументов(ТаблицаИзменений, РезультатОбработки);
	
	БылиОшибки = БылиОшибки Или РезультатОбработки.БылиОшибки;

КонецПроцедуры

Процедура ОтменитьРегистрациюИзмененийДокументов(ТаблицаИзменений, РезультатВыгрузки)

	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ТаблицаИзменений;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ПубликуемыйДокумент");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
		Запрос.УстановитьПараметр("Выгружено", РезультатВыгрузки.Выгружено);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИзменений.ПубликуемыйДокумент КАК ПубликуемыйДокумент,
		|	ТаблицаИзменений.ВерсияДанных КАК ВерсияДанных,
		|	ТаблицаИзменений.ТипОбъекта КАК ТипОбъекта
		|ПОМЕСТИТЬ ВТОтменитьИзменения
		|ИЗ
		|	&ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	(ТаблицаИзменений.ПубликуемыйДокумент В (&Выгружено)
		|			ИЛИ НЕ ТаблицаИзменений.Публикуется)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтменитьИзменения.ПубликуемыйДокумент КАК ПубликуемыйДокумент,
		|	ОтменитьИзменения.ТипОбъекта КАК ТипОбъекта
		|ИЗ
		|	ВТОтменитьИзменения КАК ОтменитьИзменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ИзмененияДляСервиса
		|		ПО ОтменитьИзменения.ПубликуемыйДокумент = ИзмененияДляСервиса.Ссылка
		|			И ОтменитьИзменения.ТипОбъекта = ИзмененияДляСервиса.ТипОбъекта
		|			И ОтменитьИзменения.ВерсияДанных = ИзмененияДляСервиса.ВерсияДанных";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Выборка.ПубликуемыйДокумент;
			МенеджерЗаписи.ТипОбъекта 	= Выборка.ТипОбъекта;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		Для Каждого ПубликуемыйДокумент Из РезультатВыгрузки.ОтменитьРегистрацию Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= ПубликуемыйДокумент;
			МенеджерЗаписи.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ОписаниеИзменений = НСтр("ru = 'Документы кадрового ЭДО';
								|en = 'HR EDI documents'");
		ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки());
		РезультатВыгрузки.БылиОшибки = Истина;
	КонецПопытки;

КонецПроцедуры

Процедура ДополнитьРезультатОбработки(РезультатОбработки, РезультатВыгрузки)

	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.Выгружено, РезультатВыгрузки.Выгружено);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, РезультатВыгрузки.НеВыгружено);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.ОтменитьРегистрацию, РезультатВыгрузки.ОтменитьРегистрацию);
	РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;	

КонецПроцедуры

Функция ЭлектронныйДокументРезультатыСогласования(ПараметрыОбмена, ДанныеЭлектронногоДокумента, ИдентификаторДокумента)
	
	Результат = Новый Структура("ЭлектронныйДокумент,РезультатыСогласования");
	
	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	
	Оригинал 		= ДанныеЭлектронногоДокумента.Оригинал;
	Подписи 		= ДанныеЭлектронногоДокумента.Подписи;
	Представления 	= ДанныеЭлектронногоДокумента.Представления;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Оригинал.АдресВоВременномХранилище);
	
	ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанныеФайла);
	
	ТекстОписания = НСтр("ru = 'Ошибка публикации файла.';
						|en = 'File publishing error.'");
	ВладелецФайлаТекст = НСтр("ru = 'Владелец файла';
								|en = 'Location'");
	ФайлТекст = НСтр("ru = 'Файл';
					|en = 'File'"); ;
	ШаблонОписанияОшибки = СтрШаблон("%1%2%3: %4, %5:", ТекстОписания, Символы.ПС, ВладелецФайлаТекст, Строка(Оригинал.Владелец), ФайлТекст);
	
	ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанныеФайла, Оригинал.РасширениеФайла);
	Если ИдентификаторФайла = Неопределено Тогда
		ОписаниеОшибки = СтрШаблон("%1 %2.", ШаблонОписанияОшибки, Оригинал.ИмяФайлаСРасширением);
		ЗаписатьОшибкуВыгрузкиФайла(ОписаниеОшибки);
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеОбъектРезультатСогласования = ОписаниеОбъектРезультатСогласования(ВерсияDTO);
	РезультатыСогласования = Новый Массив;
	Для каждого Подпись Из Подписи Цикл
		
		Если Перечисления.РолиПодписантовКЭДО.ЭтоПодписьСервиса(РолиПодписантовРезультатаСогласования(Подпись)) Тогда
			Продолжить;
		КонецЕсли;
		
		Подписант = "";
		ИмяПодписанта = "";
		Если ЗначениеЗаполнено(Подпись.УстановившийПодпись) Тогда
			Подписант = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подпись.УстановившийПодпись, "ФизическоеЛицо");
			Если ЗначениеЗаполнено(Подписант) И КабинетСотрудника.ФизическоеЛицоПубликуется(Подписант) Тогда
				ИмяПодписанта = Строка(Подписант);
			Иначе
				Подписант = "";
			КонецЕсли;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Подписант) И ЗначениеЗаполнено(Подпись.КомуВыданСертификат) Тогда
			ИмяПодписанта = Подпись.КомуВыданСертификат;
		КонецЕсли;
		
		ДвоичныеДанныеПодпись = ПолучитьИзВременногоХранилища(Подпись.АдресВоВременномХранилище);
		
		РезультатСогласования = ОписаниеОбъекта(ОписаниеОбъектРезультатСогласования);
		РезультатСогласования.Подписант 			= Подписант;
		РезультатСогласования.ИмяПодписанта 		= ИмяПодписанта;
		РезультатСогласования.ИдентификаторДокумента = ИдентификаторДокумента;
		РезультатСогласования.РезультатСогласования = Перечисления.РезультатыСогласованияБЗК.Согласовано;
		РезультатСогласования.ЭлектроннаяПодпись 	= Base64Строка(ДвоичныеДанныеПодпись);
		РезультатСогласования.ВерсияДокумента 		= ВерсияФайла;
		РезультатСогласования.ДатаПодписи			= ОбщегоНазначения.ПредставлениеЛокальнойДатыСоСмещением(Подпись.ДатаПодписи);
		РезультатСогласования.РольПодписанта 		= Перечисления.РолиПодписантовКЭДО.Организация;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатСогласования, "ИмяФайла") Тогда
			РезультатСогласования.ИмяФайла = Подпись.ИмяФайлаСРасширением;
		КонецЕсли;
		
		ДобавитьВКоллекциюРезультатСогласования(РезультатыСогласования, РезультатСогласования, Подпись, ОписаниеОбъектРезультатСогласования);
		
	КонецЦикла;
	
	ОбъектИсходныйДокумент = ОбъектСервисаФайл(ИдентификаторФайла, Оригинал.ИмяФайлаБезРасширения, Оригинал.РасширениеФайла, Оригинал.Размер, ВерсияDTO, ВерсияФайла);
	
	ПредставленияДокумента = Новый Массив;
	Для каждого ПредставлениеДокумента Из Представления Цикл
		
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ПредставлениеДокумента.АдресВоВременномХранилище);
		
		ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанныеФайла, ПредставлениеДокумента.РасширениеФайла);
		Если ИдентификаторФайла = Неопределено Тогда
			ОписаниеОшибки = СтрШаблон("%1 %2.", ШаблонОписанияОшибки, ПредставлениеДокумента.ИмяФайлаСРасширением);
			ЗаписатьОшибкуВыгрузкиФайла(ОписаниеОшибки);
			Возврат Результат;
		КонецЕсли;
		
		ОбъектФайл = ОбъектСервисаФайл(ИдентификаторФайла, ПредставлениеДокумента.ИмяФайлаБезРасширения, ПредставлениеДокумента.РасширениеФайла, ПредставлениеДокумента.Размер, ВерсияDTO);
		ПредставленияДокумента.Добавить(ОбъектФайл);
		
	КонецЦикла;
	
	ОписаниеОбъектаЭлектронныйДокумент = ОписаниеОбъектаЭлектронныйДокумент(ВерсияDTO);
	ОписаниеЭлектронныйДокумент = ОписаниеОбъекта(ОписаниеОбъектаЭлектронныйДокумент);
	ОписаниеЭлектронныйДокумент.ИсходныйДокумент = ОбъектИсходныйДокумент;
	Если ПредставленияДокумента.Количество() > 0 Тогда
		ОписаниеЭлектронныйДокумент.ПредставленияДокумента = ПредставленияДокумента;
	КонецЕсли;
	
	Результат.ЭлектронныйДокумент = ОбъектСервисаПоОписанию(ОписаниеЭлектронныйДокумент, ОписаниеОбъектаЭлектронныйДокумент);
	Результат.РезультатыСогласования = РезультатыСогласования;
	
	Возврат Результат;
	
КонецФункции

Функция ОбъектВходящийФайлПоПрисоединенномуФайлу(ПрисоединенныйФайл, ПараметрыОбмена) Экспорт

	Попытка
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл, РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла());
	Исключение
		ЗаписатьОшибкуВыгрузкиИзменений(ПрисоединенныйФайл, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанныеФайла, ДанныеФайла.Расширение);
	Если ИдентификаторФайла = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка публикации файла.';
								|en = 'File publishing error.'");
		ЗаписатьОшибкуВыгрузкиИзменений(ПрисоединенныйФайл, ОписаниеОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеФайла = ОписаниеОбъекта(ОписаниеОбъектаВходящийФайл());
	ОписаниеФайла.НаименованиеФайла 	= ДанныеФайла.ИмяФайла;
	ОписаниеФайла.РасширениеФайла 		= ДанныеФайла.Расширение;
	ОписаниеФайла.РазмерФайла 			= ДанныеФайла.Размер;
	ОписаниеФайла.ИдентификаторФайла 	= ИдентификаторФайла;
	ОбъектФайл = ОбъектСервисаПоОписанию(ОписаниеФайла, ОписаниеОбъектаВходящийФайл());
	
	Возврат ОбъектФайл;

КонецФункции

#Область ВыгрузкаДокументовНаПодпись

Функция РезультатВыгрузкиДокументовНаПодпись(ПараметрыОбмена, ТаблицаДокументовНаПодпись)

	РезультатОбработки = НовыйРезультатВыгрузкиДокументов();
	Если ТаблицаДокументовНаПодпись.Количество() = 0 Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	ИспользоватьФормат303 = КабинетСотрудника.ИспользоватьФормат303();
	ИспользоватьФормат503 = КабинетСотрудника.ИспользоватьФормат503();
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	ИспользуетсяВерсия_1_2 = КабинетСотрудника.ИспользуетсяВерсияФормата("1.2");
	
	ПубликоватьДокументыНаПодпись = ИспользоватьФормат303 И ИспользуетсяКадровыйЭДО;
	Если Не ПубликоватьДокументыНаПодпись Тогда
		Для каждого СтрокаТЗ Из ТаблицаДокументовНаПодпись Цикл
			РезультатОбработки.ОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
		КонецЦикла;
		Возврат РезультатОбработки;
	КонецЕсли;
	
	// Массив ссылок на документы КЭДО, по которым
	// не удалось сформировать объект для выгрузки.
	НеОбработанныеДокументы = Новый Массив;
	
	ТаблицаДанных = ДанныеДляВыгрузкиДокументовНаПодпись(ТаблицаДокументовНаПодпись);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	
	ФизическиеЛица = Новый Массив;
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛица, СтрокаТЗ.ФизическиеЛица, Истина);
	КонецЦикла;
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(ФизическиеЛица, ТипОбъекта);
	
	// Коллекция документов с результатами согласования, электронной подписью.
	// Ключ - ссылка на Документ,
	// Значение - массив объектов РезультатСогласования.
	ДокументРезультатыСогласования = Новый Соответствие;
	
	// Коллекция документов КЭДО.
	// Ключ - идентификатор документа,
	// Значение - ссылка на ДокументКЭДО.
	ИдентификаторыДокументов = Новый Соответствие;
	
	// Массив объектов документов для публикации.
	ДокументыНаПодписьКПубликации = Новый Массив;
	
	// Результаты согласования для заполнения идентификаторов.
	ВсеРезультатыСогласования = Новый Массив;
	
	ОписаниеОбъектаДокументНаПодпись = ОписаниеОбъектаДокументНаПодпись(ПараметрыОбмена.ВерсияDTO);
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		Ответ = ЭлектронныйДокументРезультатыСогласования(ПараметрыОбмена, СтрокаТЗ.ДанныеЭлектронногоДокумента, СтрокаТЗ.ИдентификаторДокумента);
		
		ОбъектЭлектронныйДокумент 	= Ответ.ЭлектронныйДокумент;
		Если ОбъектЭлектронныйДокумент = Неопределено Тогда
			РезультатОбработки.БылиОшибки = Истина;
			НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
			Продолжить;
		КонецЕсли;
		
		Если Не СтрокаТЗ.РасчетныйЛисток Тогда
			РезультатыСогласования = Ответ.РезультатыСогласования;	
			Если ЗначениеЗаполнено(РезультатыСогласования) Тогда
				ДокументРезультатыСогласования.Вставить(СтрокаТЗ.ПубликуемыйДокумент, РезультатыСогласования);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеРезультатыСогласования, РезультатыСогласования);
			КонецЕсли;
		КонецЕсли;
		
		ОписаниеДокументНаПодпись =  ОписаниеОбъекта(ОписаниеОбъектаДокументНаПодпись);
		ОписаниеДокументНаПодпись.ИдентификаторДокумента			= СтрокаТЗ.ИдентификаторДокумента;
		ОписаниеДокументНаПодпись.Организация						= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		ФизическиеЛица = Новый Массив;
		Для каждого ФизическоеЛицо Из СтрокаТЗ.ФизическиеЛица Цикл
			ФизическиеЛица.Добавить(ФизическоеЛицоИдентификатор[ФизическоеЛицо]);
		КонецЦикла;
		ОписаниеДокументНаПодпись.ФизическиеЛица					= ФизическиеЛица;
		ОписаниеДокументНаПодпись.Дата								= СтрокаТЗ.ДатаДокумента;
		ОписаниеДокументНаПодпись.НазваниеДокумента					= СтрокаТЗ.НазваниеДокумента;
		ОписаниеДокументНаПодпись.ЭлектронныйДокумент				= ОбъектЭлектронныйДокумент;
		Если ИспользуетсяВерсия_1_2 Тогда
			Если СтрокаТЗ.РасчетныйЛисток Тогда
				ОписаниеДокументНаПодпись.ОтправлятьУведомление = Ложь;
			Иначе
				ОписаниеДокументНаПодпись.ОтправлятьУведомление = Истина;
			КонецЕсли;
		КонецЕсли;
		Если ИспользоватьФормат503 Тогда
			ОписаниеДокументНаПодпись.ВозможноОбновлениеПредставлений = СтрокаТЗ.ВозможноОбновлениеПредставлений;
		КонецЕсли;
		ОбъектДокументНаПодпись = ОбъектСервисаПоОписанию(ОписаниеДокументНаПодпись, ОписаниеОбъектаДокументНаПодпись);
		
		ДокументыНаПодписьКПубликации.Добавить(ОбъектДокументНаПодпись);
		ИдентификаторыДокументов.Вставить(СтрокаТЗ.ИдентификаторДокумента, СтрокаТЗ.ПубликуемыйДокумент);
		
	КонецЦикла;
	
	РесурсСервиса = РесурсДокументыНаПодпись();
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				ДокументыНаПодписьКПубликации,
				"ID", РазмерПакета);
		
	РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования);
	
	ВыгруженныеДокументы = Новый Соответствие;
	РезультатыСогласованияКПубликации = Новый Массив;
	Для Каждого ИдентификаторДокумента Из РезультатВыгрузки.Выгружено Цикл
		ДокументНаПодпись = ИдентификаторыДокументов[ИдентификаторДокумента];
		ВыгруженныеДокументы.Вставить(ДокументНаПодпись, Истина);
		РезультатыСогласования = ДокументРезультатыСогласования[ДокументНаПодпись];
		Если РезультатыСогласования <> Неопределено Тогда
			Для каждого РезультатСогласования Из РезультатыСогласования Цикл
				РезультатыСогласованияКПубликации.Добавить(РезультатСогласования);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
		СтрокаДанных = ТаблицаДанных.Найти(Ошибка.Ключ, "ИдентификаторДокумента");
		РезультатОбработки.НеВыгружено.Добавить(СтрокаДанных.ПубликуемыйДокумент);
		ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаДанных.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
	КонецЦикла;
	
	Если РезультатыСогласованияКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсРезультатыСогласования();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
			ПараметрыОбмена,
			РесурсСервиса,
			РезультатыСогласованияКПубликации,
			"documentID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			СтрокаДанных = ТаблицаДанных.Найти(Ошибка.Ключ, "ИдентификаторДокумента");
			РезультатОбработки.НеВыгружено.Добавить(СтрокаДанных.ПубликуемыйДокумент);
			ВыгруженныеДокументы.Удалить(СтрокаДанных.ПубликуемыйДокумент);
			ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаДанных.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ЭлементКоллекции Из ВыгруженныеДокументы Цикл
		РезультатОбработки.Выгружено.Добавить(ЭлементКоллекции.Ключ);
		КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(ЭлементКоллекции.Ключ, ТекущаяДатаСеанса());
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, НеОбработанныеДокументы);
	
	Возврат РезультатОбработки
	
КонецФункции

Функция ДанныеДляВыгрузкиДокументовНаПодпись(ТаблицаИзменений)

	ТаблицаДокументыНаПодпись = Новый ТаблицаЗначений;
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ПубликуемыйДокумент");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ЭлектронныйДокумент");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ИдентификаторДокумента");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ДанныеЭлектронногоДокумента");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("Организация");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ФизическиеЛица");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ДатаДокумента");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("НазваниеДокумента");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("ВозможноОбновлениеПредставлений");
	ТаблицаДокументыНаПодпись.Колонки.Добавить("РасчетныйЛисток", Новый ОписаниеТипов("Булево"));
	
	ДокументыОснования = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "Документ", Истина);
	ДокументыОснованияРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДокументыОснования, "Дата,Представление");
	
	ЭлектронныеДокументы = ТаблицаИзменений.ВыгрузитьКолонку("ЭлектронныйДокумент");
	ДанныеЭлектронныхДокументов = КадровыйЭДОВызовСервера.ДанныеФайловПечатныхФорм(ЭлектронныеДокументы);
	
	ПубликуемыеДокументы = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "ПубликуемыйДокумент", Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", ПубликуемыеДокументы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументКадровогоЭДО.Ссылка КАК Ссылка,
	|	ДокументКадровогоЭДО.Организация КАК Организация,
	|	ДокументКадровогоЭДО.ВнешниеПодписанты.(
	|		ФизическоеЛицо КАК ФизическоеЛицо
	|	) КАК ВнешниеПодписанты,
	|	ВЫБОР
	|		КОГДА ДокументКадровогоЭДО.КатегорияДокумента = ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовКадровогоЭДО.РасчетныйЛисток)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РасчетныйЛисток
	|ИЗ
	|	Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|ГДЕ
	|	ДокументКадровогоЭДО.Ссылка В(&Ссылки)";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	ПубликуемыеДокументыРеквизиты = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ЗначенияРеквизитов = Новый Структура("Организация,ФизическиеЛица,РасчетныйЛисток");
		ЗначенияРеквизитов.Организация = Выборка.Организация;
		ЗначенияРеквизитов.ФизическиеЛица = Выборка.ВнешниеПодписанты.Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		ЗначенияРеквизитов.РасчетныйЛисток = Выборка.РасчетныйЛисток;
		ПубликуемыеДокументыРеквизиты.Вставить(Выборка.Ссылка, ЗначенияРеквизитов);
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
		НоваяСтрока = ТаблицаДокументыНаПодпись.Добавить();
		НоваяСтрока.ПубликуемыйДокумент 	= СтрокаТЗ.ПубликуемыйДокумент;
		НоваяСтрока.ЭлектронныйДокумент 	= СтрокаТЗ.ЭлектронныйДокумент;
		НоваяСтрока.ИдентификаторДокумента 	= СтрокаТЗ.ИдентификаторДокумента;
		НоваяСтрока.ДанныеЭлектронногоДокумента = ДанныеЭлектронныхДокументов[СтрокаТЗ.ЭлектронныйДокумент];
		НоваяСтрока.Организация 		= ПубликуемыеДокументыРеквизиты[СтрокаТЗ.ПубликуемыйДокумент].Организация;
		НоваяСтрока.ФизическиеЛица 		= ПубликуемыеДокументыРеквизиты[СтрокаТЗ.ПубликуемыйДокумент].ФизическиеЛица;
		НоваяСтрока.РасчетныйЛисток 	= ПубликуемыеДокументыРеквизиты[СтрокаТЗ.ПубликуемыйДокумент].РасчетныйЛисток;
		НоваяСтрока.ДатаДокумента 		= ДокументыОснованияРеквизиты[СтрокаТЗ.Документ].Дата;
		НазваниеДокумента = "";
		Если ЗначениеЗаполнено(НоваяСтрока.ДанныеЭлектронногоДокумента) Тогда
			НазваниеДокумента = НоваяСтрока.ДанныеЭлектронногоДокумента.Оригинал.ИмяФайлаСРасширением;
		Иначе
			НазваниеДокумента = ДокументыОснованияРеквизиты[СтрокаТЗ.Документ].Представление;
		КонецЕсли;
		НоваяСтрока.НазваниеДокумента 	= НазваниеДокумента;
		НоваяСтрока.ВозможноОбновлениеПредставлений = НоваяСтрока.ДанныеЭлектронногоДокумента <> Неопределено;
		
	КонецЦикла;
	
	Возврат ТаблицаДокументыНаПодпись;

КонецФункции

#КонецОбласти

#Область ВыгрузкаСправокСРаботы

Функция РезультатВыгрузкиСправокСРаботы(ПараметрыОбмена, ТаблицаПоТипуДанных)

	РезультатОбработки = НовыйРезультатВыгрузкиДокументов();
	
	ИспользоватьФормат303 = КабинетСотрудника.ИспользоватьФормат303();
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	
	// Массив ссылок на документы КЭДО, по которым
	// не удалось сформировать объект для выгрузки.
	НеОбработанныеДокументы = Новый Массив;
	
	// Массив ссылок на документы КЭДО, по которым
	// заявка отказана, отменяем публикацию таких документов.
	ДокументыОтменитьРегистрацию = Новый Массив;
	
	ТаблицаДанных = ДанныеДляВыгрузкиСправокСРаботы(ТаблицаПоТипуДанных);
	Если ТаблицаДанных.Количество() = 0 Тогда
		РезультатОбработки.БылиОшибки = Истина;
		ОписаниеИзменений = НСтр("ru = 'Справки с места работы';
								|en = 'Statements of employment'");
		ОписаниеОшибки = НСтр("ru = 'Не удалось получить данные справок';
								|en = 'Failed to receive statement data'");
		ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки);
		Для каждого СтрокаТЗ Из ТаблицаПоТипуДанных Цикл
			НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, НеОбработанныеДокументы);
		Возврат РезультатОбработки;
	КонецЕсли;
	
	// Коллекция заявок с результатами согласования.
	// Ключ - ИдентификаторДокумента,
	// Значение - массив объектов РезультатСогласования.
	ДокументыРезультатыСогласования = Новый Соответствие;
	
	// Коллекция для получения документа КЭДО.
	// Ключ - идентификатор документа
	// Значение - ссылка на документ КЭДО.
	ИдентификаторыДокументов = Новый Соответствие;
	
	// Массив объектов справка для публикации.
	СправкиКПубликации = Новый Массив;
	
	// Результаты согласования для заполнения идентификаторов.
	ВсеРезультатыСогласования = Новый Массив;
	
	ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	
	ОписаниеОбъектаСправкаСРаботы = ОписаниеОбъектаСправкаСРаботы(ПараметрыОбмена.ВерсияDTO);
	ОписаниеОбъектаЭлектронныйДокумент = ОписаниеОбъектаЭлектронныйДокумент(ПараметрыОбмена.ВерсияDTO);
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		Если СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Отказ Тогда
			ДокументыОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
			Продолжить;
		ИначеЕсли СтрокаТЗ.СостояниеЗаявки <> Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена Тогда
			// Заявка еще не выполнена, пропускаем обработку документа.
			Продолжить;
		КонецЕсли;
		
		ИдентификаторыДокументов.Вставить(СтрокаТЗ.ИдентификаторДокумента, СтрокаТЗ.ПубликуемыйДокумент);
		
		ОписаниеСправки = ОписаниеОбъекта(ОписаниеОбъектаСправкаСРаботы);
		ОписаниеСправки.Заявка 				= СтрокаТЗ.ИдентификаторДокумента;
		ОписаниеСправки.ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗаявки;
		ОписаниеСправки.Организация 		= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		ОписаниеСправки.ФизическоеЛицо 		= ФизическоеЛицоИдентификатор[СтрокаТЗ.ФизическоеЛицо];
		ОписаниеСправки.Комментарий 		= СтрокаТЗ.Комментарий;
		
		Если ИспользоватьФормат303 Тогда
			
			ТребуетсяЭП = (СтрокаТЗ.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ФайлСЭП);
				
			Если ТребуетсяЭП И СтрокаТЗ.ДанныеЭлектронногоДокумента <> Неопределено Тогда
			
				Ответ = ЭлектронныйДокументРезультатыСогласования(ПараметрыОбмена, СтрокаТЗ.ДанныеЭлектронногоДокумента, СтрокаТЗ.ИдентификаторДокумента);
				
				ОбъектЭлектронныйДокумент 	= Ответ.ЭлектронныйДокумент;
				РезультатыСогласования 		= Ответ.РезультатыСогласования;
				Если ОбъектЭлектронныйДокумент = Неопределено Тогда
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					Продолжить;
				ИначеЕсли Не ЗначениеЗаполнено(РезультатыСогласования) Тогда
					// нет ЭП с соответствующей ролью подписанта
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					ОписаниеОшибки = НСтр("ru = 'Отсутствует электронная подпись файла.';
											|en = 'Digital signature is missing for the file.'");
					ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.ЭлектронныйДокумент, ОписаниеОшибки);
					Продолжить;
				КонецЕсли;
				
				ДокументыРезультатыСогласования.Вставить(СтрокаТЗ.ИдентификаторДокумента, РезультатыСогласования);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеРезультатыСогласования, РезультатыСогласования);
				
			Иначе
				
				Если Не ЗначениеЗаполнено(СтрокаТЗ.ЭлектронныйДокумент) Тогда
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					ОписаниеОшибки = НСтр("ru = 'Нет данных электронного документа.';
											|en = 'No electronic document data.'");
					ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.ПубликуемыйДокумент, ОписаниеОшибки);
					Продолжить;	
				КонецЕсли;
				
				ОбъектФайл = ОбъектФайлПоПрисоединенномуФайлу(СтрокаТЗ.ЭлектронныйДокумент, ПараметрыОбмена);
				
				Если ОбъектФайл = Неопределено Тогда
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					Продолжить;
				КонецЕсли;
				
				ОписаниеЭлектронныйДокумент = ОписаниеОбъекта(ОписаниеОбъектаЭлектронныйДокумент);
				ОписаниеЭлектронныйДокумент.ИсходныйДокумент = ОбъектФайл;
				ОбъектЭлектронныйДокумент = ОбъектСервисаПоОписанию(ОписаниеЭлектронныйДокумент, ОписаниеОбъектаЭлектронныйДокумент);
				
			КонецЕсли;
			
			ОписаниеСправки.ЭлектронныйДокумент = ОбъектЭлектронныйДокумент;
			
		Иначе
			
			ОбъектВходящийФайл = ОбъектВходящийФайлПоПрисоединенномуФайлу(СтрокаТЗ.ЭлектронныйДокумент, ПараметрыОбмена);
			Если ОбъектВходящийФайл = Неопределено Тогда
				РезультатОбработки.БылиОшибки = Истина;
				НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				Продолжить;
			КонецЕсли;
			
			ОписаниеСправки.Вложение = ОбъектВходящийФайл;
			
		КонецЕсли;
		
		ОбъектСправка = ОбъектСервисаПоОписанию(ОписаниеСправки, ОписаниеОбъектаСправкаСРаботы);
		
		СправкиКПубликации.Добавить(ОбъектСправка);
		
	КонецЦикла;
	
	РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования);
	
	РезультатыСогласованияКПубликации = Новый Массив;
	ВыгруженныеДокументы = Новый Соответствие;
	
	Если СправкиКПубликации.Количество() > 0  Тогда
		
		РесурсСервиса = РесурсСправкиСМестаРаботы();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СправкаСМестаРаботы);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
			ПараметрыОбмена,
			РесурсСервиса,
			СправкиКПубликации,
			"ID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого ИдентификаторДокумента Из РезультатВыгрузки.Выгружено Цикл
			ВыгруженныеДокументы.Вставить(ИдентификаторыДокументов[ИдентификаторДокумента], Истина);
			РезультатыСогласования = ДокументыРезультатыСогласования[ИдентификаторДокумента];
			Если РезультатыСогласования <> Неопределено Тогда
				Для каждого РезультатСогласования Из РезультатыСогласования Цикл
					РезультатыСогласованияКПубликации.Добавить(РезультатСогласования);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			РезультатОбработки.НеВыгружено.Добавить(ИдентификаторыДокументов[Ошибка.Ключ]);
			ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторыДокументов[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Если РезультатыСогласованияКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсРезультатыСогласования();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
			ПараметрыОбмена,
			РесурсСервиса,
			РезультатыСогласованияКПубликации,
			"documentID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			РезультатОбработки.НеВыгружено.Добавить(ИдентификаторыДокументов[Ошибка.Ключ]);
			ВыгруженныеДокументы.Удалить(ИдентификаторыДокументов[Ошибка.Ключ]);
			ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторыДокументов[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ЭлементКоллекции Из ВыгруженныеДокументы Цикл
		РезультатОбработки.Выгружено.Добавить(ЭлементКоллекции.Ключ);
		КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(ЭлементКоллекции.Ключ, ТекущаяДатаСеанса());
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.ОтменитьРегистрацию, ДокументыОтменитьРегистрацию);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, НеОбработанныеДокументы);
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ДанныеДляВыгрузкиСправокСРаботы(ТаблицаИзменений)

	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("ПубликуемыйДокумент");
	ТаблицаДанных.Колонки.Добавить("ИдентификаторДокумента");
	ТаблицаДанных.Колонки.Добавить("Заявка");
	ТаблицаДанных.Колонки.Добавить("СостояниеЗаявки");
	ТаблицаДанных.Колонки.Добавить("ИдентификаторЗаявки");
	ТаблицаДанных.Колонки.Добавить("Организация");
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо");
	ТаблицаДанных.Колонки.Добавить("ЭлектронныйДокумент");
	ТаблицаДанных.Колонки.Добавить("ДанныеЭлектронногоДокумента");
	ТаблицаДанных.Колонки.Добавить("ВариантФормированияФайлаОтвета");
	ТаблицаДанных.Колонки.Добавить("Комментарий");
	
	Заявки = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "Документ", Истина);
	ДанныеЗаявок = БизнесПроцессыЗаявокСотрудников.ДанныеЗаявокСправкиСРаботы(Заявки);
	Если ДанныеЗаявок = Неопределено Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	
	ЭлектронныеДокументыСправок = Новый Массив;
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника") Тогда
		ЭлектронныеДокументыСправок = ТаблицаИзменений.ВыгрузитьКолонку("ЭлектронныйДокумент");
	Иначе
		Отбор = Новый Структура("ВариантФормированияФайлаОтвета", Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ФайлСЭП);
		НайденныеСтроки = ДанныеЗаявок.НайтиСтроки(Отбор);
		Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
			Если СтрокаТЗ.СостояниеЗаявки <> Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицыИзменений = ТаблицаИзменений.Найти(СтрокаТЗ.Заявка, "Документ");
			Если ЗначениеЗаполнено(СтрокаТаблицыИзменений) Тогда
				ЭлектронныеДокументыСправок.Добавить(СтрокаТаблицыИзменений.ЭлектронныйДокумент);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЭлектронныеДокументы = Новый Массив;
	Для каждого ЭлектронныйДокумент Из ЭлектронныеДокументыСправок Цикл
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			ЭлектронныеДокументы.Добавить(ЭлектронныйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеЭлектронныхДокументов = Новый Соответствие;
	Если ЭлектронныеДокументы.Количество() > 0 Тогда
		Попытка
			ДанныеЭлектронныхДокументов = КадровыйЭДОВызовСервера.ДанныеФайловПечатныхФорм(ЭлектронныеДокументы);
		Исключение
			Возврат ТаблицаДанных;
		КонецПопытки;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
		
		СтрокаЗаявки = ДанныеЗаявок.Найти(СтрокаТЗ.Документ, "Заявка");
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		НоваяСтрока.ПубликуемыйДокумент 	= СтрокаТЗ.ПубликуемыйДокумент;
		НоваяСтрока.ИдентификаторДокумента 	= СтрокаТЗ.ИдентификаторДокумента;
		НоваяСтрока.Заявка 					= СтрокаТЗ.Документ;
		НоваяСтрока.ЭлектронныйДокумент 	= СтрокаТЗ.ЭлектронныйДокумент;
		НоваяСтрока.ИдентификаторЗаявки 	= СтрокаЗаявки.ИдентификаторЗаявки;
		НоваяСтрока.Организация 			= СтрокаЗаявки.Организация;
		НоваяСтрока.ФизическоеЛицо 			= СтрокаЗаявки.ФизическоеЛицо;
		НоваяСтрока.Комментарий 			= СтрокаЗаявки.Комментарий;
		НоваяСтрока.СостояниеЗаявки 		= СтрокаЗаявки.СостояниеЗаявки;
		НоваяСтрока.ВариантФормированияФайлаОтвета 	= СтрокаЗаявки.ВариантФормированияФайлаОтвета;
		НоваяСтрока.ДанныеЭлектронногоДокумента 	= ДанныеЭлектронныхДокументов[СтрокаТЗ.ЭлектронныйДокумент];
	
	КонецЦикла;
	
	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Область Выгрузка2НФДЛ

Функция РезультатВыгрузки2НДФЛ(ПараметрыОбмена, ТаблицаПоТипуДанных)

	РезультатОбработки = НовыйРезультатВыгрузкиДокументов();
	
	ИспользоватьФормат303 = КабинетСотрудника.ИспользоватьФормат303();
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	
	ТаблицаДанных = ДанныеДляВыгрузки2НДФЛ(ТаблицаПоТипуДанных);
	ТаблицаДанных.Сортировать("ИдентификаторЗаявки");
	
	// Массив ссылок на документы КЭДО, которые не обрабатываем,
	// если ИспользоватьФормат303 = Ложь, снимаем с регистрации изменений
	// после выгрузки данных.
	ДокументыОтменитьРегистрацию = Новый Массив;
	
	// Массив ссылок на документы КЭДО, по которым
	// не удалось сформировать объект для выгрузки.
	НеОбработанныеДокументы = Новый Массив;
	
	// Коллекция с результатами согласования.
	// Ключ - ИдентификаторДокумента
	// Значение - массив объектов РезультатСогласования.
	ДокументРезультатыСогласования = Новый Соответствие;
	
	// Массив объектов справка 2НДФЛ для публикации когда ИспользоватьФормат303 = Истина.
	СправкиКПубликации = Новый Массив;
	
	// Коллекция объектов справка 2НДФЛ для формирования ответа на запрос справок,
	// когда ИспользоватьФормат303 = Ложь.
	// Ключ - ИдентификаторЗаявки
	// Значение - массив объектов справка 2НДФЛ.
	ОбъектыСправкаПоИдентификаторам = Новый Соответствие;
	
	// Коллекция для получения документа КЭДО.
	// Ключ - идентификатор документа
	// Значение - ссылка на документ КЭДО.
	ИдентификаторыДокументов = Новый Соответствие;
	
	// Коллекция для получения документа КЭДО.
	// Ключ - идентификатор документа
	// Значение - ссылка на справку 2НДФЛ.
	ИдентификаторыДокументовСправки = Новый Соответствие;
	
	// Результаты согласования для заполнения идентификаторов.
	ВсеРезультатыСогласования = Новый Массив;
	
	ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаДанных, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	
	ОписаниеОбъектаСправка2НДФЛ = ОписаниеОбъектаСправка2НДФЛ(ПараметрыОбмена.ВерсияDTO);
	ОписаниеОбъектаЭлектронныйДокумент = ОписаниеОбъектаЭлектронныйДокумент(ПараметрыОбмена.ВерсияDTO);
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		Если СтрокаТЗ.СостояниеЗаявки <> Неопределено Тогда
			Если СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Отказ Тогда
				ДокументыОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				Продолжить;
			ИначеЕсли СтрокаТЗ.СостояниеЗаявки <> Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена Тогда
				// Заявка еще не выполнена, пропускаем обработку документа.
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ИдентификаторыДокументов.Вставить(СтрокаТЗ.ИдентификаторДокумента, СтрокаТЗ.ПубликуемыйДокумент);
		ИдентификаторыДокументовСправки.Вставить(СтрокаТЗ.ИдентификаторДокумента, СтрокаТЗ.Справка2НДФЛ);
		
		Если Не ИспользоватьФормат303 И Не ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторЗаявки) Тогда
			ДокументыОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
			Продолжить;
		КонецЕсли;
		
		ОписаниеСправка2НДФЛ = ОписаниеОбъекта(ОписаниеОбъектаСправка2НДФЛ);
		ОписаниеСправка2НДФЛ.Справка2НДФЛ 		= СтрокаТЗ.ИдентификаторДокумента;
		ОписаниеСправка2НДФЛ.Организация 		= ОрганизацияИдентификатор[СтрокаТЗ.Организация];
		ОписаниеСправка2НДФЛ.ФизическоеЛицо 	= ФизическоеЛицоИдентификатор[СтрокаТЗ.ФизическоеЛицо];
		ОписаниеСправка2НДФЛ.НалоговыйПериод 	= СтрокаТЗ.НалоговыйПериод;
		ОписаниеСправка2НДФЛ.СуммаДохода 		= СтрокаТЗ.СуммаДохода;
		ОписаниеСправка2НДФЛ.СуммаНалога 		= СтрокаТЗ.СуммаНалога;
		ОписаниеСправка2НДФЛ.ДатаСоздания 		= СтрокаТЗ.ДатаСоздания;
		ОписаниеСправка2НДФЛ.Комментарий 		= СтрокаТЗ.Комментарий;
		
		Если ИспользоватьФормат303 Тогда
			
			// Если это справка на основании заявки из сервиса.
			Если ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторЗаявки) Тогда
				ОписаниеСправка2НДФЛ.ИдентификаторЗаявки = СтрокаТЗ.ИдентификаторЗаявки;
			КонецЕсли;
			
			ТребуетсяЭП = СтрокаТЗ.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ФайлСЭП;
				
			Если ТребуетсяЭП И СтрокаТЗ.ДанныеЭлектронногоДокумента <> Неопределено Тогда
				
				Ответ = ЭлектронныйДокументРезультатыСогласования(ПараметрыОбмена, СтрокаТЗ.ДанныеЭлектронногоДокумента, СтрокаТЗ.ИдентификаторДокумента);
				
				ОбъектЭлектронныйДокумент 	= Ответ.ЭлектронныйДокумент;
				РезультатыСогласования 		= Ответ.РезультатыСогласования;
				Если ОбъектЭлектронныйДокумент = Неопределено Тогда
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					Продолжить;
				ИначеЕсли Не ЗначениеЗаполнено(РезультатыСогласования) Тогда
					// нет ЭП с соответствующей ролью подписанта
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					ОписаниеОшибки = НСтр("ru = 'Отсутствует электронная подпись файла.';
											|en = 'Digital signature is missing for the file.'");
					ЗаписатьОшибкуВыгрузкиИзменений(СтрокаТЗ.ЭлектронныйДокумент, ОписаниеОшибки);
					Продолжить;
				КонецЕсли;
				
				ДокументРезультатыСогласования.Вставить(СтрокаТЗ.ИдентификаторДокумента, РезультатыСогласования);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеРезультатыСогласования, РезультатыСогласования);
				
			Иначе
				
				ОбъектФайл = ОбъектФайлПоПрисоединенномуФайлу(СтрокаТЗ.ЭлектронныйДокумент, ПараметрыОбмена);
				
				Если ОбъектФайл = Неопределено Тогда
					РезультатОбработки.БылиОшибки = Истина;
					НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					Продолжить;
				КонецЕсли;
				
				ОписаниеЭлектронныйДокумент = ОписаниеОбъекта(ОписаниеОбъектаЭлектронныйДокумент);
				ОписаниеЭлектронныйДокумент.ИсходныйДокумент = ОбъектФайл;
				ОбъектЭлектронныйДокумент = ОбъектСервисаПоОписанию(ОписаниеЭлектронныйДокумент, ОписаниеОбъектаЭлектронныйДокумент);
				
			КонецЕсли;
			
			ОписаниеСправка2НДФЛ.ЭлектронныйДокумент = ОбъектЭлектронныйДокумент;
			
			ОбъектСправка2НДФЛ = ОбъектСервисаПоОписанию(ОписаниеСправка2НДФЛ, ОписаниеОбъектаСправка2НДФЛ);
			СправкиКПубликации.Добавить(ОбъектСправка2НДФЛ);
			
		Иначе
			
			ОбъектВходящийФайл = ОбъектВходящийФайлПоПрисоединенномуФайлу(СтрокаТЗ.ЭлектронныйДокумент, ПараметрыОбмена);
						
			Если ОбъектВходящийФайл = Неопределено Тогда
				РезультатОбработки.БылиОшибки = Истина;
				НеОбработанныеДокументы.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				Продолжить;
			КонецЕсли;
			
			ОписаниеСправка2НДФЛ.Вложение = ОбъектВходящийФайл;
			
			ОбъектСправка2НДФЛ = ОбъектСервисаПоОписанию(ОписаниеСправка2НДФЛ, ОписаниеОбъектаСправка2НДФЛ);
			
			МассивСправок = ОбъектыСправкаПоИдентификаторам[СтрокаТЗ.ИдентификаторЗаявки];
			Если МассивСправок = Неопределено Тогда
				МассивСправок = Новый Массив;
			КонецЕсли;
			МассивСправок.Добавить(ОбъектСправка2НДФЛ);
			ОбъектыСправкаПоИдентификаторам.Вставить(СтрокаТЗ.ИдентификаторЗаявки, МассивСправок);
			
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования);
	
	Если ИспользоватьФормат303 И СправкиКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсСправки2НДФЛ(ПараметрыОбмена.ВерсияAPI);
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Справка2НДФЛ);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				СправкиКПубликации,
				"ID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		ВыгруженныеДокументы = Новый Соответствие;
		// Добавим в РезультатыСогласованияКПубликации данные по выгруженным справкам.
		РезультатыСогласованияКПубликации = Новый Массив;
		Для Каждого ИдентификаторДокумента Из РезультатВыгрузки.Выгружено Цикл
			ВыгруженныеДокументы.Вставить(ИдентификаторыДокументов[ИдентификаторДокумента]);
			РезультатыСогласования = ДокументРезультатыСогласования[ИдентификаторДокумента];
			Если РезультатыСогласования <> Неопределено Тогда
				Для каждого РезультатСогласования Из РезультатыСогласования Цикл
					РезультатыСогласованияКПубликации.Добавить(РезультатСогласования);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			РезультатОбработки.НеВыгружено.Добавить(ИдентификаторыДокументов[Ошибка.Ключ]);
			ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторыДокументов[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
		Если РезультатыСогласованияКПубликации.Количество() > 0 Тогда
			
			РесурсСервиса = РесурсРезультатыСогласования();
			РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
			РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
					ПараметрыОбмена,
					РесурсСервиса,
					РезультатыСогласованияКПубликации,
					"documentID", РазмерПакета);
			
			РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
			
			Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
				РезультатОбработки.НеВыгружено.Добавить(ИдентификаторыДокументов[Ошибка.Ключ]);
				ВыгруженныеДокументы.Удалить(ИдентификаторыДокументов[Ошибка.Ключ]);
				ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторыДокументов[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
			КонецЦикла;
			
		КонецЕсли;
		
		Для каждого ЭлементКоллекции Из ВыгруженныеДокументы Цикл
			РезультатОбработки.Выгружено.Добавить(ЭлементКоллекции.Ключ);
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ИспользоватьФормат303 И ОбъектыСправкаПоИдентификаторам.Количество() > 0 Тогда
		
		ОписаниеОбъектаОтветНаЗапросСправок2НДФЛ = ОписаниеОбъектаОтветНаЗапросСправок2НДФЛ();
		
		ТаблицаДанных.Индексы.Добавить("ИдентификаторЗаявки");
		Отбор = Новый Структура("ИдентификаторЗаявки");
		
		ОтветыНаЗапросыКПубликации = Новый Массив;
		Для каждого ЭлементКоллекции Из ОбъектыСправкаПоИдентификаторам Цикл
		
			ИдентификаторЗаявки = ЭлементКоллекции.Ключ;
			ОбъектыСправка = ЭлементКоллекции.Значение;
			
			Организация 	= ОбъектыСправка[0].employerID;
			ФизическоеЛицо 	= ОбъектыСправка[0].personID;
			
			ОписаниеОтвет = ОписаниеОбъекта(ОписаниеОбъектаОтветНаЗапросСправок2НДФЛ);
			ОписаниеОтвет.ИдентификаторЗаявки 	= ИдентификаторЗаявки;
			ОписаниеОтвет.Организация 			= Организация;
			ОписаниеОтвет.ФизическоеЛицо 		= ФизическоеЛицо;
			ОписаниеОтвет.Справки2НДФЛ 			= ОбъектыСправка;
			
			ОбъектОтвет = ОбъектСервисаПоОписанию(ОписаниеОтвет, ОписаниеОбъектаОтветНаЗапросСправок2НДФЛ);
			ОтветыНаЗапросыКПубликации.Добавить(ОбъектОтвет);
		
		КонецЦикла;
		
		РесурсСервиса = РесурсОтветыНаЗапросыСправок2НДФЛ(ПараметрыОбмена.ВерсияAPI);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				ОтветыНаЗапросыКПубликации,
				"requestID");
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого ИдентификаторЗаявки Из РезультатВыгрузки.Выгружено Цикл
			Отбор.ИдентификаторЗаявки = ИдентификаторЗаявки;
			НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				РезультатОбработки.Выгружено.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(СтрокаТЗ.ПубликуемыйДокумент, ТекущаяДатаСеанса());
			КонецЦикла;
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			// Не найденные заявки с таким идентификатором, регистрируем как успешно выгруженные.
			РегистрироватьКаКВыгружена = (ЗначениеЗаполнено(Ошибка.Значение) И СтрНайти(Ошибка.Значение, "objectNotFound") > 0);
			ИдентификаторЗаявки = Ошибка.Ключ;
			Отбор.ИдентификаторЗаявки = ИдентификаторЗаявки;
			НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				Если РегистрироватьКаКВыгружена Тогда
					РезультатОбработки.Выгружено.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
					КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(СтрокаТЗ.ПубликуемыйДокумент, ТекущаяДатаСеанса());
				Иначе	
					РезультатОбработки.НеВыгружено.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
				КонецЕсли;
			КонецЦикла;
			ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаТЗ.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, НеОбработанныеДокументы);
	РезультатОбработки.ОтменитьРегистрацию = ДокументыОтменитьРегистрацию;
		
	Возврат РезультатОбработки;
	
КонецФункции

Функция ДанныеДляВыгрузки2НДФЛ(ТаблицаИзменений)

	Справки2НДФЛ = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "Документ", Истина);
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	БизнесПроцессыЗаявокСотрудников.СоздатьВТЗаявкиПоСправкам2НДФЛ(МенеджерВТ, Справки2НДФЛ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ПубликуемыйДокумент КАК ПубликуемыйДокумент,
	|	ТаблицаИзменений.Документ КАК Справка2НДФЛ,
	|	ТаблицаИзменений.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ТаблицаИзменений.ИдентификаторДокумента КАК ИдентификаторДокумента
	|ПОМЕСТИТЬ ВТИзменения
	|ИЗ
	|	&ТаблицаИзменений КАК ТаблицаИзменений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Изменения.Справка2НДФЛ КАК Ссылка
	|ПОМЕСТИТЬ ВТСсылки
	|ИЗ
	|	ВТИзменения КАК Изменения";
	Запрос.Выполнить();
	
	УчетНДФЛ.СоздатьВТДанныеСправок2НДФЛДляПубликации(МенеджерВТ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ПубликуемыйДокумент КАК ПубликуемыйДокумент,
	|	ТаблицаИзменений.Справка2НДФЛ КАК Справка2НДФЛ,
	|	ТаблицаИзменений.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ТаблицаИзменений.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ЕСТЬNULL(Заявки.ИдентификаторЗаявки, """") КАК ИдентификаторЗаявки,
	|	ЕСТЬNULL(Заявки.ОтветПоЗаявке, """") КАК Комментарий,
	|	ЕСТЬNULL(Заявки.Заявка, НЕОПРЕДЕЛЕНО) КАК Заявка,
	|	ЕСТЬNULL(Заявки.ВариантФормированияФайлаОтвета, НЕОПРЕДЕЛЕНО) КАК ВариантФормированияФайлаОтвета,
	|	ЕСТЬNULL(Заявки.СостояниеЗаявки, НЕОПРЕДЕЛЕНО) КАК СостояниеЗаявки,
	|	ДанныеСправок2НДФЛ.Организация КАК Организация,
	|	ДанныеСправок2НДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеСправок2НДФЛ.ДатаСоздания КАК ДатаСоздания,
	|	ДанныеСправок2НДФЛ.НалоговыйПериод КАК НалоговыйПериод,
	|	ДанныеСправок2НДФЛ.СуммаДохода КАК СуммаДохода,
	|	ДанныеСправок2НДФЛ.СуммаНалога КАК СуммаНалога
	|ИЗ
	|	ВТИзменения КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаявкиПоСправкам2НДФЛ КАК Заявки
	|		ПО ТаблицаИзменений.Справка2НДФЛ = Заявки.СправкаНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСправок2НДФЛДляПубликации КАК ДанныеСправок2НДФЛ
	|		ПО ТаблицаИзменений.Справка2НДФЛ = ДанныеСправок2НДФЛ.Ссылка";
	
	ДанныеДляВыгрузки = Запрос.Выполнить().Выгрузить();
	ДанныеДляВыгрузки.Колонки.Добавить("ДанныеЭлектронногоДокумента");
	
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника") Тогда
		
		ЭлектронныеДокументы = Новый Массив;
		Для каждого СтрокаТЗ Из ДанныеДляВыгрузки Цикл
			Если СтрокаТЗ.СостояниеЗаявки <> Неопределено 
				И СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена
				И ЗначениеЗаполнено(СтрокаТЗ.ЭлектронныйДокумент)Тогда
				ЭлектронныеДокументы.Добавить(СтрокаТЗ.ЭлектронныйДокумент);
			КонецЕсли;
		КонецЦикла;
		ДанныеЭлектронныхДокументов = КадровыйЭДОВызовСервера.ДанныеФайловПечатныхФорм(ЭлектронныеДокументы);
		
		Для каждого СтрокаТЗ Из ДанныеДляВыгрузки Цикл
			СтрокаТЗ.ДанныеЭлектронногоДокумента = ДанныеЭлектронныхДокументов[СтрокаТЗ.ЭлектронныйДокумент];
		КонецЦикла;
		
	Иначе
		
		Отбор = Новый Структура("ВариантФормированияФайлаОтвета", Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ФайлСЭП);
		НайденныеСтроки = ДанныеДляВыгрузки.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ЭлектронныеДокументы = Новый Массив;
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				Если СтрокаТЗ.СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена 
					И ЗначениеЗаполнено(СтрокаТЗ.ЭлектронныйДокумент)Тогда
					ЭлектронныеДокументы.Добавить(СтрокаТЗ.ЭлектронныйДокумент);
				КонецЕсли;
			КонецЦикла;
			ДанныеЭлектронныхДокументов = КадровыйЭДОВызовСервера.ДанныеФайловПечатныхФорм(ЭлектронныеДокументы);
			
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				СтрокаТЗ.ДанныеЭлектронногоДокумента = ДанныеЭлектронныхДокументов[СтрокаТЗ.ЭлектронныйДокумент];
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеДляВыгрузки;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаСогласийКЭДО

Функция РезультатВыгрузкиСогласийКЭДО(ПараметрыОбмена, ТаблицаИзменений)

	РезультатОбработки = НовыйРезультатВыгрузкиДокументов();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника") Тогда
		Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
			РезультатОбработки.ОтменитьРегистрацию.Добавить(СтрокаТЗ.ПубликуемыйДокумент);
		КонецЦикла;
		Возврат РезультатОбработки;
	КонецЕсли;
	
	ВерсияDTO = ПараметрыОбмена.ВерсияDTO;
	ВерсияAPI = ПараметрыОбмена.ВерсияAPI;
	ДокументыНаПодпись = ДанныеДляВыгрузкиДокументовНаПодпись(ТаблицаИзменений);
	ИспользуетсяВерсия_1_2 = КабинетСотрудника.ИспользуетсяВерсияФормата("1.2");
	
	ДокументыОтбор = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИзменений, "Документ", Истина);
	ТаблицаСогласий = ДанныеСогласийНаПрисоединениеККЭДО(ДокументыОтбор);
	
	СогласиеИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаСогласий, "Ссылка", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО);
	ИдентификаторСогласие = СоответствиеНаоборот(СогласиеИдентификатор);
	ОрганизацияИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаСогласий, "Организация", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	ФизическоеЛицоИдентификатор = СсылкаПубличныйИдентификатор(ТаблицаСогласий, "ФизическоеЛицо", Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	
	// Массив ссылок на документы КЭДО, по которым
	// не удалось сформировать объект для выгрузки.
	НеОбработанныеДокументы = Новый Массив;
	
	// Массив объектов документов для публикации.
	ДокументыНаПодписьКПубликации = Новый Массив;
	
	// Коллекция документов с результатами согласования.
	// Ключ - ссылка на Документ,
	// Значение - массив объектов РезультатСогласования.
	ДокументРезультатыСогласования = Новый Соответствие;
	
	// Коллекция документов с результатами согласования.
	// Ключ - ссылка на Документ,
	// Значение - объект Согласие.
	ДокументСогласие = Новый Соответствие;
	
	// Коллекция документов КЭДО.
	// Ключ - идентификатор документа,
	// Значение - ссылка на ДокументКЭДО.
	ИдентификаторыДокументов = Новый Соответствие;
	
	// Результаты согласования для заполнения идентификаторов.
	ВсеРезультатыСогласования = Новый Массив;
	
	ОписаниеОбъектаДокументНаПодпись = ОписаниеОбъектаДокументНаПодпись(ВерсияDTO);
	ОписаниеОбъектаСогласиеНаПрисоединениеККЭДО = ОписаниеОбъектаСогласиеНаПрисоединениеККЭДО(ВерсияDTO);
	Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
	
		ДокументНаПодпись = ДокументыНаПодпись.Найти(СтрокаТЗ.ПубликуемыйДокумент, "ПубликуемыйДокумент");
		
		Ответ = ЭлектронныйДокументРезультатыСогласования(ПараметрыОбмена, ДокументНаПодпись.ДанныеЭлектронногоДокумента, ДокументНаПодпись.ИдентификаторДокумента);
		
		ОбъектЭлектронныйДокумент 	= Ответ.ЭлектронныйДокумент;
		РезультатыСогласования 		= Ответ.РезультатыСогласования;
		Если ОбъектЭлектронныйДокумент = Неопределено Тогда
			РезультатОбработки.БылиОшибки = Истина;
			НеОбработанныеДокументы.Добавить(ДокументНаПодпись.ПубликуемыйДокумент);
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатыСогласования) Тогда
			ДокументРезультатыСогласования.Вставить(ДокументНаПодпись.ПубликуемыйДокумент, РезультатыСогласования);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеРезультатыСогласования, РезультатыСогласования);
		КонецЕсли;
		
		ОписаниеДокументНаПодпись =  ОписаниеОбъекта(ОписаниеОбъектаДокументНаПодпись);
		ОписаниеДокументНаПодпись.ИдентификаторДокумента 	= ДокументНаПодпись.ИдентификаторДокумента;
		ОписаниеДокументНаПодпись.Организация 				= ОрганизацияИдентификатор[ДокументНаПодпись.Организация];
		ФизическиеЛица = Новый Массив;
		Для каждого ФизическоеЛицо Из ДокументНаПодпись.ФизическиеЛица Цикл
			ФизическиеЛица.Добавить(ФизическоеЛицоИдентификатор[ФизическоеЛицо]);
		КонецЦикла;
		ОписаниеДокументНаПодпись.ФизическиеЛица 			= ФизическиеЛица;
		ОписаниеДокументНаПодпись.Дата 						= ДокументНаПодпись.ДатаДокумента;
		ОписаниеДокументНаПодпись.НазваниеДокумента 		= ДокументНаПодпись.НазваниеДокумента;
		ОписаниеДокументНаПодпись.ЭлектронныйДокумент 		= ОбъектЭлектронныйДокумент;
		Если ИспользуетсяВерсия_1_2 Тогда
			ОписаниеДокументНаПодпись.ОтправлятьУведомление = Ложь;
		КонецЕсли;
		ОбъектДокументНаПодпись = ОбъектСервисаПоОписанию(ОписаниеДокументНаПодпись, ОписаниеОбъектаДокументНаПодпись);
		
		ДокументыНаПодписьКПубликации.Добавить(ОбъектДокументНаПодпись);
		ИдентификаторыДокументов.Вставить(ДокументНаПодпись.ИдентификаторДокумента, ДокументНаПодпись.ПубликуемыйДокумент);
		
		ДанныеСогласия = ТаблицаСогласий.Найти(СтрокаТЗ.Документ, "Ссылка");
		
		ОписаниеСогласия =  ОписаниеОбъекта(ОписаниеОбъектаСогласиеНаПрисоединениеККЭДО);
		ОписаниеСогласия.Согласие 							= СогласиеИдентификатор[ДанныеСогласия.Ссылка];
		ОписаниеСогласия.Организация 						= ОрганизацияИдентификатор[ДанныеСогласия.Организация];
		ОписаниеСогласия.ФизическоеЛицо 					= ФизическоеЛицоИдентификатор[ДанныеСогласия.ФизическоеЛицо];
		ОписаниеСогласия.ИдентификаторДокументаНаПодпись 	= ДокументНаПодпись.ИдентификаторДокумента;
		ОписаниеСогласия.ДатаСоздания 						= ДанныеСогласия.ДатаСоздания;
		Если ИспользуетсяВерсия_1_2 Тогда
			ОписаниеСогласия.ОтправлятьУведомление = Истина;
		КонецЕсли;
		ОбъектСогласие = ОбъектСервисаПоОписанию(ОписаниеСогласия, ОписаниеОбъектаСогласиеНаПрисоединениеККЭДО);
		
		ДокументСогласие.Вставить(ДокументНаПодпись.ПубликуемыйДокумент, ОбъектСогласие);
	
	КонецЦикла;
	
	РезультатыСогласованияКонвертироватьСсылки(ВсеРезультатыСогласования);
	
	РесурсСервиса = РесурсДокументыНаПодпись();
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				ДокументыНаПодписьКПубликации,
				"ID", РазмерПакета);
		
	РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	ВыгруженныеДокументы = Новый Соответствие;
	РезультатыСогласованияКПубликации = Новый Массив;
	Для Каждого ИдентификаторДокумента Из РезультатВыгрузки.Выгружено Цикл
		ДокументНаПодпись = ИдентификаторыДокументов[ИдентификаторДокумента];
		ВыгруженныеДокументы.Вставить(ДокументНаПодпись, Истина);
		РезультатыСогласования = ДокументРезультатыСогласования[ДокументНаПодпись];
		Если РезультатыСогласования <> Неопределено Тогда
			Для каждого РезультатСогласования Из РезультатыСогласования Цикл
				РезультатыСогласованияКПубликации.Добавить(РезультатСогласования);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
		СтрокаДанных = ТаблицаИзменений.Найти(Ошибка.Ключ, "ИдентификаторДокумента");
		РезультатОбработки.НеВыгружено.Добавить(СтрокаДанных.ПубликуемыйДокумент);
		ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаДанных.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
		ДокументСогласие.Удалить(СтрокаДанных.ПубликуемыйДокумент);
	КонецЦикла;
	
	Если РезультатыСогласованияКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсРезультатыСогласования();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
			ПараметрыОбмена,
			РесурсСервиса,
			РезультатыСогласованияКПубликации,
			"documentID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			СтрокаДанных = ТаблицаИзменений.Найти(Ошибка.Ключ, "ИдентификаторДокумента");
			РезультатОбработки.НеВыгружено.Добавить(СтрокаДанных.ПубликуемыйДокумент);
			ВыгруженныеДокументы.Удалить(СтрокаДанных.ПубликуемыйДокумент);
			ДокументСогласие.Удалить(СтрокаДанных.ПубликуемыйДокумент);
			ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаДанных.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;

	СогласияКПубликации = Новый Массив;
	Для каждого ЭлементКоллекции Из ДокументСогласие Цикл
		СогласияКПубликации.Добавить(ЭлементКоллекции.Значение);
	КонецЦикла;
	
	Если СогласияКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсСогласиеНаПрисоединениеККЭДО(ВерсияAPI);
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеККЭДО);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
			ПараметрыОбмена,
			РесурсСервиса,
			СогласияКПубликации,
			"ID", РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			Согласие = ИдентификаторСогласие[Ошибка.Ключ];
			СтрокаДанных = ТаблицаИзменений.Найти(Согласие, "Документ");
			РезультатОбработки.НеВыгружено.Добавить(СтрокаДанных.ПубликуемыйДокумент);
			ВыгруженныеДокументы.Удалить(СтрокаДанных.ПубликуемыйДокумент);
			ЗарегистрироватьОшибкуПубликацииОбъекта(СтрокаДанных.ПубликуемыйДокумент, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ЭлементКоллекции Из ВыгруженныеДокументы Цикл
		РезультатОбработки.Выгружено.Добавить(ЭлементКоллекции.Ключ);
		КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(ЭлементКоллекции.Ключ, ТекущаяДатаСеанса());
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатОбработки.НеВыгружено, НеОбработанныеДокументы);
	
	Возврат РезультатОбработки

КонецФункции

Функция ДанныеСогласийНаПрисоединениеККЭДО(ДокументыОтбор)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Согласия", ДокументыОтбор);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СогласиеНаПрисоединениеККЭДО.Ссылка КАК Ссылка,
	|	СогласиеНаПрисоединениеККЭДО.Организация КАК Организация,
	|	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СогласиеНаПрисоединениеККЭДО.Дата КАК ДатаСоздания
	|ИЗ
	|	Документ.СогласиеНаПрисоединениеККЭДО КАК СогласиеНаПрисоединениеККЭДО
	|ГДЕ
	|	СогласиеНаПрисоединениеККЭДО.Ссылка В(&Согласия)";
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПубликацияРасчетныхЛистков

Функция РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации) Экспорт

	Результат = Новый Структура("БылиОшибки,НеОбработаны,КоличествоВыгружено", Ложь, Новый Массив, 0);
	
	ЗаписатьСобытиеНачалоПубликацииРЛ();
	
	КоличествоВыгружено = 0;
	Попытка
		
		ПараметрыОбмена = ПараметрыОбмена(ПараметрыПубликации.ВестиПротокол);
		
		ПроверитьВерсиюФорматаОбмена(ПараметрыОбмена);
		
		Для каждого ЭлементКоллекции Из ДанныеДляПубликации Цикл
			
			ПараметрыПубликации.Месяц = НачалоМесяца(ЭлементКоллекции.Ключ);
			ФизическиеЛицаОрганизаций = ЭлементКоллекции.Значение;
			
			Для каждого ФизическиеЛицаОрганизации Из ФизическиеЛицаОрганизаций Цикл
				
				ПараметрыПубликации.Организация = ФизическиеЛицаОрганизации.Ключ;
				ПараметрыПубликации.СписокФизическихЛиц = ФизическиеЛицаОрганизации.Значение;
				
				Ответ = РезультатВыгрузкиРасчетныхЛистков(ПараметрыОбмена, ПараметрыПубликации);
				Результат.БылиОшибки = Результат.БылиОшибки Или Ответ.БылиОшибки;
				КоличествоВыгружено = КоличествоВыгружено + Ответ.КоличествоВыгружено;
				
				Если ЗначениеЗаполнено(Ответ.НеОбработаны) Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.НеОбработаны, Ответ.НеОбработаны);
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЦикла;
		
	Исключение
		Результат.БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(КабинетСотрудника.ИмяСобытияПрочиеСобытия(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ЗаписатьСобытиеОкончаниеПубликацииРЛ(Результат.БылиОшибки, КоличествоВыгружено);
	
	Результат.КоличествоВыгружено = КоличествоВыгружено;
	
	Возврат Результат;

КонецФункции

Функция РезультатВыгрузкиРасчетныхЛистков(ПараметрыОбмена, ПараметрыВыгрузки) 
	
	// КоличествоВыгружено - количество выгруженных данных,
	// БылиОшибки - признак наличия ошибок при выгрузке,
	// НеОбработаны - Массив - физические лица, по которым нет данных о зарплате.
	РезультатОбработки = Новый Структура("КоличествоВыгружено,БылиОшибки,НеОбработаны", 0, Ложь);
	
	Организация 			= ПараметрыВыгрузки.Организация;
	Месяц 					= НачалоМесяца(ПараметрыВыгрузки.Месяц);
	ПерваяПоловинаМесяца 	= ПараметрыВыгрузки.ПерваяПоловинаМесяца;
	СписокФизическихЛиц 	= ПараметрыВыгрузки.СписокФизическихЛиц;
	Данные = ДанныеРасчетныхЛистов(ПараметрыВыгрузки, ПараметрыОбмена.ВерсияDTO);
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат РезультатОбработки;
	КонецЕсли;
	
	Опубликовано = ОпубликоватьСоставныеЧастиЗарплаты(ПараметрыОбмена, Данные.СоставныеЧастиЗарплаты);
	Если Не Опубликовано Тогда
		РезультатОбработки.БылиОшибки = Истина;
		Возврат РезультатОбработки;
	КонецЕсли; 
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ФизическоеЛицоИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(СписокФизическихЛиц, ТипОбъекта);
	ИдентификаторФизическоеЛицо = СоответствиеНаоборот(ФизическоеЛицоИдентификатор);
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
	Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	ОрганизацииИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(Организации, ТипОбъекта);
	
	ПубликоватьДокументыНаПодпись = КабинетСотрудника.ИспользоватьФормат303();
	ИспользоватьФормат503 = КабинетСотрудника.ИспользоватьФормат503();
	ИспользуетсяВерсия_1_2 = КабинетСотрудника.ИспользуетсяВерсияФормата("1.2");
	
	// Расширение файла расчетного листка.
	РасширениеФайла = "pdf";
	
	// Соответствие с именами файлов расчетных листков
	// Ключ - Физическое лицо
	// Значение - Строка - сформированное имя файла.
	ФизическиеЛицаИменаФайлов = Данные.ФизическиеЛицаИменаФайлов;
	
	// Соответствие с существующими документами КЭДО
	// Ключ - ФизическоеЛицо,
	// Значение - ДокументКЭДО.
	ДокументыКЭДО = Данные.ДокументыКЭДО;
	
	ДокументыНаПодписьКПубликации 	= Новый Массив;
	РасчетныеЛисткиКПубликации 		= Новый Массив;
	
	ФизическиеЛицаДокументыКЭДО 	= Новый Соответствие;
	ИдентификаторыДокументов 		= Новый Соответствие;
	
	ДокументыКЭДОПрежниеФайлы = Новый Соответствие;
	
	// Ключ - Строка - идентификатор документа КЭДО
	// Значение - Структура - ОбъектИнформацияОЗарплате 
	ИдентификаторДокументаРасчетныйЛисток 	= Новый Соответствие;
	
	НеОбработаны = Новый Массив;
	
	ОписаниеОбъектаЭлектронныйДокумент = ОписаниеОбъектаЭлектронныйДокумент(ПараметрыОбмена.ВерсияDTO);
	ОписаниеОбъектаДокументНаПодпись = ОписаниеОбъектаДокументНаПодпись(ПараметрыОбмена.ВерсияDTO);
	Для каждого ФизическоеЛицо Из СписокФизическихЛиц Цикл
	
		ОбъектИнформацияОЗарплате = Данные.ИнформацияОЗарплате[ФизическоеЛицо];
		Если ОбъектИнформацияОЗарплате = Неопределено Тогда
			НеОбработаны.Добавить(ФизическоеЛицо);
			Продолжить;
		КонецЕсли;
		
		РасчетныйЛистДокумент = Данные.РасчетныеЛистыДокументы.Получить(ФизическоеЛицо);
		Если РасчетныйЛистДокумент = Неопределено Тогда
			НеОбработаны.Добавить(ФизическоеЛицо);
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			ДокументКЭДООбъект 	= Неопределено;
			ДокументКЭДОФайл 	= Неопределено;
			ДокументКЭДОСсылка = ДокументыКЭДО[ФизическоеЛицо];
			Если ДокументКЭДОСсылка <> Неопределено Тогда
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Документ.ДокументКадровогоЭДО");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументКЭДОСсылка);
				Блокировка.Заблокировать();
				
				ДокументКЭДООбъект = ДокументКЭДОСсылка.ПолучитьОбъект();
				ДокументКЭДОФайл   = ДокументКЭДООбъект.ЭлектронныйДокумент;
				
			Иначе
				
				ДокументКЭДОСсылка = Документы.ДокументКадровогоЭДО.ПолучитьСсылку();
				ДокументКЭДООбъект = ИнтеграцияУправлениеПерсоналомСлужебный.НовыйДокументКЭДОРасчетныйЛисток(ДокументКЭДОСсылка, Организация, ФизическоеЛицо);
				
			КонецЕсли;
			
			ИмяФайла = ФизическиеЛицаИменаФайлов[ФизическоеЛицо];
			ДанныеФайлаРЛ = ИнтеграцияУправлениеПерсоналомСлужебный.ДанныеФайлаРасчетногоЛистка(Организация, ФизическоеЛицо, ДокументКЭДОСсылка, РасчетныйЛистДокумент, ИмяФайла, РасширениеФайла);
			ФайлРасчетногоЛистка = ДанныеФайлаРЛ.ФайлРасчетногоЛистка;
			
			// публикация файла в сервисе
			ИдентификаторФайла = ОпубликоватьПрисоединенныйФайл(ПараметрыОбмена, ФайлРасчетногоЛистка, РасширениеФайла);
			Если ИдентификаторФайла = Неопределено Тогда
				ОтменитьТранзакцию();
				ОписаниеОшибки = НСтр("ru = 'Ошибка публикации файла расчетного листка.';
										|en = 'An error occurred while publishing the payslip file.'");
				ЗаписатьОшибкуВыгрузкиИзменений(ФизическоеЛицо, ОписаниеОшибки);
				РезультатОбработки.БылиОшибки = Истина;
				Продолжить;
			КонецЕсли;
			
			// запоминаем "старый" файл
			Если ЗначениеЗаполнено(ДокументКЭДОФайл) Тогда
				ФайлыДокумента = Новый Структура("СтарыйФайл,НовыйФайл", ДокументКЭДОФайл, ФайлРасчетногоЛистка);
				ДокументыКЭДОПрежниеФайлы.Вставить(ДокументКЭДОСсылка, ФайлыДокумента);
			КонецЕсли;
			
			// обновим ссылку на файл - ЭлектронныйДокумент
			ДокументКЭДООбъект.ЭлектронныйДокумент = ФайлРасчетногоЛистка;
			ДокументКЭДООбъект.Записать();
			КадровыйЭДО.ЗарегистрироватьДокументКЭДОКПересчетуСостояний(ДокументКЭДООбъект.Ссылка);
			
			ИдентификаторыДокументов.Вставить(ДокументКЭДООбъект.ИдентификаторДокумента, ДокументКЭДОСсылка);
			ФизическиеЛицаДокументыКЭДО.Вставить(ФизическоеЛицо, ДокументКЭДОСсылка);
			
			Если ПубликоватьДокументыНаПодпись Тогда
				
				ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДанныеФайлаРЛ.ДвоичныеДанные);
				ОбъектФайл = ОбъектСервисаФайл(ИдентификаторФайла, ИмяФайла, РасширениеФайла, ДанныеФайлаРЛ.РазмерФайла, ПараметрыОбмена.ВерсияDTO, ВерсияФайла);
				
				ОписаниеЭлектронныйДокумент = ОписаниеОбъекта(ОписаниеОбъектаЭлектронныйДокумент);
				ОписаниеЭлектронныйДокумент.ИсходныйДокумент = ОбъектФайл;
				ОбъектЭлектронныйДокумент = ОбъектСервисаПоОписанию(ОписаниеЭлектронныйДокумент, ОписаниеОбъектаЭлектронныйДокумент);
				
				ОписаниеДокументНаПодпись = ОписаниеОбъекта(ОписаниеОбъектаДокументНаПодпись);
				ОписаниеДокументНаПодпись.ИдентификаторДокумента			= ДокументКЭДООбъект.ИдентификаторДокумента;
				ОписаниеДокументНаПодпись.Организация						= ОрганизацииИдентификатор[Организация];
				ОписаниеДокументНаПодпись.ФизическиеЛица					= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицоИдентификатор[ФизическоеЛицо]);
				ОписаниеДокументНаПодпись.Дата								= ТекущаяДатаСеанса();
				ОписаниеДокументНаПодпись.НазваниеДокумента					= ИмяФайла;
				ОписаниеДокументНаПодпись.ЭлектронныйДокумент				= ОбъектЭлектронныйДокумент;
				Если ИспользуетсяВерсия_1_2 Тогда
					ОписаниеДокументНаПодпись.ОтправлятьУведомление = Ложь;
				КонецЕсли;
				Если ИспользоватьФормат503 Тогда
					ОписаниеДокументНаПодпись.ВозможноОбновлениеПредставлений = Истина;
				КонецЕсли;
				ОбъектДокументНаПодпись = ОбъектСервисаПоОписанию(ОписаниеДокументНаПодпись, ОписаниеОбъектаДокументНаПодпись);
				
				ДокументыНаПодписьКПубликации.Добавить(ОбъектДокументНаПодпись);
				
				// дозаполнение объекта ОбъектИнформацияОЗарплате
				ОписаниеДокументНаПодпись = ОписаниеОбъекта(ОписаниеОбъектаДокументНаПодпись);
				ОписаниеДокументНаПодпись.ИдентификаторДокумента = ДокументКЭДООбъект.ИдентификаторДокумента;
				ОбъектДокументНаПодпись = ОбъектСервисаПоОписанию(ОписаниеДокументНаПодпись, ОписаниеОбъектаДокументНаПодпись);
				ОбъектИнформацияОЗарплате.documentsToBeSigned = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектДокументНаПодпись);
				
				ИдентификаторДокументаРасчетныйЛисток.Вставить(ДокументКЭДООбъект.ИдентификаторДокумента, ОбъектИнформацияОЗарплате);
				
			Иначе
				
				ОбъектФайл = Новый Структура;
				ОбъектФайл.Вставить("fileID", 		ИдентификаторФайла);
				ОбъектФайл.Вставить("name", 		СтрШаблон("%1.%2", ИмяФайла, РасширениеФайла));
				ОбъектФайл.Вставить("extension", 	РасширениеФайла);
				ОбъектИнформацияОЗарплате.Вставить("attachment", ОбъектФайл);
				
				РасчетныеЛисткиКПубликации.Добавить(ОбъектИнформацияОЗарплате);
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОписаниеИзменений = НСтр("ru = 'Расчетные листки';
									|en = 'Payslips'");
			ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки());
			РезультатОбработки.БылиОшибки = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
	РезультатОбработки.НеОбработаны = НеОбработаны;
	
	ОпубликованныеДокументы = Новый Соответствие;
	ОпубликованныеФизическиеЛица = Новый Соответствие;
	
	Если ДокументыНаПодписьКПубликации.Количество() > 0 Тогда
		
		РесурсСервиса = РесурсДокументыНаПодпись();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				ДокументыНаПодписьКПубликации,
				"ID",
				РазмерПакета);
		
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		
		// Добавим в РасчетныеЛисткиКПубликации данные по физическим лицам,
		// для которых выгружены документы на подпись.
		Для Каждого ИдентификаторДокумента Из РезультатВыгрузки.Выгружено Цикл
			ОбъектИнформацияОЗарплате = ИдентификаторДокументаРасчетныйЛисток[ИдентификаторДокумента];
			РасчетныеЛисткиКПубликации.Добавить(ОбъектИнформацияОЗарплате);
			ОпубликованныеДокументы.Вставить(ИдентификаторыДокументов[ИдентификаторДокумента], Истина);
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторыДокументов[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Если РасчетныеЛисткиКПубликации.Количество() = 0 Тогда
		РезультатОбработки.КоличествоВыгружено = 0;
	Иначе
		
		РесурсСервиса = РесурсРасчетныеЛисты();
		РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РасчетныйЛисток);
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
				ПараметрыОбмена,
				РесурсСервиса,
				РасчетныеЛисткиКПубликации,
				"personID",
				РазмерПакета);
				
		РезультатОбработки.БылиОшибки = РезультатОбработки.БылиОшибки Или РезультатВыгрузки.БылиОшибки;
		РезультатОбработки.КоличествоВыгружено = РезультатВыгрузки.Выгружено.Количество();
		
		Для каждого Идентификатор Из РезультатВыгрузки.Выгружено Цикл
			ОпубликованныеФизическиеЛица.Вставить(ИдентификаторФизическоеЛицо[Идентификатор], Истина);
		КонецЦикла;
		
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			ЗарегистрироватьОшибкуПубликацииОбъекта(ИдентификаторФизическоеЛицо[Ошибка.Ключ], РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРЛ = Новый ТаблицаЗначений;
	ТаблицаРЛ.Колонки.Добавить("Организация");
	ТаблицаРЛ.Колонки.Добавить("ФизическоеЛицо");
	ТаблицаРЛ.Колонки.Добавить("Месяц");
	ТаблицаРЛ.Колонки.Добавить("ПерваяПоловинаМесяца");
	ТаблицаРЛ.Колонки.Добавить("СостояниеПубликации");
	ТаблицаРЛ.Колонки.Добавить("ДатаПубликации");
	ТаблицаРЛ.Колонки.Добавить("ДокументКадровогоЭДО");
	ТаблицаРЛ.Колонки.Добавить("Ответственный");
	
	ПодписиДокументовКУдалению = Новый ТаблицаЗначений;
	ПодписиДокументовКУдалению.Колонки.Добавить("Объект");
	ПодписиДокументовКУдалению.Колонки.Добавить("ФизическоеЛицо");
	
	ДокументыВозвратФайлов = Новый ТаблицаЗначений;
	ДокументыВозвратФайлов.Колонки.Добавить("ДокументКадровогоЭДО");
	ДокументыВозвратФайлов.Колонки.Добавить("ЭлектронныйДокумент");
	
	ФайлыКУдалению = Новый ТаблицаЗначений;
	ФайлыКУдалению.Колонки.Добавить("Файл");
	
	ДатаПубликации = ТекущаяДатаСеанса();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Для каждого ЭлементКоллекции Из ФизическиеЛицаДокументыКЭДО Цикл
	
		ФизическоеЛицо = ЭлементКоллекции.Ключ;
		ДокументКЭДО = ЭлементКоллекции.Значение;
		
		Если ОпубликованныеФизическиеЛица[ФизическоеЛицо] = Неопределено Тогда
			// не удалось опубликовать расчетный листок
			СостояниеПубликации = Перечисления.СостоянияРасчетныхЛистковКабинетСотрудника.ОшибкаОбработки;
		Иначе
			СостояниеПубликации = Перечисления.СостоянияРасчетныхЛистковКабинетСотрудника.Опубликован;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРЛ.Добавить();
		НоваяСтрока.Организация 			= Организация;
		НоваяСтрока.ФизическоеЛицо 			= ФизическоеЛицо;
		НоваяСтрока.Месяц 					= Месяц;
		НоваяСтрока.ПерваяПоловинаМесяца 	= ПерваяПоловинаМесяца;
		НоваяСтрока.СостояниеПубликации 	= СостояниеПубликации;
		НоваяСтрока.ДатаПубликации 			= ДатаПубликации;
		НоваяСтрока.ДокументКадровогоЭДО 	= ДокументКЭДО;
		НоваяСтрока.Ответственный 			= ТекущийПользователь;
		
		НоваяСтрока = ПодписиДокументовКУдалению.Добавить();
		НоваяСтрока.Объект 			= ДокументКЭДО;
		НоваяСтрока.ФизическоеЛицо 	= ФизическоеЛицо;
		
		ФайлыДокумента = ДокументыКЭДОПрежниеФайлы[ДокументКЭДО];
		Если ЗначениеЗаполнено(ФайлыДокумента) Тогда
			
			Если ОпубликованныеДокументы[ДокументКЭДО] = Неопределено Тогда
				
				Если ЗначениеЗаполнено(ФайлыДокумента.СтарыйФайл) Тогда
					// документ на подпись не опубликован, возвращаем прежний файл
					НоваяСтрока = ДокументыВозвратФайлов.Добавить();
					НоваяСтрока.ДокументКадровогоЭДО = ДокументКЭДО;
					НоваяСтрока.ЭлектронныйДокумент = ФайлыДокумента.СтарыйФайл;
					
					НоваяСтрока = ФайлыКУдалению.Добавить();
					НоваяСтрока.Файл = ФайлыДокумента.НовыйФайл;
				КонецЕсли;
				
			Иначе
				НоваяСтрока = ФайлыКУдалению.Добавить();
				НоваяСтрока.Файл = ФайлыДокумента.СтарыйФайл;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасчетныеЛисткиКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаРЛ;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ФизическоеЛицо", "ФизическоеЛицо");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Месяц", "Месяц");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПерваяПоловинаМесяца", "ПерваяПоловинаМесяца");
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодписиДокументовКЭДО");
		ЭлементБлокировки.ИсточникДанных = ПодписиДокументовКУдалению;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Объект");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ФизическоеЛицо", "ФизическоеЛицо");
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.ДокументКадровогоЭДО");
		ЭлементБлокировки.ИсточникДанных = ДокументыВозвратФайлов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ДокументКадровогоЭДО");
		
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ДокументКадровогоЭДОПрисоединенныеФайлы");
		ЭлементБлокировки.ИсточникДанных = ФайлыКУдалению;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Файл");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаРЛ Цикл
			МенеджерЗаписи = РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
			КадровыйЭДО.ЗарегистрироватьПубликациюДокументаКЭДО(СтрокаТЗ.ДокументКадровогоЭДО, ТекущаяДатаСеанса());
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из ПодписиДокументовКУдалению Цикл
			МенеджерЗаписи = РегистрыСведений.ПодписиДокументовКЭДО.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из ДокументыВозвратФайлов Цикл
			ДокументОбъект = СтрокаТЗ.ДокументКадровогоЭДО.ПолучитьОбъект();
			ДокументОбъект.ЭлектронныйДокумент = СтрокаТЗ.ЭлектронныйДокумент;
			ДокументОбъект.Записать();
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из ФайлыКУдалению Цикл
			ФайлОбъект = СтрокаТЗ.Файл.ПолучитьОбъект();
			ФайлОбъект.ДополнительныеСвойства.Вставить("УдалениеПечатныхФорм");
			ФайлОбъект.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ОписаниеИзменений = НСтр("ru = 'Расчетные листки';
								|en = 'Payslips'");
		ЗаписатьОшибкуПриВыгрузкеИзменений(ОписаниеИзменений, ОписаниеОшибки());
		РезультатОбработки.БылиОшибки = Истина;
	КонецПопытки;
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ОпубликоватьСоставныеЧастиЗарплаты(ПараметрыОбмена, СоставныеЧастиЗарплаты)

	РесурсСервиса = РесурсСоставныеЧастиЗарплаты();
	РазмерПакета = РазмерПакета(ПараметрыОбмена, Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВидСоставнойЧастиЗарплаты);
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
		ПараметрыОбмена,
		РесурсСервиса,
		СоставныеЧастиЗарплаты,
		"ID", РазмерПакета);
		
	Если РезультатВыгрузки.БылиОшибки Тогда
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			ЗарегистрироватьОшибкуПубликацииОбъекта(Ошибка.Ключ, РесурсСервиса, Ошибка.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Не РезультатВыгрузки.БылиОшибки;

КонецФункции

// Выполняет публикацию присоединенного файла и возвращает идентификатор файла, полученный из сервиса.
// 	Параметры:
// 		ПараметрыОбмена - Структура - параметры подключения
// 		ПрисоединенныйФайл - Ссылка - ссылка на присоединенный файл
// 		Расширение - Строка - расширение файла
//
// 	Возвращаемое значение:
// 		Идентификатор файла - Строка
//
Функция ОпубликоватьПрисоединенныйФайл(ПараметрыОбмена, ПрисоединенныйФайл, Расширение)
	
	ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(ПрисоединенныйФайл);
	Возврат ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанные, Расширение);
	
КонецФункции

#КонецОбласти

Функция НоваяТаблицаОбработано()

	ТаблицаОбработано = Новый ТаблицаЗначений;
	ТаблицаОбработано.Колонки.Добавить("Ссылка");
	ТаблицаОбработано.Колонки.Добавить("ТипОбъекта");
	
	Возврат ТаблицаОбработано;

КонецФункции

#КонецОбласти

#Область НастройкиФункциональностиСервиса

Процедура ПроверитьОбновитьНастройкиФункциональностиСервиса(ПараметрыОбмена)

	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Если Настройки.ТребуетсяОбновитьНастройкиФункциональности Тогда
		ОбновитьНастройкиФункциональностиСервиса(ПараметрыОбмена);
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьНастройкиФункциональностиСервиса(ПараметрыОбмена = Неопределено) Экспорт

	Если Не КабинетСотрудника.ВерсионированиеИспользуется() Тогда
		ОбновитьНастройкиФункциональностиСервисаПоВерсииПриложения(ПараметрыОбмена);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбмена = Неопределено Тогда
		ПараметрыОбмена = ПараметрыОбмена(Ложь);
	КонецЕсли;
	
	НастройкиФункциональности = КабинетСотрудника.НовыеНастройкиФункциональностиСервиса();
	
	ОбъектСервиса = Новый Соответствие;
	ОписаниеОбъекта = ОписаниеПолейОбъектаИспользуемаяФункциональность(ПараметрыОбмена.ВерсияDTO);
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		ОбъектСервиса.Вставить(ЭлементКоллекции.Ключ, НастройкиФункциональности[ЭлементКоллекции.Значение]);
	КонецЦикла;
	
	НастройкиУстановлены = ОпубликоватьНастройкиФункциональностиСервиса(ПараметрыОбмена, ОбъектСервиса);
	Если НастройкиУстановлены Тогда
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция НовоеОписаниеНастроекФункциональностиСервиса(ВерсияDTO) Экспорт

	ОписаниеОбъекта = ОписаниеПолейОбъектаИспользуемаяФункциональность(ВерсияDTO);
	
	Описание = Новый Структура;
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		Описание.Вставить(ЭлементКоллекции.Значение, Истина);
	КонецЦикла;
	
	Возврат Описание;

КонецФункции

Функция ОпубликоватьНастройкиФункциональностиСервиса(ПараметрыОбмена, ОбъектСервиса)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	РесурсСервиса = РесурсИспользуемыеФункции();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

#Область НастройкиФункциональностиСервисаПоВерсииПриложения

Процедура ОбновитьНастройкиФункциональностиСервисаПоВерсииПриложения(ПараметрыОбмена)

	Если Не КабинетСотрудника.ИспользоватьФормат301() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбмена = Неопределено Тогда
		ПараметрыОбмена = ПараметрыОбмена(Ложь);
	КонецЕсли;
	
	ТекущиеНастройки = ЗапросТекущиеНастройкиФункциональностиСервиса(ПараметрыОбмена);
	Если Не ЗначениеЗаполнено(ТекущиеНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	НовыеНастройки = КабинетСотрудника.ДоступнаяФункциональностьСервисаПоВерсииПриложения();
	
	ТребуетсяОбновление = Ложь;
	Для каждого ЭлементКоллекции Из ТекущиеНастройки Цикл
		Если НовыеНастройки[ЭлементКоллекции.Ключ] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементКоллекции.Значение <> НовыеНастройки[ЭлементКоллекции.Ключ] Тогда
			ТребуетсяОбновление = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТребуетсяОбновление Тогда
		ЗаполнитьЗначенияСвойств(ТекущиеНастройки, НовыеНастройки);
		НастройкиУстановлены = ЗапросУстановитьНастройкиФункциональностиСервиса(ПараметрыОбмена, ТекущиеНастройки);
		Если НастройкиУстановлены Тогда
			РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Ложь);
		КонецЕсли;
	Иначе
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Ложь);
	КонецЕсли;

КонецПроцедуры

Функция ЗапросТекущиеНастройкиФункциональностиСервиса(ПараметрыОбмена)

	НастройкиФункциональности = Неопределено;
	
	РесурсСервиса = РесурсИспользуемыеФункции();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			НастройкиФункциональности = НастройкиФункциональностиСервисаПоОтветуНаЗапрос(ОбъектОтвета);
		Исключение
			КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
	
	Возврат НастройкиФункциональности;

КонецФункции

Функция ЗапросУстановитьНастройкиФункциональностиСервиса(ПараметрыОбмена, Настройки)

	Результат = Новый Структура("СообщениеОбОшибке");
	
	ОбъектСервиса = ОбъектСервисаНастройкиФункциональности(Настройки);
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	РесурсСервиса = РесурсИспользуемыеФункции();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ОбъектСервисаНастройкиФункциональности(НастройкиФункциональности)

	ОбъектСервиса = Новый Соответствие;
	ОписаниеОбъекта = ОписаниеПолейОбъектаИспользуемаяФункциональность("");
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		Если НастройкиФункциональности.Свойство(ЭлементКоллекции.Значение) Тогда
			ОбъектСервиса.Вставить(ЭлементКоллекции.Ключ, НастройкиФункциональности[ЭлементКоллекции.Значение]);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбъектСервиса;

КонецФункции

Функция НастройкиФункциональностиСервисаПоОтветуНаЗапрос(ОбъектСервиса)
	
	ОписаниеОбъекта = ОписаниеПолейОбъектаИспользуемаяФункциональность("");
	Описание = Новый Структура;
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		Если ОбъектСервиса[ЭлементКоллекции.Ключ] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Описание.Вставить(ЭлементКоллекции.Значение, ОбъектСервиса[ЭлементКоллекции.Ключ]);
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

Функция НовоеОписаниеИспользуемыеФункции() Экспорт
	
	ОписаниеОбъекта = ОписаниеПолейОбъектаИспользуемаяФункциональность("");
	
	Описание = Новый Структура;
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		Описание.Вставить(ЭлементКоллекции.Значение, Ложь);
	КонецЦикла;
	
	Возврат Описание;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область НастройкиЗаявокНаОтпуск

Процедура ОпубликоватьНастройкиЗаявокНаОтпуск(ПараметрыОбмена)

	Если Не КабинетСотрудника.ИспользоватьФормат303() Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.НастройкиЗаявокНаОтпуск();
	Если Не Настройки.ОбновитьНастройкиВСервисе Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОбновлены = ЗапросОбновитьНастройкиЗаявокНаОтпуск(ПараметрыОбмена, Настройки);
	Если НастройкиОбновлены Тогда
		// Проверка, что настройки не изменились за время публикации.
		ТекущиеНастройки = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.НастройкиЗаявокНаОтпуск();
		Если ОбщегоНазначения.КоллекцииИдентичны(Настройки, ТекущиеНастройки) Тогда
			Настройки.ОбновитьНастройкиВСервисе = Ложь;
			ИнтеграцияКабинетСотрудника.СохранитьНастройкиЗаявокНаОтпуск(Настройки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ЗапросОбновитьНастройкиЗаявокНаОтпуск(ПараметрыОбмена, Настройки)

	ОбъектСервиса = ОбъектСервисаНастройкиЗаявокНаОтпуск(Настройки, ПараметрыОбмена.ВерсияDTO);
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	РесурсСервиса = РесурсНастройкиОтпусков();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ОбъектСервисаНастройкиЗаявокНаОтпуск(Настройки, ВерсияDTO)

	ОбъектСервиса = Новый Соответствие;
	ОписаниеОбъекта = ОписаниеПолейОбъектаНастройкиЗаявокНаОтпуск(ВерсияDTO);
	Для каждого ЭлементКоллекции Из ОписаниеОбъекта Цикл
		ОбъектСервиса.Вставить(ЭлементКоллекции.Ключ, Настройки[ЭлементКоллекции.Значение]);
	КонецЦикла;
	
	Возврат ОбъектСервиса;

КонецФункции

#КонецОбласти

#Область РаботаСКабинетами

Функция ИдентификаторыАктивныхФизическихЛиц() Экспорт

	РесурсСервиса = РесурсФизическиеЛицаАктивные();
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	Возврат ЗапросИдентификаторыФизическихЛиц(ПараметрыОбмена, РесурсСервиса);

КонецФункции

Функция ДеактивироватьФизическихЛиц(ФизическиеЛица) Экспорт

	РесурсСервиса = РесурсФизическиеЛицаНеактивные();
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыОбмена, РесурсСервиса, ФизическиеЛица);
	Возврат Не Результат.БылиОшибки;

КонецФункции

Функция ИдентификаторыОпубликованныхФизическихЛиц(ПараметрыОбмена = Неопределено) Экспорт

	ПубликуемыеФизическиеЛица = Неопределено;
	
	Если ПараметрыОбмена = Неопределено Тогда
		ПараметрыОбмена = ПараметрыОбмена(Ложь);
	КонецЕсли;
	
	РесурсСервиса = РесурсФизическиеЛицаАктивные();
	Идентификаторы = ЗапросИдентификаторыФизическихЛиц(ПараметрыОбмена, РесурсСервиса);
	Если Идентификаторы = Неопределено Тогда
		Возврат ПубликуемыеФизическиеЛица;
	КонецЕсли;
	
	РесурсСервиса = РесурсФизическиеЛицаНеактивные();
	ИдентификаторыНеактивных = ЗапросИдентификаторыФизическихЛиц(ПараметрыОбмена, РесурсСервиса);
	Если ИдентификаторыНеактивных = Неопределено Тогда
		Возврат ПубликуемыеФизическиеЛица;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Идентификаторы, ИдентификаторыНеактивных);
	
	Возврат Идентификаторы;

КонецФункции

Функция ЗапросИдентификаторыФизическихЛиц(ПараметрыОбмена, РесурсСервиса)
	
	Идентификаторы = Неопределено;
	
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			Идентификаторы = ПрочитатьJSON(ЧтениеJSON, Истина);
		Исключение
			КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Возврат Идентификаторы;
	
КонецФункции

#КонецОбласти

#Область ОбслуживаниеОбсуждений

Функция ОбновитьНастройкиСистемыВзаимодействия(ПользовательСВ, ИдентификаторОбсужденияСВ) Экспорт

	Результат = Новый Структура("СообщениеОбОшибке");
	
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	ОписаниеПолей = ОписаниеПолейНастройкиСистемыВзаимодействия(ПараметрыОбмена.ВерсияDTO);
	
	ОписаниеОбъекта = НовоеОписаниеОбъекта(ОписаниеПолей);
	ОписаниеОбъекта.ИдентификаторПользователяСВ = Строка(ПользовательСВ.Идентификатор);
	ОписаниеОбъекта.ИмяПользователяСВ 			= ПользовательСВ.Имя;
	ОписаниеОбъекта.ИдентификаторОбсужденияСВ 	= ИдентификаторОбсужденияСВ;
	
	ОбъектСервиса = ОбъектСервисаПоОписаниюПолей(ОписаниеОбъекта, ОписаниеПолей);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	РесурсСервиса = РесурсСистемаВзаимодействияНастройки();
	
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
	
	Если Ответ = Неопределено Или Ответ.КодСостояния <> 200 Тогда
		Результат.СообщениеОбОшибке = НСтр("ru = 'Ошибка связи с сервисом 1С:Кабинет сотрудника.';
											|en = 'An error occurred when connecting to 1C:Employee Account service.'");
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ПриложениеСистемыВзаимодействия() Экспорт

	Результат = Новый Структура("ОписаниеПриложения,СообщениеОбОшибке", Новый Структура("Идентификатор,Имя"));
		
	РесурсСервиса = РесурсСистемаВзаимодействияПриложение();
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	ПараметрыЗапроса = ПараметрыЗапроса(РесурсСервиса, "GET");
	ПараметрыЗапроса.РазрешенныйКодОтвета = 404;
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса);
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Описание = ПрочитатьJSON(ЧтениеJSON, Истина);
		Результат.ОписаниеПриложения.Идентификатор = Описание["ID"];
		Результат.ОписаниеПриложения.Имя = Описание["name"];
	ИначеЕсли Ответ <> Неопределено И Ответ.КодСостояния = 404 Тогда
		// приложение еще не подключено к системе взаимодействия
	Иначе
		СообщениеОбОшибке = НСтр("ru = 'Не удалось получить настройки приложения для совместного использования.';
								|en = 'Cannot get application settings for sharing.'");
		Результат.СообщениеОбОшибке = СтрШаблон("%1 %2", СообщениеОбОшибке, КабинетСотрудника.ПодробностиВЖурналеРегистрации());
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСЛокальнымСервисом

Функция ИнформацияОПриложении() Экспорт

	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	Возврат ЗапросИнформацияОПриложении(ПараметрыОбмена);

КонецФункции

Функция УстановитьАдресЛокальногоПриложения(АдресПриложения) Экспорт

	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	
	ОписаниеПолей = ОписаниеПолейНастройкиПриложения(ПараметрыОбмена.ВерсияDTO);
	ОписаниеОбъекта = НовоеОписаниеОбъекта(ОписаниеПолей);
	ОписаниеОбъекта.АдресПриложения = АдресПриложения;
	
	ОбъектСервиса = ОбъектСервисаПоОписаниюПолей(ОписаниеОбъекта, ОписаниеПолей);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	РесурсСервиса = РесурсНастройкиПриложения();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "PUT", СтрокаТела));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция УстановитьАдминистратораЛокальногоПриложения(ОписаниеАдминистратора) Экспорт

	Результат = Новый Структура("СсылкаДляАдминистратора,БылиОшибки",,Ложь);
	
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	
	ОписаниеПолей = ОписаниеВнешнееФизическоеЛицо(ПараметрыОбмена.ВерсияDTO);
	ОписаниеОбъекта = НовоеОписаниеОбъекта(ОписаниеПолей);
	ОписаниеОбъекта.ФизическоеЛицо = ОписаниеАдминистратора.ФизическоеЛицо;
	
	ОбъектСервиса = ОбъектСервисаПоОписаниюПолей(ОписаниеОбъекта, ОписаниеПолей);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОбъектСервиса);
	СтрокаТела = ЗаписьJSON.Закрыть();
	
	
	РесурсСервиса = РесурсАдминистратор();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "POST", СтрокаТела));
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			Результат.СсылкаДляАдминистратора = ОбъектОтвета["logonUrl"];
		Исключение
			КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
			Результат.БылиОшибки = Истина;
		КонецПопытки;
	Иначе
		Результат.БылиОшибки = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ВерсионированиеAPI

Процедура ПроверитьВерсиюФорматаОбмена(ПараметрыОбмена = Неопределено) Экспорт
	
	Если ПараметрыОбмена = Неопределено Тогда
		ПараметрыОбмена = ПараметрыОбмена(Ложь); 
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	
	Ответ = РезультатЗапросаВерсииФорматаОбмена(ПараметрыОбмена);
	Если Ответ.Использовать = Неопределено Тогда
		// не получен ответ сервера
		Если Не ЗначениеЗаполнено(Настройки.ВерсияAPI) Тогда
			// версии обмена не используются
			ПроверитьВерсиюПриложения(ПараметрыОбмена);
		КонецЕсли;
	ИначеЕсли Ответ.Использовать Тогда  
		Если Не ЗначениеЗаполнено(Ответ.ВерсииФормата) Тогда
			// ошибка получения версий
			Если Не ЗначениеЗаполнено(Настройки.ВерсияAPI) Тогда
				// версии обмена еще не используются, проверим версию приложения
				ПроверитьВерсиюПриложения(ПараметрыОбмена);
			КонецЕсли;
		Иначе
			УстановитьВерсиюФорматаОбмена(ПараметрыОбмена, Ответ.ВерсииФормата, Настройки);
		КонецЕсли;
	Иначе
		// версии обмена не используются
		ПроверитьВерсиюПриложения(ПараметрыОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВерсиюФорматаОбмена(ПараметрыОбмена, НовыеВерсии, ТекущиеВерсии)

	Если НовыеВерсии.ВерсияDTO <> ТекущиеВерсии.ВерсияDTO Тогда
		НоваяВерсия = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(НовыеВерсии.ВерсияDTO, ВерсииФорматаОбмена());
		Если НоваяВерсия <> ТекущиеВерсии.ВерсияDTO Тогда
			ПараметрыОбмена.ВерсияDTO = НоваяВерсия;
			РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюDTO(НоваяВерсия);
			КабинетСотрудника.ВыполнитьДействияПриПереходеНаВерсиюФормата(ТекущиеВерсии.ВерсияDTO, НоваяВерсия);
		КонецЕсли;
	КонецЕсли;
	
	Если НовыеВерсии.ВерсияAPI <> ТекущиеВерсии.ВерсияAPI Тогда
		НоваяВерсия = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(НовыеВерсии.ВерсияAPI, ВерсииAPI());
		Если НоваяВерсия <> ТекущиеВерсии.ВерсияAPI Тогда
			ПараметрыОбмена.ВерсияAPI = НоваяВерсия;
			РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюAPI(НоваяВерсия);
			КабинетСотрудника.ВыполнитьДействияПриПереходеНаВерсиюAPI(ТекущиеВерсии.ВерсияAPI, НоваяВерсия);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция РезультатЗапросаВерсииФорматаОбмена(ПараметрыОбмена)

	Результат = Новый Структура("Использовать,ВерсииФормата");
	
	РесурсСервиса = РесурсВерсияAPI();
	Ответ = HTTPОтветСервисаЗапросБезВерсионирования(ПараметрыОбмена, РесурсСервиса, "GET", 404);
	Если Ответ = Неопределено Тогда
		// не удалось получить ответ сервера
	Иначе
		Если Ответ.КодСостояния = 404 Тогда
			Результат.Использовать = Ложь;
		ИначеЕсли Ответ.КодСостояния = 200 Тогда
			Результат.Использовать = Истина;
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
			Попытка
				ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
				Результат.ВерсииФормата = ОписаниеОбъектаИзОбъектаОтвета(ОбъектОтвета, ОписаниеПолейОбъектаВерсииФормата());
			Исключение
				КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ВерсииФорматаОбмена() Экспорт

	Версии = Новый Массив;
	Версии.Добавить("1.0");
	Версии.Добавить("1.1");
	Версии.Добавить("1.2");
	
	Возврат Версии;

КонецФункции

Функция ВерсииAPI() Экспорт

	Версии = Новый Массив;
	Версии.Добавить("1.0");
	
	Возврат Версии;

КонецФункции

#КонецОбласти

#Область Прочие

Функция ПроверкаАвторизации() Экспорт

	Результат = Новый Структура("ТокенПолучен,НеактуальныеКлючи",Ложь,Ложь);
	
	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	Ответ = НовыйТокенАутентификации(ПараметрыОбмена);
	
	Результат.ТокенПолучен = ЗначениеЗаполнено(Ответ.Токен);
	Результат.НеактуальныеКлючи = Ответ.НеактуальныеКлючи;
	
	Возврат Результат;

КонецФункции

Процедура ПроверитьДоступностьАдресаПоИмени(ВестиПротокол) Экспорт

	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(НастройкиСервиса.АдресПриложенияПоИмени);
	
	ПараметрыОбмена = НовоеОписаниеПараметрыОбмена();
	ПараметрыОбмена.СтруктураАдреса = СтруктураАдреса;
	ПараметрыОбмена.Соединение 		= НовоеHTTPСоединение(СтруктураАдреса);
	ПараметрыОбмена.ВестиПротокол 	= ВестиПротокол Или Константы.РегистрироватьВЖурналеСобытийЗапросы.Получить();
	
	РесурсСервиса = РесурсПинг();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьАдресПриложенияПоИмениДоступен(Истина);
	КонецЕсли;

КонецПроцедуры

Функция ЗапросИнформацияОПриложении(ПараметрыОбмена)

	ИнформацияОПриложении = Неопределено;
	
	РесурсСервиса = РесурсИнформацияОПриложении();
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			ИнформацияОПриложении = СтруктураИзОбъектаСервиса(ОбъектОтвета, ОписаниеОбъектаИнформацияОПриложении(ПараметрыОбмена.ВерсияDTO));
		Исключение
			КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Возврат ИнформацияОПриложении;

КонецФункции

Функция РезультатПубликацииФизическогоЛица(ФизическоеЛицо) Экспорт

	ПараметрыОбмена = ПараметрыОбмена(Ложь);
	РезультатВыгрузки = РезультатВыгрузкиФизическихЛиц(ПараметрыОбмена, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	
	Возврат Не ЗначениеЗаполнено(РезультатВыгрузки.Выгружено);

КонецФункции

Функция ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанные, Расширение)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ОписаниеФайла = Новый Структура("ИмяФайла,Расширение",ИмяВременногоФайла,Расширение);
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсФайлы(), "POST",,ОписаниеФайла));
	УдалитьФайлы(ИмяВременногоФайла);
	Если Ответ = Неопределено Или Ответ.КодСостояния >= 300 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	Попытка
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
	Исключение
		КабинетСотрудника.ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ОбъектОтвета.Получить("fileID");
	
КонецФункции

Функция РезультатОзнакомленияСРасчетнымЛистком(ПараметрыОбмена, ФизическоеЛицо, Месяц) Экспорт
	
	РесурсСервиса = РесурсОзнакомлениеСРасчетнымЛистком();

	personID = Строка(ФизическоеЛицо.УникальныйИдентификатор());
	РесурсСервиса = СтрЗаменить(РесурсСервиса, "{personID}", personID);
	
	month = Формат(Месяц, "ДФ=yyyy-MM");
	РесурсСервиса = СтрЗаменить(РесурсСервиса, "{month}", month);
	
	Ответ = HTTPОтветСервиса(ПараметрыОбмена, ПараметрыЗапроса(РесурсСервиса, "GET"));
	
	Ознакомлен = Ложь;
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Ознакомлен = Истина;
	КонецЕсли;
	
	Возврат Ознакомлен;
	
КонецФункции

// Получает хеш по алгоритму SHA256 и преобразовывает в HexСтроку.
//
// Возвращаемое значение:
// 	Строка
//
Функция ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанные)

	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанных.Добавить(ДвоичныеДанные);
	
	Возврат ВРег(ПолучитьHexСтрокуИзДвоичныхДанных(ХешированиеДанных.ХешСумма));

КонецФункции

Процедура СохранитьЭлектроннуюПодписьРезультатаСогласования(ЭлектронныйДокумент, РезультатСогласования, Отпечаток = "")

	РолиПодписантов = РолиПодписантовРезультатаСогласования(РезультатСогласования);
	Если Не Перечисления.РолиПодписантовКЭДО.ЭтоПодписьСервиса(РолиПодписантов) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрокаРольПодписанта = КабинетСотрудника.СтрокаРольПодписантаДляЭП(РолиПодписантов[0]);
	Подпись = Base64Значение(РезультатСогласования.ЭлектроннаяПодпись);
	
	Подписи = ЭлектроннаяПодписьКЭДО.УстановленныеПодписи(ЭлектронныйДокумент);
	ПодписиКУдалению = Новый Массив;
	Для каждого СвойстваПодписи Из Подписи Цикл
		Если СвойстваПодписи.Подпись = Подпись Тогда
			ПодписиКУдалению.Добавить(СвойстваПодписи.ПорядковыйНомер);
		КонецЕсли;
	КонецЦикла;
	Если ПодписиКУдалению.Количество() > 0 Тогда
		ЭлектроннаяПодпись.УдалитьПодпись(ЭлектронныйДокумент, ПодписиКУдалению);
	КонецЕсли;
	
	Сертификат = Неопределено;
	Если ЗначениеЗаполнено(РезультатСогласования.СертификатЭП) Тогда
		Сертификат = Base64Значение(РезультатСогласования.СертификатЭП);
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA1);
		ХешированиеДанных.Добавить(Сертификат);
		Отпечаток = ПолучитьBase64СтрокуИзДвоичныхДанных(ХешированиеДанных.ХешСумма);
	КонецЕсли;
	
	СвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();
	СвойстваПодписи.Вставить("Подпись", 			Подпись);
	СвойстваПодписи.Вставить("Сертификат", 			Сертификат);
	СвойстваПодписи.Вставить("Отпечаток", 			Отпечаток);
	СвойстваПодписи.Вставить("КомуВыданСертификат", РезультатСогласования.ИмяПодписанта);
	СвойстваПодписи.Вставить("Комментарий", 		СтрокаРольПодписанта);
	СвойстваПодписи.Вставить("ДатаПодписи", 		РезультатСогласования.ДатаПодписи);
	СвойстваПодписи.ИдентификаторПодписи = Новый УникальныйИдентификатор;
	
	ЭлектроннаяПодпись.ДобавитьПодпись(ЭлектронныйДокумент, СвойстваПодписи);
	
	РезультатПодписания = Новый Структура;
	РезультатПодписания.Вставить("Объект", ЭлектронныйДокумент);
	РезультатПодписания.Вставить("СвойстваПодписи", СвойстваПодписи);
	РезультатПодписания.Вставить("ИдентификаторПодписи", СвойстваПодписи.ИдентификаторПодписи);
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатСогласования, "НаборСвойствПодписиКЭДО") Тогда
		ДанныеПодписейКЭДО = РезультатСогласования.НаборСвойствПодписиКЭДО;
	Иначе
		ДанныеПодписейКЭДО = Новый Массив;
		ДанныеПодписейКЭДО.Добавить(
			Новый Структура("МЧД,РольПодписанта",
				Неопределено,
				РезультатСогласования.РольПодписанта));
	КонецЕсли;
	
	КадровыйЭДОВызовСервера.ПриДобавленииПодписи(
		РезультатПодписания, ДанныеПодписейКЭДО);

КонецПроцедуры

Функция ИмяФайлаБезРасширения(ИмяФайла, Расширение)

	ИмяФайлаБезРасширения = ИмяФайла;
	Если СтрЗаканчиваетсяНа(СокрЛП(ВРег(ИмяФайла)), "." + ВРег(Расширение)) Тогда
		КоличествоСимволов = СтрДлина(ИмяФайла) - СтрДлина(Расширение) - 1;
		ИмяФайлаБезРасширения = Лев(ИмяФайла, КоличествоСимволов);
	КонецЕсли;
	
	Возврат ИмяФайлаБезРасширения;

КонецФункции

Функция ОбъектФайлПоПрисоединенномуФайлу(ПрисоединенныйФайл, ПараметрыОбмена) Экспорт
	
	Попытка
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл, РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла());
	Исключение
		ЗаписатьОшибкуВыгрузкиИзменений(ПрисоединенныйФайл, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	ИдентификаторФайла = ОпубликоватьДвоичныеДанныеФайла(ПараметрыОбмена, ДвоичныеДанныеФайла, ДанныеФайла.Расширение);
	Если ИдентификаторФайла = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка публикации файла.';
								|en = 'File publishing error.'");
		ЗаписатьОшибкуВыгрузкиИзменений(ПрисоединенныйФайл, ОписаниеОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	ВерсияФайла = ВерсияФайлаПоДвоичнымДанным(ДвоичныеДанныеФайла);
	ИмяФайлаБезРасширения = ИмяФайлаБезРасширения(ДанныеФайла.ИмяФайла, ДанныеФайла.Расширение);
	
	Возврат ОбъектСервисаФайл(ИдентификаторФайла, ИмяФайлаБезРасширения, ДанныеФайла.Расширение, ДанныеФайла.Размер, ПараметрыОбмена.ВерсияDTO, ВерсияФайла);

КонецФункции

Функция РазмерПакета(ПараметрыОбмена, ТипОбъекта) Экспорт

	РазмерПакета = ПараметрыОбмена.ТипОбъектаРазмерПакета[ТипОбъекта];
	Если Не ЗначениеЗаполнено(РазмерПакета) Тогда
		РазмерПакета = 100;
	КонецЕсли;
	
	Возврат РазмерПакета;
	
КонецФункции

Процедура ДобавитьВКоллекциюРезультатСогласования(РезультатыСогласования, РезультатСогласования, УстановленнаяПодпись, ОписаниеОбъектРезультатСогласования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(УстановленнаяПодпись, "НаборСвойствПодписиКЭДО")
		И ЗначениеЗаполнено(УстановленнаяПодпись.НаборСвойствПодписиКЭДО) Тогда
		
		Для Каждого ЭлементСвойств Из УстановленнаяПодпись.НаборСвойствПодписиКЭДО Цикл
			Если Не Перечисления.РолиПодписантовКЭДО.ЭтоПодписьСервиса(ЭлементСвойств.РольПодписанта) Тогда
				РезультатСогласованияПодписи = ОбщегоНазначения.СкопироватьРекурсивно(РезультатСогласования);
				РезультатСогласованияПодписи.РольПодписанта = ЭлементСвойств.РольПодписанта;
				Если РезультатСогласованияПодписи.Свойство("МЧД")
					И ЗначениеЗаполнено(ЭлементСвойств.МЧД) Тогда
					РезультатСогласованияПодписи.МЧД = ЭлементСвойств.МЧД.Ссылка;
				КонецЕсли;
				ОбъектРезультатСогласования = ОбъектСервисаПоОписанию(РезультатСогласованияПодписи, ОписаниеОбъектРезультатСогласования);
				РезультатыСогласования.Добавить(ОбъектРезультатСогласования);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Если Не Перечисления.РолиПодписантовКЭДО.ЭтоПодписьСервиса(РезультатСогласования.РольПодписанта) Тогда
			ОбъектРезультатСогласования = ОбъектСервисаПоОписанию(РезультатСогласования, ОписаниеОбъектРезультатСогласования);
			РезультатыСогласования.Добавить(ОбъектРезультатСогласования);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция РолиПодписантовРезультатаСогласования(РезультатСогласования)
	
	РолиПодписантов = Новый Массив;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатСогласования, "НаборСвойствПодписиКЭДО") Тогда
		Если ЗначениеЗаполнено(РезультатСогласования.НаборСвойствПодписиКЭДО) Тогда
			Для Каждого ДанныеСвойств Из РезультатСогласования.НаборСвойствПодписиКЭДО Цикл
				РолиПодписантов.Добавить(ДанныеСвойств.РольПодписанта);
			КонецЦикла;
		КонецЕсли;
	Иначе
		РолиПодписантов.Добавить(РезультатСогласования.РольПодписанта);
	КонецЕсли;
	
	Возврат РолиПодписантов;
	
КонецФункции

Процедура ЗаполнитьРезультатРезультатамиВыгрузки(Результат, РезультатВыгрузки, ТипОбъекта, АдресРесурса, РегистрироватьВыгрузку) Экспорт
	
	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено,
			ВыгруженныеОбъектыПоИдентификаторам(РезультатВыгрузки.Выгружено, ТипОбъекта, РегистрироватьВыгрузку),
			Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатВыгрузки.НеВыгружено) Тогда
		Возврат;
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначенияБЗККлиентСервер.КлючиСоответствия(РезультатВыгрузки.НеВыгружено);
	ИдентификаторСсылка = ИнтеграцияУправлениеПерсоналомОбмен.ПубличныйИдентификаторСсылка(Идентификаторы, ТипОбъекта);
	
	Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
		Ссылка = ИдентификаторСсылка[Ошибка.Ключ];
		Если ЗначениеЗаполнено(Ссылка) Тогда
			Результат.НеВыгружено.Вставить(Ссылка, Ошибка.Значение);
		КонецЕсли;
		ЗарегистрироватьОшибкуВыгрузкиОбъекта(Ссылка, АдресРесурса, Ошибка.Значение);
	КонецЦикла;

КонецПроцедуры

Процедура ЗарегистрироватьОшибкуВыгрузкиОбъекта(Ссылка, АдресРесурса, ОписаниеОшибки)

	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредставлениеОбъекта = Строка(Ссылка);
	Иначе
		ПредставлениеОбъекта = НСтр("ru = '<объект не найден>';
									|en = '<object is not found>'");
	КонецЕсли;
	
	ШаблонОписания = НСтр(
	"ru = 'Ошибка выгрузки: ресурс: %1, объект: %2
	|Описание ошибки:
	|%3';
	|en = 'Export error: resource: %1, object: %2
	|Error description:
	|%3'");
	Комментарий = СтрШаблон(ШаблонОписания, АдресРесурса, ПредставлениеОбъекта, ОписаниеОшибки);
	ИмяСобытия = ИнтеграцияКабинетСотрудника.ИменаСобытийЖР().ПрочиеСобытия;
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);

КонецПроцедуры

Функция ВыгруженныеОбъектыПоИдентификаторам(Идентификаторы, ТипОбъекта, РегистрироватьВыгрузку)
	
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.УстановитьПараметр("Приложение", Приложение);
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Идентификаторы.Ссылка КАК Ссылка,
	|	Идентификаторы.ТипОбъекта КАК ТипОбъекта,
	|	Идентификаторы.Идентификатор КАК Идентификатор,
	|	&Приложение КАК Приложение,
	|	ЕСТЬNULL(ВыгруженныеОбъекты.Выгружался, ЛОЖЬ) КАК Выгружался
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом КАК Идентификаторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК ВыгруженныеОбъекты
	|		ПО Идентификаторы.Ссылка = ВыгруженныеОбъекты.Ссылка
	|			И Идентификаторы.ТипОбъекта = ВыгруженныеОбъекты.ТипОбъекта
	|			И (ВыгруженныеОбъекты.Приложение = &Приложение)
	|ГДЕ
	|	Идентификаторы.ТипОбъекта = &ТипОбъекта
	|	И Идентификаторы.Идентификатор В(&Идентификаторы)";
	ТаблицаИдентификаторов = Запрос.Выполнить().Выгрузить();
	
	Если РегистрироватьВыгрузку Тогда
		
		НовыеОбъекты = ТаблицаИдентификаторов.Скопировать(Новый Структура("Выгружался", Ложь));
		Если НовыеОбъекты.Количество() > 0 Тогда
			
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом");
				ЭлементБлокировки.ИсточникДанных = НовыеОбъекты;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Приложение", "Приложение");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
				Блокировка.Заблокировать();
				
				Для каждого СтрокаТЗ Из НовыеОбъекты Цикл
					МенеджерЗаписи = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
					МенеджерЗаписи.Выгружался = Истина;
					МенеджерЗаписи.Записать();
				КонецЦикла;
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаИдентификаторов.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#Область УстаревшиеПрочие

Процедура ПроверитьВерсиюПриложения(ПараметрыОбмена)

	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	ПрежняяВерсия = Настройки.ВерсияПриложения;
	НоваяВерсия = Неопределено;
	
	ИнформацияОПриложении = ЗапросИнформацияОПриложении(ПараметрыОбмена);
	Если ИнформацияОПриложении <> Неопределено Тогда
		НоваяВерсия = ИнформацияОПриложении.ВерсияПриложения;
	КонецЕсли;
	
	Если НоваяВерсия = Неопределено И Не Константы.СервисКабинетСотрудникаВЛокальнойСети.Получить() Тогда
		НоваяВерсия = КабинетСотрудникаМенеджерСервиса.ВерсияПриложения(ПараметрыОбмена.ВестиПротокол);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоваяВерсия) Тогда
		ПараметрыОбмена.ВерсияПриложения = НоваяВерсия;
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюПриложения(НоваяВерсия);
		КабинетСотрудника.ВыполнитьДействияПриПереходеНаВерсию(ПрежняяВерсия, НоваяВерсия);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПреобразованиеВыгружаемыхОбъектов

Функция МассивИзТаблицы(ТаблицаДанных, ОписаниеПолей) Экспорт
	
	Результат = Новый Массив;
	Для Каждого СтрокаТаблицыДанных Из ТаблицаДанных Цикл
		Результат.Добавить(СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПолей));
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПолей)
	
	Результат = Новый Структура;
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			Значение = СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПоля["ОписаниеПолей"]);
		ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("ТаблицаЗначений") Тогда
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, ОписаниеПоля["ИмяПоляКонфигурации"]);
			Если ЗначениеЗаполнено(Значение) Тогда
				Значение = МассивИзТаблицы(Значение, ОписаниеПоля["ОписаниеПолей"]);
			КонецЕсли;
		Иначе
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, ОписаниеПоля["ИмяПоляКонфигурации"]);
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение)
				Или ТипЗнч(Значение) = Тип("Число") Тогда
			Результат.Вставить(ОписаниеПоля["ИмяПоляСервиса"], Значение);
		ИначеЕсли ОписаниеПоля["ОбязательноеПоле"] Тогда
			Результат.Вставить(ОписаниеПоля["ИмяПоляСервиса"], "");
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ВыбираемыеПоля(СоответствиеПолей)
	
	ВыбираемыеПоля = Новый Массив;
	Для Каждого КлючИЗначение Из СоответствиеПолей Цикл
		Поле = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(Поле) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Поле) = Тип("Соответствие") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВыбираемыеПоля, ВыбираемыеПоля(Поле), Истина);
		ИначеЕсли ВыбираемыеПоля.Найти(Поле) = Неопределено Тогда
			ВыбираемыеПоля.Добавить(Поле);
		КонецЕсли;
	КонецЦикла;
	Возврат ВыбираемыеПоля;
	
КонецФункции

Функция ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей) Экспорт
	
	МассивЭлементов = Новый Массив;
	Для Каждого СтрокаТаблицыДанных Из ТаблицаДанных Цикл
		МассивЭлементов.Добавить(СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, СоответствиеПолей));
	КонецЦикла;
	Возврат МассивЭлементов;
	
КонецФункции

Функция СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, СоответствиеПолей)
	
	Результат = Новый Структура;
	Для Каждого КлючИЗначение Из СоответствиеПолей Цикл
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Соответствие") Тогда
			Значение = СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, КлючИЗначение.Значение);
		Иначе
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, КлючИЗначение.Значение);
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение) Тогда
			Результат.Вставить(КлючИЗначение.Ключ, Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ЗначениеПоИмениПоля(СтрокаТаблицыДанных, Поле)
	
	Если Не ЗначениеЗаполнено(Поле) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ВладелецСтроки = СтрокаТаблицыДанных.Владелец();
	Если ВладелецСтроки.Колонки.Найти(Поле) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат СтрокаТаблицыДанных[Поле];
	
КонецФункции

#КонецОбласти

#КонецОбласти 




