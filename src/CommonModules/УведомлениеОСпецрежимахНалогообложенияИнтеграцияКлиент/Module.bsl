#Область ПрограммныйИнтерфейс

Процедура ОткрытьИЗаполнитьФормуУведомленияККТ(ДанныеЗаполнения, ИмяФормы = "Форма2018_1") Экспорт
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("Организация")
		И ДанныеЗаполнения.Свойство("РегистрацияВИФНС") Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ДанныеПомощника", ДанныеЗаполнения);
		ПараметрыОткрытия.Вставить("Организация", ДанныеЗаполнения.Организация);
		ПараметрыОткрытия.Вставить("РегистрацияВИФНС", ДанныеЗаполнения.РегистрацияВИФНС);
		
		Если ДанныеЗаполнения.Свойство("Ссылка") И ЗначениеЗаполнено(ДанныеЗаполнения.Ссылка) Тогда
			ПараметрыОткрытия.Вставить("Ключ", ДанныеЗаполнения.Ссылка);
		КонецЕсли;
		
		ОткрытьФорму("Отчет.РегламентированноеУведомлениеУменьшениеНалогаККТ.Форма." + ИмяФормы, ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

Функция НовыеДанныеИзвещения() Экспорт

	ДанныеИзвещения = Новый Структура;
	ДанныеИзвещения.Вставить("ДатаДокумента", '00010101');
	ДанныеИзвещения.Вставить("НомерДокумента", "");
	ДанныеИзвещения.Вставить("ПоясненияТранспортныеСредства", Новый Массив); // элемент массива - см. НовыеПоясненияОТранспортномСредстве()
	ДанныеИзвещения.Вставить("ПоясненияНедвижимость", Новый Массив); // элемент массива - см. НовыеПоясненияОНедвижимости()

	Возврат ДанныеИзвещения;

КонецФункции

Функция НовыеПоясненияОТранспортномСредстве() Экспорт 

	Пояснения = Новый Структура;
	Пояснения.Вставить("ВидТС", ""); // от 01 до 16
	Пояснения.Вставить("РегЗнакТС", ""); // гос номер ТС
	Пояснения.Вставить("СодержПоясн", ""); // текст пояснений
	
	Возврат Пояснения;

КонецФункции

Функция НовыеПоясненияОНедвижимости() Экспорт 

	Пояснения = Новый Структура;
	Пояснения.Вставить("НомКадастр", ""); // кадастровый номер недвижимости
	Пояснения.Вставить("СодержПоясн", ""); // текст пояснений
	
	Возврат Пояснения;

КонецФункции

Процедура ОткрытьТрудовойДоговорСИностранцем(Организация, 
		ВидУведомления, 
		ДанныеЗаполнения,
		РежимОткрытия = Неопределено,
		ВладелецФормы = Неопределено) Экспорт 
		
		
	Если ТипЗнч(Организация) <> Тип("СправочникСсылка.Организации")
		Или Не ЗначениеЗаполнено(Организация) Тогда 
		
		ВызватьИсключение "Не заполнена организация";
	КонецЕсли;
	Если ВидУведомления <> ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗаключениеТрудовогоДоговора")
		И ВидУведомления <> ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.РасторжениеТрудовогоДоговора")
		И ВидУведомления <> ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОбязательстваВыплатаЗП")
		И ВидУведомления <> ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ХодатайствоОПривлечении") Тогда 
		
		ВызватьИсключение "Некорректный вид уведомления";
	КонецЕсли;
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда 
		
		ВызватьИсключение "Некорректные данные заполнения";
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("ЗаполнитьПриОткрытии", Истина);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация", Организация);
	ПараметрыОткрытия.Вставить("ВидУведомления", ВидУведомления);
	ПараметрыОткрытия.Вставить("Данные", ДанныеЗаполнения);
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.ФормаОбъекта", 
		ПараметрыОткрытия, ВладелецФормы,,,,, РежимОткрытия);
КонецПроцедуры

#КонецОбласти
