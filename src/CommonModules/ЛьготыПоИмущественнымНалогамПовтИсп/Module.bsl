
#Область ПрограммныйИнтерфейс

// Данные о льготах по имущественным налогам.
// 
// Параметры:
//  Налог - ПеречислениеСсылка.ВидыИмущественныхНалогов
//  ВключатьНеактуальные - Булево - если Истина, то будут включены данные о льготах, которых нет в действующей редакции НК
// 
// Возвращаемое значение:
//  Структура - Данные о льготах по имущественным налогам:
// * ЕстьНеактуальные - Булево - Истина, если по налогу применялись льготы по НК, которых нет в действующей редакции НК
// * ФедеральныеЛьготы - Соответствие - ключ - код льготы, значение - описание льготы п нормативным документам
// * МестныеЛьготы - Соответствие - ключ - вид льготы (Перечисления.ВидыЛьготПоИмущественнымНалогам),
//					значение - описание льготы п нормативным документам
Функция ДанныеОЛьготахПоИмущественнымНалогам(Налог, ВключатьНеактуальные = Истина) Экспорт 

	Результат = Новый Структура;
	
	ФедеральныеЛьготы = Новый Соответствие;
	
	ИмяМакетаКлассификатора = "КлассификаторЛьгот_" + ОбщегоНазначения.ИмяЗначенияПеречисления(Налог);
	
	Попытка
		МакетЛьгот = Справочники.ОснованияЛьготПоИмущественнымНалогам.ПолучитьМакет(ИмяМакетаКлассификатора);
	Исключение
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден макет классификатора льгот ""%1""';
										|en = 'Cannot find the %1 relief classifier template'"), ИмяМакетаКлассификатора);
	КонецПопытки;
	
	ОбластьМакета = МакетЛьгот.ПолучитьОбласть("ФедеральныеЛьготы"); // есть в макетах для всех налогов
	КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
	
	Результат.Вставить("ЕстьНеактуальные", Ложь);
	
	// Актуальные льготы
	Для НомерСтроки = 1 По КоличествоЛьгот Цикл
		
		КодЛьготы = СокрЛП(ОбластьМакета.Область(НомерСтроки, 1).Текст);
		
		ОписаниеЛьготы = Новый Структура;
		
		ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
		ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
		ОписаниеЛьготы.Вставить("Основание", ОбластьМакета.Область(НомерСтроки, 4).Текст);
		
		ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 5).Текст;
		ОписаниеЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой]);
		ОписаниеЛьготы.Вставить("ПроцентЛьготы", 0);
		Если ОписаниеЛьготы.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент
			 Или ОписаниеЛьготы.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент Тогда
			ПроцентЛьготы = ОбластьМакета.Область(НомерСтроки, 6).Текст;
			ОписаниеЛьготы.Вставить("ПроцентЛьготы", ?(ЗначениеЗаполнено(ПроцентЛьготы), Число(ПроцентЛьготы), 0));
		КонецЕсли;
		ОписаниеЛьготы.Вставить("Актуальная", Истина);
		
		ФедеральныеЛьготы.Вставить(КодЛьготы, ОписаниеЛьготы);
		
	КонецЦикла;
	
	// Неактуальные льготы - для обратной совместимости. Для некоторых налогов область может отсутствовать.
	Результат.Вставить("ЕстьНеактуальные", Истина);
	Если МакетЛьгот.Области.Найти("ФедеральныеЛьготы_Неактуальные") = Неопределено Тогда
		Результат.Вставить("ЕстьНеактуальные", Ложь);	
	ИначеЕсли ВключатьНеактуальные Тогда
		ОбластьМакета = МакетЛьгот.ПолучитьОбласть("ФедеральныеЛьготы_Неактуальные");	
		КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
		Для НомерСтроки = 1 По КоличествоЛьгот Цикл
			
			КодЛьготы = СокрЛП(ОбластьМакета.Область(НомерСтроки, 1).Текст);
			
			ОписаниеЛьготы = Новый Структура;
			
			ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
			ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
			ОписаниеЛьготы.Вставить("Основание", ОбластьМакета.Область(НомерСтроки, 4).Текст);
			
			ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 5).Текст;
			ОписаниеЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой]);
			ПроцентЛьготы = ОбластьМакета.Область(НомерСтроки, 6).Текст;
			ОписаниеЛьготы.Вставить("ПроцентЛьготы", ?(ЗначениеЗаполнено(ПроцентЛьготы), Число(ПроцентЛьготы), 0));
			ОписаниеЛьготы.Вставить("Актуальная", Ложь);
			
			ФедеральныеЛьготы.Вставить(КодЛьготы, ОписаниеЛьготы);
			
		КонецЦикла;
	КонецЕсли;	
	
	Результат.Вставить("ФедеральныеЛьготы", ФедеральныеЛьготы);
	
	МестныеЛьготы = Новый Соответствие;
	ОбластьМакета = МакетЛьгот.ПолучитьОбласть("МестныеЛьготы");	
	КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
	
	// Для региональных льгот больше информации несет вид льготы - по нему можно однозначно определить код льготы
	Для НомерСтроки = 1 По КоличествоЛьгот Цикл
		
		ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 4).Текст;
		ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой];
				
		ОписаниеЛьготы = Новый Структура;
		
		ОписаниеЛьготы.Вставить("КодЛьготы", ОбластьМакета.Область(НомерСтроки, 1).Текст);
		ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
		ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
		
		МестныеЛьготы.Вставить(ВидЛьготы, ОписаниеЛьготы);
		
	КонецЦикла;
	
	Результат.Вставить("МестныеЛьготы", МестныеЛьготы);
	
	Возврат Результат;
	
КонецФункции
 
#КонецОбласти
