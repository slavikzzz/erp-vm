#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийФормы

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ВзаиморасчетыПоПрочимДоходамКлиент.ИмяСобытияИзмененияОплатыВедомости() Тогда
		
		Форма.УстановитьДоступностьЭлементов();
		Форма.УстановитьПредставлениеОплаты();
		
		Если Форма.Элементы.ВнешниеОперацииГруппа.Видимость Тогда
			ВедомостьПрочихДоходовКлиентСервер.УстановитьОтображениеВХО(Форма);
		КонецЕсли	
		
	ИначеЕсли ИмяСобытия = "ВыборДокументовОснованийВедомостейПрочихДоходов" Тогда
		
		Форма.Активизировать();
	
		Форма.Объект.Основания.Очистить();
		Для Каждого Основание Из Параметр Цикл
			Строка = Форма.Объект.Основания.Добавить();
			Строка.Документ = Основание.Значение
		КонецЦикла;	
		ВедомостьПрочихДоходовКлиентСервер.УстановитьПредставлениеОснований(Форма);
		
		Если Форма.Объект.Основания.Количество() > 0 Тогда
			Заполнить(Форма);
		Иначе
			Очистить(Форма);
		КонецЕсли	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ПредставлениеОснованийНажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныеДокументы = Новый СписокЗначений;
	Для Каждого Основание Из Форма.Объект.Основания Цикл
		ВыбранныеДокументы.Добавить(Основание.Документ)
	КонецЦикла;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособВыплаты", Форма.Объект.СпособВыплаты);
	ПараметрыФормы.Вставить("ДатаОстатков", КонецМесяца(Форма.Объект.ПериодРегистрации));
	ПараметрыФормы.Вставить("Организация", Форма.Объект.Организация);
	ПараметрыФормы.Вставить("ВыбранныеДокументы", ВыбранныеДокументы);
	ПараметрыФормы.Вставить("ИсключаемыйРегистратор", Форма.Объект.Ссылка);
	
	ОткрытьФорму("ОбщаяФорма.ВыборДокументовОснованийВедомостейПрочихДоходов", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ПеречислениеНДФЛВыполненоПриИзменении(Форма, Элемент) Экспорт
	ВедомостьПрочихДоходовКлиентСервер.УстановитьОтображениеВХО(Форма);
КонецПроцедуры

Процедура ДатаПриИзменении(Форма) Экспорт
	Форма.ДатаВыплаты = ВедомостьПрочихДоходовКлиентСервер.ДатаВыплаты(Форма.Объект);
КонецПроцедуры

Процедура ДатаВыплатыПриИзменении(Форма) Экспорт
	Форма.Объект.ДатаВыплаты = Форма.ДатаВыплаты;
	Форма.ДатаВыплаты = ВедомостьПрочихДоходовКлиентСервер.ДатаВыплаты(Форма.Объект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

Процедура СоставВыбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт

	ТекущиеДанные = Форма.Элементы.Состав.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
		
		Если Поле.ГиперссылкаЯчейки И Поле.Имя = "СоставНДФЛРасшифровка" ИЛИ Поле.Имя = "СоставНДФЛ" Тогда
			Форма.РедактироватьНДФЛСтроки(ТекущиеДанные);
			СтандартнаяОбработка = Ложь;
			Возврат
		КонецЕсли;
		
		Если Поле.ГиперссылкаЯчейки ИЛИ Поле.ТолькоПросмотр Тогда
			Форма.РедактироватьВыплатуСтроки(ТекущиеДанные);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставПередУдалением(Форма, Элемент, Отказ) Экспорт
	
	ИдентификаторыСтрок = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
		
		Строка = Форма.Объект.Состав.НайтиПоИдентификатору(ВыделеннаяСтрока);
		
		Если Строка <> Неопределено Тогда
			ИдентификаторыСтрок.Добавить(Строка.ИдентификаторСтроки);
		КонецЕсли	
		
	КонецЦикла;	
	
	Форма.ИдентификаторыСтрок = Новый ФиксированныйМассив(ИдентификаторыСтрок);
	
КонецПроцедуры

Процедура СоставКВыплатеОткрытие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка = Ложь;
	Форма.РедактироватьВыплатуСтроки(Форма.Элементы.Состав.ТекущиеДанные);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

Процедура Заполнить(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.ЗаполнитьНаСервере()
	
КонецПроцедуры

Процедура Подобрать(Форма) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	КадровыйУчетКлиент.ПодобратьФизическихЛицОрганизации(
		Форма.Элементы.Состав, 
		Форма.Объект.Организация, 
		Форма.АдресСпискаПодобранныхФизическихЛиц(),,
		ПараметрыОткрытияФормы);
	
КонецПроцедуры

Процедура Очистить(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.ОчиститьНаСервере()
	
КонецПроцедуры

Процедура РедактироватьВыплатуСтроки(Форма, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("Организация",                          ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("ПериодРегистрации",                    Дата(1,1,1));
	ПараметрыОткрытия.Вставить("СтатьяФинансирования",                 ПредопределенноеЗначение("Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("СтатьяРасходов",                       ПредопределенноеЗначение("Справочник.СтатьиРасходовЗарплата.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("ВидДоходаИсполнительногоПроизводства", ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка"));
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Форма.Объект);
	
	ПараметрыОткрытия.Вставить("ИдентификаторСтроки",            ДанныеСтроки.ИдентификаторСтроки);
	ПараметрыОткрытия.Вставить("ФизическоеЛицо",                 ДанныеСтроки.ФизическоеЛицо);
	ПараметрыОткрытия.Вставить("АдресВХранилищеВыплатыПоСтроке", Форма.АдресВХранилищеВыплатыПоСтроке(ДанныеСтроки.ИдентификаторСтроки));
	
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	Оповещение = Новый ОписаниеОповещения("РедактироватьВыплатуСтрокиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.РедактированиеВыплатыСтрокиВедомости", ПараметрыОткрытия, Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура РедактироватьВыплатуСтрокиЗавершение(РезультатыРедактирования, ДополнительныеПараметры) Экспорт
	
	Если РезультатыРедактирования <> Неопределено И РезультатыРедактирования.Модифицированность Тогда
		ДополнительныеПараметры.Форма.РедактироватьВыплатуСтрокиЗавершениеНаСервере(РезультатыРедактирования)
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

