
#Область СлужебныеПроцедурыИФункции

#Область СопоставлениеДанных

// Возвращает доступные варианты результатов сопоставления загружаемой строки.
//
// Возвращаемое значение:
//   Структура из КлючИЗначение - в ключе указывается предопределенное значение,
//                                в значении представление с загружаемой строке.
//
Функция ВариантыРезультатовСопоставления() Экспорт

	ВариантыРезультатовСопоставления = Новый Структура;
	ВариантыРезультатовСопоставления.Вставить("СтрокаСопоставлена",   "СтрокаСопоставлена");
	ВариантыРезультатовСопоставления.Вставить("СтрокаНеСопоставлена", "СтрокаНеСопоставлена");
	ВариантыРезультатовСопоставления.Вставить("Неоднозначность",      "Неоднозначность");

	Возврат ВариантыРезультатовСопоставления;

КонецФункции

// Устанавливает результат сопоставления в строке.
// В заполняемой строке в колонке РезультатСопоставленияСтроки содержится ограниченный список значений
// (см. ВариантыРезультатовСопоставления).
//
// Параметры:
//   ТекущиеДанные                    - СтрокаТаблицыЗначений, ДанныеФормыЭлементКоллекции - строка заполнения
//                                           результата сопоставления.
//   КолонкиОбязательныеПриПроверке   - СписокЗначений Из Строка - список обязательных колонок.
//   КолонкиДополнительныеПриПроверке - СписокЗначений Из Строка - список проверяемых дополнительных колонок.
//
Процедура ЗаполнитьРезультатСопоставленияСтроки(ТекущиеДанные, КолонкиОбязательныеПриПроверке,
		КолонкиДополнительныеПриПроверке) Экспорт

	ВариантыРезультатовСопоставления = ВариантыРезультатовСопоставления();
	ОписаниеОшибки = "";

	Для Каждого Колонка Из КолонкиОбязательныеПриПроверке Цикл
		Если Не ЗначениеЗаполнено(ТекущиеДанные[Колонка.Значение]) Тогда
			ОписаниеОшибки = ОписаниеОшибки + Колонка.Значение + ";";
		КонецЕсли;
	КонецЦикла;

	Для Каждого Колонка Из КолонкиДополнительныеПриПроверке Цикл
		Если Не ЗначениеЗаполнено(ТекущиеДанные[Колонка.Значение]) Тогда
			ОписаниеОшибки = ОписаниеОшибки + Колонка.Значение + ";";
		КонецЕсли;
	КонецЦикла;

	ЗаполненыВсеОбязательныеКолонки = ПустаяСтрока(ОписаниеОшибки);

	Если ТекущиеДанные.РезультатСопоставленияСтроки = ВариантыРезультатовСопоставления.Неоднозначность Тогда
		ТекущиеДанные.СтрокаОбработана = ЗаполненыВсеОбязательныеКолонки;
		Возврат;
	КонецЕсли;

	ТекущиеДанные.РезультатСопоставленияСтроки = ?(ЗаполненыВсеОбязательныеКолонки,
		ВариантыРезультатовСопоставления.СтрокаСопоставлена,
		ВариантыРезультатовСопоставления.СтрокаНеСопоставлена);
	ТекущиеДанные.ОписаниеОшибки               = ОписаниеОшибки;
	ТекущиеДанные.СтрокаОбработана             = ЗаполненыВсеОбязательныеКолонки;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
