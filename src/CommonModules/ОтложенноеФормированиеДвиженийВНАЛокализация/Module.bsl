////////////////////////////////////////////////////////////////////////////////
// Процедуры механизма отложенного формирования движений по документам внеоборотных активов.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ВернутьДокументыКОтражению(Ссылка, Организация, Дата, ТаблицыДляДвижений) Экспорт
	
	//++ Локализация
	
	Если Ссылка.Метаданные().Движения.Содержит(Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете) Тогда
	
		ИмяТаблицы = "Таблица" + "ОтражениеДокументовВРеглУчете";
		
		Если ТаблицыДляДвижений.Свойство(ИмяТаблицы) Тогда
			ТаблицаОтражениеДокументовВУчете = ТаблицыДляДвижений[ИмяТаблицы];
		ИначеЕсли ТаблицыДляДвижений.Свойство("ОтражениеДокументовВРеглУчете") Тогда
			ТаблицаОтражениеДокументовВУчете = ТаблицыДляДвижений.ОтражениеДокументовВРеглУчете;
		Иначе
			ТаблицаОтражениеДокументовВУчете = ВнеоборотныеАктивыСлужебный.ТаблицаОтражениеДокументов(Ссылка, Организация, Дата);
		КонецЕсли;
		
		РеглУчетПроведениеСервер.ВернутьДокументыКОтражению(ТаблицаОтражениеДокументовВУчете);
	
	КонецЕсли;
	
	//-- Локализация

КонецПроцедуры

Процедура ПроверитьОтложенноеФормированиеДвижений(ПараметрыВыполнения) Экспорт

	//++ Локализация
	
	ИспользуемыеВТ = ОбщегоНазначенияУТ.СписокВременныхТаблиц(ПараметрыВыполнения.МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыВыполнения.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПараметрыВыполнения.КонецПериода));
	
	#Область ТребуетсяПродажаПоСхеме22
	
	Если Константы.ИспользоватьВнеоборотныеАктивы2_2.Получить() Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реализация.Ссылка КАК Ссылка,
		|	Реализация.Организация КАК Организация,
		|	Реализация.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Реализация.Дата КАК Дата,
		|	ТаблицаОС.ВнеоборотныйАктив КАК ОбъектУчета
		|
		|ПОМЕСТИТЬ втСписокОбъектовДляПроверки
		|
		|ИЗ
		|	Документ.РеализацияУслугПрочихАктивов КАК Реализация
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = Реализация.Ссылка
		|
		|ГДЕ
		|	Реализация.Ссылка В (
		|		ВЫБРАТЬ
		|			ВтПорцияДанныхКРасчету.Ссылка
		|		ИЗ
		|			ВтПорцияДанныхКРасчету КАК ВтПорцияДанныхКРасчету)
		|
		|	И Реализация.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов), 
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектУчета,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПервоначальныеСведенияОС.Организация КАК Организация,
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОбъектУчета,
		|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ КАК ДокументСнятияСУчета
		|
		|ПОМЕСТИТЬ ДокументыСнятияСУчета
		|
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&КонецМесяца,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					втСписокОбъектовДляПроверки.ОбъектУчета
		|				ИЗ
		|					втСписокОбъектовДляПроверки)) КАК ПервоначальныеСведенияОС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПервоначальныеСведенияНМА.Организация,
		|	ПервоначальныеСведенияНМА.НематериальныйАктив,
		|	ПервоначальныеСведенияНМА.ДокументСписания
		|
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
		|			&КонецМесяца,
		|			(НематериальныйАктив, Организация) В
		|				(ВЫБРАТЬ
		|					втСписокОбъектовДляПроверки.ОбъектУчета,
		|					втСписокОбъектовДляПроверки.Организация
		|				ИЗ
		|					втСписокОбъектовДляПроверки)) КАК ПервоначальныеСведенияНМА
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектУчета,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втСписокОбъектовДляПроверки.Ссылка КАК Ссылка,
		|	втСписокОбъектовДляПроверки.Организация КАК Организация,
		|	ТаблицаДокумента.Номер КАК НомерДокумента,
		|	ТаблицаДокумента.Дата КАК ДатаДокумента,
		|	втСписокОбъектовДляПроверки.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	втСписокОбъектовДляПроверки.Дата КАК Дата
		|
		|ИЗ
		|	втСписокОбъектовДляПроверки КАК втСписокОбъектовДляПроверки
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов КАК ТаблицаДокумента
		|		ПО (ТаблицаДокумента.Ссылка = втСписокОбъектовДляПроверки.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыСнятияСУчета КАК ДокументыСнятияСУчета
		|		ПО (ДокументыСнятияСУчета.ОбъектУчета = втСписокОбъектовДляПроверки.ОбъектУчета)
		|			И (ДокументыСнятияСУчета.Организация = втСписокОбъектовДляПроверки.Организация)
		|
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ДокументыСнятияСУчета.ДокументСнятияСУчета) В (
		|					ТИП(Документ.ПодготовкаКПередачеНМА),
		|					ТИП(Документ.ПодготовкаКПередачеОС))
		|
		|	И ДокументыСнятияСУчета.ДокументСнятияСУчета <> ЗНАЧЕНИЕ(Документ.ПодготовкаКПередачеНМА.ПустаяСсылка)
		|	И ДокументыСнятияСУчета.ДокументСнятияСУчета <> ЗНАЧЕНИЕ(Документ.ПодготовкаКПередачеОС.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ПараметрыВыполнения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыВыполнения.КонецПериода));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПараметрыВыполнения.КонецПериода));
	
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав Тогда
				ОписаниеПроблемы = НСтр("ru = 'В документе Реализация услуг и прочих активов №%1 от %2 необходимо использовать операцию ""Реализация с отложенным переходом прав"", для основных средств, подготовленных к передаче, до перехода на версию учета 2.4.
                                         |Для этого необходимо оформить реализацию на основании документа ""Подготовка к передаче ОС"".';
                                         |en = 'Use the ""Sale with deferred handover of rights"" operation in ""Sales of services and other assets"" document No. %1 dated %2 for fixed assets prepared for handover before migration to accounting 2.4.
                                         |To do this, register the sale based on the ""Preparation for FA handover"" document.'");
			Иначе
				ОписаниеПроблемы = НСтр("ru = 'В документе Реализация услуг и прочих активов №%1 от %2 необходимо использовать операцию ""Реализация"", для внеоборотных активов, подготовленных к передаче, до перехода на версию учета 2.4.';
										|en = 'In the Sales of services and other assets No.%1 document dated %2, use the Sales operation for capital assets prepared for transfer before migrating to the accounting version 2.4.'");
			КонецЕсли; 
			
			ОписаниеПроблемы = СтрШаблон(ОписаниеПроблемы, 
										Выборка.НомерДокумента, 
										Формат(Выборка.ДатаДокумента, "ДЛФ=D"));
			
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				Перечисления.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоВнеоборотнымАктивам, 
				ПараметрыВыполнения.КонецПериода,
				Выборка.Организация,
				ОписаниеПроблемы,,
				Выборка.Ссылка);
					
		КонецЦикла; 
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ПараметрыВыполнения.МенеджерВременныхТаблиц,, ИспользуемыеВТ);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПустоеНазначениеПриЦелевомФинансировании
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтПорцияДанныхКРасчету.Ссылка КАК Ссылка,
	|	ВтПорцияДанныхКРасчету.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Номер КАК НомерДокумента,
	|	ТаблицаДокумента.Ссылка.Дата КАК ДатаДокумента,
	|	ТаблицаДокумента.ОсновноеСредство КАК ВнеоборотныйАктив,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерСтроки, 0) КАК НомерСтроки
	|
	|ИЗ
	|	ВтПорцияДанныхКРасчету КАК ВтПорцияДанныхКРасчету
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4.ОС КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ВтПорцияДанныхКРасчету.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
	|		ПО (СтоимостьВНА.Ссылка = ТаблицаДокумента.Ссылка)
	|			И (СтоимостьВНА.ОбъектУчета = ТаблицаДокумента.ОсновноеСредство)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ВтПорцияДанныхКРасчету.Ссылка) = ТИП(Документ.ИзменениеПараметровОС2_4)
	|	И ТаблицаДокумента.Ссылка.НаправлениеДеятельностиФлаг
	|	И ТаблицаДокумента.Ссылка.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтПорцияДанныхКРасчету.Ссылка КАК Ссылка,
	|	ВтПорцияДанныхКРасчету.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Номер КАК НомерДокумента,
	|	ТаблицаДокумента.Ссылка.Дата КАК ДатаДокумента,
	|	ТаблицаДокумента.НематериальныйАктив КАК ВнеоборотныйАктив,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерСтроки, 0) КАК НомерСтроки
	|
	|ИЗ
	|	ВтПорцияДанныхКРасчету КАК ВтПорцияДанныхКРасчету
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ВтПорцияДанныхКРасчету.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
	|		ПО (СтоимостьВНА.Ссылка = ТаблицаДокумента.Ссылка)
	|			И (СтоимостьВНА.ОбъектУчета = ТаблицаДокумента.НематериальныйАктив)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ВтПорцияДанныхКРасчету.Ссылка) = ТИП(Документ.ИзменениеПараметровНМА2_4)
	|	И ТаблицаДокумента.Ссылка.НаправлениеДеятельностиФлаг
	|	И ТаблицаДокумента.Ссылка.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ТипЗнч(Выборка.ВнеоборотныйАктив) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
			ОписаниеПроблемы = НСтр("ru = 'В документе Изменение параметров ОС №%1 от %2 для внеоборотного актива ""%3"" необходимо указать направление деятельности, т.к. используется целевое финансирование.';
									|en = 'In the Change of FA parameters document No.%1 of %2 for the capital asset ""%3"", you need to specify the line of business, since targeted financing is used.'");
		Иначе
			ОписаниеПроблемы = НСтр("ru = 'В документе Изменение параметров НМА №%1 от %2 для внеоборотного актива ""%3"" необходимо указать направление деятельности, т.к. используется целевое финансирование.';
									|en = 'In the Change of IA parameters document No.%1 of %2 for capital asset ""%3"", you need to specify the line of business, since targeted financing is used.'");
		КонецЕсли; 
		
		ОписаниеПроблемы = СтрШаблон(ОписаниеПроблемы, 
									Выборка.НомерДокумента, 
									Формат(Выборка.ДатаДокумента, "ДЛФ=D"), 
									Выборка.ВнеоборотныйАктив);
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			Перечисления.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоВнеоборотнымАктивам, 
			ПараметрыВыполнения.КонецПериода,
			Выборка.Организация,
			ОписаниеПроблемы,,
			Выборка.Ссылка);
				
	КонецЦикла; 
	
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ПараметрыВыполнения.МенеджерВременныхТаблиц,, ИспользуемыеВТ);
	
	#КонецОбласти
	
	#Область РеализацияДоПодготовкиКПередаче
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаРасходы.ВнеоборотныйАктив
	|
	|ПОМЕСТИТЬ ТаблицаВНА
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|
	|ГДЕ
	|	ТаблицаРасходы.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтПорцияДанныхКРасчету.Ссылка
	|		ИЗ
	|			ВтПорцияДанныхКРасчету КАК ВтПорцияДанныхКРасчету
	|		ГДЕ
	|			ТИПЗНАЧЕНИЯ(ВтПорцияДанныхКРасчету.Ссылка) = ТИП(Документ.РеализацияУслугПрочихАктивов))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВнеоборотныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	ПервоначальныеСведения.ОсновноеСредство КАК ВнеоборотныйАктив,
	|
	|	ЕСТЬNULL(ПодготовкаКПередачеБУ.Дата, ДАТАВРЕМЯ(1,1,1)) КАК ДатаСписанияБУ,
	|	ЕСТЬNULL(ПодготовкаКПередачеНУ.Дата, ДАТАВРЕМЯ(1,1,1)) КАК ДатаСписанияНУ,
	|
	|	ПервоначальныеСведения.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведения.ДатаВводаВЭксплуатациюНУ КАК ДатаВводаВЭксплуатациюНУ
	|
	|ПОМЕСТИТЬ ПервоначальныеСведения
	|
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|		&КонецМесяца,
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ
	|				ТаблицаВНА.ВнеоборотныйАктив
	|			ИЗ
	|				ТаблицаВНА КАК ТаблицаВНА)) КАК ПервоначальныеСведения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС2_4 КАК ПодготовкаКПередачеБУ
	|		ПО (ПодготовкаКПередачеБУ.Ссылка = ПервоначальныеСведения.ДокументСнятияСУчетаБУ)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС2_4 КАК ПодготовкаКПередачеНУ
	|		ПО (ПодготовкаКПередачеНУ.Ссылка = ПервоначальныеСведения.ДокументСнятияСУчетаНУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	ПервоначальныеСведения.НематериальныйАктив КАК ВнеоборотныйАктив,
	|
	|	ЕСТЬNULL(ПодготовкаКПередаче.Дата, ДАТАВРЕМЯ(1,1,1)) КАК ДатаСписанияБУ,
	|	ЕСТЬNULL(ПодготовкаКПередаче.Дата, ДАТАВРЕМЯ(1,1,1)) КАК ДатаСписанияНУ,
	|
	|	ПервоначальныеСведения.ДатаПринятияКУчетуБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведения.ДатаПринятияКУчетуБУ КАК ДатаВводаВЭксплуатациюНУ
	|
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|		&КонецМесяца,
	|		НематериальныйАктив В (
	|			ВЫБРАТЬ
	|				ТаблицаВНА.ВнеоборотныйАктив
	|			ИЗ
	|				ТаблицаВНА КАК ТаблицаВНА)) КАК ПервоначальныеСведения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА2_4 КАК ПодготовкаКПередаче
	|		ПО (ПодготовкаКПередаче.Ссылка = ПервоначальныеСведения.ДокументСписания)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВнеоборотныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтПорцияДанныхКРасчету.Ссылка КАК Ссылка,
	|	ВтПорцияДанныхКРасчету.Организация КАК Организация,
	|	ТаблицаДокумента.Номер КАК НомерДокумента,
	|	ТаблицаДокумента.Дата КАК ДатаДокумента,
	|	ВтПорцияДанныхКРасчету.Дата КАК Дата,
	|	ТаблицаРасходы.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	ЕСТЬNULL(ТаблицаРасходы.НомерСтроки, 0) КАК НомерСтроки,
	|	
	|	ЕСТЬNULL(ПервоначальныеСведения.ДатаВводаВЭксплуатациюБУ, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			И (ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияБУ, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|				ИЛИ ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияБУ, ДАТАВРЕМЯ(1,1,1)) > ТаблицаДокумента.Дата) КАК ОшибкаБУ,
	|
	|	ЕСТЬNULL(ПервоначальныеСведения.ДатаВводаВЭксплуатациюНУ, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			И (ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияНУ, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|				ИЛИ ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияНУ, ДАТАВРЕМЯ(1,1,1)) > ТаблицаДокумента.Дата) КАК ОшибкаНУ
	|
	|ИЗ
	|	ВтПорцияДанныхКРасчету КАК ВтПорцияДанныхКРасчету
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ВтПорцияДанныхКРасчету.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ПО (ТаблицаРасходы.Ссылка = ВтПорцияДанныхКРасчету.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	|		ПО (ПервоначальныеСведения.ВнеоборотныйАктив = ТаблицаРасходы.ВнеоборотныйАктив)
	|			И (ПервоначальныеСведения.Организация = ТаблицаДокумента.Организация)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ВтПорцияДанныхКРасчету.Ссылка) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|
	|	И (ЕСТЬNULL(ПервоначальныеСведения.ДатаВводаВЭксплуатациюБУ, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			И (ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияБУ, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|				ИЛИ ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияБУ, ДАТАВРЕМЯ(1,1,1)) > ТаблицаДокумента.Дата)
	|
	|		ИЛИ ЕСТЬNULL(ПервоначальныеСведения.ДатаВводаВЭксплуатациюНУ, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			И (ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияНУ, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|				ИЛИ ЕСТЬNULL(ПервоначальныеСведения.ДатаСписанияНУ, ДАТАВРЕМЯ(1,1,1)) > ТаблицаДокумента.Дата))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ТипЗнч(Выборка.ВнеоборотныйАктив) = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
			
			Если Выборка.ОшибкаБУ Тогда
				
				ОписаниеПроблемы = НСтр("ru = 'Реализация услуг и прочих активов №%1 от %2 для внеоборотного актива %3 оформлена раньше чем подготовка к передаче в бух. учете.';
										|en = 'Customer invoice — Services and Assets #%1 from %2 for fixed asset %3 is registered earlier than the preparation for transfer to accounting.'");
			
				ОписаниеПроблемы = СтрШаблон(ОписаниеПроблемы, 
											Выборка.НомерДокумента, 
											Формат(Выборка.ДатаДокумента, "ДЛФ=D"), 
											Выборка.ВнеоборотныйАктив);
				
				ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
					Перечисления.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоВнеоборотнымАктивам, 
					ПараметрыВыполнения.КонецПериода,
					Выборка.Организация,
					ОписаниеПроблемы,,
					Выборка.Ссылка);
					
			КонецЕсли;
					
			Если Выборка.ОшибкаНУ Тогда
				
				ОписаниеПроблемы = НСтр("ru = 'Реализация услуг и прочих активов №%1 от %2 для внеоборотного актива %3 оформлена раньше чем подготовка к передаче в нал. учете.';
										|en = 'Customer invoice — Services and Assets #%1 from %2 for fixed asset %3 is registered earlier than the preparation for transfer to tax accounting.'");
			
				ОписаниеПроблемы = СтрШаблон(ОписаниеПроблемы, 
											Выборка.НомерДокумента, 
											Формат(Выборка.ДатаДокумента, "ДЛФ=D"), 
											Выборка.ВнеоборотныйАктив);
				
				ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
					Перечисления.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоВнеоборотнымАктивам, 
					ПараметрыВыполнения.КонецПериода,
					Выборка.Организация,
					ОписаниеПроблемы,,
					Выборка.Ссылка);
					
			КонецЕсли;
			
		Иначе
			
			ОписаниеПроблемы = НСтр("ru = 'Реализация услуг и прочих активов №%1 от %2 для внеоборотного актива %3 оформлена раньше чем подготовка к передаче.';
									|en = 'Customer invoice — Services and Assets #%1 from %2 for fixed asset %3 is registered earlier than preparations for the transfer.'");
		
			ОписаниеПроблемы = СтрШаблон(ОписаниеПроблемы, 
										Выборка.НомерДокумента, 
										Формат(Выборка.ДатаДокумента, "ДЛФ=D"), 
										Выборка.ВнеоборотныйАктив);
			
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				Перечисления.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоВнеоборотнымАктивам, 
				ПараметрыВыполнения.КонецПериода,
				Выборка.Организация,
				ОписаниеПроблемы,,
				Выборка.Ссылка);
				
		КонецЕсли;

	КонецЦикла; 
	#КонецОбласти
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьИдентификаторыДокументовПоКоторымФормируютсяОтложенныеДвижения(СписокОбъектов) Экспорт
	
	//++ Локализация
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("ДокументСсылка.ПараметрыНачисленияЗемельногоНалога"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПараметрыНачисленияНалогаНаИмущество"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПараметрыНачисленияТранспортногоНалога"));
	СписокТипов.Добавить(Тип("ДокументСсылка.СнятиеСРегистрацииЗемельныхУчастков"));
	СписокТипов.Добавить(Тип("ДокументСсылка.СнятиеСРегистрацииТранспортныхСредств"));
	
	Для каждого ТипОбъекта Из СписокТипов Цикл
		СписокОбъектов.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипОбъекта));
	КонецЦикла; 

	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьСписокТаблицПоКоторымФормируютсяЗаданияКФормированиюДвижений(СписокТаблиц) Экспорт

	//++ Локализация
	СписокТаблиц.Добавить("ПараметрыНачисленияЗемельногоНалога");
	СписокТаблиц.Добавить("ПараметрыНачисленияНалогаНаИмущество");
	СписокТаблиц.Добавить("ПараметрыНачисленияТранспортногоНалога");
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьТипыДокументовКоторыеИзменяютСтоимостьВКонцеМесяца(ТипыОбъектов) Экспорт
	
	//++ Локализация
	ВнеоборотныеАктивыСлужебный.ДобавитьТипОбъектаМетаданных("РегламентнаяОперация", Ложь, ТипыОбъектов);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти
