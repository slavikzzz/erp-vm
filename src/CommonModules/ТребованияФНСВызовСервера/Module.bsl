////////////////////////////////////////////////////////////////////////////////
//
// Серверные процедуры и функции регламентированных отчетов общего назначения 
// с кешируемым результатом на время сеанса.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция СостояниеТребованийДляПанели(Организация = Неопределено) Экспорт
	
	Подтвердить = ТребованияФНС.ТребующиеСкорогоПодтверждения(Организация).Количество();
	БезОтвета   = ТребованияФНС.ТребующиеСкорогоОтвета(Организация).Количество();
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Подтвердить", Подтвердить);
	ДополнительныеПараметры.Вставить("БезОтвета",   БезОтвета);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ВидыТребований(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка.ВидДокумента КАК ВидДокумента
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|ГДЕ
		|	НЕ ЖурналОтправокВКонтролирующиеОрганы.ПометкаУдаления
		|	И ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Входящие)
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.Скрыт
		|	И ЖурналОтправокВКонтролирующиеОрганы.Ссылка ССЫЛКА Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов
		|	И ЖурналОтправокВКонтролирующиеОрганы.Ссылка.ВидДокумента <> НЕОПРЕДЕЛЕНО";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТребованияФНС.ДобавитьОтборВЗапросЖурнала(Запрос, "Организация", Организация);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выгрузка = РезультатЗапроса.Выгрузить();
	
	Выгрузка.Сортировать("ВидДокумента");
	
	ВидыДокументов = Выгрузка.ВыгрузитьКолонку("ВидДокумента");
	
	Возврат ВидыДокументов;
	
КонецФункции

Функция ПроизводственныйКалендарь() Экспорт

	Календарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	Возврат Календарь;

КонецФункции

Функция ЕстьТребующиеПодтверждения(Организация = Неопределено) Экспорт
	
	Требования = ТребованияФНС.ТребующиеПодтверждения(Организация);
	Возврат Требования.Количество() > 0
	
КонецФункции

Функция ЕстьТребующиеСкорогоПодтверждения(Организация = Неопределено) Экспорт
	
	Требования = ТребованияФНС.ТребующиеСкорогоПодтверждения(Организация);
	Возврат Требования.Количество() > 0
	
КонецФункции

Процедура ВосстановитьСвойстваФайлов(ФайлыПослеОбработки, ФайлыДоОбработки) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	Для каждого ОписаниеФайла Из ФайлыПослеОбработки Цикл
		
		Если ОбработкаФайловPDFКлиентСервер.ЭтоPDFФайл(ОписаниеФайла.Имя) Тогда
			
			Для каждого ФайлДоОбработки Из ФайлыДоОбработки Цикл
				
				ДвДанныеДо = ПолучитьИзВременногоХранилища(ФайлДоОбработки.Адрес);
				ДвДанныеПосле = ПолучитьИзВременногоХранилища(ОписаниеФайла.Адрес);
				
				ИмяДо = КонтекстЭДОСервер.ИмяФайлаБезРасширения(ФайлДоОбработки.Имя);
				ИмяПосле = КонтекстЭДОСервер.ИмяФайлаБезРасширения(ОписаниеФайла.Имя);
				
				Если ДвДанныеДо = ДвДанныеПосле И ИмяДо = ИмяПосле Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ОписаниеФайла, ФайлДоОбработки, Истина);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РазмерПоАдресу(Адрес) Экспорт
	
	ДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	Возврат ДвДанные.Размер();
	
КонецФункции

Функция СвойствоОтчета(Отчет, Имя) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Отчет) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка = &Отчет";
		
		Запрос.УстановитьПараметр("Отчет", Отчет);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Возврат ВыборкаДетальныеЗаписи[Имя];
		КонецЕсли;
		
	Исключение
		// Не требует обработки. Может не быть отчетом и выпасть в исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции

Функция ЭтоСообщениеОбИсчисленииИмущественныхНалогов(Требование) Экспорт

	Виды = Перечисления.ВидыНалоговыхДокументов;
	Вид  = Требование.ВидДокумента;

	ЭтоСообщениеОбИсчисленииИмущественныхНалогов = 
		(Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоНалога
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоИмущественногоЗемельногоНалогов
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеЗемельногоНалога);

	Возврат ЭтоСообщениеОбИсчисленииИмущественныхНалогов;
	
КонецФункции

Функция ПодтвержденияОтправляетОператор() Экспорт
	
	// Федеральный закон от 08.08.2024 N 259-ФЗ:
	// "В случае направления налоговым органом налогоплательщику, указанному в абзаце первом пункта 5.1 
	// статьи 23 настоящего Кодекса, документа в электронной форме по телекоммуникационным каналам связи 
	// через оператора электронного документооборота датой его получения считается шестой день со дня 
	// направления такого документа, указанного в подтверждении даты отправки электронного документа. 
	// Подтверждение даты отправки электронного документа направляется оператором электронного документооборота 
	// в налоговый орган не позднее дня, следующего за днем направления документа налогоплательщику.";
	
	Если НаступилаДатаОтменыПодтвержденияПриема() Тогда
		Возврат ТекущаяДатаСеанса() >= ДатаОтменыПодтверждений();
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ДатаОтменыПодтверждений() Экспорт
	
	Возврат Дата(2025, 2, 5);

КонецФункции

Функция ТекстПроОтменуПодтвержденияПриема() Экспорт

	Возврат НСтр("ru = 'С 5 февраля 2025 г. срок ответа отсчитывается от 6-го дня после отправки требования из ИФНС и больше не зависит от даты отправки подтверждения приема';
				|en = 'С 5 февраля 2025 г. срок ответа отсчитывается от 6-го дня после отправки требования из ИФНС и больше не зависит от даты отправки подтверждения приема'");

КонецФункции

Функция НаступилаДатаОтменыПодтвержденияПриема() Экспорт
	
	Возврат Истина;

КонецФункции

Функция ПодтверждениеОтправилОператор(Ссылка) Экспорт
	
	Если НЕ НаступилаДатаОтменыПодтвержденияПриема() Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Отправил = Ложь;
	
	ЭтоДокументРеализацииПолномочий = 
		ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникОбъект.ДокументыРеализацииПолномочийНалоговыхОрганов");
		
	ЭтоОписьВходящихДокументов = 
		ТипЗнч(Ссылка) = Тип("СправочникСсылка.ОписиВходящихДокументовИзНалоговыхОрганов")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникОбъект.ОписиВходящихДокументовИзНалоговыхОрганов");
		
	Если ЭтоДокументРеализацииПолномочий Тогда
			
		Отправил = (Ссылка.ДатаОтправки >= ДатаОтменыПодтверждений()
			ИЛИ ТребованияФНС.ДатаОтправкиТребования(Ссылка) >= ДатаОтменыПодтверждений()
			ИЛИ Ссылка.ДатаДокумента >= ДатаОтменыПодтверждений());
			
	ИначеЕсли ЭтоОписьВходящихДокументов Тогда
			
		Отправил = Ссылка.ДатаОтправки >= ДатаОтменыПодтверждений();
		
	КонецЕсли;
	
	Возврат Отправил;

КонецФункции

Функция ПодтверждениеОтправилОператорПоТС(ТранспортноеСообщение) Экспорт
	
	Если ЗначениеЗаполнено(ТранспортноеСообщение) Тогда
		
		Возврат ПодтверждениеОтправилОператорПоЦиклуОбмена(ТранспортноеСообщение.ЦиклОбмена);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ПодтверждениеОтправилОператорПоЦиклуОбмена(ЦиклОбмена) Экспорт
	
	Если ЗначениеЗаполнено(ЦиклОбмена)
		И ЗначениеЗаполнено(ЦиклОбмена.Предмет) Тогда
		
		Требование = ЦиклОбмена.Предмет;
		Возврат ПодтверждениеОтправилОператор(Требование);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция СкорректированныеДнейДоОтвета(Требование, Знач ДнейДоОтвета) Экспорт
	
	// Может быть, если не распознали дату из pdf-файла
	Если НЕ ЗначениеЗаполнено(ДнейДоОтвета) Тогда
		Возврат ДнейДоОтвета;
	КонецЕсли;
	
	// Федеральный закон от 08.08.2024 N 259-ФЗ:
	// 8) пункт 4 статьи 31 дополнить абзацем следующего содержания:
	// "В случае направления налоговым органом налогоплательщику, указанному в абзаце первом пункта 5.1 
	//статьи 23 настоящего Кодекса, документа в электронной форме по телекоммуникационным каналам связи через 
	//оператора электронного документооборота датой его получения считается _шестой_ день со дня направления 
	//такого документа, указанного в подтверждении даты отправки электронного документа. Подтверждение даты 
	//отправки электронного документа направляется оператором электронного документооборота в налоговый орган 
	//не позднее дня, следующего за днем направления документа налогоплательщику.";
	
	ДнейДоОтвета = ДнейДоОтвета + 6;
	
	Возврат ДнейДоОтвета;
	
КонецФункции

#КонецОбласти