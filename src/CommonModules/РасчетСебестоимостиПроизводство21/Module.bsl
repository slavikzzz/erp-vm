
// Вызываются процедуры и функции общих модулей:
//	- РасчетСебестоимостиПрикладныеАлгоритмы
//	- РасчетСебестоимостиПроизводство

#Область ПрограммныйИнтерфейс

// Этап 6 (Контекст: "РаспределениеМатериаловИРаботПоБазе")
// ПУ 2.1: РассчитатьРаспределениеМатериаловИРабот(), область РаспределениеМатериаловИРабот.
// 
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеМатериаловИРаботПоБазе");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийМатериаловИРаботПоБазе(ПараметрыРасчета));

	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру МатериалыИРаботыВПроизводстве.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

// Этап 7 (Контекст: "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями")
// ПУ 2.1: РассчитатьПартииНЗП(), область РасчетПартийНезавершенногоПроизводства.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийМатериаловНЗП(ПараметрыРасчета));

	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияМатериаловНЗП(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру ПартииНезавершенногоПроизводства.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

#Область Инициализация

// Инициализирует общие параметры расчета, описывающие обслуживаемые механизмом расчета регистры.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ИнициализироватьОбслуживаемыеРегистры(ПараметрыРасчета) Экспорт
	
	ПараметрыРасчета.РегистрыСРасчетнымиОборотами.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.Имя, Истина);	
	
КонецПроцедуры

// Дополняет перечень документов, которые могут иметь движения в разных месяцах или по нескольким организациям.
//
// Параметры:
//	РазныеПериоды - Булево - добавлять в результат документы с движениями в разных периодах
//	РазныеОрганизации - Булево - добавлять в результат документы с движениями по нескольким организациям
//	ИмяРегистра - Строка - имя регистра накопления, для которого нужно получить перечень документов;
//		пустое значение - перечень документов для всех регистров.
//	ОписаниеДокументов - Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ДополнитьДокументыСРазнымиПериодамиИлиОрганизациямиВДвижениях(РазныеПериоды, РазныеОрганизации, ИмяРегистра, ОписаниеДокументов) Экспорт
	
	// Для движений в других периодах Значение = Истина означает
	// - наличие первичных+расчетных движений,
	// - расчетные движения формируются при расчете их периода.
	// Если указано значение Ложь, то это означает, что
	// - расчетные движения могут быть без первичных движений,
	// - расчетные движения любых периодов формируются при расчете периода документа.
	// Для движений других организаций Значение должно быть только Истина.
	
	Значение = Истина;
	
	//++ НЕ УТКА
	Если ИмяРегистра = ""
	 ИЛИ ИмяРегистра = Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя Тогда
	 
		Если РазныеПериоды Тогда
			ОписаниеДокументов.Вставить(Метаданные.Документы.МаршрутныйЛистПроизводства, Значение);
		КонецЕсли;
	 
	КонецЕсли;
	
	Если ИмяРегистра = ""
	 ИЛИ ИмяРегистра = Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.Имя Тогда
		
		Если РазныеПериоды Тогда
			ОписаниеДокументов.Вставить(Метаданные.Документы.МаршрутныйЛистПроизводства, Значение); // при длительном производстве движения по материалам формируются в разных периодах
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапа1а_РаспределениеТрудозатрат

Функция ТекстЗапросаДляРаспределенияТрудозатрат(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// выборка данных
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПервичныеТрудозатратыБезЗаказов()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПотребление()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПотреблениеАвто()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПеремещение()
		
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстПервичныеТрудозатратыБезЗаказов() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПервичныеТрудозатратыБезЗаказов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПартияБезЗаказа) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	Приходы.Количество КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.НормативнаяСтоимость) КАК НормативнаяСтоимость,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.СтоимостьНДД) КАК СтоимостьНДД,
		|	ИСТИНА КАК Первичное,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ДД.СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыБезЗаказов КАК Приходы
		|		ПО Приходы.Организация = ДД.Организация
		|		И Приходы.Подразделение = ДД.Подразделение
		|		И Приходы.ВидРабот = ДД.ВидРабот
		|		И Приходы.ГруппаПродукции = ДД.ГруппаПродукции
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	И ДД.КодСтрокиПродукция = 0
		|	И ДД.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ВидФондаВзносов,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	Приходы.Количество,
		|	ДД.СтатьяКалькуляцииБезЗаказа
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПотребление() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПотребление"" КАК ЗапросИсточник,
		|	40 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ВтВидыФондов.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ВидыРабот.ВидРабот,
		|	ДД.Номенклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	СУММА(ВидыРабот.Количество) КАК Знаменатель,
		|	МАКСИМУМ(ВЫБОР Выпуски.ДоляСтоимости
		|				КОГДА 0
		|					ТОГДА Выпуски.Количество
		|				ИНАЧЕ Выпуски.ДоляСтоимости
		|	КОНЕЦ) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ВидыРабот
		|		ПО ВидыРабот.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпускиБезЗаказов КАК Выпуски
		|		ПО Выпуски.Ссылка = ДД.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыФондов КАК ВтВидыФондов
		|		ПО ВтВидыФондов.Организация = ДД.Организация
		|		И ВтВидыФондов.Подразделение = ВидыРабот.Подразделение
		|		И ВтВидыФондов.ВидРабот = ВидыРабот.ВидРабот
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.СтатьяКалькуляции,
		|	ВтВидыФондов.ВидФондаВзносов,
		|	ДД.Номенклатура
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПотреблениеАвто() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПотреблениеАвто"" КАК ЗапросИсточник,
		|	45 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПотреблениеАвто) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ВтВидыФондов.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ВидыРабот.ВидРабот,
		|	ДД.Номенклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	СУММА(ВидыРабот.Количество) КАК Знаменатель,
		|	МАКСИМУМ(ВЫБОР Выпуски.ДоляСтоимости
		|				КОГДА 0
		|					ТОГДА Выпуски.Количество
		|				ИНАЧЕ Выпуски.ДоляСтоимости
		|	КОНЕЦ) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ВидыРабот
		|		ПО ВидыРабот.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпускиБезЗаказов КАК Выпуски
		|		ПО Выпуски.Ссылка = ДД.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыФондов КАК ВтВидыФондов
		|		ПО ВтВидыФондов.Организация = ДД.Организация
		|		И ВтВидыФондов.Подразделение = ВидыРабот.Подразделение
		|		И ВтВидыФондов.ВидРабот = ВидыРабот.ВидРабот
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.СтатьяКалькуляции,
		|	ВтВидыФондов.ВидФондаВзносов,
		|	ДД.Номенклатура
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПеремещение() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПеремещение"" КАК ЗапросИсточник,
		|	60 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	СУММА(ВЫБОР Выходы.ДоляСтоимости
		|			КОГДА 0
		|				ТОГДА Выходы.Количество
		|			ИНАЧЕ Выходы.ДоляСтоимости
		|	КОНЕЦ) КАК Знаменатель,
		|	СУММА(Выходы.Количество) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	Выходы.Распоряжение КАК ДокументВыпуска,
		|	Выходы.КодСтроки КАК КодСтроки,
		|	Выходы.Распоряжение КАК ДокументИсточник,
		|	Выходы.Номенклатура КАК Продукция,
		|	Выходы.Характеристика КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК Выходы
		|		ПО Выходы.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	Выходы.Номенклатура,
		|	Выходы.Характеристика,
		|	Выходы.Распоряжение,
		|	Выходы.КодСтроки,
		|	Выходы.Распоряжение,
		|	Выходы.КодСтроки
		|";
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапа1б_РаспределениеТрудозатратНаВыпуск

//++ НЕ УТКА

Функция ТекстТрудозатратыНЗППодготовитьСлужебныеТаблицы() Экспорт
	
	Возврат
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.Подразделение,
		|	Т.ПартияПроизводства,
		|	Т.ЗаказНаПроизводство,
		|	Т.КодСтрокиПродукция,
		|	Т.Этап,
		|	Т.СтатьяКалькуляции,
		|	Т.ВидРабот,
		|	Т.ГруппаПродукции,
		|	Т.ВидФондаВзносов,
		|	Т.КоличествоОстаток,
		|	Т.НормативнаяСтоимостьОстаток,
		|	Т.СтоимостьОстаток,
		|	Т.СтоимостьРеглОстаток,
		|	Т.ВременнаяРазницаОстаток,
		|	Т.ПостояннаяРазницаОстаток,
		|	Т.СтоимостьНДДОстаток
		|ПОМЕСТИТЬ НачальныеОстаткиТрудозатратНЗП
		|ИЗ
		|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства.Остатки(
		|			&ГраницаНачалоПериода,
		|			Организация В (&МассивОрганизаций)
		|				И КодСтрокиПродукция > 0) КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Распоряжение
		|ПОМЕСТИТЬ ЗаказыКРаспределениюПредварительная
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции.ОстаткиИОбороты(&НачалоПериода, &КонецПериода) КАК Т
		|ГДЕ
		|	Т.ЗаказаноПриход <> 0
		|	ИЛИ Т.ЗаказаноКонечныйОстаток <> 0
	    |
		|ИНДЕКСИРОВАТЬ ПО
		|	Т.Распоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МаршрутныйЛист.Распоряжение КАК Заказ,
		|	МаршрутныйЛист.КодСтроки КАК КодСтроки
		|ПОМЕСТИТЬ ЗаказыКРаспределению
		|ИЗ
		|	Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыКРаспределениюПредварительная КАК Т
		|		ПО МаршрутныйЛист.Ссылка = Т.Распоряжение
		|ГДЕ
		|	МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗаказыКРаспределениюПредварительная
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	СУММА(ВЫБОР ДД.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ
		|		* (МаршрутныйЛист.Произведено + МаршрутныйЛист.Брак)) КАК ДоляСтоимости
		|ПОМЕСТИТЬ ДолиСтоимости
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыКРаспределению КАК Заказы
		|		ПО Заказы.Заказ = МаршрутныйЛист.Распоряжение
		|			И Заказы.КодСтроки = МаршрутныйЛист.КодСтроки
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|СГРУППИРОВАТЬ ПО
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец";
	
КонецФункции
//-- НЕ УТКА

Функция ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск() Экспорт
	
	ТекстЗапроса = ""
		// выборка данных
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыВыпускиБезЗаказов()
		//++ НЕ УТКА
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстОстаткиТрудозатратНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПервичныеТрудозатратыНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыМаршрутныеЛисты()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыВыпускиПоЗаказам()
		//-- НЕ УТКА
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстТрудозатратыВыпускиБезЗаказов()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыВыпускиБезЗаказов"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВыпускБезЗаказа) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	СУММА(ДД.Количество) КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры ЕСТЬ НЕ NULL
		|			ТОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ДД.АналитикаУчетаПродукции
		|	КОНЕЦ КАК КорАналитикаУчетаПродукции,
		|	ВЫБОР
		|		КОГДА ДД.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК КорРазделУчета,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА РаботыДляДавальца.ВидЗапасов ЕСТЬ НЕ NULL
		|			ТОГДА РаботыДляДавальца.ВидЗапасов
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ДД.КорВидЗапасов
		|	КОНЕЦ КАК КорВидЗапасовПродукции,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	ДД.Регистратор КАК ДокументВыпуска,
		|	ДД.КодСтроки КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	Аналитика.Номенклатура КАК Продукция,
		|	Аналитика.Характеристика КАК ХарактеристикаПродукции,
		|	ДД.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаСписаниеПоНормативам КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = Аналитика.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = ДД.Назначение
		//++ НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
		|		ПО РаботыДляДавальца.Регистратор = ДД.Регистратор
		|			И РаботыДляДавальца.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|			И РаботыДляДавальца.КорВидЗапасов = ДД.КорВидЗапасов
		//-- НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО Продукция.Ссылка = ДД.Регистратор
		|		И Продукция.КодСтроки = ДД.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО СтатьиРасходов.Ссылка = Продукция.СтатьяРасходов
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
		|		ПО ДД.Регистратор = Движения.Регистратор
		|		И Продукция.КодСтроки = Движения.КодСтроки
		|		И Движения.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|		И НЕ Движения.РасчетПартий
		|		И НЕ Движения.РасчетСебестоимости
		// исключение движений по работам для давальца
		|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры ЕСТЬ НЕ NULL
		|			ТОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ДД.АналитикаУчетаПродукции
		|	КОНЕЦ,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА РаботыДляДавальца.ВидЗапасов ЕСТЬ НЕ NULL
		|			ТОГДА РаботыДляДавальца.ВидЗапасов
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ДД.КорВидЗапасов
		|	КОНЕЦ,
		|	ДД.Назначение,
		|	ДД.КодСтроки,
		|	ВЫБОР
		|		КОГДА ДД.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
		|	Аналитика.Номенклатура,
		|	Аналитика.Характеристика
		|";
КонецФункции

//++ НЕ УТКА

Функция ТекстТрудозатратыВыпускиПоЗаказам()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыВыпускиПоЗаказам"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Выпуск) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	МаршрутныйЛист.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	Этапы.Владелец КАК Спецификация,
		|	МаршрутныйЛист.Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	СУММА(ДД.Заказано) КАК Знаменатель,
		|	СУММА(ДД.Заказано) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаПродукции,
		|	(ВЫБОР КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КОНЕЦ) КАК КорРазделУчета,
		|	ДД.ВидЗапасов КАК КорВидЗапасовПродукции,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.Номенклатура КАК Продукция,
		|	ДД.Характеристика КАК ХарактеристикаПродукции,
		|	Аналитика.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = МаршрутныйЛист.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаТовары
		|		ПО ДД.Регистратор = ТаблицаТовары.Ссылка
		|			И ДД.КодСтроки = ТаблицаТовары.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО ТаблицаТовары.СтатьяРасходов = Статьи.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
		|		ПО ДД.Регистратор = Движения.Регистратор
		|		И ТаблицаТовары.КодСтроки = Движения.КодСтроки
		|		И Движения.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|		И НЕ Движения.РасчетПартий
		|		И НЕ Движения.РасчетСебестоимости
		// исключение движений по работам для давальца
		|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	МаршрутныйЛист.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	Аналитика.Назначение,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец,
		|	МаршрутныйЛист.Этап,
		|	(ВЫБОР КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
		|";
КонецФункции

Функция ТекстОстаткиТрудозатратНЗП()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстОстаткиТрудозатратНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Остаток) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов,
		|	Этапы.Владелец КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	ЕСТЬNULL(Доли.ДоляСтоимости, 1) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.НормативнаяСтоимостьОстаток КАК НормативнаяСтоимость,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.СтоимостьНДДОстаток КАК СтоимостьНДД,
		|	ИСТИНА КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	НачальныеОстаткиТрудозатратНЗП КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|ГДЕ
		|	ДД.КодСтрокиПродукция > 0
		|";
КонецФункции

Функция ТекстПервичныеТрудозатратыНЗП()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПервичныеТрудозатратыНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов,
		|	Этапы.Владелец КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	МАКСИМУМ(ЕСТЬNULL(Доли.ДоляСтоимости, 1)) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.НормативнаяСтоимость) КАК НормативнаяСтоимость,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.СтоимостьНДД) КАК СтоимостьНДД,
		|	ИСТИНА КАК Первичное,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	ДД.Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	И ДД.КодСтрокиПродукция > 0
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов,
		|	Этапы.Владелец,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	ДД.Сотрудник
		|";
КонецФункции

Функция ТекстТрудозатратыМаршрутныеЛисты()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыМаршрутныеЛисты"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.МаршрутныйЛист) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	МаршрутныйЛист.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	Этапы.Владелец КАК Спецификация,
		|	МаршрутныйЛист.Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	(ВЫБОР
		|		КОГДА НЕ ПродукцияЗаказа.ГруппаАналитическогоУчета ЕСТЬ NULL ТОГДА ПродукцияЗаказа.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК ГруппаПродукции,
		|	СУММА(ВЫБОР ДД.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ
		|		* (МаршрутныйЛист.Произведено + МаршрутныйЛист.Брак)) КАК Знаменатель,
		|	СУММА(ДД.Заказано) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.Номенклатура КАК Продукция,
		|	ДД.Характеристика КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК СтрокиЗаказа
		|		ПО СтрокиЗаказа.Ссылка = МаршрутныйЛист.Распоряжение
		|		И СтрокиЗаказа.КодСтроки = МаршрутныйЛист.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ПродукцияЗаказа
		|		ПО ПродукцияЗаказа.Ссылка = СтрокиЗаказа.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	МаршрутныйЛист.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец,
		|	МаршрутныйЛист.Этап,
		|	(ВЫБОР
		|		КОГДА НЕ ПродукцияЗаказа.ГруппаАналитическогоУчета ЕСТЬ NULL ТОГДА ПродукцияЗаказа.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика
		|";
КонецФункции
//-- НЕ УТКА

#КонецОбласти

#Область ПроцедурыЭтапов_1_2_3_4_5

Процедура ДополнитьЗапросДаннымиПоТрудозатратам(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// собственное производство 2.1 без заказов, фактические трудозатраты
	|ВЫБРАТЬ
	|	ДД.Организация						КАК Организация,
	|	ДД.Подразделение					КАК Подразделение,
	|	ДД.СтатьяКалькуляции				КАК СтатьяКалькуляции,
	|	ДД.ВидРабот							КАК ВидРабот,
	|	ДД.ГруппаПродукции					КАК ГруппаПродукции,
	|	Товары.Назначение					КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ДД.ДокументВыпуска					КАК ЗаказНаПроизводство,
	|	ДД.КодСтроки						КАК КодСтрокиПродукция,
	|	ЕСТЬNULL(Товары.Назначение.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СУММА(ДД.Количество)				КАК Количество,
	|	СУММА(ДД.НормативнаяСтоимость)		КАК НормативнаяСтоимость,
	|	СУММА(ДД.Стоимость)					КАК Стоимость,
	|	СУММА(ДД.СтоимостьРегл)				КАК СтоимостьРегл,
	//++ Локализация
	|	СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница)	КАК СтоимостьНУ,
	//-- Локализация
	|	СУММА(ДД.СтоимостьНДД)				КАК СтоимостьНДД
	|ИЗ
	|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Товары
	|			ПО ДД.ДокументВыпуска = Товары.Ссылка
	|			И ДД.КодСтроки = Товары.КодСтроки
	|
	|ГДЕ
	|	(&РасчетСебестоимостиВыполнен ИЛИ НЕ &РасшифровкаРаспределения)
	|	И &ВключатьДанныеПроизводства21
	|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение,
	|	ДД.СтатьяКалькуляции,
	|	ДД.ВидРабот,
	|	Товары.Назначение,
	|	ДД.Организация,
	|	ДД.ДокументВыпуска,
	|	ДД.КодСтроки,
	|	Товары.Назначение.НаправлениеДеятельности,
	|	ДД.ГруппаПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// собственное производство 2.1 без заказов, плановые трудозатраты для отчета
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Организация			КАК Организация,
	|	ТаблицаТрудозатраты.Подразделение		КАК Подразделение,
	|	ТаблицаТрудозатраты.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ТаблицаТрудозатраты.ВидРабот			КАК ВидРабот,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ								КАК ГруппаПродукции,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ДД.Распоряжение						КАК ЗаказНаПроизводство,
	|	ДД.КодСтроки						КАК КодСтрокиПродукция,
	|	ЕСТЬNULL(ТаблицаТовары.Назначение.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СУММА(ТаблицаТрудозатраты.Количество * (ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) / Итоги.Всего) КАК Количество,
	|	СУММА(ЕСТЬNULL(Расценки.Расценка, 0) * ТаблицаТрудозатраты.Количество * (ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) / Итоги.Всего) КАК НормативнаяСтоимость,
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.Стоимость / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК Стоимость,
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.СтоимостьРегл / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК СтоимостьРегл,
	//++ Локализация
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.СтоимостьНУ / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК СтоимостьНУ,
	//-- Локализация
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.СтоимостьНДД / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК СтоимостьНДД
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка,
	|		СУММА(ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) КАК Всего
	|	ИЗ
	|		Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск КАК Реквизиты
	|			ПО ДД.Ссылка = Реквизиты.Ссылка
	|	ГДЕ
	|		&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен
	|		И &ВключатьДанныеПроизводства21
	|		И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Реквизиты.Организация В (&МассивОрганизаций)
	|		И Реквизиты.Проведен
	|		
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Ссылка
	|		
	|	ИМЕЮЩИЕ
	|		СУММА(ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) > 0
	|		
	|	) КАК ИТОГИ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск КАК РеквизитыДокумента
	|			ПО Итоги.Ссылка = РеквизитыДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ДД
	|			ПО Итоги.Ссылка = ДД.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ТаблицаТрудозатраты
	|			ПО Итоги.Ссылка = ТаблицаТрудозатраты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаТовары
	|			ПО ДД.Распоряжение = ТаблицаТовары.Ссылка
	|			И ДД.КодСтроки = ТаблицаТовары.КодСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&КонецПериода) КАК Расценки
	|			ПО Расценки.ВидРабот = ТаблицаТрудозатраты.ВидРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеТрудозатратыНЗП КАК ТрудозатратыНЗП
	|			ПО РеквизитыДокумента.Организация = ТрудозатратыНЗП.Организация
	|				И ТаблицаТрудозатраты.Подразделение = ТрудозатратыНЗП.Подразделение
	|				И ТаблицаТрудозатраты.СтатьяКалькуляции = ТрудозатратыНЗП.СтатьяКалькуляции
	|				И ТаблицаТрудозатраты.ВидРабот = ТрудозатратыНЗП.ВидРабот
	|	ГДЕ
	|		&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТрудозатраты.Подразделение,
	|	ТаблицаТрудозатраты.СтатьяКалькуляции,
	|	ТаблицаТрудозатраты.ВидРабот,
	|	ТаблицаТовары.Назначение,
	|	РеквизитыДокумента.Организация,
	|	ДД.Распоряжение,
	|	ДД.КодСтроки,
	|	ТаблицаТовары.Номенклатура
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// собственное производство 2.1 по заказам
	|ВЫБРАТЬ
	|	ДД.Организация						КАК Организация,
	|	ДД.Подразделение					КАК Подразделение,
	|	ДД.СтатьяКалькуляции				КАК СтатьяКалькуляции,
	|	ДД.ВидРабот							КАК ВидРабот,
	|	ДД.ГруппаПродукции					КАК ГруппаПродукции,
	|	ТаблицаПродукция.Назначение			КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ДД.ЗаказНаПроизводство				КАК ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция				КАК КодСтрокиПродукция,
	|	ЕСТЬNULL(ТаблицаПродукция.Назначение.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СУММА(ДД.Количество)				КАК Количество,
	|	СУММА(ДД.НормативнаяСтоимость)		КАК НормативнаяСтоимость,
	|	СУММА(ДД.Стоимость)					КАК Стоимость,
	|	СУММА(ДД.СтоимостьРегл)				КАК СтоимостьРегл,
	//++ Локализация
	|	СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница)	КАК СтоимостьНУ,
	//-- Локализация
	|	СУММА(ДД.СтоимостьНДД)				КАК СтоимостьНДД
	|ИЗ
	|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ТаблицаПродукция
	|			ПО ДД.ЗаказНаПроизводство = ТаблицаПродукция.Ссылка
	|			И ДД.КодСтрокиПродукция = ТаблицаПродукция.КодСтроки
	|
	|ГДЕ
	|	&ВключатьДанныеПроизводства21
	|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И ДД.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение,
	|	ДД.СтатьяКалькуляции,
	|	ДД.ВидРабот,
	|	ТаблицаПродукция.Назначение,
	|	ДД.Организация,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ТаблицаПродукция.Назначение.НаправлениеДеятельности,
	|	ДД.ГруппаПродукции
	//-- НЕ УТКА
	|";
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа6_РаспределениеМатериаловИРаботПоБазе

// Инициализация данных

Функция ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	Возврат "Регистратор, Серия";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений	= ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	
	// Потребление <- (Партия)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Потребление, ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Потребление,
		Перечисления.ТипыЗаписейПартий.Партия,		ПоляПотреблений);
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеМатериаловИРаботПоБазе");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Партия");
	ОписаниеДвижений.Вставить("БазисПрихода", "Количество");
	ОписаниеДвижений.Вставить("БазисРасхода", "Количество");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период"); // ПолеПорядка
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Партия, Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей);
	
КонецФункции

Функция ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	ДопПараметры = ПараметрыРасчета.ДополнительныеПараметрыЗапросаЭтапа;
	ДопПараметры.Вставить("ОтборПоМатериаламИерархия", РасчетСебестоимостиПроизводство.ОтборПоМатериаламИерархия(ПараметрыРасчета));
	
	// Нумерация строк ВТ Данные
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"ЗаказНаПроизводство", // разделитель
		"Количество, КРаспределению, Объем, Вес, Стоимость", // ресурсы
		"Период, Регистратор, ТипЗаписи"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ ТекстРаспределениеМатериаловИРаботИнициализация() // вт ОтборПоМатериалам, Распределить 
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстДанныеПоУказаннымМатериалам() // вт Данные_ПоУказаннымМатериалам
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстДанныеПоПродукции() // вт ФактЭтапов_ПоПродукции, ПланЭтаповПоЗаказам_ПоПродукции, Данные_ПоПродукции
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстДанныеПоПлановойСтоимости() // вт Данные_ПоПлановойСтоимости
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстДанныеПоУказаннымТрудозатратам() // вт Данные_ПоУказаннымТрудозатратам
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстДанныеПоНормативамРасходаМатериала() // вт ФактЭтапов_ПоНормативам, ПланЭтаповПоЗаказам_ПоНормативам, Данные_ПоНормативамРасходаМатериала
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + РасчетСебестоимостиПроизводство.ТекстРаспаковкаНаборов("Данные_ПоНормативамРасходаМатериала") // вт МатериалыИУслугиСпецификаций
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстБазаРаспределения() // вт БазаРаспределения
		+ ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстСуммыПоказателей() // вт ОбщиеСуммы
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстКРаспределению()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРаспределениеПоБазе()
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе()
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) 		 								 КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка) 		  		 КАК ТипЗаписи,
		|	ЛОЖЬ 														  		 КАК РасчетЗавершен,
		|
		|	ДД.ВидДвижения 								 						 КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) 										 КАК Период,
		|	ДД.Регистратор 														 КАК Регистратор,
		|	ДД.Организация 						 								 КАК Организация,
		|	ДД.Номенклатура 						 							 КАК Номенклатура,
		|	ДД.Характеристика 						 							 КАК Характеристика,
		|	ДД.Подразделение 						 							 КАК Подразделение,
		|	ДД.Серия 						 							 		 КАК Серия,
		|	ДД.Назначение 						 							 	 КАК Назначение,
		|	0 																	 КАК Количество,
		|	ДД.СтатьяКалькуляции 						 						 КАК СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство 												 КАК ЗаказНаПроизводство,
		|	0 																	 КАК КодСтрокиПродукция,
		|	ДД.Этап 						 							 		 КАК Этап,
		|	ДД.АналитикаУчетаПродукции 						 					 КАК АналитикаУчетаПродукции,
		|	ДД.Спецификация 						 							 КАК Спецификация,
		|	ДД.СтатьяРасходов 						 							 КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов 												 КАК АналитикаРасходов,
		|	ДД.АналитикаУчетаНоменклатуры 						 				 КАК АналитикаУчетаНоменклатуры,
		|	ДД.ПодразделениеПолучатель 						 					 КАК ПодразделениеПолучатель,
		|	ДД.БазаРаспределения 						 						 КАК БазаРаспределения,
		|	0 																	 КАК КРаспределению,
		|	0 																	 КАК Объем,
		|	0 																	 КАК Вес,
		|	0 																	 КАК Стоимость
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|";

	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

// ... формирования временные таблиц

Функция ТекстРаспределениеМатериаловИРаботИнициализация() // вт ОтборПоМатериалам, Распределить
	Возврат
		"ВЫБРАТЬ
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Материал КАК Материал
		|ПОМЕСТИТЬ ОтборПоМатериалам
		|ИЗ
		|	&ОтборПоМатериаламИерархия КАК ДД
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Дата,
		|	ДД.Организация,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Серия,
		|	ДД.Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Назначение,
		|	ДД.БазаРаспределения,
		|	ДД.КРаспределению,
		|	ЕСТЬNULL(ОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО) КАК Материал,
		|	ЕСТЬNULL(ОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО) КАК ВидРабот,
		|	ЕСТЬNULL(ОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО) КАК ГруппаПродукции,
		|	(ВЫБОР
		|		КОГДА ОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК ПоВсемГруппамПродукции
		|ПОМЕСТИТЬ Распределить
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Ссылка КАК Регистратор,
		|		ДД.Дата,
		|		ДД.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ДД.Серия,
		|		ДД.Подразделение,
		|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|		ДД.БазаРаспределения КАК БазаРаспределения,
		|		ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ДД.Назначение КАК Назначение,
		|		ДД.КоличествоКРаспределениюПоПравилу КАК КРаспределению
		|	ИЗ
		|		Документ.РаспределениеПроизводственныхЗатрат КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Номенклатура = ДД.Номенклатура
		|			И Аналитика.Характеристика = ДД.Характеристика
		|			И Аналитика.Серия = ДД.Серия
		|			И Аналитика.МестоХранения = ДД.Подразделение
		|			И (Аналитика.Назначение = ДД.Назначение
		|				ИЛИ Аналитика.Назначение.Партнер = ДД.Назначение.Партнер
		|				ИЛИ Аналитика.Назначение.Договор = ДД.Назначение.Договор
		|				ИЛИ Аналитика.Назначение.НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|				)
		|			И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	ГДЕ
		|		ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Проведен
		|		И ДД.СтатусУказанияСерий НЕ В(6,9,10)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ДД.Ссылка КАК Регистратор,
		|		Т.Дата,
		|		Т.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ДД.Серия,
		|		Т.Подразделение,
		|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|		Т.БазаРаспределения КАК БазаРаспределения,
		|		Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		Т.Назначение КАК Назначение,
		|		ДД.КоличествоКРаспределениюПоПравилу КАК КРаспределению
		|	ИЗ
		|		Документ.РаспределениеПроизводственныхЗатрат.Серии КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат КАК Т
		|			ПО Т.Ссылка = ДД.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Номенклатура = ДД.Номенклатура
		|			И Аналитика.Характеристика = ДД.Характеристика
		|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			И Аналитика.МестоХранения = Т.Подразделение
		|			И (Аналитика.Назначение = Т.Назначение
		|				ИЛИ Аналитика.Назначение.Партнер = Т.Назначение.Партнер
		|				ИЛИ Аналитика.Назначение.Договор = Т.Назначение.Договор
		|				ИЛИ Аналитика.Назначение.НаправлениеДеятельности = Т.Назначение.НаправлениеДеятельности
		|				ИЛИ Т.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|				)
		|			И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	ГДЕ
		|		Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Т.Организация В (&МассивОрганизаций)
		|		И Т.Проведен
		|		И Т.СтатусУказанияСерий В (6,9,10)
		|	) КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОтборПоМатериалам КАК ОтборПоМатериалам
		|		ПО ОтборПоМатериалам.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоГруппамПродукции КАК ОтборПоГруппамПродукции
		|		ПО ОтборПоГруппамПродукции.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоВидамРабот КАК ОтборПоВидамРабот
		|		ПО ОтборПоВидамРабот.Ссылка = ДД.Регистратор
		|";
КонецФункции

Функция ТекстДанныеПоНормативамРасходаМатериала() // вт ФактЭтапов_ПоНормативам, ПланЭтаповПоЗаказам_ПоНормативам, Данные_ПоНормативамРасходаМатериала
	Возврат
		"
		//++ НЕ УТКА
		|ВЫБРАТЬ
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ДД.Этап.Владелец КАК Спецификация,
		|	СУММА(ДД.ЗапланированоПроизводством + ДД.КВыполнению + ДД.Выполнено + ДД.Брак) КАК ФактЭтапов
		|ПОМЕСТИТЬ ФактЭтапов_ПоНормативам
		|ИЗ
		|	РегистрНакопления.ЭтапыПроизводства КАК ДД
		|ГДЕ
		// Выполняющийся этап
		|	((ДД.НачалоПредварительногоБуфера <= &КонецПериода
		|		И ДД.ОкончаниеЗавершающегоБуфера >= &НачалоПериода
		|		И (ДД.ЗапланированоПроизводством <> 0 ИЛИ ДД.КВыполнению <> 0))
		|	ИЛИ
		// Выполненный этап
		|	(ДД.ОкончаниеЗавершающегоБуфера МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (ДД.Выполнено <> 0 ИЛИ ДД.Брак <> 0)))
		|	И ДД.Распоряжение.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ДД.Этап.Владелец
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Распоряжение,
		|	Т.КодСтрокиПродукция,
		|	Т.Спецификация,
		|	СУММА(ДД.ЗапланированоЗаказом) КАК ПланЭтапов
		|ПОМЕСТИТЬ ПланЭтаповПоЗаказам_ПоНормативам
		|ИЗ
		|	ФактЭтапов_ПоНормативам КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЭтапыПроизводства КАК ДД
		|		ПО Т.Распоряжение = ДД.Распоряжение
		|		И Т.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Т.Этап = ДД.Этап
		|		И ДД.ЗапланированоЗаказом <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Распоряжение,
		|	Т.КодСтрокиПродукция,
		|	Т.Спецификация
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|
		//-- НЕ УТКА

		// Данные по выпуску без распоряжений.
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Номенклатура КАК НоменклатураВыпуска,
		|	ДД.Характеристика КАК ХарактеристикаВыпуска,
		|	ДД.Спецификация КАК Спецификация,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество
		|ПОМЕСТИТЬ Данные_ПоНормативамРасходаМатериала
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И Выпуск.Проведен
		|
		//++ НЕ УТКА

		// Данные по выпуску по распоряжениям.
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказНаПроизводство.Организация,
		|	Строки.Подразделение,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	Строки.Назначение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	Строки.Номенклатура,
		|	Строки.Характеристика,
		|	ДД.Спецификация,
		|	Строки.Количество,
		|	План.ПланЭтапов,
		|	ДД.ФактЭтапов,
		|	ВЫБОР
		|		КОГДА План.ПланЭтапов <> 0
		|		ТОГДА Строки.Количество * ДД.ФактЭтапов / План.ПланЭтапов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Количество //Это расчетный выпуск
		|
		|ИЗ ФактЭтапов_ПоНормативам КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланЭтаповПоЗаказам_ПоНормативам КАК План
		|		ПО ДД.Распоряжение = План.Распоряжение
		|		И ДД.КодСтрокиПродукция = План.КодСтрокиПродукция
		|		И ДД.Спецификация = План.Спецификация
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК СтрокиПродукция
		|		ПО ДД.Распоряжение = СтрокиПродукция.Ссылка
		|		И ДД.КодСтрокиПродукция = СтрокиПродукция.КодСтроки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК Строки
		|		ПО СтрокиПродукция.Ссылка = Строки.Ссылка
		|		И СтрокиПродукция.КлючСвязи = Строки.КлючСвязиПродукция
		|		И ДД.Этап = Строки.Этап
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|		ПО ДД.Распоряжение = ЗаказНаПроизводство.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО Строки.Номенклатура = СН.Ссылка
		//-- НЕ УТКА
		|";
КонецФункции

Функция ТекстДанныеПоУказаннымМатериалам() // вт Данные_ПоУказаннымМатериалам
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Назначение.НаправлениеДеятельности
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Выпуск.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ КАК НаправлениеДеятельности,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Материал,
		|	(ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Ссылка ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Номенклатура
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Продукция,
		|	(ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Ссылка ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Номенклатура.ГруппаАналитическогоУчета
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(&Вес * ДД.Количество) КАК Вес
		|ПОМЕСТИТЬ Данные_ПоУказаннымМатериалам
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		//++ НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказыНаПроизводство
		|		ПО ЗаказыНаПроизводство.Ссылка = ДД.ЗаказНаПроизводство
		|		И ЗаказыНаПроизводство.КодСтроки = ДД.КодСтрокиПродукция
		//-- НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Выпуск
		|		ПО Выпуск.Ссылка = ДД.ЗаказНаПроизводство
		|		И Выпуск.КодСтроки = ДД.КодСтрокиПродукция
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И (ДД.СтатьяРасходов = НЕОПРЕДЕЛЕНО ИЛИ ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|	И ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|	И НЕ ДД.Регистратор Ссылка Документ.ВозвратМатериаловИзПроизводства
		|	И НЕ ДД.Регистратор Ссылка Документ.ОтчетПереработчика
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура,
		|	(ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Ссылка ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Номенклатура
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	(ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Ссылка ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Номенклатура.ГруппаАналитическогоУчета
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	ДД.Назначение,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ЗаказыНаПроизводство.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА ЗаказыНаПроизводство.Назначение.НаправлениеДеятельности
		//-- НЕ УТКА
		|		КОГДА НЕ Выпуск.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Выпуск.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ
		|";
		
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ПодставитьШаблоныВесаИОбъемаВТекстЗапроса(ТекстЗапроса);
	
КонецФункции

Функция ТекстДанныеПоПродукции() // вт ФактЭтапов_ПоПродукции, ПланЭтаповПоЗаказам_ПоПродукции, Данные_ПоПродукции
	
	ТекстЗапроса =
		"
		//++ НЕ УТКА
		|ВЫБРАТЬ
		|	ДД.Подразделение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ДД.Этап.Владелец КАК Спецификация,
		|	СУММА(ДД.ЗапланированоПроизводством + ДД.КВыполнению + ДД.Выполнено + ДД.Брак) КАК ФактЭтапов
		|ПОМЕСТИТЬ ФактЭтапов_ПоПродукции
		|ИЗ
		|	РегистрНакопления.ЭтапыПроизводства КАК ДД
		|ГДЕ
		// Выполняющийся этап
		|	((ДД.НачалоПредварительногоБуфера <= &КонецПериода
		|		И ДД.ОкончаниеЗавершающегоБуфера >= &НачалоПериода
		|		И (ДД.ЗапланированоПроизводством <> 0 ИЛИ ДД.КВыполнению <> 0))
		|	ИЛИ
		// Выполненный этап
		|	(ДД.ОкончаниеЗавершающегоБуфера МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (ДД.Выполнено <> 0 ИЛИ ДД.Брак <> 0)))
		|	И ДД.Распоряжение.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Подразделение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ДД.Этап.Владелец
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Спецификация,
		|	СУММА(Этапы.ЗапланированоЗаказом) КАК ПланЭтапов
		|ПОМЕСТИТЬ ПланЭтаповПоЗаказам_ПоПродукции
		|ИЗ
		|	ФактЭтапов_ПоПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Распоряжение = ДД.Распоряжение
		|		И Этапы.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Этапы.Этап = ДД.Этап
		|		И Этапы.ЗапланированоЗаказом <> 0
		|		И Этапы.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Спецификация
		|;
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|
		//-- НЕ УТКА

		// Данные по выпуску без распоряжений
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))КАК НаправлениеДеятельности,
		|	ДД.Номенклатура КАК Продукция,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество,
		|	&Объем * ДД.Количество КАК Объем,
		|	&Вес * ДД.Количество КАК Вес
		|ПОМЕСТИТЬ Данные_ПоПродукции
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Выпуск.Проведен
		|
		//++ НЕ УТКА

		// Данные по выпуску по распоряжениям
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказНаПроизводство.Организация,
		|	ДД.Подразделение,
		|	ЕСТЬNULL(Строки.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	Строки.Номенклатура КАК Продукция,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	Строки.Назначение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	Строки.Количество,
		|	План.ПланЭтапов,
		|	ДД.ФактЭтапов,
		|	ВЫБОР
		|		КОГДА План.ПланЭтапов <> 0
		|		ТОГДА Строки.Количество * ДД.ФактЭтапов / План.ПланЭтапов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Количество, //Это расчетный выпуск
		|	ВЫБОР
		|		КОГДА План.ПланЭтапов <> 0
		|		ТОГДА &Объем * Строки.Количество * ДД.ФактЭтапов / План.ПланЭтапов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Объем,
		|	ВЫБОР
		|		КОГДА План.ПланЭтапов <> 0
		|		ТОГДА &Вес * Строки.Количество * ДД.ФактЭтапов / План.ПланЭтапов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Вес
		|
		|ИЗ ФактЭтапов_ПоПродукции КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланЭтаповПоЗаказам_ПоПродукции КАК План
		|		ПО План.Распоряжение = ДД.Распоряжение
		|		И План.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И План.Спецификация = ДД.Спецификация
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Строки
		|		ПО Строки.Ссылка = ДД.Распоряжение
		|		И Строки.КодСтроки = ДД.КодСтрокиПродукция
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|		ПО ЗаказНаПроизводство.Ссылка = ДД.Распоряжение
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = Строки.Номенклатура
		|
		//-- НЕ УТКА
		|";
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ПодставитьШаблоныВесаИОбъемаВТекстЗапроса(ТекстЗапроса);
	
КонецФункции

Функция ТекстДанныеПоПлановойСтоимости() // вт ФактЭтапов_ПоПлановойСтоимости, ПланЭтаповПоЗаказам_ПоПлановойСтоимости, Данные_ПоПлановойСтоимости
	Возврат
		"
		//++ НЕ УТКА
		|ВЫБРАТЬ
		|	ДД.Подразделение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	СУММА(ДД.ЗапланированоПроизводством + ДД.КВыполнению + ДД.Выполнено + ДД.Брак) КАК ФактЭтапов
		|ПОМЕСТИТЬ ФактЭтапов_ПоПлановойСтоимости
		|ИЗ
		|	РегистрНакопления.ЭтапыПроизводства КАК ДД
		|ГДЕ
		// Выполняющийся этап
		|	((ДД.НачалоПредварительногоБуфера <= &КонецПериода
		|		И ДД.ОкончаниеЗавершающегоБуфера >= &НачалоПериода
		|		И (ДД.ЗапланированоПроизводством <> 0 ИЛИ ДД.КВыполнению <> 0))
		|	ИЛИ
		// Выполненный этап
		|	ДД.ОкончаниеЗавершающегоБуфера МЕЖДУ &НачалоПериода И &КонецПериода
		|	)
		|	И ДД.Распоряжение.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Подразделение,
		|	ДД.Распоряжение,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Распоряжение,
		|	Т.КодСтрокиПродукция,
		|	СУММА(ДД.ЗапланированоЗаказом) КАК ПланЭтапов
		|ПОМЕСТИТЬ ПланЭтаповПоЗаказам_ПоПлановойСтоимости
		|ИЗ
		|	ФактЭтапов_ПоПлановойСтоимости КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЭтапыПроизводства КАК ДД
		|		ПО Т.Распоряжение = ДД.Распоряжение
		|		И Т.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Т.Этап = ДД.Этап
		|		И ДД.ЗапланированоЗаказом <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Распоряжение,
		|	Т.КодСтрокиПродукция
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|
		//-- НЕ УТКА

		// Данные по выпуску без распоряжений.
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ДД.Номенклатура КАК Номенклатура,
		|	ДД.Характеристика КАК Характеристика,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество
		|ПОМЕСТИТЬ Данные_ПоПлановойСтоимости
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Выпуск.Проведен
		//++ НЕ УТКА

		// Данные по выпуску по распоряжениям.
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Д.Организация,
		|	ФК.Подразделение,
		|	ЕСТЬNULL(П.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	П.Номенклатура КАК Номенклатура,
		|	П.Характеристика КАК Характеристика,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	П.Назначение,
		|	ФК.Распоряжение,
		|	ФК.КодСтрокиПродукция,
		|	ФК.Этап,
		|	П.Количество,
		|	ПЛ.ПланЭтапов,
		|	ФК.ФактЭтапов,
		|	ВЫБОР
		|		КОГДА ФК.ФактЭтапов <> 0 И ПЛ.ПланЭтапов <> 0
		|		ТОГДА П.Количество * ФК.ФактЭтапов / ПЛ.ПланЭтапов
		|		ИНАЧЕ П.Количество
		|	КОНЕЦ КАК Количество //Это расчетный выпуск
		|
		|ИЗ
		|	ФактЭтапов_ПоПлановойСтоимости КАК ФК
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланЭтаповПоЗаказам_ПоПлановойСтоимости КАК ПЛ
		|	ПО ФК.Распоряжение = ПЛ.Распоряжение
		|	И ФК.КодСтрокиПродукция = ПЛ.КодСтрокиПродукция
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК П
		|	ПО ФК.Распоряжение = П.Ссылка
		|	И ФК.КодСтрокиПродукция = П.КодСтроки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Д
		|	ПО ФК.Распоряжение = Д.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|	ПО П.Номенклатура = СН.Ссылка
		|
		//-- НЕ УТКА
		|";
КонецФункции

Функция ТекстДанныеПоУказаннымТрудозатратам() // вт Данные_ПоУказаннымТрудозатратам
	Возврат
		"ВЫБРАТЬ
		|	ДД.Организация КАК Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(Выпуски.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.КорАналитикаУчетаПродукции.Номенклатура КАК Продукция,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	Выпуски.Назначение КАК Назначение,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.ДокументВыпуска КАК ДокументВыпуска,
		|	ДД.КодСтроки КАК КодСтроки,
		|	ДД.Количество КАК Количество
		|ПОМЕСТИТЬ Данные_ПоУказаннымТрудозатратам
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|	ПО ДД.КорАналитикаУчетаПродукции.Номенклатура = СН.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Выпуски
		|	ПО ДД.ДокументВыпуска = Выпуски.Ссылка
		|	И ДД.КодСтроки = Выпуски.КодСтроки
		|
		|ГДЕ
		|	ДД.КодСтрокиПродукция = 0
		|	И ДД.Количество <> 0
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		//++ НЕ УТКА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Организация КАК Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(Строки.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.КорАналитикаУчетаПродукции.Номенклатура КАК Продукция,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ЕСТЬNULL(З.Назначение, НЕОПРЕДЕЛЕНО) КАК Назначение,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	ДД.Этап КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	ДД.Количество КАК Количество
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|	ПО ДД.КорАналитикаУчетаПродукции.Номенклатура = СН.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК З
		|	ПО ДД.ЗаказНаПроизводство = З.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Строки
		|	ПО ДД.ЗаказНаПроизводство = Строки.Ссылка
		|	И ДД.КодСтрокиПродукция = Строки.КодСтроки
		|
		|ГДЕ
		|	ДД.Количество <> 0
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		//-- НЕ УТКА
		|";
КонецФункции

Функция ТекстБазаРаспределения() // вт БазаРаспределения
	
	ТекстЗапроса = "
		|
		// по указанным материалам
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	Данные.Объем,
		|	Данные.Вес,
		|	0 КАК Стоимость
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоУказаннымМатериалам КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И Данные.Материал = ДД.Материал
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Партнер = ДД.Назначение.Партнер
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Договор = ДД.Назначение.Договор
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
		|
		// по продукции
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	Данные.Объем,
		|	Данные.Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоПродукции КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Партнер = ДД.Назначение.Партнер
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Договор = ДД.Назначение.Договор
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|		И Данные.Подразделение = ДД.Подразделение
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции)
		|
		// по плановой стоимости
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	Данные.Количество * Цены.Цена КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоПлановойСтоимости КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Партнер = ДД.Назначение.Партнер
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Договор = ДД.Назначение.Договор
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|		И Данные.Подразделение = ДД.Подразделение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода, ВидЦены = &ВидЦеныПлановойСтоимостиМатериаловРабот) КАК Цены
		|		ПО Цены.Номенклатура = Данные.Номенклатура
		|		И Цены.Характеристика = Данные.Характеристика
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции)
		|
		// по указанным трудозатратам
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	ВЫБОР
		|		КОГДА Данные.ДокументВыпуска <> ЗНАЧЕНИЕ(Документ.ВыпускПродукции.ПустаяСсылка)
		|			И Данные.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Данные.ДокументВыпуска
		|		ИНАЧЕ Данные.ЗаказНаПроизводство
		|	КОНЕЦ КАК ЗаказНаПроизводство,
		|	ВЫБОР
		|		КОГДА Данные.ДокументВыпуска <> ЗНАЧЕНИЕ(Документ.ВыпускПродукции.ПустаяСсылка)
		|			И Данные.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Данные.КодСтроки
		|		ИНАЧЕ Данные.КодСтрокиПродукция
		|	КОНЕЦ КАК КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоУказаннымТрудозатратам КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И Данные.ВидРабот = ДД.ВидРабот
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Партнер = ДД.Назначение.Партнер
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Договор = ДД.Назначение.Договор
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов)
		|
		// по нормативам
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	(ВЫБОР
		|		КОГДА СУММА(ВЫРАЗИТЬ(Продукция.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3))) <> 0
		|			ТОГДА СУММА(Данные.Количество) * СУММА(Материалы.Количество)
		|					/ СУММА(ВЫРАЗИТЬ(Продукция.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3)))
		|		ИНАЧЕ 0 КОНЕЦ) КАК Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоНормативамРасходаМатериала КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Партнер = ДД.Назначение.Партнер
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).Договор = ДД.Назначение.Договор
		|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
		|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыИУслугиСпецификаций КАК Материалы
		|		ПО Материалы.Ссылка = Данные.Спецификация
		|		И Материалы.Номенклатура = Аналитика.Номенклатура
		|		И Материалы.Характеристика = Аналитика.Характеристика
		|		И (Материалы.Характеристика = Аналитика.Характеристика ИЛИ Материалы.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Продукция
		|		ПО Продукция.Ссылка = Данные.Спецификация
		|		И ВЫБОР
		|			КОГДА Продукция.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ
		|				Продукция.Номенклатура = Данные.НоменклатураВыпуска
		|				И Продукция.Характеристика = Данные.ХарактеристикаВыпуска
		|				И (Продукция.Характеристика = Данные.ХарактеристикаВыпуска ИЛИ Продукция.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|		КОНЕЦ
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап
		|";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Продукция.Упаковка",
			"Данные.НоменклатураВыпуска"));
			
	Возврат ТекстЗапроса;
		
КонецФункции

Функция ТекстСуммыПоказателей() // вт ОбщиеСуммы
	Возврат
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Серия,
		|	СУММА(ДД.Количество) КАК ОбщееКоличество,
		|	СУММА(ДД.Объем) КАК ОбщийОбъем,
		|	СУММА(ДД.Вес) КАК ОбщийВес,
		|	СУММА(ДД.Стоимость) КАК ОбщаяСтоимость
		|ПОМЕСТИТЬ ОбщиеСуммы
		|ИЗ
		|	БазаРаспределения КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор,
		|	ДД.Серия
		|";
КонецФункции

// ... выборки данных

Функция ТекстКРаспределению()
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ТекстКРаспределению"" КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Дата КАК Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Подразделение,
		|	ДД.Серия,
		|	ДД.Назначение КАК Назначение,
		|	Итог.ОбщееКоличество КАК Количество,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель,
		|	ДД.БазаРаспределения,
		|	ДД.КРаспределению,
		|	Итог.ОбщийОбъем КАК Объем,
		|	Итог.ОбщийВес КАК Вес,
		|	Итог.ОбщаяСтоимость КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщиеСуммы КАК Итог
		|		ПО Итог.Регистратор = ДД.Регистратор
		|		И Итог.Серия = ДД.Серия
		|ГДЕ
		|	ДД.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|";
КонецФункции

Функция ТекстРаспределениеПоБазе()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстРаспределениеПоБазе"" КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Дата КАК Период,
		|	ДД.Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.Серия КАК Серия,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	ДД.Количество,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель,
		|	НЕОПРЕДЕЛЕНО КАК БазаРаспределения,
		|	0 КАК КРаспределению,
		|	ДД.Объем,
		|	ДД.Вес,
		|	ДД.Стоимость
		|ИЗ
		|	БазаРаспределения КАК ДД
		|";
КонецФункции

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Потребление;
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход,
		"Организация, Номенклатура, Характеристика, Подразделение, Серия,
		|АналитикаУчетаНоменклатуры, БазаРаспределения, Назначение, СтатьяКалькуляции");
	
	Если Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Количество = 0, 0, Расход.Количество / Приход.Количество), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Объем = 0, 0, Расход.Объем / Приход.Объем), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Вес = 0, 0, Расход.Вес / Приход.Вес), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Стоимость = 0, 0, Расход.Стоимость / Приход.Стоимость), 3);
		
	КонецЕсли;
	
	РасчетнаяПартия.КРаспределению = Приход.КРаспределению;
	
	// корректируем базу расчета в приходе
	Приход.КРаспределению = Приход.КРаспределению - РасчетнаяПартия.Количество;
	Приход.Количество 	  = Приход.Количество - Расход.Количество;
	Приход.Объем 		  = Приход.Объем - Расход.Объем;
	Приход.Вес 			  = Приход.Вес - Расход.Вес;
	Приход.Стоимость 	  = Приход.Стоимость - Расход.Стоимость;
	
	// корректируем базу расчета в расходе
	Расход.Количество = 0;
	Расход.Объем 	  = 0;
	Расход.Вес 		  = 0;
	Расход.Стоимость  = 0;
	
	РасчетнаяПартия.РасчетЗавершен = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа7_РаспределениеМатериаловНЗП

// Инициализация данных

Функция ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	Возврат "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, ДокументВыпуска, НомерГруппыЗатрат";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений	= ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета);
	
	// Распределение <- (Партия, Остаток, ПолуфабрикатДавальца)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Распределение, 		 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.Партия,				 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.Остаток,				 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца, ПоляПотреблений);
	
	// Выпуск <- (Распределение)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Выпуск, 		  		  "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, Продукция, ХарактеристикаПродукции");
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Выпуск,
		Перечисления.ТипыЗаписейПартий.Распределение, 		  "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, Продукция, ХарактеристикаПродукции");
	
	// Дополнение <- (<без источников - для формирования записей по данным запроса>)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеДополнения(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Дополнение);
		
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	ОписаниеДвижений.Вставить("ИсключенияПоказатели", "Знаменатель, КоличествоПродукции, НомерГруппыЗатрат");
	ОписаниеДвижений.Вставить("БазисПрихода", "Знаменатель");
	ОписаниеДвижений.Вставить("БазисРасхода", "Знаменатель");
	ОписаниеДвижений.Вставить("КлючРасхода", "ДокументИсточник");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период"); // ПолеПорядка
	
	ОписаниеДвижений.Вставить("ПоляСуммирования", "Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	ОписаниеДвижений.Вставить("ИсключенияПоляСуммирования", "Знаменатель, КоличествоПродукции, НомерГруппыЗатрат");
	ОписаниеДвижений.Вставить("ПоляГруппировки",  
		РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения, ОписаниеДвижений.ПоляСуммирования));
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийМатериаловНЗП(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Остаток, Истина);
	
	НезаписываемыеРазделы = Новый Соответствие;
	НезаписываемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей, НезаписываемыеРазделы);
	
КонецФункции

Функция ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"Организация", // разделитель
		"Знаменатель, КоличествоПродукции, Количество, Стоимость,
			|СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница", // ресурсы
		"Организация, ЗаказНаПроизводство, Приоритет, Период, Регистратор"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ ТекстПартииНЗПИнициализация() // вт ПартииНЗПНачальныйОстаток, ПрошлыеПоступленияНЗП, ЗаказыКРаспределению, ДолиСтоимости, ВыпускиБезЗаказов
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстОстаткиПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПервичныеПартииНЗП()
		//++ НЕ УТКА
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстДополнениеПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстМаршрутныеЛисты()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПрошлыеМаршрутныеЛисты()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстВыпускиПоЗаказам()
		//-- НЕ УТКА
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП()
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) 											КАК ЗапросИсточник,
		|	0 																		КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка) 					КАК ТипЗаписи,
		|	ЛОЖЬ 																	КАК РасчетЗавершен,
		|
		|	ДД.ВидДвижения 															КАК ВидДвижения,
		|	ДД.Период 																КАК Период,
		|	ДД.Регистратор 															КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) 	КАК РазделУчета,
		|	ДД.Организация 															КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 					КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры 											КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления 													КАК ДокументПоступления,
		|	ДД.ВидЗапасов 															КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство 													КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция 													КАК КодСтрокиПродукция,
		|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка) 				КАК Спецификация,
		|	ДД.Этап																	КАК Этап,
		|	ДД.СтатьяКалькуляции 													КАК СтатьяКалькуляции,
		|	ДД.АналитикаУчетаПартий 												КАК АналитикаУчетаПартий,
		|	ДД.АналитикаУчетаПродукции 												КАК АналитикаУчетаПродукции,
		|	0 																		КАК Знаменатель,
		|	0 																		КАК КоличествоПродукции,
		|	0 																		КАК Количество,
		|	0 																		КАК Стоимость,
		|	0 																		КАК СтоимостьБезНДС,
		|	0 																		КАК СтоимостьРегл,
		|	0 																		КАК НДСРегл,
		|	0 																		КАК ПостояннаяРазница,
		|	0 																		КАК ВременнаяРазница,
		|	ЛОЖЬ 																	КАК Первичное,
		|	ЛОЖЬ 																	КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) 				КАК ХозяйственнаяОперация,
		|	ДД.КорРазделУчета 														КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры 											КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов 														КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) 											КАК ДатаРегистратора,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) 							КАК Назначение,
		|	ДД.ДокументИсточник 													КАК ДокументИсточник,
		|	ДД.ДокументВыпуска 														КАК ДокументВыпуска,
		|	0 																		КАК НомерГруппыЗатрат,
		|	ДД.Продукция 															КАК Продукция,
		|	ДД.ХарактеристикаПродукции 												КАК ХарактеристикаПродукции
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	РегистрНакопления.ПартииНезавершенногоПроизводства КАК ДД
		|";

	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

// ... формирования временные таблиц

Функция ТекстПартииНЗПИнициализация() // вт ПартииНЗПНачальныйОстаток, ПрошлыеПоступленияНЗП, ЗаказыКРаспределению, ДолиСтоимости
	Возврат
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ЗаказНаПроизводство,
		|	Т.КодСтрокиПродукция,
		|	Т.ДокументПоступления,
		|	Т.Этап,
		|	Т.СтатьяКалькуляции,
		|	Т.АналитикаУчетаПартий,
		|	Т.КоличествоОстаток,
		|	Т.СтоимостьОстаток,
		|	Т.СтоимостьБезНДСОстаток,
		|	Т.СтоимостьРеглОстаток,
		|	Т.НДСРеглОстаток,
		|	Т.ПостояннаяРазницаОстаток,
		|	Т.ВременнаяРазницаОстаток
		|ПОМЕСТИТЬ ПартииНЗПНачальныйОстаток
		|ИЗ
		|	РегистрНакопления.ПартииНезавершенногоПроизводства.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК Т
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ ПрошлыеПоступленияНЗП
		|ИЗ
		|	ПартииНЗПНачальныйОстаток КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииНезавершенногоПроизводства КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления
		|;
		|////////////////////////////////////////////////////////////////////////////

		//++ НЕ УТКА
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Распоряжение
		|ПОМЕСТИТЬ ЗаказыКРаспределениюПредварительная
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции.ОстаткиИОбороты(&НачалоПериода, &КонецПериода) КАК Т
		|ГДЕ
		|	Т.ЗаказаноПриход <> 0
		|	ИЛИ Т.ЗаказаноКонечныйОстаток <> 0
	    |
		|ИНДЕКСИРОВАТЬ ПО
		|	Т.Распоряжение
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МаршрутныйЛист.Распоряжение КАК Заказ,
		|	МаршрутныйЛист.КодСтроки КАК КодСтроки
		|ПОМЕСТИТЬ ЗаказыКРаспределению
		|ИЗ
		|	Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыКРаспределениюПредварительная КАК Т
		|		ПО МаршрутныйЛист.Ссылка = Т.Распоряжение
		|ГДЕ
		|	МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|;
		|////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗаказыКРаспределениюПредварительная
		|;
		|////////////////////////////////////////////////////////////////////////////

		//-- НЕ УТКА
		|ВЫБРАТЬ
		|	Строки.ЗаказПереработчику КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	Строки.НомерГруппыЗатрат,
		|	СУММА(ВЫБОР Строки.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ Строки.ДоляСтоимости КОНЕЦ) КАК ДоляСтоимости
		|ПОМЕСТИТЬ ДолиСтоимости
		|ИЗ
		|	Документ.ОтчетПереработчика КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Строки.ЗаказПереработчику,
		|	Строки.НомерГруппыЗатрат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Строки.ДокументПоступления КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	Строки.НомерГруппыЗатрат,
		|	СУММА(ВЫБОР Строки.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ Строки.ДоляСтоимости КОНЕЦ) КАК ДоляСтоимости
		|ИЗ
		|	Документ.ОтчетПереработчика КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Строки.ДокументПоступления,
		|	Строки.НомерГруппыЗатрат
		//++ НЕ УТКА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	0 КАК НомерГруппыЗатрат,
		|	СУММА(ВЫБОР ДД.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ
		|		* (МаршрутныйЛист.Произведено + МаршрутныйЛист.Брак)) КАК ДоляСтоимости
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыКРаспределению КАК Заказы
		|		ПО Заказы.Заказ = МаршрутныйЛист.Распоряжение
		|			И Заказы.КодСтроки = МаршрутныйЛист.КодСтроки
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|СГРУППИРОВАТЬ ПО
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец
		//-- НЕ УТКА
		|";
КонецФункции

// ... выборки данных

Функция ТекстОстаткиПартийНЗП()
	// Передачи материалов в производство и в переработку, возвраты материалов из производства и от переработчика.
	Возврат
		"ВЫБРАТЬ
		|	""ТекстОстаткиПартийНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Остаток) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.ВидЗапасов.ГруппаПродукции КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ЕСТЬNULL(Этапы.Владелец, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	ЕСТЬNULL(Доли.ДоляСтоимости, 1) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	Аналитика.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции
		|ИЗ
		|	ПартииНЗПНачальныйОстаток КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступленияНЗП КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|";
КонецФункции

Функция ТекстПервичныеПартииНЗП()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПервичныеПартииНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	МАКСИМУМ(ЕСТЬNULL(Доли.ДоляСтоимости, 1)) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ДД.Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И ДД.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	И НЕ (ЕСТЬNULL(Аналитика.Назначение.ТипНазначения, НЕОПРЕДЕЛЕНО) В (ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое21),
		|														ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22))
		|		И ДД.Количество < 0
		|		И (ДД.Регистратор Ссылка Документ.СписаниеЗатратНаВыпуск
		//++ НЕ УТКА
		|				ИЛИ ДД.Регистратор Ссылка Документ.МаршрутныйЛистПроизводства
		//-- НЕ УТКА
		|			)
		|		)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	Этапы.Владелец,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Назначение
		|";
КонецФункции

//++ НЕ УТКА

Функция ТекстДополнениеПартийНЗП()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстДополнениеПартийНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	ВЫБОР
		|		КОГДА ДД.ЗаказНаПроизводство = НЕОПРЕДЕЛЕНО
		|			ТОГДА ЕСТЬNULL(Этапы.Владелец, ДД.Спецификация)
		|		ИНАЧЕ ДД.ЗаказНаПроизводство
		|	КОНЕЦ КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	ЕСТЬNULL(Этапы.Владелец, ДД.Спецификация) КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	0 КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ДД.Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	ВЫБОР
		|		КОГДА НЕ Заказ.Номенклатура ЕСТЬ NULL ТОГДА Заказ.Номенклатура
		|		КОГДА НЕ ЗаказПереработчику.Номенклатура ЕСТЬ NULL ТОГДА ЗаказПереработчику.Номенклатура
		|		ИНАЧЕ ЕСТЬNULL(КорАналитика.Номенклатура, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ КАК Продукция,
		|	ВЫБОР
		|		КОГДА НЕ Заказ.Характеристика ЕСТЬ NULL ТОГДА Заказ.Характеристика
		|		КОГДА НЕ ЗаказПереработчику.Характеристика ЕСТЬ NULL ТОГДА ЗаказПереработчику.Характеристика
		|		ИНАЧЕ ЕСТЬNULL(КорАналитика.Характеристика, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ КАК ХарактеристикаПродукции
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорАналитика
		|		ПО КорАналитика.Ссылка = ДД.АналитикаУчетаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Заказ
		|		ПО Заказ.Ссылка = ДД.ЗаказНаПроизводство
		|		И Заказ.КодСтроки = ДД.КодСтрокиПродукция
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Продукция КАК ЗаказПереработчику
		|		ПО ЗаказПереработчику.Ссылка = ДД.ЗаказНаПроизводство
		|		И ЗаказПереработчику.КодСтроки = ДД.КодСтрокиПродукция
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И НЕ ДД.ЗаказНаПроизводство ССЫЛКА Документ.ВыпускПродукции
		|	И (ДД.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат)
		|	И (ДД.СтатьяРасходов = НЕОПРЕДЕЛЕНО
		|		ИЛИ ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|	И НЕ (ЕСТЬNULL(Аналитика.Назначение.ТипНазначения, НЕОПРЕДЕЛЕНО) В (ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое21),
		|														ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22))
		|		И ДД.Количество < 0
		|		И (ДД.Регистратор Ссылка Документ.СписаниеЗатратНаВыпуск
		|				ИЛИ ДД.Регистратор Ссылка Документ.МаршрутныйЛистПроизводства
		|			)
		|		)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ЗаказНаПроизводство,
		|	ВЫБОР
		|		КОГДА ДД.ЗаказНаПроизводство = НЕОПРЕДЕЛЕНО
		|			ТОГДА ЕСТЬNULL(Этапы.Владелец, ДД.Спецификация)
		|		ИНАЧЕ ДД.ЗаказНаПроизводство
		|	КОНЕЦ,
		|	ДД.КодСтрокиПродукция,
		|	Этапы.Владелец,
		|	ДД.Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Назначение,
		|	ВЫБОР
		|		КОГДА НЕ Заказ.Номенклатура ЕСТЬ NULL ТОГДА Заказ.Номенклатура
		|		КОГДА НЕ ЗаказПереработчику.Номенклатура ЕСТЬ NULL ТОГДА ЗаказПереработчику.Номенклатура
		|		ИНАЧЕ ЕСТЬNULL(КорАналитика.Номенклатура, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ Заказ.Характеристика ЕСТЬ NULL ТОГДА Заказ.Характеристика
		|		КОГДА НЕ ЗаказПереработчику.Характеристика ЕСТЬ NULL ТОГДА ЗаказПереработчику.Характеристика
		|		ИНАЧЕ ЕСТЬNULL(КорАналитика.Характеристика, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ
		|";
КонецФункции

Функция ТекстМаршрутныеЛисты()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстМаршрутныеЛисты"" КАК ЗапросИсточник,
		|	40 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Распределение) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	МаршрутныйЛист.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	СУММА(ВЫБОР ДД.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ
		|		* (МаршрутныйЛист.Произведено + МаршрутныйЛист.Брак)) КАК Знаменатель,
		|	СУММА(ДД.Заказано) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	ДД.Номенклатура КАК Продукция,
		|	ДД.Характеристика КАК ХарактеристикаПродукции
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И ДД.Заказано <> 0
		|СГРУППИРОВАТЬ ПО
		|	МаршрутныйЛист.Организация,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.АналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстПрошлыеМаршрутныеЛисты()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПрошлыеМаршрутныеЛисты"" КАК ЗапросИсточник,
		|	40 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Распределение) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	МаршрутныйЛист.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	МаршрутныйЛист.Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	СУММА(ВЫБОР Продукция.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ Продукция.ДоляСтоимости КОНЕЦ
		|		* (МаршрутныйЛист.Произведено + МаршрутныйЛист.Брак)) КАК Знаменатель,
		|	СУММА(Продукция.Количество) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	Продукция.Номенклатура КАК Продукция,
		|	Продукция.Характеристика КАК ХарактеристикаПродукции
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспоряженияНаВыпускПродукции КАК Приходы
		|		ПО Приходы.Регистратор = ДД.Распоряжение
		|		И Приходы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК Продукция
		|		ПО Продукция.Ссылка = МаршрутныйЛист.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Приходы.Регистратор ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	МаршрутныйЛист.Организация,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец,
		|	МаршрутныйЛист.Этап,
		|	Продукция.Номенклатура,
		|	Продукция.Характеристика
		|";
КонецФункции

Функция ТекстВыпускиПоЗаказам()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстВыпускиПоЗаказам"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Выпуск) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	МаршрутныйЛист.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	МаршрутныйЛист.Распоряжение КАК ЗаказНаПроизводство,
		|	МаршрутныйЛист.КодСтроки КАК КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	СУММА(ДД.Заказано) КАК Знаменатель,
		|	СУММА(ДД.Заказано) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	(ВЫБОР
		|		КОГДА ДД.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КОНЕЦ) КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	Аналитика.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	ДД.Номенклатура КАК Продукция,
		|	ДД.Характеристика КАК ХарактеристикаПродукции
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаВыпускПродукции КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО МаршрутныйЛист.Ссылка = ДД.Распоряжение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = МаршрутныйЛист.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = Аналитика.Назначение
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И МаршрутныйЛист.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	МаршрутныйЛист.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.КодСтроки,
		|	Этапы.Владелец,
		|	(ВЫБОР
		|		КОГДА ДД.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	Аналитика.Назначение
		|";
КонецФункции
//-- НЕ УТКА

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловНЗП(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход.Знаменатель = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммовыеРесурсы = Новый Структура("Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");

	// расчетная партия заполняется на наибольшее общее вычитаемое
	ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Количество = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
	
	Для Каждого ИмяРесурса Из СуммовыеРесурсы Цикл
		Если РасчетнаяПартия.Количество = Приход.Количество Тогда
			РасчетнаяПартия[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ];
		Иначе
			РасчетнаяПартия[ИмяРесурса.Ключ] = Окр(ЗнаменательСписания * Приход[ИмяРесурса.Ключ] / Приход.Знаменатель, 2);
		КонецЕсли;
	КонецЦикла;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Знаменатель = Расход.КоличествоПродукции;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 И (Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Остаток
	  ИЛИ Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца) Тогда
		РасчетнаяПартия.ТипЗаписи = Приход.ТипЗаписи;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Выпуск
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ТипЗаписи = Приход.ТипЗаписи;
	КонецЕсли;
	
	// корректируем базу расчета в приходе
	Для Каждого ИмяРесурса Из СуммовыеРесурсы Цикл
		Приход[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ] - РасчетнаяПартия[ИмяРесурса.Ключ];
	КонецЦикла;
	
	Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
	Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;

	// Заполняем партионную идентификацию в расчетной партии
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка();	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ЗаказНаПроизводство)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ЗаказНаПроизводство = Приход.ЗаказНаПроизводство;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.КодСтрокиПродукция)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.КодСтрокиПродукция = Приход.КодСтрокиПродукция;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Этап)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Этап = Приход.Этап;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Спецификация)
		ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Спецификация = Приход.Спецификация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Выпуск
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ВыпускБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Переработка
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ОтчетДавальцу
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Партия
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа Тогда
		РасчетнаяПартия.ДокументВыпуска = Приход.ДокументВыпуска;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
		РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ВидЗапасов)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
	КонецЕсли;
	
	РасчетнаяПартия.ГруппаПродукции = Приход.ГруппаПродукции;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Период)
	 И РасчетнаяПартия.ТипЗаписи <> Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.Период = Приход.Период;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Регистратор)
	 И РасчетнаяПартия.ТипЗаписи <> Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.Регистратор = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Назначение) Тогда
		РасчетнаяПартия.Назначение = Приход.Назначение;
	КонецЕсли;
	
	Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
	Иначе
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
	КонецЕсли;
	
	РасчетнаяПартия.АналитикаУчетаПартий  	   = Приход.АналитикаУчетаПартий;
	РасчетнаяПартия.АналитикаУчетаНоменклатуры = Приход.АналитикаУчетаНоменклатуры;
	РасчетнаяПартия.Подразделение 			   = Приход.Подразделение;
	РасчетнаяПартия.СтатьяКалькуляции 		   = Приход.СтатьяКалькуляции;
	
	РасчетнаяПартия.РасчетЗавершен 			   = Приход.РасчетЗавершен;
	
	Если РасчетнаяПартия.Количество = 0
	 И РасчетнаяПартия.Стоимость = 0
	 И РасчетнаяПартия.СтоимостьБезНДС = 0
	 И РасчетнаяПартия.СтоимостьРегл = 0
	 И РасчетнаяПартия.НДСРегл = 0
	 И РасчетнаяПартия.ПостояннаяРазница = 0
	 И РасчетнаяПартия.ВременнаяРазница = 0 Тогда
		
		РасчетнаяПартия.Знаменатель = 0;
		РасчетнаяПартия.РасчетЗавершен = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа15_РаспределениеДолейПроизводственныхРасходов

// Дополняет массив запросов локализованными фактическими материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросФактическимиМатериальнымЗатратам(ТекстыЗапросов) Экспорт
	
	//++ НЕ УТКА

	// Материалы версии 2.1 собственное производство по заказу
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                      КАК Организация,
		|	Аналитика.МестоХранения             КАК Подразделение,
		|	Аналитика.СтатьяКалькуляции         КАК СтатьяКалькуляции,
		|	Аналитика.Номенклатура              КАК Материал,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ                               КАК ГруппаПродукции,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ВыпускПродукции),
		|		ТИП(Документ.ЭтапПроизводства2_2),
		|		ТИП(Документ.ПроизводствоБезЗаказа)) КАК ЭтоПолуфабрикатСобственный,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ОтчетПереработчика)) КАК ЭтоПолуфабрикатПереработчика,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.КорПартия                        КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                        КАК КодСтрокиПродукция,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	СУММА(
		|	(ВЫБОР
		|		КОГДА ДД.Стоимость + ДД.ДопРасходы <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.Стоимость + ДД.ДопРасходы
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0))
		|	КОНЕЦ))                             КАК Стоимость,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0))
		|		КОНЕЦ))                                              КАК СтоимостьБезНДС,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0))
		|		КОНЕЦ))                                              КАК СтоимостьРегл,
		|
		//++ НЕ УТ

		//++ Локализация
		|	СУММА((ВЫБОР
		|		КОГДА ДД.ПостояннаяРазница <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.ПостояннаяРазница
		|		ИНАЧЕ ДД.Количество * ЕСТЬNULL(Цены.ПостояннаяРазница, 0)
		|		КОНЕЦ))                                              КАК ПостояннаяРазница,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.ВременнаяРазница <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.ВременнаяРазница
		|		ИНАЧЕ ДД.Количество * ЕСТЬNULL(Цены.ВременнаяРазница, 0)
		|		КОНЕЦ))                                              КАК ВременнаяРазница,
		|	СУММА((ВЫБОР
		|		КОГДА (ДД.СтоимостьРегл + ДД.ДопРасходыРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница) <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) - ЕСТЬNULL(Цены.ПостояннаяРазница, 0) - ЕСТЬNULL(Цены.ВременнаяРазница, 0))
		|		КОНЕЦ))                                              КАК СтоимостьНУ,
		//-- Локализация

		//-- НЕ УТ
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0))
		|		КОНЕЦ))                                              КАК СтоимостьУпр,
		|	СУММА((ВЫБОР
		|			КОГДА ДД.СтоимостьНДД <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|				ТОГДА ДД.СтоимостьНДД
		|			ИНАЧЕ ДД.Количество *
		|				ЕСТЬNULL(Цены.СтоимостьНДД, 0)
		|			КОНЕЦ))                     КАК СтоимостьНДД,
		|	СУММА(ДД.Количество)                КАК Количество,
		|	СУММА(&Объем * ДД.Количество)       КАК Объем,
		|	СУММА(&Вес * ДД.Количество)         КАК Вес
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО СН.Ссылка = Аналитика.Номенклатура
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО ДД.Регистратор = МаршрутныйЛист.Ссылка
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Продукция
		|		ПО МаршрутныйЛист.Распоряжение = Продукция.Ссылка
		|			И МаршрутныйЛист.КодСтроки = Продукция.КодСтроки
		
		|	ГДЕ
		|		(&РасчетСебестоимостиВыполнен ИЛИ НЕ &РасшифровкаРаспределения)
		|		И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ДД.Организация В(&МассивОрганизаций)
		|		И ДД.КорОрганизация В (ДД.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
		// Берем расходное движение, т.к. оно единственное в маршрутном листе и равнозначно приходу в НЗП.
		|		И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	Аналитика.СтатьяКалькуляции,
		|	Аналитика.Номенклатура,
		|	Продукция.Номенклатура.ГруппаАналитическогоУчета,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ВыпускПродукции),
		|		ТИП(Документ.ЭтапПроизводства2_2),
		|		ТИП(Документ.ПроизводствоБезЗаказа)),
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ОтчетПереработчика)),
		|	ДД.КорПартия,
		|	ДД.КодСтроки,
		|	МаршрутныйЛист.Этап,
		|	Продукция.Назначение.НаправлениеДеятельности";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА

	// материалы версии 2.1 собственное производство без заказа
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                      КАК Организация,
		|	Аналитика.МестоХранения             КАК Подразделение,
		|	Аналитика.СтатьяКалькуляции         КАК СтатьяКалькуляции,
		|	Аналитика.Номенклатура              КАК Материал,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ                               КАК ГруппаПродукции,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ВыпускПродукции),
		//++ НЕ УТКА
		|		ТИП(Документ.ЭтапПроизводства2_2),
		//-- НЕ УТКА
		|		ТИП(Документ.ПроизводствоБезЗаказа)) КАК ЭтоПолуфабрикатСобственный,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ОтчетПереработчика)) КАК ЭтоПолуфабрикатПереработчика,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.Регистратор                      КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                        КАК КодСтрокиПродукция,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	СУММА(
		|	(ВЫБОР
		|		КОГДА ДД.Стоимость + ДД.ДопРасходы <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.Стоимость + ДД.ДопРасходы
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0))
		|	КОНЕЦ))                             КАК Стоимость,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0))
		|		КОНЕЦ))                                              КАК СтоимостьБезНДС,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0))
		|		КОНЕЦ))                                              КАК СтоимостьРегл,
		|
		//++ НЕ УТ

		//++ Локализация
		|	СУММА((ВЫБОР
		|		КОГДА ДД.ПостояннаяРазница <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.ПостояннаяРазница
		|		ИНАЧЕ ДД.Количество * ЕСТЬNULL(Цены.ПостояннаяРазница, 0)
		|		КОНЕЦ))                                              КАК ПостояннаяРазница,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.ВременнаяРазница <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.ВременнаяРазница
		|		ИНАЧЕ ДД.Количество * ЕСТЬNULL(Цены.ВременнаяРазница, 0)
		|		КОНЕЦ))                                              КАК ВременнаяРазница,
		|	СУММА((ВЫБОР
		|		КОГДА (ДД.СтоимостьРегл + ДД.ДопРасходыРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница) <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) - ЕСТЬNULL(Цены.ПостояннаяРазница, 0) - ЕСТЬNULL(Цены.ВременнаяРазница, 0))
		|		КОНЕЦ))                                              КАК СтоимостьНУ,
		//-- Локализация

		//-- НЕ УТ
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0))
		|		КОНЕЦ))                                              КАК СтоимостьУпр,
		|	СУММА((ВЫБОР
		|			КОГДА ДД.СтоимостьНДД <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|				ТОГДА ДД.СтоимостьНДД
		|			ИНАЧЕ ДД.Количество *
		|				ЕСТЬNULL(Цены.СтоимостьНДД, 0)
		|		КОНЕЦ))                         КАК СтоимостьНДД,
		|	СУММА(ДД.Количество)                КАК Количество,
		|	СУММА(&Объем * ДД.Количество)       КАК Объем,
		|	СУММА(&Вес * ДД.Количество)         КАК Вес
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = Аналитика.Номенклатура)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО ДД.Регистратор = Продукция.Ссылка
		|			И ДД.КодСтроки = Продукция.КодСтроки
		
		|ГДЕ
		|	(&РасчетСебестоимостиВыполнен ИЛИ НЕ &РасшифровкаРаспределения)
		|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Продукция.Ссылка.ВыпускПоРаспоряжениям
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	Аналитика.СтатьяКалькуляции,
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ВыпускПродукции),
		//++ НЕ УТКА
		|		ТИП(Документ.ЭтапПроизводства2_2),
		//-- НЕ УТКА
		|		ТИП(Документ.ПроизводствоБезЗаказа)),
		|	ТИПЗНАЧЕНИЯ(ДД.Партия) В (
		|		ТИП(Документ.ОтчетПереработчика)),
		|	Аналитика.Номенклатура,
		|	ДД.КорАналитикаФинансовогоУчета,
		|	ДД.Регистратор,
		|	ДД.КодСтроки,
		|	Продукция.Назначение.НаправлениеДеятельности,
		|	Продукция.Номенклатура.ГруппаАналитическогоУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными предварительными материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросПредварительнымиМатериальнымЗатратам(ТекстыЗапросов) Экспорт
	
	// Производство 2.1, по заказам, без заказов, распределения.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                      КАК Организация,
		|	ДД.Подразделение                    КАК Подразделение,
		|	ДД.СтатьяКалькуляции                КАК СтатьяКалькуляции,
		|	ДД.Номенклатура                     КАК Материал,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции ТОГДА
		|			ВЫБОР
		//++ НЕ УТКА
		|				КОГДА НЕ ПродукцияЗаказа.Номенклатура.ГруппаАналитическогоУчета ЕСТЬ NULL
		|					ТОГДА ПродукцияЗаказа.Номенклатура.ГруппаАналитическогоУчета
		//-- НЕ УТКА
		|				КОГДА НЕ ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета ЕСТЬ NULL
		|					ТОГДА ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета
		|				ИНАЧЕ
		|					ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ                               КАК ГруппаПродукции,
		|	ЛОЖЬ                                КАК ЭтоПолуфабрикатСобственный,
		|	ЛОЖЬ                                КАК ЭтоПолуфабрикатПереработчика,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство              КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция               КАК КодСтрокиПродукция,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА НЕ ПродукцияЗаказа.Назначение.НаправлениеДеятельности ЕСТЬ NULL
		|			ТОГДА ПродукцияЗаказа.Назначение.НаправлениеДеятельности
		//-- НЕ УТКА
		|		КОГДА НЕ ПродукцияВыпуска.Назначение.НаправлениеДеятельности ЕСТЬ NULL
		|			ТОГДА ПродукцияВыпуска.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ                               КАК НаправлениеДеятельности,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0))) КАК Стоимость,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0))) КАК СтоимостьБезНДС,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0))) КАК СтоимостьРегл,
		|
		//++ НЕ УТ

		//++ Локализация
		|	СУММА(ДД.Количество *
		|		ЕСТЬNULL(Цены.ПостояннаяРазница, 0)) КАК ПостояннаяРазница,
		|	СУММА(ДД.Количество *
		|		ЕСТЬNULL(Цены.ВременнаяРазница, 0)) КАК ВременнаяРазница,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) - ЕСТЬNULL(Цены.ПостояннаяРазница, 0) - ЕСТЬNULL(Цены.ВременнаяРазница, 0))) КАК СтоимостьНУ,
		//-- Локализация

		//-- НЕ УТ
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0))) КАК СтоимостьУпр,
		|	0 КАК СтоимостьНДД,
		|	СУММА(ДД.Количество)                КАК Количество,
		|	СУММА(&Объем * ДД.Количество)       КАК Объем,
		|	СУММА(&Вес * ДД.Количество)         КАК Вес
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСредняяСтоимость КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|			И Цены.Номенклатура = ДД.Номенклатура
		|			И Цены.Характеристика = ДД.Характеристика
		|			И Цены.Склад = ДД.Подразделение
		//++ НЕ УТКА
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ПродукцияЗаказа
		|		ПО ДД.ЗаказНаПроизводство = ПродукцияЗаказа.Ссылка
		|			И ДД.КодСтрокиПродукция = ПродукцияЗаказа.КодСтроки
		//-- НЕ УТКА
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ПродукцияВыпуска
		|		ПО ДД.ЗаказНаПроизводство = ПродукцияВыпуска.Ссылка
		|			И ДД.КодСтрокиПродукция = ПродукцияВыпуска.КодСтроки
		|
		|ГДЕ
		|	(&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен)
		|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И (ДД.Регистратор Ссылка Документ.РаспределениеПроизводственныхЗатрат
		|		ИЛИ ДД.Регистратор Ссылка Документ.СписаниеЗатратНаВыпуск
		//++ НЕ УТКА
		|		ИЛИ ДД.Регистратор Ссылка Документ.МаршрутныйЛистПроизводства
		//-- НЕ УТКА
		|		)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Номенклатура,
		//++ НЕ УТКА
		|	ПродукцияЗаказа.Номенклатура.ГруппаАналитическогоУчета,
		|	ПродукцияЗаказа.Назначение.НаправлениеДеятельности,
		//-- НЕ УТКА
		|	ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета,
		|	ПродукцияВыпуска.Назначение.НаправлениеДеятельности,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными предварительными материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросБазамиРаспределенияДолейПоЭтапам(ТекстыЗапросов) Экспорт
	
	// Производство 2.1, по заказам, без заказов, распределения.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Дата,
		|	Данные.СтатьяКалькуляции,
		|	Продукция.Ссылка.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка),
		|	Данные.ДокументВыпуска,
		|	Данные.КодСтроки,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			И Продукция.Номенклатура.ГруппаАналитическогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|				ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоПродукции),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Данные.ДоляСтоимости	КАК Стоимость,
		|	Данные.ДоляСтоимости	КАК СтоимостьБезНДС,
		|	Данные.ДоляСтоимости	КАК СтоимостьРегл,
		|	Данные.ДоляСтоимости	КАК СтоимостьУпр,
		//++ Локализация
		|	Данные.ДоляСтоимости	КАК СтоимостьНУ,
		//-- Локализация
		|	ВЫБОР
		|		КОГДА НастройкиПризнанияРасходовНДД.ВариантПризнанияРасходов ЕСТЬ НЕ NULL
		|			ТОГДА Данные.ДоляСтоимости
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьНДД
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ВыпускиБезРаспоряжения КАК Данные
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК ДД
		|		ПО ДД.Регистратор = Данные.Ссылка
		|		И ДД.ПартииУказаныВручную
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО Данные.ДокументВыпуска = Продукция.Ссылка
		|		И Данные.КодСтроки = Продукция.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДД
		|		ПО ДД.Организация = НастройкиПризнанияРасходовНДД.Организация
		|		И &ТаблицаИсточник_ОбъектРаздельногоУчетаНДД = НастройкиПризнанияРасходовНДД.ОбъектРаздельногоУчетаНДД
		|";
		
	РасчетСебестоимостиЛокализация.ПодставитьВЗапросеТипОбъектаРаздельногоУчетаНДД(ТекстЗапроса, "", "Продукция.Ссылка", "Продукция.Назначение");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//++ НЕ УТКА
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Дата,
		|	Данные.СтатьяКалькуляции,
		|	Данные.Этап.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка),
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			И Продукция.Номенклатура.ГруппаАналитическогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|				ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоПродукции),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Данные.ДоляСтоимости	КАК Стоимость,
		|	Данные.ДоляСтоимости	КАК СтоимостьБезНДС,
		|	Данные.ДоляСтоимости	КАК СтоимостьРегл,
		|	Данные.ДоляСтоимости	КАК СтоимостьУпр,
		//++ Локализация
		|	Данные.ДоляСтоимости	КАК СтоимостьНУ,
		//-- Локализация
		|	ВЫБОР
		|		КОГДА НастройкиПризнанияРасходовНДД.ВариантПризнанияРасходов ЕСТЬ НЕ NULL
		|			ТОГДА Данные.ДоляСтоимости
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьНДД
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.Вручную КАК Данные
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК ДД
		|		ПО ДД.Регистратор = Данные.Ссылка
		|		И ДД.ПартииУказаныВручную
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Продукция
		|		ПО Данные.ЗаказНаПроизводство = Продукция.Ссылка
		|		И Данные.КодСтрокиПродукция = Продукция.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДД
		|		ПО ДД.Организация = НастройкиПризнанияРасходовНДД.Организация
		|		И &ТаблицаИсточник_ОбъектРаздельногоУчетаНДД = НастройкиПризнанияРасходовНДД.ОбъектРаздельногоУчетаНДД
		|";
		
	РасчетСебестоимостиЛокализация.ПодставитьВЗапросеТипОбъектаРаздельногоУчетаНДД(ТекстЗапроса, "", "Данные.Этап", "Продукция.Назначение");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа16_РаспределениеДолейПроизводственныхРасходов

// Дополняет массив запросов локализованными материальными затратами в незавершенном производстве.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросМатериальнымиЗатратамиНЗП(ТекстыЗапросов) Экспорт
	
	//++ НЕ УТКА

	// Данные производства 2.1 по заказам.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                                           Организация,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения              КАК Подразделение,
		|	ДД.Регистратор                                           КАК РегистраторРасхода,
		|	ДД.Период                                                КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)     КАК ПартияПроизводства,
		|	ДД.Партия                                                КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                             КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаНоменклатуры                         КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов                                         КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                               КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                        КАК КорРазделУчета,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.Стоимость, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК Стоимость,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьБезНДС,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыРегл, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)))      КАК СтоимостьРегл,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьУпр, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыУпр, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьУпр,
		//++ Локализация
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыРегл, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)
		|		- ЕСТЬNULL(Цены.ПостояннаяРазница, 0)
		|		- ЕСТЬNULL(Цены.ВременнаяРазница, 0)))               КАК СтоимостьНУ,
		//-- Локализация
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьНДД, 0)))                    КАК СтоимостьНДД
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		|
		|ГДЕ
		|	ДД.Партия ССЫЛКА Документ.ЗаказНаПроизводство
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.Партия,
		|	ДД.КодСтроки,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА

	// материалы версии 2.1 собственное производство без заказа
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                                           Организация,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения              КАК Подразделение,
		|	ДД.Регистратор                                           КАК РегистраторРасхода,
		|	ДД.Период                                                КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)     КАК ПартияПроизводства,
		|	ДД.Регистратор                                           КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                             КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаНоменклатуры                         КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов										 КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                               КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                        КАК КорРазделУчета,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.Стоимость, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК Стоимость,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьБезНДС,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыРегл, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)))      КАК СтоимостьРегл,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьУпр, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыУпр, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьУпр,
		//++ Локализация
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыРегл, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)
		|		- ЕСТЬNULL(Цены.ПостояннаяРазница, 0)
		|		- ЕСТЬNULL(Цены.ВременнаяРазница, 0)))               КАК СтоимостьНУ,
		//-- Локализация
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьНДД, 0)))                    КАК СтоимостьНДД
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК ВыпускПродукции
		|		ПО ДД.Регистратор = ВыпускПродукции.Ссылка
		|
		|ГДЕ
		|	НЕ ВыпускПродукции.ВыпускПоРаспоряжениям
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.КодСтроки,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными трудозатратами в незавершенном производстве.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросТрудозатратамиНЗП(ТекстыЗапросов) Экспорт
	
	//++ НЕ УТКА

	// Трудозатраты версии 2.1 собственное производство по заказам.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                                          КАК Организация,
		|	ДД.Подразделение                                        КАК Подразделение,
		|	ДД.Регистратор                                          КАК РегистраторРасхода,
		|	ДД.Период                                               КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство                                  КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция                                   КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаПродукции                           КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасовПродукции                               КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                              КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                       КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК Стоимость,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.СтоимостьРегл) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.СтоимостьРегл)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьРегл,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА НЕ &УправленческийУчетОрганизаций
		|			ТОГДА 0
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьУпр,
		//++ Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьНУ,
		//-- Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА СУММА(ДД.СтоимостьНДД) = 0
		|				ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|			ИНАЧЕ СУММА(ДД.СтоимостьНДД)
		|		КОНЕЦ КАК ЧИСЛО(23,10))                             КАК СтоимостьНДД
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|ГДЕ
		|	ДД.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорВидЗапасовПродукции,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА

	// Трудозатраты версии 2.1 собственное производство без заказа.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                                          КАК Организация,
		|	ДД.Подразделение                                        КАК Подразделение,
		|	ДД.Регистратор                                          КАК РегистраторРасхода,
		|	ДД.Период                                               КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	ДД.ДокументВыпуска                                      КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                            КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаПродукции                           КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасовПродукции                               КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                              КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                       КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК Стоимость,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.СтоимостьРегл) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.СтоимостьРегл)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьРегл,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА НЕ &УправленческийУчетОрганизаций
		|			ТОГДА 0
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьУпр,
		//++ Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьНУ,
		//-- Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА СУММА(ДД.СтоимостьНДД) = 0
		|				ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|			ИНАЧЕ СУММА(ДД.СтоимостьНДД)
		|		КОНЕЦ КАК ЧИСЛО(23,10))                             КАК СтоимостьНДД
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|ГДЕ
		|	ДД.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.ДокументВыпуска,
		|	ДД.КодСтроки,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорВидЗапасовПродукции,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованной выпущенной продукцией.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросВыпущеннойПродукцией(ТекстыЗапросов) Экспорт
	
	// Производство 2.1 без заказов.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	""ТекстВыпущеннаяПродукция_9""                          КАК ЗапросИсточник,
		|	Реквизиты.Ссылка                                        КАК РегистраторРасхода,
		|	Реквизиты.Дата                                          КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	Реквизиты.Подразделение                                 КАК Подразделение,
		|	Реквизиты.Ссылка                                        КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                            КАК КодСтрокиПродукция,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ                                                   КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов                                           КАК КорВидЗапасов,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК КорАналитикаУчетаПартий,
		|	ВЫБОР
		|		КОГДА ДД.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветХранение)
		|		КОГДА Реквизиты.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ                                                   КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(СУММА(
		|		ВЫБОР
		|			КОГДА СписаниеЗатрат.Ссылка ЕСТЬ NULL
		|				ТОГДА ДД.Количество
		|			КОГДА СписаниеЗатрат.ДоляСтоимости = 0
		|				ТОГДА СписаниеЗатрат.Количество
		|			ИНАЧЕ СписаниеЗатрат.ДоляСтоимости
		|		КОНЕЦ) КАК ЧИСЛО(23,10))                            КАК Стоимость,
		|	ВЫРАЗИТЬ(СУММА(
		|		ВЫБОР
		|			КОГДА СписаниеЗатрат.Ссылка ЕСТЬ NULL
		|				ТОГДА ДД.Количество
		|			КОГДА СписаниеЗатрат.ДоляСтоимости = 0
		|				ТОГДА СписаниеЗатрат.Количество
		|			ИНАЧЕ СписаниеЗатрат.ДоляСтоимости
		|		КОНЕЦ) КАК ЧИСЛО(23,10))                            КАК ПолнаяСтоимость,
		|	0                                                       КАК ПлановоеКоличествоПродукции,
		|	Реквизиты.Организация                                   КАК Организация
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК СписаниеЗатрат
		|		ПО ДД.Ссылка = СписаниеЗатрат.Распоряжение
		|		И ДД.КодСтроки = СписаниеЗатрат.КодСтроки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Реквизиты
		|		ПО ДД.Ссылка = Реквизиты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокументаБезНазначения
		|	ПО
		|		АналитикаДокументаБезНазначения.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И АналитикаДокументаБезНазначения.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И АналитикаДокументаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаДокументаБезНазначения.СтатьяКалькуляции = ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
		|		И АналитикаДокументаБезНазначения.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И АналитикаДокументаБезНазначения.Серия = ДД.АналитикаУчетаНоменклатуры.Серия
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделении
		|	ПО
		|		ДД.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И ДД.Характеристика = АналитикаВПодразделении.Характеристика
		|		И ДД.Назначение = АналитикаВПодразделении.Назначение
		|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаВПодразделении.СтатьяКалькуляции
		|		И (Реквизиты.Подразделение = АналитикаВПодразделении.МестоХранения)
		|		И ВЫБОР
		|			КОГДА ДД.СтатусУказанияСерий = 14
		|			ТОГДА ДД.Серия = АналитикаВПодразделении.Серия
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаВПодразделении.Серия
		|		КОНЕЦ
		|		И ДД.СписатьНаРасходы
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделенииБезНазначения
		|	ПО
		|		АналитикаВПодразделенииБезНазначения.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И АналитикаВПодразделенииБезНазначения.Характеристика = АналитикаВПодразделении.Характеристика
		|		И АналитикаВПодразделенииБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаВПодразделенииБезНазначения.СтатьяКалькуляции = АналитикаВПодразделении.СтатьяКалькуляции
		|		И АналитикаВПодразделенииБезНазначения.МестоХранения = АналитикаВПодразделении.МестоХранения
		|		И АналитикаВПодразделенииБезНазначения.Серия = АналитикаВПодразделении.Серия
		|		И ДД.СписатьНаРасходы
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
		|		ПО ДД.Ссылка = Движения.Регистратор
		|		И ДД.КодСтроки = Движения.КодСтроки
		|		И Движения.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|		И НЕ Движения.РасчетПартий
		|		И НЕ Движения.РасчетСебестоимости
		// исключение движений по работам для давальца
		|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Реквизиты.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Реквизиты.Проведен
		|	И Реквизиты.Организация В(&МассивОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	Реквизиты.Ссылка,
		|	Реквизиты.Подразделение,
		|	ДД.КодСтроки,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ,
		|	ДД.ВидЗапасов,
		|	ДД.ПередатьДавальцу,
		|	ДД.ВидЗапасов.ТипЗапасов,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
		|	Реквизиты.Организация
		|";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//++ НЕ УТКА

	// Выпущенная продукция по заказам 2.1
	// При производстве по заказам доля стоимости наследуется из маршрутного листа.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	""ТекстВыпущеннаяПродукция_10""                         КАК ЗапросИсточник,
		|	Реквизиты.Ссылка                                        КАК РегистраторРасхода,
		|	Реквизиты.Дата                                          КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	Реквизиты.Подразделение                                 КАК Подразделение,
		|	ДД.Распоряжение.Распоряжение                            КАК ЗаказНаПроизводство,
		|	ДД.Распоряжение.КодСтроки                               КАК КодСтрокиПродукция,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ                                                   КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов                                           КАК КорВидЗапасов,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК КорАналитикаУчетаПартий,
		|	ВЫБОР
		|		КОГДА ДД.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Реквизиты.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ                                                   КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(СУММА(ВЫБОР
		|			КОГДА МаршрутныйЛист.ДоляСтоимости = 0 ИЛИ МаршрутныйЛист.Количество = 0
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ
		|				ДД.Количество * МаршрутныйЛист.ДоляСтоимости / МаршрутныйЛист.Количество
		|			КОНЕЦ) КАК ЧИСЛО(23,10))                        КАК Стоимость,
		|	ВЫРАЗИТЬ(СУММА(ВЫБОР
		|			КОГДА МаршрутныйЛист.ДоляСтоимости = 0 ИЛИ МаршрутныйЛист.Количество = 0
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ
		|				ДД.Количество * МаршрутныйЛист.ДоляСтоимости / МаршрутныйЛист.Количество
		|			КОНЕЦ) КАК ЧИСЛО(23,10))                        КАК ПолнаяСтоимость,
		|	0,
		|	Реквизиты.Организация                                 КАК Организация
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Реквизиты
		|		ПО ДД.Ссылка = Реквизиты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокументаБезНазначения
		|	ПО
		|		АналитикаДокументаБезНазначения.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И АналитикаДокументаБезНазначения.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И АналитикаДокументаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаДокументаБезНазначения.СтатьяКалькуляции = ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
		|		И АналитикаДокументаБезНазначения.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И АналитикаДокументаБезНазначения.Серия = ДД.АналитикаУчетаНоменклатуры.Серия
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделении
		|	ПО
		|		ДД.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И ДД.Характеристика = АналитикаВПодразделении.Характеристика
		|		И ДД.Назначение = АналитикаВПодразделении.Назначение
		|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаВПодразделении.СтатьяКалькуляции
		|		И ВЫБОР 
		|			КОГДА ДД.СписатьНаРасходы 
		|			ТОГДА (Реквизиты.Подразделение = АналитикаВПодразделении.МестоХранения)
		|			ИНАЧЕ (ДД.Подразделение = АналитикаВПодразделении.МестоХранения)
		|		КОНЕЦ
		|		И ВЫБОР
		|			КОГДА ДД.СтатусУказанияСерий = 14
		|			ТОГДА ДД.Серия = АналитикаВПодразделении.Серия
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаВПодразделении.Серия
		|		КОНЕЦ
		|		И НЕ Реквизиты.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделенииБезНазначения
		|	ПО
		|		АналитикаВПодразделенииБезНазначения.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И АналитикаВПодразделенииБезНазначения.Характеристика = АналитикаВПодразделении.Характеристика
		|		И АналитикаВПодразделенииБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаВПодразделенииБезНазначения.СтатьяКалькуляции = АналитикаВПодразделении.СтатьяКалькуляции
		|		И АналитикаВПодразделенииБезНазначения.МестоХранения = АналитикаВПодразделении.МестоХранения
		|		И АналитикаВПодразделенииБезНазначения.Серия = АналитикаВПодразделении.Серия
		|		И НЕ Реквизиты.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК МаршрутныйЛист
		|		ПО ДД.Распоряжение = МаршрутныйЛист.Ссылка
		|		И ДД.КодСтроки = МаршрутныйЛист.КодСтроки
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
		|		ПО ДД.Ссылка = Движения.Регистратор
		|		И ДД.КодСтроки = Движения.КодСтроки
		|		И Движения.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|		И НЕ Движения.РасчетПартий
		|		И НЕ Движения.РасчетСебестоимости
		// исключение движений по работам для давальца
		|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Реквизиты.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Реквизиты.Проведен
		|	И Реквизиты.Организация В(&МассивОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	Реквизиты.Ссылка,
		|	Реквизиты.Дата,
		|	Реквизиты.Подразделение,
		|	ДД.Распоряжение.Распоряжение,
		|	ДД.Распоряжение.КодСтроки,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ,
		|	ДД.ВидЗапасов,
		|	ДД.ПередатьДавальцу,
		|	ДД.ВидЗапасов.ТипЗапасов,
		|	ЕСТЬNULL(Движения.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
		|	Реквизиты.Организация
		|";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА
	
КонецПроцедуры

Функция ДополнитьЗапросСлужебнымиТаблицами() Экспорт
	
	//++ НЕ УТКА
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ЗаказНаПроизводство                         КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция                          КАК КодСтрокиПродукция,
		|	СУММА(ДД.Стоимость)                            КАК Стоимость
		|ПОМЕСТИТЬ втИтогиДолейПоЗаказам21
		|ИЗ
		|	ВыпущеннаяПродукция КАК ДД
		|ГДЕ
		|	ДД.КодСтрокиПродукция > 0
		|	И ДД.ЗаказНаПроизводство Ссылка Документ.ЗаказНаПроизводство
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция
		|ПОМЕСТИТЬ ПолучателиПостатейныхРасходов21
		|ИЗ
		|	ЗатратыПродукции КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Реквизиты
		|		ПО ДД.ЗаказНаПроизводство = Реквизиты.Ссылка
		|ГДЕ
		|	ДД.ПравилоОтнесенияНаВыпуск В (ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоМатериальнымЗатратам), ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоМатериальнымИТрудозатратам))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство,
		|	КодСтрокиПродукция";
	Возврат ТекстЗапроса
		+ ОбщегоНазначения.РазделительПакетаЗапросов();
	//-- НЕ УТКА

КонецФункции

// Дополняет тексты запросов.
// 
// Параметры:
// 	ТекстыЗапросов - Массив -
//
Процедура ДополнитьЗапросОстаткамиБазРаспределения(ТекстыЗапросов) Экспорт
	
	//++ НЕ УТКА
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДвижениеМатериалов.Организация,
		|	ДвижениеМатериалов.Подразделение,
		|	НЕОПРЕДЕЛЕНО,
		|	ДвижениеМатериалов.Партия,
		|	ДвижениеМатериалов.КодСтроки,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоМатериальнымЗатратам),
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(ДвижениеМатериалов.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(ДвижениеМатериалов.СтоимостьБезНДС)
		|	КОНЕЦ КАК ЧИСЛО(23,10)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(ДвижениеМатериалов.СтоимостьРегл)
		|	КОНЕЦ КАК ЧИСЛО(23,10)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(ДвижениеМатериалов.СтоимостьУпр)
		|	КОНЕЦ КАК ЧИСЛО(23,10)),
		//++ Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|			ТОГДА 0
		|		ИНАЧЕ СУММА(ДвижениеМатериалов.СтоимостьНУ)
		|	КОНЕЦ КАК ЧИСЛО(23,10)),
		//-- Локализация
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА СУММА(ДвижениеМатериалов.Количество) = 0
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ДвижениеМатериалов.СтоимостьНДД)
		|		КОНЕЦ КАК ЧИСЛО(23,10))
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДД.Организация КАК Организация,
		|		Аналитики.МестоХранения КАК Подразделение,
		|		ДД.Партия КАК Партия,
		|		ДД.КодСтроки КАК КодСтроки,
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ КАК Количество,
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК Стоимость,
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК СтоимостьБезНДС,
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)) КАК СтоимостьРегл,
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК СтоимостьУпр,
		//++ Локализация
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0) - ЕСТЬNULL(Цены.ПостояннаяРазница, 0) - ЕСТЬNULL(Цены.ВременнаяРазница, 0)) КАК СтоимостьНУ,
		//-- Локализация
		|		ВЫБОР
		|			КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьНДД, 0)) КАК СтоимостьНДД
		|	ИЗ
		|		ПолучателиПостатейныхРасходов21 КАК Партии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК ДД
		|			ПО Партии.ЗаказНаПроизводство = ДД.Партия
		|				И Партии.КодСтрокиПродукция = ДД.КодСтроки
		|				И (ДД.Период < &НачалоПериода)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитики
		|			ПО (ДД.АналитикаУчетаНоменклатуры = Аналитики.КлючАналитики)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|			ПО (Цены.Организация = ДД.Организация)
		|				И (Цены.Партия = ДД.Партия)
		|				И (Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий)
		|				И (Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета)
		|				И (Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС)
		|				И (Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры)
		|				И (Цены.ВидЗапасов = ДД.ВидЗапасов)
		|				И (Цены.РазделУчета = ДД.РазделУчета)
		|	ГДЕ
		|		НЕ ДД.Количество = 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		Аналитики.МестоХранения,
		|		ДД.Партия,
		|		ДД.КодСтроки,
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК Стоимость,
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК СтоимостьБезНДС,
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)) КАК СтоимостьРегл,
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)) КАК СтоимостьУпр,
		//++ Локализация
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0) + ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0) - ЕСТЬNULL(Цены.ПостояннаяРазница, 0) - ЕСТЬNULL(Цены.ВременнаяРазница, 0)) КАК СтоимостьНУ,
		//-- Локализация
		|		ВЫБОР
		|			КОГДА ДД.СлужебноеВидДвиженияПриход
		|				ТОГДА ДД.Количество
		|			ИНАЧЕ -ДД.Количество
		|		КОНЕЦ * (ЕСТЬNULL(Цены.СтоимостьНДД, 0)) КАК СтоимостьНДД
		|	ИЗ
		|		ПолучателиПостатейныхРасходов21 КАК Партии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|			ПО Партии.ЗаказНаПроизводство = ДД.Партия
		|				И Партии.КодСтрокиПродукция = ДД.КодСтроки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитики
		|			ПО (ДД.АналитикаУчетаНоменклатуры = Аналитики.КлючАналитики)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|			ПО (Цены.Организация = ДД.Организация)
		|				И (Цены.Партия = ДД.Партия)
		|				И (Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий)
		|				И (Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета)
		|				И (Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС)
		|				И (Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры)
		|				И (Цены.ВидЗапасов = ДД.ВидЗапасов)
		|				И (Цены.РазделУчета = ДД.РазделУчета)
		|	ГДЕ
		|		НЕ ДД.Количество = 0) КАК ДвижениеМатериалов
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвижениеМатериалов.Организация,
		|	ДвижениеМатериалов.Подразделение,
		|	ДвижениеМатериалов.Партия,
		|	ДвижениеМатериалов.КодСтроки";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА
	
КонецПроцедуры

// Дополняет тексты запросов.
// 
// Параметры:
// 	ТекстыЗапросов - Массив -
//
Процедура ДополнитьЗапросРасходовНЗПЛокализованнымиДанными(ТекстыЗапросов) Экспорт
	
	//++ НЕ УТКА
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	""ТекстРасходыПрочихРасходовНЗППоПродукции""        КАК ЗапросИсточник,
		|	10                                                  КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение) КАК ТипЗаписи,
		|	ИСТИНА                                              КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
		|	Продукция.ДатаПроизводства                          КАК Период,
		|	Продукция.РегистраторРасхода                        КАК Регистратор,
		|	ДД.Организация                                      КАК Организация,
		|	ДД.Подразделение                                    КАК Подразделение,
		|	ДД.ПартияПроизводства                               КАК ПартияПроизводства,
		|	ДД.АналитикаПартииВыпуска                           КАК АналитикаПартииВыпуска,
		|	ДД.ЗаказНаПроизводство                              КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция                               КАК КодСтрокиПродукция,
		|	ДД.НаправлениеДеятельности                          КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО                                        КАК Спецификация,
		|	ДД.Этап                                             КАК Этап,
		|	ДД.СтатьяКалькуляции                                КАК СтатьяКалькуляции,
		|	ДД.СтатьяРасходов                                   КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов                                КАК АналитикаРасходов,
		|	ДД.ГруппаПродукции                                  КАК ГруппаПродукции,
		|	ДД.ПравилоОтнесенияНаВыпуск                         КАК ПравилоОтнесенияНаВыпуск,
		|	ЕСТЬNULL(РаботыДляДавальца.КорАналитикаУчетаНоменклатуры, Продукция.КорАналитикаУчетаНоменклатуры) КАК АналитикаУчетаПродукции,
		|	Продукция.КорАналитикаУчетаПартий                   КАК КорАналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности							КАК КорНаправлениеДеятельности,
		|	0                                                   КАК Знаменатель,
		|	0                                                   КАК КоличествоПродукции,
		|	ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10)) КАК ДоляСтоимости,
		|	0                                                   КАК Стоимость,
		|	0                                                   КАК СтоимостьБезНДС,
		|	0                                                   КАК СтоимостьРегл,
		|	0                                                   КАК ПостояннаяРазница,
		|	0                                                   КАК ВременнаяРазница,
		|	0													КАК СтоимостьНДД,
		|	ВЫБОР 
		|		КОГДА ДД.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпуск,
		|	ВЫБОР 
		|		КОГДА ДД.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпускБезНДС,
		|	ВЫБОР 
		|		КОГДА ДД.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпускРегл,
		|	ВЫБОР 
		|		КОГДА ДД.СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпускНУ,
		|	ВЫБОР 
		|		КОГДА &УправленческийУчетОрганизаций
		|			И ДД.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпускУпр,
		|	ВЫБОР 
		|		КОГДА ДД.ВариантРаспределенияРасходовНДД = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ТОГДА ВЫРАЗИТЬ(Продукция.Стоимость * ДД.ДоляСтоимостиОстаток / втИтогиДолейПоЗаказам21.Стоимость КАК ЧИСЛО(23,10))
		|		ИНАЧЕ 0
		|	КОНЕЦ												КАК ПоказательОтнесенияНаВыпускНДД,
		|	0                                                   КАК ПоказательОтнесенияНаПартию,
		|	0                                                   КАК ПоказательОтнесенияНаПартиюБезНДС,
		|	0                                                   КАК ПоказательОтнесенияНаПартиюРегл,
		|	0                                                   КАК ПоказательОтнесенияНаПартиюНУ,
		|	0                                                   КАК ПоказательОтнесенияНаПартиюУпр,
		|	0                                                   КАК ПоказательОтнесенияНаПартиюНДД,
		|	ИСТИНА                                              КАК Первичное,
		|	НЕОПРЕДЕЛЕНО                                        КАК ХозяйственнаяОперация,
		|	ЕСТЬNULL(РаботыДляДавальца.КорРазделУчета, Продукция.КорРазделУчета) КАК РазделУчета,
		|	ЕСТЬNULL(РаботыДляДавальца.КорВидЗапасов, Продукция.КорВидЗапасов)   КАК ВидЗапасов,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ
		|			ЕСТЬNULL(
		|				ВЫРАЗИТЬ(ДД.АналитикаПартииВыпуска КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Назначение.ВидДеятельностиНДС, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ 												КАК ВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО                                        КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО                                        КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                                        КАК ДокументИсточник,
		|	ДД.ДокументВыпуска                                  КАК ДокументВыпуска,
		|	НЕОПРЕДЕЛЕНО                                        КАК Продукция,
		|	НЕОПРЕДЕЛЕНО                                        КАК ХарактеристикаПродукции
		|ИЗ
		|	ПартииДляОтнесенияРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпущеннаяПродукция КАК Продукция
		|		ПО Продукция.Организация = ДД.Организация
		|		И Продукция.ПартияПроизводства = ДД.ПартияПроизводства
		|		И Продукция.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Продукция.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИтогиДолейПоЗаказам21 КАК втИтогиДолейПоЗаказам21
		|		ПО втИтогиДолейПоЗаказам21.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И втИтогиДолейПоЗаказам21.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|	ЛЕВОЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
		|		ПО Продукция.РегистраторРасхода = РаботыДляДавальца.Регистратор
		|		И Продукция.КорАналитикаУчетаНоменклатуры = РаботыДляДавальца.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.ПравилоОтнесенияНаВыпуск = ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПустаяСсылка)";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//-- НЕ УТКА
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа_ЗаполнениеПартийВРегистреСебестоимостьТоваров

Функция ТекстЗапросаДляПартийТоваров() Экспорт
	
	ТекстЗапроса = ""
		//++ НЕ УТКА
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРаспределенияНаЗаказыНаПроизводство()
		//-- НЕ УТКА
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеПриходыПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеРасходыПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеСписанияЗатратНаВыпуск() // использует Выпуски
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеРасходыМатериаловВПроизводстве()
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

//++ НЕ УТКА

Функция ТекстРаспределенияНаЗаказыНаПроизводство()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстРаспределенияНаЗаказыНаПроизводство"" 	      	  КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Распределение)	  КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК ЭтоСторно,
		|	90 										  				  КАК Приоритет,
		|	ДД.Количество							  				  КАК Знаменатель,
		|	ЛОЖЬ 													  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ЗаказПроизводство.ПроизводствоПоЗаказу ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КОНЕЦ) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			И НЕ ДД.Продукция.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА ДД.Продукция.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ													 КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО											 КАК ВидДеятельностиНДС,
		|
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	0 КАК РезервПодОбесценениеРегл,
		|	0 КАК РезервПодОбесценениеУпр,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетнаяХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	ДД.Продукция.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчетаДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ОперацияУчетаСебестоимости,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетныйИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаСчетовУчета,
		|	ЛОЖЬ КАК Сторно,
		|	ЛОЖЬ КАК ВыполненРасчетПартий,
		|	НЕОПРЕДЕЛЕНО КАК РежимЗакрытияМесяца
		|ИЗ
		|	ВТКэшРасчетныеОборотыПартииНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК ЗаказПроизводство
		|		ПО ЗаказПроизводство.Ссылка = ДД.ЗаказНаПроизводство
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО КлючиАналитики.Номенклатура = Аналитика.Номенклатура
		|			И КлючиАналитики.Характеристика = Аналитика.Характеристика
		|			И КлючиАналитики.Серия = Аналитика.Серия
		|			И КлючиАналитики.МестоХранения = Аналитика.МестоХранения
		|			И КлючиАналитики.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиАналитики.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	// До перехода на ПУ 2.2 в регистре ПартииНезавершенногоПроизводства был заполнен вид запасов,
		|	// а затраты в регистре СебестоимостьТоваров были отражены на разделе учета ПроизводственныеЗатраты.
		|	// Такие затраты нужно перенести в раздел учета НезавершенноеПроизводство.
		|	ИЛИ НЕ ДД.СлужебноеВидДвиженияПриход
		|		И ДД.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|";
КонецФункции

//-- НЕ УТКА

Функция ТекстТекущиеПриходыПартийНЗП()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеПриходыПартийНЗП"" 	      				  КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПеремещениеАвто)  КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК ЭтоСторно,
		|	95 										  				  КАК Приоритет,
		|	ДД.Количество							  				  КАК Знаменатель,
		|	ЛОЖЬ 													  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство КАК Партия,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	0 КАК РезервПодОбесценениеРегл,
		|	0 КАК РезервПодОбесценениеУпр,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетнаяХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчетаДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ОперацияУчетаСебестоимости,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетныйИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаСчетовУчета,
		|	ЛОЖЬ КАК Сторно,
		|	ЛОЖЬ КАК ВыполненРасчетПартий,
		|	НЕОПРЕДЕЛЕНО КАК РежимЗакрытияМесяца
		|ИЗ
		|	ВТКэшРасчетныеОборотыПартииНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО КлючиАналитики.Номенклатура = Аналитика.Номенклатура
		|			И КлючиАналитики.Характеристика = Аналитика.Характеристика
		|			И КлючиАналитики.Серия = Аналитика.Серия
		|			И КлючиАналитики.МестоХранения = Аналитика.МестоХранения
		|			И КлючиАналитики.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиАналитики.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	И ДД.РасчетПартий
		|	// До перехода на ПУ 2.2 в регистре ПартииНезавершенногоПроизводства был заполнен вид запасов,
		|	// а затраты в регистре СебестоимостьТоваров были отражены на разделе учета ПроизводственныеЗатраты.
		|	// Такие затраты нужно перенести в раздел учета НезавершенноеПроизводство.
		|	ИЛИ НЕ ДД.СлужебноеВидДвиженияПриход
		|		И ДД.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|";
КонецФункции

Функция ТекстТекущиеРасходыПартийНЗП()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеРасходыПартийНЗП"" 	      				  КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК ЭтоСторно,
		|	96 										  				  КАК Приоритет,
		|	МАКСИМУМ(ДД.КоличествоПродукции)		  				  КАК Знаменатель,
		|	ЛОЖЬ 													  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство КАК Партия,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	0 КАК РезервПодОбесценениеРегл,
		|	0 КАК РезервПодОбесценениеУпр,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетнаяХозяйственнаяОперация,
		|	(ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА ДД.АналитикаУчетаПродукции
		|		ИНАЧЕ АналитикаПродукцииБезНазначения.КлючАналитики КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорРазделУчета,
		|	(ВЫБОР КОГДА ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КОНЕЦ) КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПоступлениеОтПереработчика ТОГДА Поступление.Подразделение
		|		ИНАЧЕ Аналитика.МестоХранения КОНЕЦ) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчетаДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ОперацияУчетаСебестоимости,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетныйИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаСчетовУчета,
		|	ЛОЖЬ КАК Сторно,
		|	ЛОЖЬ КАК ВыполненРасчетПартий,
		|	НЕОПРЕДЕЛЕНО КАК РежимЗакрытияМесяца
		|ИЗ
		|	ВТКэшРасчетныеОборотыПартииНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПродукции
		|		ПО АналитикаПродукции.Ссылка = ДД.АналитикаУчетаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПродукцииБезНазначения
		|		ПО АналитикаПродукции.Номенклатура = АналитикаПродукцииБезНазначения.Номенклатура
		|		И АналитикаПродукции.Характеристика = АналитикаПродукцииБезНазначения.Характеристика
		|		И АналитикаПродукции.Серия = АналитикаПродукцииБезНазначения.Серия
		|		И АналитикаПродукции.МестоХранения = АналитикаПродукцииБезНазначения.МестоХранения
		|		И АналитикаПродукции.СтатьяКалькуляции = АналитикаПродукцииБезНазначения.СтатьяКалькуляции
		|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПродукцииБезНазначения.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО КлючиАналитики.Номенклатура = Аналитика.Номенклатура
		|			И КлючиАналитики.Характеристика = Аналитика.Характеристика
		|			И КлючиАналитики.Серия = Аналитика.Серия
		|			И КлючиАналитики.МестоХранения = Аналитика.МестоХранения
		|			И КлючиАналитики.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиАналитики.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеОтПереработчика КАК Поступление
		|		ПО Поступление.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПоПартнерамИзОтчетовДавальцам КАК АналитикаДавальца
		|		ПО АналитикаДавальца.Регистратор = ДД.Регистратор
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И ДД.РасчетПартий
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ,
		|	Аналитика.Номенклатура,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	КлючиАналитики.КлючАналитики,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.АналитикаУчетаПродукции,
		|	АналитикаПродукцииБезНазначения.КлючАналитики,
		|	ДД.КорРазделУчета,
		|	ВЫБОР КОГДА ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПоступлениеОтПереработчика ТОГДА Поступление.Подразделение
		|		ИНАЧЕ Аналитика.МестоХранения КОНЕЦ,
		|	ДД.КодСтрокиПродукция,
		|	ДД.СтатьяКалькуляции
		|";
КонецФункции

Функция ТекстТекущиеСписанияЗатратНаВыпуск() // использует Выпуски
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеСписанияЗатратНаВыпуск"" 	      			  КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияНаВыпуск)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК ЭтоСторно,
		|	95 										  				  КАК Приоритет,
		|	МАКСИМУМ(Выпуски.Знаменатель)			  				  КАК Знаменатель,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО											  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	(ВЫБОР
		// Для записей СторноСписанияНаВыпуск партии должны быть подобраны в первую очередь.
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	ДД.ЗаказНаПроизводство									  КАК Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|		ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ПродукцияДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	Выпуски.ГруппаАналитическогоУчета КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	СУММА(ДД.Количество),
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	0 КАК РезервПодОбесценениеРегл,
		|	0 КАК РезервПодОбесценениеУпр,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетнаяХозяйственнаяОперация,
		|	Выпуски.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ВидЗапасов.ТипЗапасов = Значение(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК КорРазделУчета,
		|	Выпуски.ВидЗапасов КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчетаДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ОперацияУчетаСебестоимости,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетныйИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаСчетовУчета,
		|	ЛОЖЬ КАК Сторно,
		|	ЛОЖЬ КАК ВыполненРасчетПартий,
		|	НЕОПРЕДЕЛЕНО КАК РежимЗакрытияМесяца
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
		|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
		|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
		|		И Аналитика.Серия = АналитикаБезНазначения.Серия
		|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
		|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
		|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выпуски КАК Выпуски
		|		ПО Выпуски.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Выпуски.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияНаВыпуск)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ),
		|
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ),
		|	Аналитика.Номенклатура,
		|	ДД.Период,
		|	ДД.ЗаказНаПроизводство,
		|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|		ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Выпуски.ПродукцияДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	ДД.Организация,
		|	Выпуски.ГруппаАналитическогоУчета,
		|	Выпуски.АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ВидЗапасов.ТипЗапасов = Значение(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	Выпуски.ВидЗапасов,
		|	Аналитика.МестоХранения,
		|	ДД.СтатьяКалькуляции,
		|	ДД.КодСтрокиПродукция
		|";
КонецФункции

Функция ТекстТекущиеРасходыМатериаловВПроизводстве()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеРасходыМатериаловВПроизводстве"" 	      	КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Распределение) 	КАК ТипЗаписи,
		|	ЛОЖЬ													КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ													КАК ЭтоСторно,
		|	95 										  				КАК Приоритет,
		|	0 										  				КАК Знаменатель,
		|	ЛОЖЬ 													КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				КАК НалогообложениеПартии,
		|
		|	ДД.Дата КАК Период,
		|	ДД.Дата КАК ПериодПоступления,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	Строки.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	0 КАК РезервПодОбесценениеРегл,
		|	0 КАК РезервПодОбесценениеУпр,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетнаяХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	ДД.Подразделение,
		|	Строки.АналитикаРасходов КАК АналитикаРасходов,
		|	Строки.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	Строки.СтатьяРасходов КАК СтатьяАктивовПассивов,
		|	Строки.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	Строки.ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчетаДокумента,
		|	НЕОПРЕДЕЛЕНО КАК ОперацияУчетаСебестоимости,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ПриоритетныйИсточник,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО) КАК КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеТоваровПоТребованию) КАК НастройкаХозяйственнойОперации,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаСчетовУчета,
		|	ЛОЖЬ КАК Сторно,
		|	ЛОЖЬ КАК ВыполненРасчетПартий,
		|	НЕОПРЕДЕЛЕНО КАК РежимЗакрытияМесяца
		|ИЗ
		|	Документ.РаспределениеПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И НЕ ДД.НовоеПроизводство
		|	И ДД.Проведен
		|";
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапов_Контекстные

// Используется для всех вызовов заполнения расчетной партии.
//
Процедура ЗаполнитьРасчетнуюПартию(ПараметрыРасчета, Контекст, РасчетнаяПартия, Расход, Приход, ПартияЗаполнена) Экспорт
	
	Если Контекст = "РаспределениеМатериаловИРаботПоБазе" Тогда
		// Этап 6
		ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями" Тогда
		// Этап 7
		ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловНЗП(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеПроцедурыОписанияДанныхМеханизма

// Возвращает перечень объектов метаданных, на основании данных которых выполняется расчет партий.
// В перечень не включаются объекты, которые являются одновременно и исходящими данными механизмов расчета партий и себестоимости.
//
// Параметры:
//	ВходящиеДанные - Соответствие - уже инициализированное хранилище для описания входящих данных
//	ТолькоТребующиеПерерасчета - Булево - если установлен, то будет возвращен перечень только тех данных,
//		изменение которых влечет за собой необходимость перерасчета партий и себестоимости
//		При изменении этих данных должна создаваться запись в регистре сведений ЗаданияКРасчетуСебестоимости.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ВходящиеДанныеМеханизма(ВходящиеДанные, ТолькоТребующиеПерерасчета) Экспорт
	
	Если ТолькоТребующиеПерерасчета Тогда
		Значение = Истина; // чтобы можно было проверить вхождение объекта метаданных в это соответствие
	Иначе
		Значение = Неопределено;
	КонецЕсли;
	
	Если НЕ ТолькоТребующиеПерерасчета Тогда 
		ВходящиеДанные.Вставить(Метаданные.Документы.ПередачаМатериаловВПроизводство, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВозвратМатериаловИзПроизводства, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВыпускПродукции, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ИзделияИЗатратыНЗП, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.СписаниеЗатратНаВыпуск, Значение);
		//++ НЕ УТКА
		ВходящиеДанные.Вставить(Метаданные.Документы.ЗаказНаПроизводство, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.МаршрутныйЛистПроизводства, Значение);
		//-- НЕ УТКА
	КонецЕсли;
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РаспоряженияНаСписаниеПоНормативам, Значение);
	//++ НЕ УТКА
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РаспоряженияНаВыпускПродукции, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ЭтапыПроизводства, Значение);
	//-- НЕ УТКА
	
КонецПроцедуры

// Возвращает перечень регистров, обслуживаемых механизмом расчета партий.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ИсходящиеДанныеМеханизма(ИсходящиеДанные) Экспорт
	
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Истина);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ВыпускПродукции, Истина);
	
КонецПроцедуры

// Возвращает перечень регистров, рассчитываемых механизмом партионного учета версии 2.2, и используемых при расчете себестоимости.
//
Процедура ИспользуемыеКэшиРегистровПартионногоУчета(ВходящиеДанные) Экспорт
	
	// Здесь перечислены регистры, которые (по И)
	// - рассчитываются механизмом партионного учета версии 2.2
	// - не рассчитываются механизмом расчета себестоимости
	// - являются входящими данными для механизма расчета себестоимости (используются кэши данных регистров)
	//
	// В случае, если выполняется отдельный запуск расчета себестоимости (не из партионного учета версии 2.2),
	// эти регистры не будут инициализированы - к их кэшам обращаться нельзя.
	//
	// Для этого делается следующее:
	// - перед началом расчета себестоимости "принудительно" инициализируются эти регистры (для того, чтобы сформировались их расчетные кэши)
	// - перед записью движений эти регистры удаляются из параметров расчета (записывать их не надо - движения по ним не формировались)
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Тестирование

// Дополняет список этапов расчета.
// 
// Параметры:
// 	СписокЭтапов - СписокЗначений - 
//
Процедура ДополнитьЭтапыСРаспределениемПартий(СписокЭтапов) Экспорт
	
	СписокЭтапов.Добавить("РаспределениеМатериаловИРаботПоБазе");
	СписокЭтапов.Добавить("РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	
КонецПроцедуры

Процедура ТекстЗапросаДляРасчетаЭтапа(ИмяЭтапа, ПараметрыРасчета, ТекстЗапроса) Экспорт
	
	Если ИмяЭтапа = "РаспределениеМатериаловИРаботПоБазе" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	ИначеЕсли ИмяЭтапа = "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаданияКРасчетуСебестоимости

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.ПолноеИмя(),
										"Период",
										"РегламентныеОперации",
										"Организация");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти