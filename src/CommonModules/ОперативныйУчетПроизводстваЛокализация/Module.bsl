#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Дата - Дата
Процедура ДобавитьДанныеДляПодбораРаботников(Форма, Дата) Экспорт
	
	//++ Локализация
	
	УчетТрудозатратВРазрезеСотрудников = ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(Дата);
	ИспользоватьКадровыйУчет           = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	Форма.Элементы.ГруппаТолькоРаботающие.Видимость = ИспользоватьКадровыйУчет ИЛИ УчетТрудозатратВРазрезеСотрудников;
	Форма.Элементы.ГруппаОрганизация.Видимость = УчетТрудозатратВРазрезеСотрудников;
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ДанныеДляПодбораСотрудников)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.Справочники.Сотрудники)
			ИЛИ НЕ (ИспользоватьКадровыйУчет
			ИЛИ УчетТрудозатратВРазрезеСотрудников) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = Форма.Работники.ТекстЗапроса;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК Должность",
		"ВЫБОР
		|	КОГДА &ИспользоватьШтатноеРасписание
		|		ТОГДА ШтатноеРасписание.Должность
		|	ИНАЧЕ ДанныеДляПодбора.Должность
		|КОНЕЦ КАК Должность");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК Организация",
		"ДанныеДляПодбора.Организация КАК Организация");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПодразделениеОрганизации",
		"ДанныеДляПодбора.Подразделение КАК ПодразделениеОрганизации");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК Подразделение",
		"ДанныеДляПодбора.МестоВСтруктуреПредприятия КАК Подразделение");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ТабНомер",
		"Сотрудники.Код КАК ТабНомер");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ИдентификаторЗаписи",
		"ДанныеДляПодбора.ИдентификаторЗаписи КАК ИдентификаторЗаписи");
		
	Если ИспользоватьКадровыйУчет
		И НЕ УчетТрудозатратВРазрезеСотрудников
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ТекущиеКадровыеДанныеСотрудников)
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ОсновныеСотрудникиФизическихЛиц) Тогда
		ТекстЗапроса = "
			|ВЫБРАТЬ ПЕРВЫЕ 1000000
			|	АВТОНОМЕРЗАПИСИ() КАК Номер,
			|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫБОР
			|		КОГДА &ИспользоватьШтатноеРасписание
			|			ТОГДА ШтатноеРасписание.Должность
			|		ИНАЧЕ ДанныеДляПодбора.Должность
			|	КОНЕЦ КАК Должность,
			|	ДанныеДляПодбора.МестоВСтруктуреПредприятия КАК Подразделение,
			|	ДанныеДляПодбора.Организация КАК Организация,
			|	ДанныеДляПодбора.Подразделение КАК ПодразделениеОрганизации,
			|	ДанныеДляПодбора.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
			|	ДанныеДляПодбора.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ТекущиеКадровыеДанныеФизическиеЛиц
			|{ВЫБРАТЬ
			|	ФизическоеЛицо,
			|	Должность,
			|	Подразделение.*,
			|	Организация.*,
			|	ПодразделениеОрганизации.*,
			|	Сотрудник}
			|ИЗ
			|	РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО ОсновныеСотрудникиФизическихЛиц.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|		И ТекущиеКадровыеДанныеСотрудников.ДатаПриема <> ДАТАВРЕМЯ(1, 1, 1)
			|		И (ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
			|		ПО ОсновныеСотрудникиФизическихЛиц.Сотрудник = ДанныеДляПодбора.Сотрудник
			|		И (ДанныеДляПодбора.Начало В
			|			(ВЫБРАТЬ
			|			МАКСИМУМ(Т.Начало)
			|			ИЗ
			|				РегистрСведений.ДанныеДляПодбораСотрудников КАК Т
			|			ГДЕ
			|				ОсновныеСотрудникиФизическихЛиц.Сотрудник = Т.Сотрудник
			|				И Т.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)))
			|					
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|				ПО &ИспользоватьШтатноеРасписание
			|				И ДанныеДляПодбора.ДолжностьПоШтатномуРасписанию = ШтатноеРасписание.Ссылка
			|ГДЕ
			|	ОсновныеСотрудникиФизическихЛиц.ДатаОкончания = &МаксимальнаяДатаНачалоДня
			|	И ДанныеДляПодбора.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДанныеДляПодбора.Начало
			|
			|{ГДЕ
			|	ДанныеДляПодбора.МестоВСтруктуреПредприятия.*}
			|;
			|
			|ВЫБРАТЬ
			|	ФизЛица.Ссылка,
			|	ФизЛица.Код,
			|	ФизЛица.Наименование,
			|	
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Должность КАК Должность,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Подразделение КАК Подразделение,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Сотрудник.Код КАК ТабНомер,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Организация КАК Организация,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.ПодразделениеОрганизации КАК ПодразделениеОрганизации
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизЛица
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеКадровыеДанныеФизическиеЛиц КАК ТекущиеКадровыеДанныеФизическиеЛиц
			|		ПО ФизЛица.Ссылка = ТекущиеКадровыеДанныеФизическиеЛиц.ФизическоеЛицо
			|		И (ТекущиеКадровыеДанныеФизическиеЛиц.Номер В
			|			(ВЫБРАТЬ
			|			МАКСИМУМ(Т.Номер)
			|			ИЗ
			|				ТекущиеКадровыеДанныеФизическиеЛиц КАК Т
			|			ГДЕ
			|				ФизЛица.Ссылка = Т.ФизическоеЛицо))
			|		
			|ГДЕ
			|	(&ТолькоРаботающие И ТекущиеКадровыеДанныеФизическиеЛиц.ФизическоеЛицо ЕСТЬ НЕ NULL ИЛИ Не &ТолькоРаботающие)";
		
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		СвойстваСписка.ОсновнаяТаблица = "Справочник.ФизическиеЛица";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Ложь;
		
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Форма.Элементы.Работники, СвойстваСписка);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"МаксимальнаяДатаНачалоДня",
			НачалоДня(ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата()));
		
	КонецЕсли;
	
	Если УчетТрудозатратВРазрезеСотрудников Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК Сотрудник",
			"ДанныеДляПодбора.Сотрудник КАК Сотрудник");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ФизЛица.Наименование",
			"ДанныеДляПодбора.Наименование");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.ФизическиеЛица КАК ФизЛица", "
		|		РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДанныеДляПодбора.Сотрудник = Сотрудники.Ссылка
		|		И ДанныеДляПодбора.Наименование = Сотрудники.Наименование
		|		И (ДанныеДляПодбора.ИдентификаторЗаписи В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ДанныеДляПодбораСотрудниковОтбор.ИдентификаторЗаписи
		|			ИЗ
		|				РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбораСотрудниковОтбор
		|			ГДЕ
		|				ДанныеДляПодбораСотрудниковОтбор.Сотрудник = ДанныеДляПодбора.Сотрудник
		|				И ДанныеДляПодбораСотрудниковОтбор.Наименование = ДанныеДляПодбора.Наименование
		|				И ДанныеДляПодбораСотрудниковОтбор.Подразделение = ДанныеДляПодбора.Подразделение
		|				И ДанныеДляПодбораСотрудниковОтбор.Должность = ДанныеДляПодбора.Должность
		|				И ДанныеДляПодбораСотрудниковОтбор.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			УПОРЯДОЧИТЬ ПО
		|				ДанныеДляПодбораСотрудниковОтбор.ПоДоговоруГПХ,
		|				ДанныеДляПодбораСотрудниковОтбор.Начало УБЫВ,
		|				ДанныеДляПодбораСотрудниковОтбор.Организация,
		|				ДанныеДляПодбораСотрудниковОтбор.Филиал,
		|				ДанныеДляПодбораСотрудниковОтбор.Подразделение))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизЛица
		|		ПО ФизЛица.Ссылка = ДанныеДляПодбора.ФизическоеЛицо
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ПО &ИспользоватьШтатноеРасписание
		|		И ДанныеДляПодбора.ДолжностьПоШтатномуРасписанию = ШтатноеРасписание.Ссылка
		|	ГДЕ
		|		ДанныеДляПодбора.Начало <> ДАТАВРЕМЯ(1, 1, 1)
		|		И ДанныеДляПодбора.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|		И (&ТолькоРаботающие
		|		И ДанныеДляПодбора.Начало <= &ТекущаяДата
		|		И ДанныеДляПодбора.Окончание >= &ТекущаяДата
		|		ИЛИ НЕ &ТолькоРаботающие)");
		
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		СвойстваСписка.ОсновнаяТаблица = "РегистрСведений.ДанныеДляПодбораСотрудников";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
		
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Форма.Элементы.Работники, СвойстваСписка);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"ТекущаяДата",
			ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Форма.Работники,
		"ТолькоРаботающие",
		Форма.ТолькоРаботающие);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Форма.Работники,
		"ИспользоватьШтатноеРасписание",
		ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание"));
	
	Форма.Элементы.РаботникиПодразделение.Видимость            = Истина;
	Форма.Элементы.РаботникиОрганизация.Видимость              = Истина;
	Форма.Элементы.РаботникиПодразделениеОрганизации.Видимость = Истина;
	Форма.Элементы.РаботникиДолжность.Видимость                = Истина;
	Форма.Элементы.РаботникиТабНомер.Видимость                 = Истина;
	Форма.Элементы.РаботникиКод.Видимость                      = Ложь;
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти
