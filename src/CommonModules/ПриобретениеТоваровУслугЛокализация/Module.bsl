#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("ПрослеживаемостьИмпортныхТоваров");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	НепроверяемыеОперации = Новый Массив;
	НепроверяемыеОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	НепроверяемыеОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
	НепроверяемыеОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	
	Если НепроверяемыеОперации.Найти(Объект.ХозяйственнаяОперация) = Неопределено Тогда
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьЗаполнениеКоличестваПоРНПТ(Объект, Отказ, Неопределено);
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.Товары, Объект.Дата);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика формы документа
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Обрабатываемая форма документа.
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	ДополнительныеПараметрыИСМП = СобытияФормИСМПКлиентСервер.ДополнительныеПараметрыИнтеграции(Форма);
	
	Если ПроверяемыеРеквизиты.Найти("Объект") <> Неопределено
		И ДополнительныеПараметрыИСМП <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметрыИСМП, "ПроверкаЗаполненияОтключитьКонтрольСерий") Тогда
			
			ТекущийОбъект = Форма.РеквизитФормыВЗначение("Объект");
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ПроверкаЗаполненияОтключитьКонтрольСерий", Истина);
			
			Если НЕ ТекущийОбъект.ПроверитьЗаполнение() Тогда
				Отказ = Истина
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Объект");
			
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		ИнтеграцияЕГАИСУТ.ЗаполнитьПоступлениеТоваровУслугНаОснованииТТНВходящаяЕГАИС(Объект, ДанныеЗаполнения, Неопределено, СтандартнаяОбработка);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		ИнтеграцияВЕТИСУТ.ЗаполнитьПриобретениеТоваровУслугНаОснованииВходящейТранспортнойОперацииВЕТИС(Объект, ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриемкаТоваровИСМП") Тогда
		ИнтеграцияИСМПУТ.ЗаполнитьПриобретениеТоваровУслугНаОснованииПриемкиТовароИСМП(Объект, ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПогашениеСДИЗЗЕРНО") Тогда
		ИнтеграцияЗЕРНОУТ.ЗаполнитьПриобретениеТоваровУслугНаОснованииПогашенияСДИЗЗЕРНО(Объект, ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.НакладнаяСАТУРН") Тогда
		ИнтеграцияСАТУРНУТ.ЗаполнитьПриобретениеТоваровУслугНаОснованииНакладнаяСАТУРН(Объект, ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ИмпортПродукцииСАТУРН") Тогда
		ИнтеграцияСАТУРНУТ.ЗаполнитьПриобретениеТоваровУслугНаОснованииИмпортПродукцииСАТУРН(Объект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
			ДанныеЗаполнения.Вставить("ОперацияССамозанятым", ДанныеЗаполнения.РеквизитыШапки.ОперацияССамозанятым)
		КонецЕсли;
		
		Если Не ДанныеЗаполнения.Свойство("ОперацияССамозанятым") Тогда
		
			Если ЗначениеЗаполнено(Объект.Договор) Тогда
				Объект.ОперацияССамозанятым =
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ДоговорССамозанятым");
			Иначе
				
				Объект.ОперацияССамозанятым = Ложь;
				ОписаниеРеквизитов = Новый Структура;
				ДенежныеСредстваСерверЛокализация.ЗадатьОписаниеЗаполняемыхРеквизитовПриобретенияПоСтатистике(ОписаниеРеквизитов);
				ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(Объект, Объект, ОписаниеРеквизитов);
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	ЗаполнитьПризнакЕстьАлкогольнаяПродукция(Объект);
	ЗаполнитьЗначенияСвойств(Объект, ИнтеграцияГИСМ_УТ.ЗаполнитьПризнакиЕстьМаркируемаяПродукцияИЕстьКиЗ(Объект, "Товары"));
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Объект.ЕстьАлкогольнаяПродукция     = Ложь;
	Объект.ЕстьКиЗГИСМ                  = Ложь;
	Объект.ЕстьМаркируемаяПродукцияГИСМ = Ложь;
	Объект.ШтрихкодыУпаковок.Очистить();
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	
	Документы.КорректировкаВидаДеятельностиНДС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Приобретение товаров услуг".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	
	// Авансовый отчет
	Если Константы.ВидимостьДатыНачалаПечатиЕдиногоАвансовогоОтчета.Получить() Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.АвансовыйОтчет";
		КомандаПечати.Идентификатор = "АвансовыйОтчет2";
		КомандаПечати.Представление = НСтр("ru = 'Авансовый отчет';
											|en = 'Expense report'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		//++ НЕ УТ
		КомандаПечати.Обработчик = "РеглУчетКлиент.ПечатьСПроверкойОтраженияВРеглУчете";
		//-- НЕ УТ
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаПечати,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо,
			ВидСравненияКомпоновкиДанных.Равно);
			
		ДатаНачалаПечатиЕдиногоАвансовогоОтчета = Константы.ДатаНачалаПечатиЕдиногоАвансовогоОтчета.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаПечатиЕдиногоАвансовогоОтчета) Тогда
			
			ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
				КомандаПечати,
				"Дата",
				ДатаНачалаПечатиЕдиногоАвансовогоОтчета,
				ВидСравненияКомпоновкиДанных.Меньше);
		КонецЕсли;
	КонецЕсли;
	
	// Приходный ордер (М-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "М4";
	КомандаПечати.Представление = НСтр("ru = 'Приходный ордер (М-4)';
										|en = 'Goods receipt note (M-4)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Акт о приемке товара, поступившего без счета поставщика (ТОРГ-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "ТОРГ4";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приемке товара, поступившего без счета поставщика (ТОРГ-4)';
										|en = 'Certificate of goods receiving without purchase proforma invoice (TORG-4)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-1 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ1";
		КомандаПечати.Представление = НСтр("ru = 'Акт о приеме-передаче ТМЦ на хранение (МХ-1)';
											|en = 'Certificate of inventory handover for storage (MH-1)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
	//++ НЕ УТ

	// ОС-14
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС14";
	КомандаПечати.Идентификатор = "ОС14";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме оборудования (ОС-14)';
										|en = 'Equipment acceptance certificate (FA-14) '");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//++ Локализация
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АвансовыйОтчет") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"АвансовыйОтчет",
			НСтр("ru = 'Авансовый отчет';
				|en = 'Expense report'"),
			АвансовыйОтчетЛокализация.СформироватьПечатнуюФормуАвансовогоОтчетаСтараяВерсия(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Функция получает данные для формирования печатной формы  М-4
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ПриобретениеТоваровУслуг - Массив ссылок на документы, 
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМ4(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Дата                 КАК Дата,
	|	ДанныеДокументов.Валюта               КАК Валюта,
	|	ДанныеДокументов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокументов.Организация КАК Организация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Ссылка                     КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Номер                      КАК Номер,
	|	ПриобретениеТоваровУслуг.Дата                       КАК Дата,
	|	ПриобретениеТоваровУслуг.Дата                       КАК ДатаСоставления,
	|	ПриобретениеТоваровУслуг.НомерВходящегоДокумента    КАК НомерСопроводительногоДокумента,
	|	ПриобретениеТоваровУслуг.Контрагент                 КАК Поставщик,
	|	ПриобретениеТоваровУслуг.Организация                КАК Организация,
	|	ПриобретениеТоваровУслуг.Организация.Префикс        КАК Префикс,
	|	ПриобретениеТоваровУслуг.БанковскийСчетОрганизации  КАК БанковскийСчетОрганизации,
	|
	|	ВЫБОР
	|		КОГДА
	|			ПриобретениеТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|		ТОГДА
	|			ПриобретениеТоваровУслуг.Организация
	|		ИНАЧЕ
	|			ПриобретениеТоваровУслуг.Грузоотправитель
	|	КОНЕЦ                                              КАК Грузоотправитель,
	|
	|	ПриобретениеТоваровУслуг.Подразделение              КАК Подразделение,
	|	ПриобретениеТоваровУслуг.Подразделение.Наименование КАК ПредставлениеПодразделения,
	|	ПриобретениеТоваровУслуг.Валюта                     КАК Валюта,
	|	ПриобретениеТоваровУслуг.Принял        			   КАК МОЛ,
	|	ПриобретениеТоваровУслуг.ПринялДолжность            КАК ДолжностьМОЛ
	|
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ПриобретениеТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                                  КАК Ссылка,
	|	ТаблицаТоваров.Склад                                   КАК Склад,
	|	ТаблицаТоваров.Склад.Наименование                      КАК СкладНаименование,
	|	ТаблицаТоваров.Номенклатура                            КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное         КАК ТоварНаименование,
	|	
	|	ВЫБОР КОГДА &КолонкаКодов = ""Артикул"" ТОГДА
	|		ТаблицаТоваров.Номенклатура.Артикул
	|	ИНАЧЕ
	|		ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	
	|	ТаблицаТоваров.ЕдиницаИзмерения.Представление          КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код                    КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное       КАК Характеристика,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1 ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТаблицаТоваров.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|
	|	ТаблицаТоваров.КоличествоУпаковок                      КАК Количество,
	|	ТаблицаТоваров.КоличествоУпаковок                      КАК КоличествоПринято,
	|	ВЫБОР КОГДА ТаблицаТоваров.Количество <> 0 ТОГДА 
	|		ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                                                  КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС                             КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС                                КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС   КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
	|			И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаТоваров.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка, Склад
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы  ТОРГ-4
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ4(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Номер КАК Номер,
	|	ПриобретениеТоваровУслуг.Дата КАК Дата,
	|	ПриобретениеТоваровУслуг.Дата КАК ДатаДокумента,
	|	ПриобретениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаДокументаСопровождения,
	|	ПриобретениеТоваровУслуг.НомерВходящегоДокумента КАК НомерДокументаСопровождения,
	|	ПриобретениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ПриобретениеТоваровУслуг.Организация КАК Организация,
	|	ПриобретениеТоваровУслуг.Организация.Префикс КАК Префикс,
	|	ПриобретениеТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ПриобретениеТоваровУслуг.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ПриобретениеТоваровУслуг.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ВЫБОР
	|		КОГДА ПриобретениеТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ПриобретениеТоваровУслуг.Организация
	|		ИНАЧЕ ПриобретениеТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ПриобретениеТоваровУслуг.Подразделение.Наименование КАК ПредставлениеПодразделения,
	|	ПриобретениеТоваровУслуг.Валюта КАК Валюта,
	|	ПриобретениеТоваровУслуг.Принял КАК МОЛ,
	|	ПриобретениеТоваровУслуг.ПринялДолжность КАК ДолжностьМОЛ
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Склад КАК Склад,
	|	ТаблицаТоваров.Склад.Наименование КАК МестоПриемки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	1 КАК КоличествоВОдномМесте,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL 
	|						ТОГДА ТаблицаТоваров.Количество
	|					ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
	|				КОНЕЦ * &ТекстЗапросаВесУпаковки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаБрутто,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ТаблицаТоваров.Количество * &ТекстЗапросаВесНоменклатуры
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаНетто,
	|	ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
	|			И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаТоваров.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка,
	|	Склад";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("ТаблицаТоваров.Упаковка", "ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения", "ТаблицаТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("КолонкаКодов",                  КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",          Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса", ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 1.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.ПриобретениеТоваровУслуг - коллекция значений ссылок на документы,
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	НЕОПРЕДЕЛЕНО											КАК Заказ,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Организация.Префикс						КАК Префикс,
	|	НЕОПРЕДЕЛЕНО											КАК Поклажедержатель,
	|	ТоварыДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	НЕОПРЕДЕЛЕНО											КАК Договор,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И (ТоварыДокумента.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		ИЛИ НЕ ТоварыДокумента.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	ТоварыДокумента.Назначение.Заказ						КАК Заказ,
	|	ТоварыДокумента.Назначение.Договор.Контрагент			КАК Организация,
	|	""""													КАК Префикс,
	|	ДанныеДокумента.Организация								КАК Поклажедержатель,
	|	ТоварыДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ЛОЖЬ													КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	ТоварыДокумента.Назначение.Договор						КАК Договор,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И ТоварыДокумента.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.Заказ								КАК Заказ,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Поклажедержатель					КАК Поклажедержатель,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.Договор								КАК Договор,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|,
	|	ВЫБОР КОГДА Товары.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Товары.Назначение.Договор.Контрагент
	|	ИНАЧЕ
	|		Товары.Ссылка.Организация
	|	КОНЕЦ КАК Организация
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	МИНИМУМ(ВидыЗапасов.НомерСтроки) КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	СУММА(ВидыЗапасов.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ВидыЗапасов.Количество) КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.Ссылка,
	|	Шапка.Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения,
	|	Шапка.ПредварительныйРасчет,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика,
	|	ВидыЗапасов.Упаковка
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
		
	ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Дополняет текст запроса для формирования печатных форм МХ-1, МХ-3
//
// Параметры:
//	Запрос - Запрос - Выполняемый запрос.
//
Процедура ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос)
	
	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();
	ДатаПереходаНаЦенообразование25 = ЦенообразованиеВызовСервера.ДатаПереходаНаЦенообразованиеВерсии25();

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.Характеристика КАК Характеристика,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыЦенНоменклатуры
	|ИЗ
	|	ВтТовары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|		И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|		И Товары.Характеристика = ЦеныНоменклатуры.Характеристика
	|		И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|		И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|		И (ВЫБОР
	|			КОГДА &ИспользуетсяЦенообразование25
	|				ТОГДА Товары.ДатаПолученияЦены < &ДатаПереходаНаЦенообразование25
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура,
	|	ЦеныНоменклатуры.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.ВидЦены КАК ВидЦены,
	|	Товары.ДатаПолученияЦены КАК ДатаПолученияЦены,
	|	Товары.ВалютаЦены КАК ВалютаЦены,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА
	|			ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаХарактеристик.ХарактеристикаНоменклатурыДляЦенообразования
	|	КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоСерии = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаСерий.СерияНоменклатурыДляЦенообразования
	|	КОНЕЦ, ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК СерияЦО,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоУпаковке = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ КАК УпаковкаЦО,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыРеквизиты25
	|ИЗ
	|	ВтТовары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ТаблицаХарактеристик
	|		ПО Товары.Характеристика = ТаблицаХарактеристик.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК ТаблицаСерий
	|		ПО Товары.Серия = ТаблицаСерий.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = ТаблицаНоменклатуры.ВидНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.ХарактеристикаЦО КАК ХарактеристикаЦО,
	|	ЦеныНоменклатуры.СерияЦО КАК СерияЦО,
	|	ЦеныНоменклатуры.УпаковкаЦО КАК УпаковкаЦО,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыЦенНоменклатуры25
	|ИЗ
	|	ТоварыРеквизиты25 КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
	|		ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|		И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|		И Товары.ХарактеристикаЦО = ЦеныНоменклатуры.ХарактеристикаЦО
	|		И Товары.СерияЦО = ЦеныНоменклатуры.СерияЦО
	|		И Товары.УпаковкаЦО = ЦеныНоменклатуры.УпаковкаЦО
	|		И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|		И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|		И (&ИспользуетсяЦенообразование25)
	|		И (Товары.ДатаПолученияЦены >= &ДатаПереходаНаЦенообразование25)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура,
	|	ЦеныНоменклатуры.ХарактеристикаЦО,
	|	ЦеныНоменклатуры.СерияЦО,
	|	ЦеныНоменклатуры.УпаковкаЦО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка КАК Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.Цена КАК Цена,
	|	ЦеныНоменклатуры.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ ЦеныПоВидуЦен
	|ИЗ
	|	ПериодыЦенНоменклатуры КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|		И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|		И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|		И ПериодыЦенНоменклатуры.Характеристика = ЦеныНоменклатуры.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки,
	|	ЦеныНоменклатуры.Цена,
	|	ЦеныНоменклатуры.Упаковка
	|ИЗ
	|	ПериодыЦенНоменклатуры25 КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|		И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|		И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|		И ПериодыЦенНоменклатуры.ХарактеристикаЦО = ЦеныНоменклатуры.ХарактеристикаЦО
	|		И ПериодыЦенНоменклатуры.СерияЦО = ЦеныНоменклатуры.СерияЦО
	|		И ПериодыЦенНоменклатуры.УпаковкаЦО = ЦеныНоменклатуры.УпаковкаЦО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	СтоимостьТоваров.ВидЗапасов КАК ВидЗапасов,
	|	СтоимостьТоваров.Организация КАК Организация,
	|	СтоимостьТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	МАКСИМУМ(СтоимостьТоваров.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыСтоимостиТоваров
	|ИЗ
	|	ВтВидыЗапасов КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = СтоимостьТоваров.АналитикаУчетаНоменклатуры
	|		И ВидыЗапасов.Организация = СтоимостьТоваров.Организация
	|		И ВидыЗапасов.ВидЗапасов = СтоимостьТоваров.ВидЗапасов
	|		И ВидыЗапасов.ДатаПолученияЦены >= СтоимостьТоваров.Период
	|		И (ВидыЗапасов.ПредварительныйРасчет)
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.Ссылка,
	|	СтоимостьТоваров.ВидЗапасов,
	|	СтоимостьТоваров.Организация,
	|	СтоимостьТоваров.АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыСтоимостиТоваров.Ссылка,
	|	СтоимостьТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СтоимостьТоваров.ВидЗапасов КАК ВидЗапасов,
	|	СтоимостьТоваров.Организация КАК Организация,
	|	(СтоимостьТоваров.СтоимостьРегл + СтоимостьТоваров.ДопРасходыРегл + СтоимостьТоваров.ТрудозатратыРегл +
	|		СтоимостьТоваров.ПостатейныеПостоянныеРегл + СтоимостьТоваров.ПостатейныеПеременныеРегл) КАК Стоимость
	|ПОМЕСТИТЬ ЦеныПоСебестоимостиПредварительно
	|ИЗ
	|	ПериодыСтоимостиТоваров КАК ПериодыСтоимостиТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|		ПО ПериодыСтоимостиТоваров.АналитикаУчетаНоменклатуры = СтоимостьТоваров.АналитикаУчетаНоменклатуры
	|		И ПериодыСтоимостиТоваров.Организация = СтоимостьТоваров.Организация
	|		И ПериодыСтоимостиТоваров.Период = СтоимостьТоваров.Период
	|		И ПериодыСтоимостиТоваров.ВидЗапасов = СтоимостьТоваров.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.ВидЗапасов КАК ВидЗапасов,
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	СУММА(СебестоимостьТоваров.СтоимостьРегл + СебестоимостьТоваров.ДопРасходыРегл +
	|		СебестоимостьТоваров.ТрудозатратыРегл + СебестоимостьТоваров.ПостатейныеПостоянныеРегл +
	|		СебестоимостьТоваров.ПостатейныеПеременныеРегл) КАК Стоимость
	|ПОМЕСТИТЬ ЦеныПоСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыЗапасов КАК ВидыЗапасов
	|		ПО (ВидыЗапасов.Ссылка = СебестоимостьТоваров.ДокументДвижения
	|		ИЛИ ВидыЗапасов.Ссылка = СебестоимостьТоваров.Регистратор)
	|		И (ВидыЗапасов.АналитикаУчетаНоменклатуры = СебестоимостьТоваров.АналитикаУчетаНоменклатуры)
	|		И (ВидыЗапасов.Организация = СебестоимостьТоваров.Организация)
	|		И (ВидыЗапасов.ВидЗапасов = СебестоимостьТоваров.ВидЗапасов)
	|		И (НЕ ВидыЗапасов.ПредварительныйРасчет)
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.Ссылка,
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.ВидЗапасов,
	|	СебестоимостьТоваров.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтШапка.Ссылка КАК Ссылка,
	|	ВтШапка.Номер КАК Номер,
	|	ВтШапка.Дата КАК Дата,
	|	ВтШапка.ДатаДокумента КАК ДатаДокумента,
	|	ВтШапка.Организация КАК Организация,
	|	ВтШапка.Договор.Номер КАК ДоговорНомер,
	|	ВтШапка.Договор.Дата КАК ДоговорДата
	|ИЗ
	|	ВтШапка КАК ВтШапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ВтШапка.Склад = Склады.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтШапка.Ссылка КАК Ссылка,
	|	ВтШапка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	Склады.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати,
	|	ПРЕДСТАВЛЕНИЕ(ВтШапка.Склад) КАК ПредставлениеСклада,
	|	ВтШапка.Склад КАК Склад,
	|	ВтШапка.ВидЦены КАК ВидЦены,
	|	ВтШапка.ВалютаЦены КАК ВалютаЦены,
	|	ПРЕДСТАВЛЕНИЕ(Склады.Подразделение) КАК ПредставлениеПодразделения,
	|	ВЫБОР
	|		КОГДА ВтШапка.Поклажедержатель = Неопределено
	|			ТОГДА Склады.Поклажедержатель
	|		ИНАЧЕ ВтШапка.Поклажедержатель
	|	КОНЕЦ КАК Поклажедержатель,
	|	ВтШапка.Организация КАК Организация,
	|	ВтШапка.Заказ КАК Заказ,
	|	ВЫБОР
	|		КОГДА Склады.ОтветственноеХранениеДоВостребования
	|			ТОГДА &ДоВостребования
	|		ИНАЧЕ Склады.СрокОтветственногоХранения
	|	КОНЕЦ КАК СрокХранения,
	|	Склады.ОсобыеОтметки КАК ОсобыеОтметки,
	|	Склады.УсловияХраненияТоваров КАК УсловияХранения,
	|	Склады.ТекущаяДолжностьОтветственного КАК ДолжностьМОЛ,
	|	Склады.ТекущийОтветственный КАК МОЛ
	|ИЗ
	|	ВтШапка КАК ВтШапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ВтШапка.Склад = Склады.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаНоменклатуры.Артикул
	|		ИНАЧЕ ТаблицаНоменклатуры.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.НаименованиеПолное КАК ПредставлениеНоменклатуры,
	|	ТаблицаХарактеристик.НаименованиеПолное КАК ПредставлениеХарактеристики,
	|	Товары.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
	|	Товары.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
	|	Товары.КоличествоУпаковок КАК Количество,
	|	Товары.ВидУпаковки КАК ВидУпаковки,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) * Товары.Количество КАК ЧИСЛО(31,
	|		2)) КАК Сумма,
	|	ВЫБОР
	|		КОГДА Товары.КоличествоУпаковок = 0
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) * Товары.Количество КАК
	|				ЧИСЛО(31, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) * Товары.Количество /
	|			Товары.КоличествоУпаковок КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК Цена,
	|	Товары.Организация КАК Организация
	|ИЗ
	|	ВтТовары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ТаблицаХарактеристик
	|		ПО Товары.Характеристика = ТаблицаХарактеристик.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПоВидуЦен КАК Цены
	|		ПО Товары.Ссылка = Цены.Ссылка
	|		И Товары.НомерСтроки = Цены.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка,
	|	ВидыЗапасов.Склад,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаНоменклатуры.Артикул
	|		ИНАЧЕ ТаблицаНоменклатуры.Код
	|	КОНЕЦ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ТаблицаНоменклатуры.НаименованиеПолное,
	|	ТаблицаХарактеристик.НаименованиеПолное,
	|	ВидыЗапасов.ЕдиницаИзмеренияНаименование,
	|	ВидыЗапасов.ЕдиницаИзмеренияКод,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.ВидУпаковки,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.КоличествоУпаковок = 0
	|			ТОГДА Цены.Стоимость
	|		ИНАЧЕ Цены.Стоимость * ВидыЗапасов.КоличествоУпаковок
	|	КОНЕЦ,
	|	Цены.Стоимость,
	|	ВидыЗапасов.Организация КАК Организация
	|ИЗ
	|	ВтВидыЗапасов КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО ВидыЗапасов.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ТаблицаХарактеристик
	|		ПО ВидыЗапасов.Характеристика = ТаблицаХарактеристик.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЦеныПоСебестоимостиПредварительно КАК Цены
	|		ПО ВидыЗапасов.Ссылка = Цены.Ссылка
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры = Цены.АналитикаУчетаНоменклатуры
	|		И ВидыЗапасов.Организация = Цены.Организация
	|		И ВидыЗапасов.ВидЗапасов = Цены.ВидЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка,
	|	ВидыЗапасов.Склад,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаНоменклатуры.Артикул
	|		ИНАЧЕ ТаблицаНоменклатуры.Код
	|	КОНЕЦ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.Номенклатура,
	|	ТаблицаНоменклатуры.НаименованиеПолное,
	|	ТаблицаХарактеристик.НаименованиеПолное,
	|	ВидыЗапасов.ЕдиницаИзмеренияНаименование,
	|	ВидыЗапасов.ЕдиницаИзмеренияКод,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.ВидУпаковки,
	|	Цены.Стоимость,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.КоличествоУпаковок = 0
	|			ТОГДА Цены.Стоимость
	|		ИНАЧЕ Цены.Стоимость / ВидыЗапасов.КоличествоУпаковок
	|	КОНЕЦ,
	|	ВидыЗапасов.Организация КАК Организация
	|ИЗ
	|	ВтВидыЗапасов КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО ВидыЗапасов.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ТаблицаХарактеристик
	|		ПО ВидыЗапасов.Характеристика = ТаблицаХарактеристик.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЦеныПоСебестоимости КАК Цены
	|		ПО ВидыЗапасов.Ссылка = Цены.Ссылка
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры = Цены.АналитикаУчетаНоменклатуры
	|		И ВидыЗапасов.Организация = Цены.Организация
	|		И ВидыЗапасов.ВидЗапасов = Цены.ВидЗапасов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Склад,
	|	НомерСтроки,
	|	Номенклатура,
	|	ПредставлениеХарактеристики,
	|	ВидУпаковки
	|ИТОГИ
	|ПО
	|	Ссылка,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Ссылка) КАК СсылкаПредставление,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Организация) КАК ОрганизацияПредставление,
	|	ДанныеДокумента.Склад КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Склад) КАК СкладПредставление,
	|	ДанныеДокумента.СкладОтветственногоХранения КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.ОрганизацияПоклажедержатель КАК ОрганизацияПоклажедержатель
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтШапка КАК ВтШапка
	|		ПО ДанныеДокумента.Ссылка = ВтШапка.Ссылка
	|ГДЕ
	|	ВтШапка.Ссылка ЕСТЬ NULL
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Цены.Упаковка",
		"Товары.Номенклатура"));
	
	Запрос.Текст = Запрос.Текст + ТекстЗапроса;
	Запрос.УстановитьПараметр("ДоВостребования", НСтр("ru = 'до востребования';
														|en = 'until called for'"));
	Запрос.УстановитьПараметр("ИспользуетсяЦенообразование25", ИспользуетсяЦенообразование25);
	Запрос.УстановитьПараметр("ДатаПереходаНаЦенообразование25", ДатаПереходаНаЦенообразование25);
	
КонецПроцедуры

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПересчитыватьВВалютуРегл - Булево - Признак необходимости пересчета сумм табличной части в валюте регламентированного учета.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПересчитыватьВВалютуРегл = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПересчитыватьВВалютуРегл);
	
	Если ПересчитыватьВВалютуРегл Тогда
		Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСПоставщиками.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСПоставщиками.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				КонецРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(КонецРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru = 'Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.';
					|en = 'Print form is generated according to irrelevant data. 
					|Update AR/AP accounting manually, and then create the print form again.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                           КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                          КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                        КАК Характеристика,
	|	ТаблицаДокумента.Количество                            КАК Количество,
	|	ТаблицаДокумента.КоличествоУпаковок                    КАК КоличествоУпаковок,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС                             КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС                              КАК СуммаНДС,
	|	ТаблицаДокумента.Упаковка                              КАК Упаковка,
	|   &ТекстЗапросаЕдиницаИзмеренияНоменклатуры 			   КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Склад                                 КАК Склад
	|
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументов.Валюта = ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
	|	ИЛИ (НЕ &ПересчитыватьВВалютуРегл)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                            КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                           КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                         КАК Характеристика,
	|	ТаблицаДокумента.Количество                             КАК Количество,
	|	ТаблицаДокумента.КоличествоУпаковок                     КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, 0) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаНДСРегл, 0)    КАК СуммаНДС,
	|	ТаблицаДокумента.Упаковка                               КАК Упаковка,
	|   &ТекстЗапросаЕдиницаИзмеренияНоменклатуры				КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Склад                                  КАК Склад
	|
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|
	|ГДЕ
	|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
	|	И &ПересчитыватьВВалютуРегл
	|	И СуммыДокументовВВалютахУчета.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                            КАК НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Количество                             КАК Количество,
	|	ТаблицаДокумента.Количество                             КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, 0) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаНДСРегл, 0)    КАК СуммаНДС,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|   &ТекстЗапросаЕдиницаИзмеренияАналитики							КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.МестоХранения       КАК Склад
	|
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.ВидыЗапасов КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|
	|ГДЕ
	|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
	|	И &ПересчитыватьВВалютуРегл
	|	И СуммыДокументовВВалютахУчета.Активность
	|	И &ИспользуетсяДокументПоступлениеТоваров
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаЕдиницаИзмеренияНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаЕдиницаИзмеренияАналитики",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИспользуетсяДокументПоступлениеТоваров",
		"ВЫБОР
		|	КОГДА ТаблицаДокумента.Ссылка.ХозяйственнаяОперация В(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
		|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
		|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути),
		|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки),
		|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути))
		|		ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ");
	
	Запрос.Выполнить();
	
КонецПроцедуры

//++ НЕ УТ

// Функция получает данные для формирования печатной формы ОС-14
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ПриобретениеТоваровУслуг - Массив ссылок на документы, 
//	по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыОС14(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка;";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПриобретениеТоваровУслугЛокализация.ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Номер КАК Номер,
	|	ПриобретениеТоваровУслуг.Дата КАК Дата,
	|	ПриобретениеТоваровУслуг.Дата КАК ДатаДокумента,
	|	ПриобретениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаДокументаСопровождения,
	|	ПриобретениеТоваровУслуг.НомерВходящегоДокумента КАК НомерДокументаСопровождения,
	|	ПриобретениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ПриобретениеТоваровУслуг.Организация КАК Организация,
	|	ПриобретениеТоваровУслуг.Организация.Префикс КАК Префикс,
	|	ПриобретениеТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ПриобретениеТоваровУслуг.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ПриобретениеТоваровУслуг.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ВЫБОР
	|		КОГДА ПриобретениеТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ПриобретениеТоваровУслуг.Организация
	|		ИНАЧЕ ПриобретениеТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ПриобретениеТоваровУслуг.Подразделение.Наименование КАК ПредставлениеПодразделения,
	|	ПриобретениеТоваровУслуг.Валюта КАК Валюта,
	|	ПриобретениеТоваровУслуг.Принял.Наименование КАК МОЛ,
	|	ПриобретениеТоваровУслуг.ПринялДолжность КАК ДолжностьМОЛ
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК ОборудованиеНаименование,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество КАК СтоимостьЗаЕдиницу,
	|	ТаблицаТоваров.СуммаБезНДС КАК СтоимостьВсего
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаТоваров.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса", Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса", ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		ПакетРезультатов[0],
		ПакетРезультатов[1]);
	
КонецФункции
//-- НЕ УТ

//-- Локализация
#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	//++ Локализация
	
	// Общие фрагменты проводок по включению в стоимость НДС
	ТекстыОтражения.Добавить(РеглУчетВыборкиСерверПовтИсп.ТекстВключениеНДСпоПриобретеннымЦенностям());
	
	// Оприходование ценностей
	
	#Область ФактуровкаПоставки                 // (Дт 60.НП :: Кт 60, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Фактуровка поставки (Дт 60.НП :: Кт 60, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки 
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки) КАК ВидСчетаДт,
	|	Операция.Договор.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО								КАК ВалютаДт,
	|	Операция.Договор.Подразделение				КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности 	КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО			КАК СчетДт,
	// ДТ - Субконто
	|	Операция.Контрагент		КАК СубконтоДт1,
	|	Операция.Договор		КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО			КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР КОГДА Операция.ВернутьМногооборотнуюТару 
	|		И Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	// КТ - Субконто
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	(ВЫБОР
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|			ТОГДА 
	|				Суммы.СуммаПриведенная
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|	КОНЕЦ) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРКт,
	|	""Фактуровка поставки"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.ВидыЗапасов КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.Склады КАК Склады
	|	ПО
	|		Склады.Ссылка = Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|
	|ГДЕ
	|	НЕ ЕстьNULL(Склады.ЦеховаяКладовая, Ложь)
	|	И Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки))
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ФактуровкаПоставкиРаботыУслуги                 // (Дт 60.НП :: Кт 60, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Фактуровка поставки работ и услуг (Дт 60.НП :: Кт 60, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки 
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки) КАК ВидСчетаДт,
	|	Операция.Договор.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО								КАК ВалютаДт,
	|	Операция.Договор.Подразделение				КАК ПодразделениеДт,
	|	Операция.Договор.НаправлениеДеятельности 	КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО			КАК СчетДт,
	// ДТ - Субконто
	|	Операция.Контрагент		КАК СубконтоДт1,
	|	Операция.Договор		КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО			КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР КОГДА Операция.ВернутьМногооборотнуюТару 
	|		И Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	// КТ - Субконто
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	(ВЫБОР
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ)
	|			ТОГДА 
	|				Суммы.СуммаПриведенная
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|	КОНЕЦ) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРКт,
	|	""Фактуровка услуг (работ)"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.Склады КАК Склады
	|	ПО
	|		Склады.Ссылка = Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|
	|ГДЕ
	|	НЕ ЕстьNULL(Склады.ЦеховаяКладовая, Ложь)
	|	И Расчеты.Договор.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.НеотфактурованныеПоставкиТоваровИУслуг)
	|	И Строки.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки))
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ОтклоненияПриФактуровке            // (Дт 10, 20, 41 :: Кт 60.НП)

	ТекстЗапроса =
	"ВЫБРАТЬ //// Отклонения В стоимости при фактуровке (Дт 10, 20, 41 :: Кт 60.НП)
	|	Операция.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.ДопРасходыРегл КАК Сумма,
	|	Строки.ДопРасходыУпр КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				ИЛИ Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				ИЛИ Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ТОГДА НЕОПРЕДЕЛЕНО 
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА (Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|					ИЛИ Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|				И Строки.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.СкладскаяТерритория.Подразделение
	|		ИНАЧЕ Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|		ИНАЧЕ Строки.АналитикаУчетаНоменклатуры.СкладскаяТерритория.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоДт1,
	|	Строки.АналитикаУчетаНоменклатуры.МестоХранения КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА НЕ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				И Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ДопРасходыРегл 
	|		- Строки.ПостояннаяРазница 
	|		- Строки.ВременнаяРазница КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки) 	КАК ВидСчетаКт,
	|	Расчеты.Договор.ГруппаФинансовогоУчета									КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО		 													КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта 								КАК ВалютаКт,
	|	Расчеты.Договор.Подразделение  				КАК ПодразделениеКт,
	|	Расчеты.Договор.НаправлениеДеятельности 	КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО 								КАК СчетКт,
	|
	|	Расчеты.Контрагент 	КАК СубконтоКт1,
	|	Расчеты.Договор 	КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО 		КАК СубконтоКт3,
	|	
	|	Расчеты.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.ДопРасходыРегл 
	|		- Строки.ПостояннаяРазница 
	|		- Строки.ВременнаяРазница КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	
	|	""Отклонения в стоимости товаров и работ"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.СебестоимостьТоваров КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Регистратор
	|		И Операция.ХозяйственнаяОперация = Строки.ХозяйственнаяОперация
	|		И Строки.Количество = 0
	|		И (Строки.ДопРасходыРегл <> 0 ИЛИ Строки.ДопРасходыУпр <> 0
	|			ИЛИ Строки.ПостояннаяРазница <> 0 ИЛИ Строки.ВременнаяРазница <> 0)
	|		И НЕ Строки.РасчетСебестоимости
	|		И Строки.Активность
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Строки.РасчетПартий
	|		И Строки.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ОтклонениеВСтоимости)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);

	ТекстЗапроса = "
	|ВЫБРАТЬ // Отклонение по услугам (Дт 25, 26, 20 :: Кт 60.НП)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ДвиженияПрочиеРасходы.СуммаРегл КАК Сумма,
	|	ДвиженияПрочиеРасходы.СуммаУпр КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ДвиженияПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ДвиженияПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ДвиженияПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	ДвиженияПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	// ДТ - Субконто
	|	ДвиженияПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	ДвиженияПрочиеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ДвиженияПрочиеРасходы.СуммаРегл
	|		- ДвиженияПрочиеРасходы.ПостояннаяРазница 
	|		- ДвиженияПрочиеРасходы.ВременнаяРазница	КАК СуммаНУДт,
	|	ДвиженияПрочиеРасходы.ПостояннаяРазница КАК СуммаПРДт,
	|	ДвиженияПрочиеРасходы.ВременнаяРазница КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НеотфактурованныеПоставки) КАК ВидСчетаКт,
	|	Операция.Договор.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Операция.Договор.Подразделение КАК ПодразделениеКт,
	|	Операция.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	// КТ - Субконто
	|	НеотфактурованныеПоставки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт1,
	|	Операция.Контрагент 				КАК СубконтоКт2,
	|	Операция.Договор 					КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаКт,
	|	НеотфактурованныеПоставки.Количество КАК КоличествоКт,
	|	НеотфактурованныеПоставки.СтоимостьРегл 
	|		+ НеотфактурованныеПоставки.ДопРасходыРегл 
	|		- НеотфактурованныеПоставки.ПостояннаяРазница 
	|		- НеотфактурованныеПоставки.ВременнаяРазница	КАК СуммаНУКт,
	|	НеотфактурованныеПоставки.ПостояннаяРазница КАК СуммаПРКт,
	|	НеотфактурованныеПоставки.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Отклонения в стоимости услуг"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.СебестоимостьТоваров КАК НеотфактурованныеПоставки
	|	ПО 
	|		НеотфактурованныеПоставки.Регистратор = Операция.Ссылка
	|		И НеотфактурованныеПоставки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НеотфактурованныеПоставки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ПрочиеРасходы КАК ДвиженияПрочиеРасходы
	|	ПО 
	|		ДвиженияПрочиеРасходы.Регистратор = Операция.Ссылка
	|		И ДвиженияПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДвиженияПрочиеРасходы.ИдентификаторФинЗаписи = НеотфактурованныеПоставки.ИдентификаторФинЗаписи
	|		И ДвиженияПрочиеРасходы.НастройкаХозяйственнойОперации = НеотфактурованныеПоставки.НастройкаХозяйственнойОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ДвиженияПрочиеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО
	|		ДвиженияПрочиеРасходы.АналитикаРасходов = ОбъектыСтроительства.Ссылка
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса); 
	
#КонецОбласти

#Область ТоварыВПути	                        // (Дт 15 :: Кт 60, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Товары в пути (Дт 15 :: Кт 60, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки 
	|	Операция.Дата				КАК Период,
	|	Операция.Организация		КАК Организация,
	|	Строки.ИдентификаторСтроки	КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчетаДт,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	НЕОПРЕДЕЛЕНО					КАК ВалютаДт,
	|	Операция.Договор.Подразделение	КАК ПодразделениеДт,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям И Операция.Дата >= &ДатаВключенияСебестоимостиПоНазначениям
	|		ТОГДА Аналитика.Назначение.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО			КАК СчетДт,
	// ДТ - Субконто
	|	Аналитика.Номенклатура	КАК СубконтоДт1,
	|	Операция.Контрагент		КАК СубконтоДт2,
	|	Операция.Договор		КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР КОГДА Операция.ВернутьМногооборотнуюТару И Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО 		КАК СчетКт,
	|
	// КТ - Субконто
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор 	КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО 		КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	(ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаПриведенная
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|	КОНЕЦ) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) ТОГДА
	|		Суммы.СуммаБезНДСРегл
	|	КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Строки.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС) ТОГДА
	|		Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРКт,
	|	""Приобретение товаров"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.ВидыЗапасов КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		Строки.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|
	|ГДЕ
	|	Аналитика.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ЗакупкаЦенности                    // (Дт 10, 41, 76 :: Кт 60, 71, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Поступление ценностей на склад в собственность (Дт 10, 20, 41 :: Кт 60, 71, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки 
	|	Операция.Дата			КАК Период,
	|	Операция.Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО			КАК ИдентификаторСтроки,
	|
	// Сумма
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаПриведеннаяРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаПриведеннаяУпр
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоЗакупке)
	|		КОГДА ЕстьNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Назначения.Договор.ГруппаФинансовогоУчета
	|		КОГДА ЕстьNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ЕстьNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|		ТОГДА Склады.Подразделение
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Назначения.Договор.Подразделение
	|		ИНАЧЕ Склады.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям И Операция.Дата >= &ДатаВключенияСебестоимостиПоНазначениям
	|			ТОГДА ЕстьNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО			КАК СчетДт,
	// ДТ - Субконто
	|	Строки.Номенклатура		КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Строки.ВидЗапасов.Контрагент
	|		КОГДА ЕстьNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ					КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО			КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.СуммаСНДС, 0) КАК ВалютнаяСуммаДт,
	|	ВЫБОР КОГДА ЕстьNULL(Склады.ЦеховаяКладовая, ЛОЖЬ) ТОГДА 0 ИНАЧЕ Строки.Количество КОНЕЦ КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаБезНДСРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР 
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			НЕОПРЕДЕЛЕНО
	|		КОГДА Операция.ВернутьМногооборотнуюТару И Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.Валюта
	|	ИНАЧЕ
	|		Операция.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК ВалютаКт,
	|	
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА 
	|			НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР 
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			ВЫБОР КОГДА Суммы.СуммаБезНДСРегл = Суммы.СуммаБезНДС ТОГДА
	|				ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|			КОНЕЦ
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|
	// КТ - Субконто
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.ПодотчетноеЛицо
	|	ИНАЧЕ
	|		Операция.Контрагент
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Операция.Договор
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаПриведенная
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|			КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|		ТОГДА Суммы.СуммаБезНДСРегл
	|		КОГДА ВЫБОР
	|			КОГДА ЕСТЬNULL(Назначения.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|			ТОГДА Назначения.ВидДеятельностиНДС
	|			ИНАЧЕ Операция.ЗакупкаПодДеятельность КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|		ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРКт,
	|	""Поступление товаров на склад"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.Склады КАК Склады
	|	ПО
	|		Склады.Ссылка = Строки.Склад
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках КАК ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках
	|	ПО
	|		Строки.Ссылка = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Регистратор
	|		И Строки.ИдентификаторСтроки = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.Назначения КАК Назначения
	|	ПО
	|		Назначения.Ссылка = Строки.Назначение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК ДанныеСебестоимости
	|	ПО ДанныеСебестоимости.Ссылка = Строки.Ссылка
	|		И ДанныеСебестоимости.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И ДанныеСебестоимости.ИдентификаторСтроки = ДанныеСебестоимости.ИдентификаторФинЗаписи
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|
	|ГДЕ
	|	Строки.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И НЕ Строки.СписатьНаРасходы
	|	И (Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС))
	|		ИЛИ Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) И ДанныеСебестоимости.Ссылка ЕСТЬ NULL)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ЗакупкаЦенностиСоСписаниемНаРасходы    // (Дт 2Х, 44, 91 :: Кт 60, 71, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Списание товаров на расходы (Дт 2Х, 44, 91 :: Кт 60, 71, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	(ВЫБОР
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ НЕ СтатьиНДСРаспределяется.Ссылка ЕСТЬ NULL
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС)
	|	КОНЕЦ) КАК Сумма,
	|	(ВЫБОР
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ НЕ СтатьиНДСРаспределяется.Ссылка ЕСТЬ NULL
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям И Операция.Дата >= &ДатаВключенияСебестоимостиПоНазначениям
	|				И ЕстьNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ЕстьNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		КОГДА Операция.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) 
	|				И Операция.НаправлениеДеятельности.УчетЗатрат
	|			ТОГДА Операция.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,  
	|	ВЫБОР
	|		КОГДА НЕ СтатьиСтроительства.Ссылка ЕСТЬ NULL
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|			ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		КОГДА НЕ СтатьиСтроительства.Ссылка ЕСТЬ NULL
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	ВЫБОР КОГДА Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) И НЕ Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА (ВЫБОР
	|				КОГДА  Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС) ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|					ИЛИ НЕ СтатьиНДСРаспределяется.Ссылка ЕСТЬ NULL
	|					ТОГДА Суммы.СуммаБезНДСРегл
	|				КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|				ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС)
	|			КОНЕЦ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.Валюта
	|	ИНАЧЕ
	|		Операция.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			ВЫБОР 
	|				КОГДА Суммы.СуммаБезНДСРегл = Суммы.СуммаБезНДС
	|					ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал) 
	|			КОНЕЦ
	|		ИНАЧЕ 
	|			НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			Операция.ПодотчетноеЛицо
	|		ИНАЧЕ Операция.Контрагент
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Операция.Договор
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	(ВЫБОР
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыОблагаемыеНДС)
	|				ИЛИ НЕ СтатьиНДСРаспределяется.Ссылка ЕСТЬ NULL
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|			ТОГДА Строки.СуммаВзаиморасчетов - Строки.СуммаНДСВзаиморасчетов
	|		КОГДА Операция.ЗакупкаПодДеятельность В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Строки.СуммаВзаиморасчетов
	|	КОНЕЦ) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Поступление товаров со списанием на расходы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка 
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО 
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиНДСРаспределяется
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиНДСРаспределяется.Ссылка
	|		И СтатьиНДСРаспределяется.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Назначения КАК Назначения
	|	ПО
	|		Назначения.Ссылка = Строки.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Расчеты.Ссылка = Операция.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК ДанныеРасходов
	|	ПО ДанныеРасходов.Регистратор = Строки.Ссылка
	|		И ДанныеРасходов.ИдентификаторФинЗаписи = Строки.ИдентификаторСтроки
	|		И ДанныеРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|
	|ГДЕ
	|	Строки.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Строки.СписатьНаРасходы
	|	И (Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
	|		ИЛИ Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) И ДанныеРасходов.Регистратор ЕСТЬ NULL)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ЗакупкаУслуги                      // (Дт 2Х, 44, 91 :: Кт 60, 71, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Получение услуг (Дт 2Х, 44, 91 :: Кт 60, 71, 76)
	|
	|	Операция.Ссылка КАК Ссылка,
	// Период, Организация, ИдентификаторСтроки 
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	(ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыОблагаемыеНДС)
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|					И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ) КАК Сумма,
	|	(ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыОблагаемыеНДС)
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|					И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|	КОНЕЦ) КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоЗакупке)
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ Строки.СписатьНаРасходы
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Назначения.Договор.ГруппаФинансовогоУчета
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ Строки.СписатьНаРасходы
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.СтатьяРасходов
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Назначения.Договор.Подразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	// Направление деятельности для работ, не списываемых на расходы, определяется так же как для товаров - по назначению.
	// Если работа или услуга списывается на расходы, то направление наследуется из шапки документа.
	|	ВЫБОР
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			И &УчитыватьСебестоимостьТоваровПоНазначениям И НЕ Строки.СписатьНаРасходы
	|			И Операция.Дата >= &ДатаВключенияСебестоимостиПоНазначениям
	|		ТОГДА ЕСТЬNULL(Строки.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ Строки.СписатьНаРасходы
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|		ИНАЧЕ Операция.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Назначения.Договор.Контрагент
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ Строки.СписатьНаРасходы
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ Строки.СтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Строки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ Строки.СписатьНаРасходы
	|			ИЛИ ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Строки.Номенклатура
	|		ИНАЧЕ Строки.АналитикаРасходов
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			И ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.СуммаСНДС, 0) КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ИСТИНА
	|		ТОГДА (ВЫБОР
	|					КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|					ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|					КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|						ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|						ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|						КОНЕЦ В (&ТипыОблагаемыеНДС)
	|					ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|					ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|						И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|					ТОГДА Суммы.СуммаБезНДСРегл
	|					КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|						ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|						ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|						КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|					ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|			КОНЕЦ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА (Строки.СписатьНаРасходы ИЛИ Строки.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|		И ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ЛОЖЬ
	|		ТОГДА (ВЫБОР
	|				КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|					ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|					ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|					КОНЕЦ В (&ТипыОблагаемыеНДС)
	|					ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|					ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|						И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|					ТОГДА Суммы.СуммаПриведеннаяРегл
	|				КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|					ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|					ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|					КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|				ТОГДА Суммы.СуммаПриведеннаяРегл + Суммы.СуммаНДСРегл
	|			КОНЕЦ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ИСТИНА
	|		ТОГДА Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	(ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КОНЕЦ) КАК ВидСчетаКт,
	|	(ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета) КОНЕЦ) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.Валюта
	|	ИНАЧЕ
	|		Операция.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	(ВЫБОР
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|			(ВЫБОР КОГДА Суммы.СуммаБезНДСРегл = Суммы.СуммаБезНДС ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал) КОНЕЦ)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК СчетКт,
	|	(ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА Операция.ПодотчетноеЛицо
	|		ИНАЧЕ Операция.Контрагент КОНЕЦ) КАК СубконтоКт1,
	|	(ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Операция.Договор КОНЕЦ) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	(ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыОблагаемыеНДС)
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|					И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА 
	|				Суммы.СуммаПриведенная
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА 
	|				Суммы.СуммаПриведенная + Суммы.СуммаНДС
	|	КОНЕЦ) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(ВЫБОР
	|		КОГДА ЕстьNULL(Назначения.ТипНазначения, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|		ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыОблагаемыеНДС)
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.НДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ) 
	|				ИЛИ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС, ЛОЖЬ)
	|					И ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА Суммы.СуммаБезНДСРегл
	|		КОГДА ВЫБОР КОГДА ЕСТЬNULL(Строки.Назначение.ВидДеятельностиНДС,&ПустойТипНалогообложения) <> &ПустойТипНалогообложения
	|				ТОГДА Строки.Назначение.ВидДеятельностиНДС
	|				ИНАЧЕ Операция.ЗакупкаПодДеятельность
	|				КОНЕЦ В (&ТипыНеОблагаемыеНДС)
	|			ТОГДА Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл
	|	КОНЕЦ) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ТОГДА ""Получение услуг на прочие активы""
	|		ИНАЧЕ ""Поступление услуг""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = Строки.Ссылка 
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках КАК ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках
	|	ПО
	|		Строки.Ссылка = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Регистратор
	|		И Строки.ИдентификаторСтроки = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Назначения КАК Назначения
	|	ПО
	|		Назначения.Ссылка = Строки.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Расчеты.Ссылка = Операция.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК ДанныеСебестоимости
	|	ПО ДанныеСебестоимости.Ссылка = Строки.Ссылка
	|		И ДанныеСебестоимости.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И ДанныеСебестоимости.ИдентификаторСтроки = ДанныеСебестоимости.ИдентификаторФинЗаписи
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ДанныеРасходов
	|	ПО (ДанныеРасходов.Регистратор = Строки.Ссылка)
	|		И (ДанныеРасходов.ИдентификаторФинЗаписи = Строки.ИдентификаторСтроки)
	|		И (ДанныеРасходов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо))
	|
	|ГДЕ
	|	Строки.Номенклатура.ТипНоменклатуры НЕ В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И НЕ (Расчеты.Договор.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.НеотфактурованныеПоставкиТоваровИУслуг)
	|		И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки)))
	|	И (Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
	|		, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки))
	|		ИЛИ Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) И ДанныеСебестоимости.Ссылка ЕСТЬ NULL
	|		И ДанныеРасходов.Регистратор ЕСТЬ NULL)";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

	// НДС
	
#Область ЗакупкаНДС                         // (Дт 19 :: Кт 60, 71, 76)
	ТекстЗапроса = "
	|ВЫБРАТЬ //// НДС при приобретении по рег. НДСПредъявленный (Дт 19 :: Кт 60, 71, 76) @НДСПредъявленный
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	// Сумма
	|	СУММА(НДСПредъявленный.НДС) КАК Сумма,
	|	СУММА(НДСПредъявленный.НДСУпр) КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
	|	НДСПредъявленный.ВидДеятельностиНДС                                    КАК АналитикаУчетаДт,
	|	НДСПредъявленный.ВидЦенности                                           КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                               КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)                 КАК ПодразделениеДт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	// ДТ - Субконто
	|	Операция.Контрагент КАК СубконтоДт1,
	|	Операция.Ссылка КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.Валюта
	|	ИНАЧЕ
	|		Операция.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ВЫБОР 
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		ВЫБОР КОГДА Операция.Валюта = &ВалютаРеглУчета ТОГДА
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|		КОНЕЦ
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|	
	// КТ - Субконто
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.ПодотчетноеЛицо
	|	ИНАЧЕ
	|		Операция.Контрагент
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Операция.Договор
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		СУММА(Строки.СуммаНДС)
	|	ИНАЧЕ
	|		СУММА(Строки.СуммаНДСВзаиморасчетов)
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС при приобретении"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		НДСПредъявленный.Регистратор = Строки.Ссылка
	|		И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НДСПредъявленный.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И НЕ НДСПредъявленный.РегламентнаяОперация
	|		И НЕ НДСПредъявленный.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|	ПО
	|		Строки.СтатьяРасходов = Статьи.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Расчеты.Ссылка = Операция.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО ДокументыКОтражению.Ссылка = ВводСобственныхСредств.Ссылка
	|		И НДСПредъявленный.СчетФактура = ВводСобственныхСредств.СчетФактура
	|		И НДСПредъявленный.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|	ИЛИ НДСПредъявленный.ИдентификаторФинЗаписи = """"
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.Контрагент,
	|	Операция.Договор,
	|	Операция.ПодотчетноеЛицо,
	|	Операция.ВалютаВзаиморасчетов,
	|	Операция.ХозяйственнаяОперация,
	|	Операция.Подразделение,
	|	Операция.НаправлениеДеятельности,
	|	Операция.ГруппаФинансовогоУчета,
	|	НДСПредъявленный.ВидДеятельностиНДС,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности),
	|	НДСПредъявленный.ВидЦенности,
	|	Статьи.ВариантРаспределенияРасходовРегл,
	|	Строки.АналитикаРасходов,
	|	Расчеты.Подразделение, 
	|	Расчеты.НаправлениеДеятельности, 
	|	Расчеты.ГруппаФинансовогоУчета
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ //// НДС при приобретении по рег. НДСПредъявленный (Дт 19 :: Кт 60, 71, 76) @НДСПредъявленный
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	// Сумма
	|	СУММА(НДСПредъявленный.НДС) КАК Сумма,
	|	СУММА(НДСПредъявленный.НДСУпр) КАК СуммаУУ,
	|
	// ДТ - Вид счета, Аналитика учета, Место учета
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
	|	НДСПредъявленный.ВидДеятельностиНДС                                    КАК АналитикаУчетаДт,
	|	НДСПредъявленный.ВидЦенности                                           КАК МестоУчетаДт,
	|
	// ДТ - Валюта, Подразделение, Счет
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                               КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)                 КАК ПодразделениеДт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	// ДТ - Субконто
	|	Операция.Контрагент КАК СубконтоДт1,
	|	Операция.Ссылка КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	// ДТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	// КТ - Вид счета, Аналитика учета, Место учета
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЕСТЬNULL(Расчеты.ГруппаФинансовогоУчета, Операция.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	// КТ - Валюта, Подразделение, Счет
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.Валюта
	|	ИНАЧЕ
	|		Операция.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК ВалютаКт,
	|	ЕСТЬNULL(Расчеты.Подразделение, Операция.Подразделение) КАК ПодразделениеКт,
	|	ВЫБОР 
	|		КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		ВЫБОР КОГДА Операция.Валюта = &ВалютаРеглУчета ТОГДА
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|		КОНЕЦ
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|	
	// КТ - Субконто
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		Операция.ПодотчетноеЛицо
	|	ИНАЧЕ
	|		Операция.Контрагент
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Операция.Договор
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	// КТ - Валютная сумма, Количество, Суммы ПР,ВР,НУ
	|	ВЫБОР КОГДА Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо) ТОГДА
	|		СУММА(Строки.СуммаНДС)
	|	ИНАЧЕ
	|		СУММА(Строки.СуммаНДСВзаиморасчетов)
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС при приобретении"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.ВидыЗапасов КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК ДанныеТовары
	|	ПО
	|		ДанныеТовары.Ссылка = Строки.Ссылка
	|		И ДанныеТовары.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		НДСПредъявленный.Регистратор = Строки.Ссылка
	|		И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НДСПредъявленный.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И НЕ НДСПредъявленный.РегламентнаяОперация
	|		И НЕ НДСПредъявленный.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		Расчеты.Ссылка = Операция.Ссылка
	|		И Расчеты.СуммаРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО ДокументыКОтражению.Ссылка = ВводСобственныхСредств.Ссылка
	|		И НДСПредъявленный.СчетФактура = ВводСобственныхСредств.СчетФактура
	|		И НДСПредъявленный.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	ДанныеТовары.Ссылка ЕСТЬ NULL
	|	И (Операция.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|	ИЛИ НДСПредъявленный.ИдентификаторФинЗаписи = """")
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.Контрагент,
	|	Операция.Договор,
	|	Операция.ПодотчетноеЛицо,
	|	Операция.ВалютаВзаиморасчетов,
	|	Операция.ХозяйственнаяОперация,
	|	Операция.Подразделение,
	|	Операция.НаправлениеДеятельности,
	|	Операция.ГруппаФинансовогоУчета,
	|	НДСПредъявленный.ВидДеятельностиНДС,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности),
	|	НДСПредъявленный.ВидЦенности,
	|	Расчеты.Подразделение, 
	|	Расчеты.НаправлениеДеятельности, 
	|	Расчеты.ГруппаФинансовогоУчета
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
	ТекстыОтражения.Добавить(ТекстПроцентыПоДисконтированию()); // Проценты по дисконтированию (Дт 76.хх :: Кт 60, 76)
	
	ТекстыОтражения.Добавить(ТекстПодотчетникиЗакупкаЦенности()); // Поступление услуг/ценностей в собственность (Дт 10, 20, 41 :: Кт 71)
	ТекстыОтражения.Добавить(ТекстПодотчетникиСписаниеНаРасходы()); // Списание поступивших ценностей на расходы (Дт 91.2 :: Кт 71)
	ТекстыОтражения.Добавить(ТекстПодотчетникиЗакупкаНДС()); // НДС при приобретении по рег. НДСПредъявленный (Дт 19 :: Кт 71) @НДСПредъявленный
	ТекстыОтражения.Добавить(ТекстПриходТоваровЗаБалансом()); // Приход товаров за балансом (Дт 002, 003, 004 :: Кт)
	
	//-- Локализация
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	
	//++ Локализация
	
	#Область СуммыДокументовВВалютахУчета
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Суммы.Регистратор КАК Регистратор,
	|	Суммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР
	|		КОГДА Суммы.СуммаПриведеннаяРегл = 0
	|		ТОГДА Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ Суммы.СуммаПриведеннаяРегл
	|	КОНЕЦ КАК СуммаПриведеннаяРегл,
	|
	|	ВЫБОР
	|		КОГДА Суммы.СуммаПриведеннаяУпр = 0
	|		ТОГДА Суммы.СуммаБезНДСУпр
	|		ИНАЧЕ Суммы.СуммаПриведеннаяУпр
	|	КОНЕЦ КАК СуммаПриведеннаяУпр,
	|
	|	ВЫБОР
	|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|		ТОГДА Суммы.СуммаБезНДС
	|		КОГДА Суммы.СуммаПриведенная = 0
	|		ТОГДА Суммы.СуммаВзаиморасчетов - Суммы.СуммаНДСВзаиморасчетов
	|		ИНАЧЕ Суммы.СуммаПриведенная
	|	КОНЕЦ КАК СуммаПриведенная,
	|	
	|	Суммы.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	Суммы.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	Суммы.СуммаБезНДС КАК СуммаБезНДС,
	|	
	|	Суммы.СуммаНДСРегл КАК СуммаНДСРегл,
	|	Суммы.СуммаНДСУпр КАК СуммаНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|		ТОГДА Суммы.СуммаНДС
	|		ИНАЧЕ Суммы.СуммаНДСВзаиморасчетов
	|	КОНЕЦ КАК СуммаНДС
	|	
	|ПОМЕСТИТЬ СуммыДокументовВВалютахУчета
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|		ПО Суммы.Регистратор = ДокументыКОтражению.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПриобретениеТоваровУслуг КАК Документ
	|		ПО Документ.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстыЗапроса.Добавить("");
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

//++ Локализация
#Область Фискализация

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблицу с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Валюта               КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка = &Ссылка
	|;";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, Истина);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.ЕдиницаИзмерения КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	(ВЫРАЗИТЬ((ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС) /
	|		ТаблицаТоваров.КоличествоУпаковок КАК ЧИСЛО(31,2))) КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК Сумма,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Количество             КАК Количество,
	|	ТаблицаНоменклатуры.Сумма                  КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.СуммаНДС               КАК СуммаНДС,
	|	ТаблицаНоменклатуры.Цена                   КАК Цена,
	|	
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаНоменклатуры.Номенклатура) = ТИП(Строка) ТОГДА
	|			ТаблицаНоменклатуры.Номенклатура
	|		ИНАЧЕ
	|			ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаНоменклатуры.Упаковка               КАК Упаковка,
	|	ТаблицаНоменклатуры.Упаковка               КАК УпаковкаНаименование
	|
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает параметры операции фискализации чека для печати чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Структура - Структура параметров операции фискализации чека
Функция ОсновныеПараметрыОперации(Форма) Экспорт
	
	ОсновныеПараметрыОперации = ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации();
	
	ОсновныеПараметрыОперации.ДокументСсылка       = Форма.Объект.Ссылка;
	ОсновныеПараметрыОперации.Организация          = Форма.Объект.Организация;
	ОсновныеПараметрыОперации.Контрагент           = Форма.Объект.Контрагент;
	ОсновныеПараметрыОперации.Партнер              = Форма.Объект.Партнер;
	ОсновныеПараметрыОперации.Валюта               = Форма.Объект.Валюта;
	ОсновныеПараметрыОперации.ВалютаВзаиморасчетов = Форма.Объект.ВалютаВзаиморасчетов;
	ОсновныеПараметрыОперации.СуммаДокумента       = Форма.Объект.СуммаДокумента;
	
	Возврат ОсновныеПараметрыОперации;
	
КонецФункции

// Определяет, разрешено ли пробитие фискального чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Булево - Истина, если разрешено пробитие чека
Функция РазрешеноПробитиеФискальныхЧековПоДокументу(Форма) Экспорт
	
	РазрешеноПробитиеФискальныхЧековПоДокументу = Ложь;
	
	Если ЗначениеЗаполнено(Форма.Объект.Контрагент)
		И ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация) Тогда
		
		ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Контрагент, "ЮрФизЛицо");
		
		КонтрагентЯвляетсяФизЛицом = (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
		ДокументЯвляетсяРеализациейКлиенту =
		          (Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
		
		Если КонтрагентЯвляетсяФизЛицом И ДокументЯвляетсяРеализациейКлиенту Тогда
			РазрешеноПробитиеФискальныхЧековПоДокументу	= Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РазрешеноПробитиеФискальныхЧековПоДокументу;
	
КонецФункции

// Формирует массив форматированных строк для формирования гиперссылки пробития фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ-ссылка, по которому пробивается фискальный чек
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// 	МассивПредставлений - Массив из ФорматированнаяСтрока - Массив форматированных строк для формирования гиперссылки
//    пробития фискального чека.
Процедура ОбновитьГиперссылкуПробитияФискальногоЧека(ДокументСсылка, Форма, МассивПредставлений) Экспорт
	
	ФискальнаяОперацияДанныеЖурнала = ФормированиеФискальныхЧековСервер.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументСсылка);
	
	Если ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
		
		НомерЧекаККМ = ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ;
		ТекстСсылки = "ОткрытьЗаписьФискальнойОперации";
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуЧекПробит(МассивПредставлений, НомерЧекаККМ, ТекстСсылки);
		
	ИначеЕсли ФормированиеФискальныхЧековСервер.ЕстьПравоНаПробитиеФискальногоЧекаПоДокументу(ДокументСсылка) Тогда
		
		Если ФормированиеФискальныхЧековСервер.ЕстьПодключенноеОборудованиеККассамОрганизации(Форма.Объект.Организация) Тогда 
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуПробитьЧек(МассивПредставлений, "ПробитьЧек");
		Иначе
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений, "НастроитьОборудование");
		КонецЕсли;
		
	Иначе
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет виды фискальных чеков, доступных по документу
// 
// Параметры:
// 	ВидыЧеков - ТаблицаЗначений - Таблица значений, содержащая виды фискальных чеков, доступных по документу
// 	Операция - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция по документу
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ЗаполнитьВидыФискальныхЧековПоДокументу(ВидыЧеков, Операция, ИмяКомандыПробитияЧека) Экспорт
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Неопределено;
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧекКоррекции;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Перечисления.ВидыЧековКоррекции.НеприменениеККТ;
	
КонецПроцедуры

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, НалогообложениеНДС");
	Если РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
	ИначеЕсли РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	Иначе
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на поставку
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями
Функция ПредметыРасчетовПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ЦенаВключаетНДС, ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры, 
	|	ТабличнаяЧасть.ЗаказПоставщику                  КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ТабличнаяЧасть.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	ТабличнаяЧасть.Номенклатура.ЕдиницаИзмерения КАК БазоваяУпаковка,
	|	
	|	СпрНоменклатура.КодВидаНоменклатурнойКлассификации,
	|
	|	ТабличнаяЧасть.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	ТабличнаяЧасть.Номенклатура.ОсобенностьУчета КАК ОсобенностьУчета,
	|	
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Агент,
	|	
	|	ТабличнаяЧасть.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТабличнаяЧасть.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.Упаковка КАК УпаковкаНаименование,
	|
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.КоличествоУпаковок КАК КоличествоУпаковок,
	|
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ (ТабличнаяЧасть.СуммаСНДС + ТабличнаяЧасть.СуммаРучнойСкидки) / ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|
	|	ТабличнаяЧасть.СуммаРучнойСкидки КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС,
	|	ТабличнаяЧасть.СуммаСНДС КАК СуммаСНДС,
	|	СпрНоменклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений) КАК МернаяЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТабличнаяЧасть.Номенклатура
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (Не &ТребуетсяЗалогЗаТару
	|			И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	|ПОМЕСТИТЬ ТаблицаАкцизныеМарки
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО ТаблицаТовары.Номенклатура = Штрихкоды.Номенклатура
	|			И ТаблицаТовары.Характеристика = Штрихкоды.Характеристика
	|			И (ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|				ИЛИ &ТекстЗапросаКоэффициентУпаковки1 = 1
	|					И ТаблицаТовары.Номенклатура.ЕдиницаИзмерения = ТаблицаТовары.Упаковка.ЕдиницаИзмерения
	|					И Штрихкоды.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.ПриоритетШтрихКода) КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ ПриоритетыШтрихКода
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.Штрихкод) КАК Штрихкод
	|ПОМЕСТИТЬ ШтрихкодыНоменклатуры
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыШтрихКода КАК ПриоритетыШтрихКода
	|		ПО Штрихкоды.Номенклатура = ПриоритетыШтрихКода.Номенклатура
	|			И Штрихкоды.Характеристика = ПриоритетыШтрихКода.Характеристика
	|			И Штрихкоды.Упаковка = ПриоритетыШтрихКода.Упаковка
	|			И Штрихкоды.ПриоритетШтрихКода = ПриоритетыШтрихКода.ПриоритетШтрихКода
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.Заказ КАК Заказ,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.БазоваяУпаковка КАК БазоваяУпаковка,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета КАК ОсобенностьУчета,
	|	Товары.Агент КАК Агент,
	|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА Товары.МернаяЕдиницаИзмерения
	|			ТОГДА """"
	|		ИНАЧЕ ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")
	|	КОНЕЦ КАК Штрихкод
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = Товары.Номенклатура)
	|			И (ШтрихкодыНоменклатуры.Характеристика = Товары.Характеристика)
	|			И (ШтрихкодыНоменклатуры.Упаковка = Товары.Упаковка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Штрихкоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ШтрихкодыНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПриоритетыШтрихКода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.НомерСтроки КАК НомерСтроки,
	|	Серии.Серия КАК СерияТаблицыСерий,
	|	Серии.Количество КАК КоличествоУпаковок,
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Заказ,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета,
	|	Товары.Агент,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.Серия КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Упаковка,
	|	Товары.УпаковкаНаименование,
	|	Товары.БазоваяУпаковка,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.Штрихкод
	|ИЗ
	|	Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка"                   , ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"          , РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , РеквизитыДокумента.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", РеквизитыДокумента.ВернутьМногооборотнуюТару);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТовары.Упаковка",
		"ТаблицаТовары.Номенклатура"));
	Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений", Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ПредметыРасчетов = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	
	Возврат ПредметыРасчетов;
	
КонецФункции

#КонецОбласти
//-- Локализация

// Заполняет массив допустимых наименований входящих документов.
//
// Параметры:
//  МассивНаименований	 - Массив - массив наименования входящих документов.
//
Процедура ДополнитьНаименованияВходящихДокументов(МассивНаименований) Экспорт
	//++ Локализация
	МассивНаименований.Добавить(НСтр("ru = 'УПД';
									|en = 'UTD'"));
	//-- Локализация
КонецПроцедуры

// Возвращает текст запроса остатка к оформлению.
// 
// Параметры:
//  ТекстЗапроса - Строка - текст запроса.
//
Процедура ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса) Экспорт
	
//++ Локализация
	ТекстЗапроса = "ВЫБРАТЬ
	|	Таблица.Регистратор КАК Ордер
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Обороты(,, Регистратор, ДокументПоступления = &Распоряжение) КАК Таблица
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.ПриходныйОрдерНаТовары)
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Регистратор
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.КОформлениюПоступленийПоОрдерамПриход) > 0";
//-- Локализация
	
КонецПроцедуры

// Заполняет сведения о реквизитах документа, зависимых и не зависимых от хозяйственной операции.
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция документа.
//	ВсеРеквизиты - Массив Из Строка - реквизиты, которые не зависят от хозяйственной операции.
//	РеквизитыОперации - Массив Из Строка - реквизиты, которые зависят от хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, ВсеРеквизиты, РеквизитыОперации) Экспорт
	
	//++ Локализация
	ВсеРеквизиты.Добавить("Товары.КоличествоПоРНПТ");
	
	ХозОперацииЗакупкаУПоставщика = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
		Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	
	ХозОперацииЗакупкаВСтранахЕАЭС = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
		Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	
	Если ХозОперацииЗакупкаУПоставщика.Найти(ХозяйственнаяОперация) <> Неопределено
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет Тогда
		
		РеквизитыОперации.Добавить("Товары.КоличествоПоРНПТ");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо Тогда
		
		РеквизитыОперации.Добавить("Товары.КоличествоПоРНПТ");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
		
		РеквизитыОперации.Добавить("Товары.КоличествоПоРНПТ");
		
	ИначеЕсли ХозОперацииЗакупкаВСтранахЕАЭС.Найти(ХозяйственнаяОперация) <> Неопределено Тогда
		
		РеквизитыОперации.Добавить("Товары.КоличествоПоРНПТ");
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//  Объект - ДокументОбъект - Обрабатываемый объект.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОперацииСПрослеживаемымиТоварамиРасширенный(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры
//++ Локализация

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОперацииСПрослеживаемымиТоварамиРасширенный(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)

	ИмяРегистра = "ОперацииСПрослеживаемымиТоварамиРасширенный";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТоварыОрганизаций", ТекстыЗапроса) Тогда
		Документы.ПриобретениеТоваровУслуг.ТекстЗапросаВтТоварыОрганизаций(Запрос, ТекстыЗапроса);
	КонецЕсли;

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		Документы.ПриобретениеТоваровУслуг.ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтЕстьДвиженияПоРасширенномуРегиструПрослеживаемости", ТекстыЗапроса) Тогда
		УчетПрослеживаемыхТоваровЛокализация.ТекстЗапросаВтЕстьДвиженияПоРасширенномуРегиструПрослеживаемости(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ОтражатьВОтчетеОбОперациях =УчетНДСЛокализация.ПрименяетсяЛьготноеНалогообложениеНДС(Запрос.Параметры.Организация,Запрос.Параметры.Период);
	Запрос.УстановитьПараметр("ОтражатьВОтчетеОбОперациях", ОтражатьВОтчетеОбОперациях);
	
	ПрослеживаемостьУП.УстановитьПараметрыЗапросаТипыСобственныхТоваров(Запрос);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаДокумента.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)) КАК НеОблагаетсяНДС
	|ПОМЕСТИТЬ ВТДанныеДокументаПоНДС
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаДокумента;
	|
	|///////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	втТоварыОрганизаций.Период КАК Период,
	|	втТоварыОрганизаций.Организация КАК Организация,
	|	втТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	втТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	втТоварыОрганизаций.НомерГТД КАК РНПТ,
	|	СУММА(втТоварыОрганизаций.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	0 КАК СтоимостьПоРНПТ,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение)
	|	КОНЕЦ КАК КодОперации,
	|	&Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС 
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	ВЫБОР
	|		КОГДА НЕ втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	втТоварыОрганизаций КАК втТоварыОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО втТоварыОрганизаций.НомерГТД = НомераГТД.Ссылка
	|			И (НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|				ИЛИ НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта)),
	|	ВТДанныеДокументаПоНДС КАК ВТДанныеДокументаПоНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	втТоварыОрганизаций.Период,
	|	втТоварыОрганизаций.Организация,
	|	втТоварыОрганизаций.ВидЗапасов,
	|	втТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС  
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ,
	|	втТоварыОрганизаций.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(втТоварыОрганизаций.КоличествоПоРНПТ) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	ТаблицаТовары.ВидЗапасов,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.НомерГТД,
	|	СУММА(ТаблицаТовары.КоличествоПоРНПТ),
	|	СУММА(ТаблицаТовары.СтоимостьРегл),
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение),
	|	&Контрагент,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС 
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ,
	|	ТаблицаТовары.ИдентификаторСтроки
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаТовары.НомерГТД = НомераГТД.Ссылка
	|			И (НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|				ИЛИ НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта)),
	|	ВТДанныеДокументаПоНДС 
	|ГДЕ
	|	ТаблицаТовары.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаТовары.СписатьНаРасходы
	|	И НЕ &ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
	|	И НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.НомерГТД,
	|	ТаблицаТовары.ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС  
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ &НалогообложениеОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС  
	|				ИЛИ &ОтражатьВОтчетеОбОперациях
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ,
	|	ТаблицаТовары.ИдентификаторСтроки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.КоличествоПоРНПТ) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	ТаблицаТовары.ВидЗапасов,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.НомерГТД,
	|	СУММА(ТаблицаТовары.КоличествоПоРНПТ),
	|	СУММА(ТаблицаТовары.СуммаНДСРегл + ТаблицаТовары.СуммаБезНДСРегл),
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПередачаВПроизводство),
	|	&Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях),
	|	ТаблицаТовары.ИдентификаторСтроки
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаТовары.НомерГТД = НомераГТД.Ссылка
	|			И (НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|				ИЛИ НомераГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|ГДЕ
	|	ТаблицаТовары.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаТовары.СписатьНаРасходы
	|	И НЕ &ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
	|	И НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.НомерГТД,
	|	ТаблицаТовары.ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ТаблицаТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|	КОНЕЦ,
	|	ТаблицаТовары.ИдентификаторСтроки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.КоличествоПоРНПТ) <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции // ()

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.ПриобретениеТоваровУслуг.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		Документы.ПриобретениеТоваровУслуг.ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете();
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = РеглУчетПроведениеСервер.ТекстЗапросаОтраженияПоОбъектамРасчетов("Документ.ПриобретениеТоваровУслуг.РасшифровкаПлатежа");
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = РеглУчетПроведениеСервер.ТекстЗапросаОтраженияПоДоговору("&Договор");
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = РеглУчетПроведениеСервер.ТекстЗапросаОтраженияПоЗаказу("Документ.ПриобретениеТоваровУслуг", "ЗаказПоставщику");
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ

//-- Локализация

// Процедура дополняет текст запроса для регистра накопления ТоварыКОформлениюДокументовИмпорта
//
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса проведения для дополнения.
//
Процедура ДополнитьТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(ТекстЗапроса) Экспорт
//++ Локализация
	ТекстЗапросаДополнение = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Партнер КАК Поставщик,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА &Ссылка
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ КАК ДокументПоступления,
	|
	|	&ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров КАК ТипДокументаИмпорта,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.КоличествоПоРНПТ КАК КоличествоПоРНПТ,	
	|
	|	ТаблицаТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|							И &Валюта <> &РегламентированногоУчетаВалюта
	|						ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|						И &Валюта <> &РегламентированногоУчетаВалюта
	|					ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	ИСТИНА КАК Первичное,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпорта КАК ВтТоварыКОформлениюДокументовИмпорта
	|		ПО (ИСТИНА)
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) >= &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров	
	|	И ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПрослеживаемыйТовар	
	|	И ТаблицаТовары.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И &ХозяйственнаяОперация В (&ХозОперацииЗакупкаВСтранахЕАЭС)
	|	И НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Организация,
	|	&Партнер,
	// На склад.
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ТаблицаВидыЗапасов.КлючАналитики) КАК АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасовСкладские.Ссылка, ТаблицаВидыЗапасов.ВидЗапасов) КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА &Ссылка
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ,
	|
	|   &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров КАК ТипДокументаИмпорта,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|							И &Валюта <> &РегламентированногоУчетаВалюта
	|						ТОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|						И &Валюта <> &РегламентированногоУчетаВалюта
	|					ТОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ИСТИНА,
	|	""""
	|ИЗ
	|	ВтВидыЗапасовДляСоединенияАналитик КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпорта КАК ВтТоварыКОформлениюДокументовИмпорта
	|		ПО (ИСТИНА)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО ТаблицаВидыЗапасов.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаВидыЗапасов.Характеристика = Аналитика.Характеристика
	|			И ТаблицаВидыЗапасов.Серия = Аналитика.Серия
	|			И ТаблицаВидыЗапасов.Назначение = Аналитика.Назначение
	|			И ТаблицаВидыЗапасов.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|			И &Склад = Аналитика.МестоХранения 
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСкладские
	|		ПО ВидыЗапасовСкладские.Организация = ТаблицаВидыЗапасов.ВидЗапасов.Организация
	|			И ВидыЗапасовСкладские.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|			И ВидыЗапасовСкладские.ВладелецТовара = ТаблицаВидыЗапасов.ВидЗапасов.ВладелецТовара
	|			И ВидыЗапасовСкладские.НалогообложениеНДС = ТаблицаВидыЗапасов.ВидЗапасов.НалогообложениеНДС
	|			И ВидыЗапасовСкладские.ГруппаФинансовогоУчета = ТаблицаВидыЗапасов.ВидЗапасов.ГруппаФинансовогоУчета
	|			И ВидыЗапасовСкладские.ГруппаПродукции = ТаблицаВидыЗапасов.ВидЗапасов.ГруппаПродукции
	|			И ВидыЗапасовСкладские.ВидЦены = ТаблицаВидыЗапасов.ВидЗапасов.ВидЦены
	|			И НЕ ВидыЗапасовСкладские.ЭтоДубль
	|			И НЕ ВидыЗапасовСкладские.Устаревший
	|			И НЕ ВидыЗапасовСкладские.ПометкаУдаления
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) >= &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров	
	|	И ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ПрослеживаемыйТовар	
	|	И ТаблицаВидыЗапасов.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И &ХозяйственнаяОперация В (&ХозОперацииЗакупкаВСтранахЕАЭС)
	|	И НЕ &ИспользуетсяОперацияНеотфактуровка
	|	И &ИспользуетсяДокументПоступлениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Организация,
	|	&Партнер,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ТаблицаТовары.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасовСкладские.Ссылка, ТаблицаТовары.ВидЗапасов),
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА &Ссылка
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ,
	|
	|   &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров КАК ТипДокументаИмпорта,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаТовары.Количество,
	|	ВЫБОР
	|		КОГДА НЕ &КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|							И &Валюта <> &РегламентированногоУчетаВалюта
	|						ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОГДА НЕ ВтТоварыКОформлениюДокументовИмпорта.ДокументПоступленияЗаполнен
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ВалютаВзаиморасчетов = &РегламентированногоУчетаВалюта
	|						И &Валюта <> &РегламентированногоУчетаВалюта
	|					ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ИСТИНА,
	|	""""
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпорта КАК ВтТоварыКОформлениюДокументовИмпорта
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|			И ТаблицаТовары.Серия = Аналитика.Серия
	|			И ТаблицаТовары.Назначение = Аналитика.Назначение
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|			И Аналитика.МестоХранения = ТаблицаТовары.Ссылка.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСкладские
	|		ПО ВидыЗапасовСкладские.Организация = ТаблицаТовары.ВидЗапасов.Организация
	|			И ВидыЗапасовСкладские.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|			И ВидыЗапасовСкладские.ВладелецТовара = ТаблицаТовары.ВидЗапасов.ВладелецТовара
	|			И ВидыЗапасовСкладские.НалогообложениеНДС = ТаблицаТовары.ВидЗапасов.НалогообложениеНДС
	|			И ВидыЗапасовСкладские.ГруппаФинансовогоУчета = ТаблицаТовары.ВидЗапасов.ГруппаФинансовогоУчета
	|			И ВидыЗапасовСкладские.ГруппаПродукции = ТаблицаТовары.ВидЗапасов.ГруппаПродукции
	|			И ВидыЗапасовСкладские.ВидЦены = ТаблицаТовары.ВидЗапасов.ВидЦены
	|			И НЕ ВидыЗапасовСкладские.ЭтоДубль
	|			И НЕ ВидыЗапасовСкладские.Устаревший
	|			И НЕ ВидыЗапасовСкладские.ПометкаУдаления
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) >= &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров	
	|	И ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПрослеживаемыйТовар	
	|	И ТаблицаТовары.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И &ХозяйственнаяОперация В (&ХозОперацииЗакупкаВСтранахЕАЭС)
	|	И &ИспользуетсяОперацияНеотфактуровка
	|	И &ИспользуетсяДокументПоступлениеТоваров
	|";

	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(ТекстЗапроса);
	МассивСтрок.Добавить("ОБЪЕДИНИТЬ ВСЕ");
	МассивСтрок.Добавить(ТекстЗапросаДополнение);
	ТекстЗапроса = СтрСоединить(МассивСтрок, Символы.ПС);
	
//-- Локализация		
КонецПроцедуры

#КонецОбласти

#Область ГосИС

//++ Локализация
Процедура ЗаполнитьПризнакЕстьАлкогольнаяПродукция(Объект)
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатура
	|ГДЕ
	|	ТаблицаНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаНоменклатура.Ссылка В (&МассивНоменклатуры)";
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", Объект.Товары.ВыгрузитьКолонку("Номенклатура"));
	Объект.ЕстьАлкогольнаяПродукция = НЕ Запрос.Выполнить().Пустой();
	
КонецПроцедуры
//-- Локализация

#КонецОбласти

#Область ФрагментыПроводокРеглУчета

//++ Локализация

//++ НЕ УТ

Функция ТекстПроцентыПоДисконтированию()
	
	Текст = "
	|ВЫБРАТЬ // Проценты по дисконтированию (Дт 76.хх :: Кт 60, 76)
	|	
	|	РасчетыПоДисконтированию.Регистратор КАК Ссылка,
	|	РасчетыПоДисконтированию.Период КАК Период,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	РасчетыПоДисконтированию.СуммаДисконтированияРегл КАК Сумма,
	|	РасчетыПоДисконтированию.СуммаДисконтированияУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПроцентыПоДисконтированию) КАК ВидСчетаДт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	РасчетыПоДисконтированию.Валюта КАК ВалютаДт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Подразделение КАК ПодразделениеДт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Контрагент КАК СубконтоДт1,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	РасчетыПоДисконтированию.СуммаДисконтирования КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	РасчетыПоДисконтированию.СуммаДисконтированияРегл КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	РасчетыПоДисконтированию.Валюта КАК ВалютаКт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Подразделение КАК ПодразделениеКт,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Контрагент КАК СубконтоКт1,
	|	РасчетыПоДисконтированию.ОбъектРасчетов.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	РасчетыПоДисконтированию.СуммаДисконтирования КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	РасчетыПоДисконтированию.СуммаДисконтированияРегл КАК СуммаВРКт,
	|	""Проценты по дисконтированию"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПроцентныеРасходыДисконтирования КАК РасчетыПоДисконтированию
	|	ПО
	|		РасчетыПоДисконтированию.Регистратор = ДокументыКОтражению.Ссылка
	|";
	Возврат Текст;
	
КонецФункции

Функция ТекстПриходТоваровЗаБалансом()
	
	ТекстЗапроса = ПроизводствоБезЗаказаЛокализация.ТекстПриходТоваровЗаБалансом(Ложь, Ложь);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", "ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		ТОГДА ""Покупка материалов давальца""
	|		КОГДА Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение))
	|		ТОГДА ""Прием товаров на ответственное хранение""
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ТОГДА ""Прием товаров на комиссию""
	|		КОГДА Строки.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработкуКОформлениюСписания),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработкуПереданныеПартнерам))
	|		ТОГДА ""Покупка материалов, принятых в переработку""
	|		ИНАЧЕ ""Покупка товаров, принятых на хранение""
	|	КОНЕЦ");
	Возврат ТекстЗапроса;
	
КонецФункции

#Область Подотчетники

Функция ТекстПодотчетникиЗакупкаЦенности()
	
	Текст =
	"ВЫБРАТЬ // Поступление услуг/ценностей в собственность (Дт 10, 20, 41 :: Кт 71)
	|
	|	Строки.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.СуммаУУ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоЗакупке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ТОГДА Строки.ГруппаФинансовогоУчета
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ТОГДА Строки.ГруппаФинансовогоУчетаРасчетов
	|		ИНАЧЕ Строки.ГруппаПродукции
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ТОГДА Строки.Склад
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.ПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаДт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ТОГДА Строки.Склад
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ТОГДА Строки.Контрагент
	|		ИНАЧЕ Строки.ТипЗатрат
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.ГруппаПродукции
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	ЕСТЬNULL(ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.СуммаСНДС, 0) КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ПодотчетныеЛица.Валюта КАК ВалютаКт,
	|	ПодотчетныеЛица.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА ПодотчетныеЛица.СуммаРегл = ПодотчетныеЛица.Сумма
	|		ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ КАК СчетКт,
	|
	|	ПодотчетныеЛица.ПодотчетноеЛицо КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Суммы.СуммаБезНДС КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.Сумма КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|		ТОГДА ""Поступление услуг""
	|		ИНАЧЕ ""Поступление товаров на склад""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Ссылка
	|		И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|		И Строки.ИдентификаторСтроки <> """"
	|		И Строки.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ПодотчетныеЛица
	|	ПО
	|		Строки.Ссылка = ПодотчетныеЛица.Регистратор
	|		И Строки.ИдентификаторФинЗаписи = ПодотчетныеЛица.ИдентификаторФинЗаписи
	|		И ПодотчетныеЛица.Активность И НЕ ПодотчетныеЛица.Сторно
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках КАК ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках
	|	ПО
	|		Строки.Ссылка = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Регистратор
	|		И Строки.ИдентификаторСтроки = ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.ИдентификаторСтроки
	|
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)";
	
	Возврат Текст;
	
КонецФункции

Функция ТекстПодотчетникиСписаниеНаРасходы()
	
	Текст =
	"ВЫБРАТЬ // Списание поступивших ценностей на расходы (Дт 91.2 :: Кт 71)
	|
	|	РегистрПрочиеРасходы.Регистратор КАК Ссылка,
	|	РегистрПрочиеРасходы.Период КАК Период,
	|	РегистрПрочиеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	РегистрПрочиеРасходы.СуммаРегл КАК Сумма,
	|	РегистрПрочиеРасходы.СуммаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	РегистрПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РегистрПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РегистрПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	РегистрПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	РегистрПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	РегистрПрочиеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|		КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА РегистрПрочиеРасходы.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ РегистрПрочиеРасходы.СуммаРегл
	|	КОНЕЦ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ПодотчетныеЛица.Валюта КАК ВалютаКт,
	|	ПодотчетныеЛица.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА ПодотчетныеЛица.СуммаРегл = ПодотчетныеЛица.Сумма
	|		ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ КАК СчетКт,
	|
	|	ПодотчетныеЛица.ПодотчетноеЛицо КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Суммы.СуммаБезНДС КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РегистрПрочиеРасходы.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание поступивших ценностей на расходы"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК РегистрПрочиеРасходы
	|	ПО
	|		ДокументыКОтражению.Ссылка = РегистрПрочиеРасходы.Регистратор
	|		И РегистрПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|		И НЕ РегистрПрочиеРасходы.НастройкаХозяйственнойОперации В (ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость),
	|																	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеНДСПоПриобретеннымЦенностям))
	|		И РегистрПрочиеРасходы.Активность И НЕ РегистрПрочиеРасходы.Сторно
	|		И РегистрПрочиеРасходы.ИдентификаторСтроки <> """"
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ПодотчетныеЛица
	|	ПО
	|		РегистрПрочиеРасходы.Регистратор = ПодотчетныеЛица.Регистратор
	|		И РегистрПрочиеРасходы.ИдентификаторФинЗаписи = ПодотчетныеЛица.ИдентификаторФинЗаписи
	|		И ПодотчетныеЛица.Активность И НЕ ПодотчетныеЛица.Сторно
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Регистратор = РегистрПрочиеРасходы.Регистратор
	|		И Суммы.ИдентификаторСтроки = РегистрПрочиеРасходы.ИдентификаторФинЗаписи
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		РегистрПрочиеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО
	|		ОбъектыСтроительства.Ссылка = РегистрПрочиеРасходы.АналитикаРасходов
	|		И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|ГДЕ
	|	РегистрПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)";
	
	Возврат Текст;
	
КонецФункции

Функция ТекстПодотчетникиЗакупкаНДС()
	
	Текст =
	"ВЫБРАТЬ // НДС при приобретении по рег. НДСПредъявленный (Дт 19 :: Кт 71) @НДСПредъявленный
	|
	|	НДСПредъявленный.Регистратор КАК Ссылка,
	|	НДСПредъявленный.Период КАК Период,
	|	НДСПредъявленный.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	НДСПредъявленный.НДС КАК Сумма,
	|	НДСПредъявленный.НДСУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
	|	НДСПредъявленный.ВидДеятельностиНДС КАК АналитикаУчетаДт,
	|	НДСПредъявленный.ВидЦенности КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НДСПредъявленный.Поставщик КАК СубконтоДт1,
	|	НДСПредъявленный.СчетФактура КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ПодотчетныеЛица.Валюта КАК ВалютаКт,
	|	ПодотчетныеЛица.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА ПодотчетныеЛица.СуммаРегл = ПодотчетныеЛица.Сумма
	|		ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ КАК СчетКт,
	|
	|	ПодотчетныеЛица.ПодотчетноеЛицо КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Суммы.СуммаНДС КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	НДСПредъявленный.НДС КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС при приобретении"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		ДокументыКОтражению.Ссылка = НДСПредъявленный.Регистратор
	|		И НДСПредъявленный.Активность И НЕ НДСПредъявленный.Сторно
	|		И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НДСПредъявленный.ИдентификаторФинЗаписи <> """"
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ПодотчетныеЛица
	|	ПО
	|		НДСПредъявленный.Регистратор = ПодотчетныеЛица.Регистратор
	|		И НДСПредъявленный.ИдентификаторФинЗаписи = ПодотчетныеЛица.ИдентификаторФинЗаписи
	|		И ПодотчетныеЛица.Активность И НЕ ПодотчетныеЛица.Сторно
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Регистратор = НДСПредъявленный.Регистратор
	|		И Суммы.ИдентификаторСтроки = НДСПредъявленный.ИдентификаторФинЗаписи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО ДокументыКОтражению.Ссылка = ВводСобственныхСредств.Ссылка
	|		И НДСПредъявленный.СчетФактура = ВводСобственныхСредств.СчетФактура
	|
	|ГДЕ
	|	ПодотчетныеЛица.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)";
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти
