////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы ТМЦ в эксплуатации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработкаПолученияДанныхВыбораПартииТМЦВЭксплуатации(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если НЕ Параметры.Свойство("ОграничениеВыбора") 
		ИЛИ НЕ Параметры.ОграничениеВыбора Тогда
		Возврат;
	КонецЕсли;
		
	СтандартнаяОбработка = Ложь;
	
	ВозвращатьСтруктуруПриВыборе = Ложь;
	Если Параметры.Свойство("ВозвращатьСтруктуруПриВыборе") Тогда
		ВозвращатьСтруктуруПриВыборе = Параметры.ВозвращатьСтруктуруПриВыборе;
	КонецЕсли;
	
	ПараметрыВыбора = ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации();
	ЗаполнитьЗначенияСвойств(ПараметрыВыбора, Параметры);

	ОписаниеЗапроса = ТМЦВЭксплуатацииКлиентСервер.ОписаниеЗапросаДляВыбораПартииТМЦ(ПараметрыВыбора);
	
	Запрос = Новый Запрос(ОписаниеЗапроса.ТекстЗапроса);
	Для Каждого ОписаниеПараметра Из ОписаниеЗапроса.ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ОписаниеПараметра.Ключ, ОписаниеПараметра.Значение);
	КонецЦикла;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДанныеВыбора = Новый СписокЗначений;

	Пока Выборка.Следующий() Цикл
		
		Представление = ОбщегоНазначенияУТ.ПредставлениеРезультатаПоискаПоСтроке(Выборка.ПартияПредставление, Параметры.СтрокаПоиска);
		
		Если ВозвращатьСтруктуруПриВыборе Тогда
			
			ВыбранноеЗначение = ТМЦВЭксплуатацииКлиентСервер.НовоеЗначениеВыбора();
			ЗаполнитьЗначенияСвойств(ВыбранноеЗначение, Выборка);
			
			ДанныеВыбора.Добавить(ВыбранноеЗначение, Представление);
			
		Иначе
			ДанныеВыбора.Добавить(Выборка.Партия, Представление);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

// Заполняет список выбора при авто подборе по инвентарному номеру.
// 
// Параметры:
//  ПараметрыПодбора - см. ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации.
//  ДанныеВыбора - СписокЗначений - Данные выбора.
//
Процедура ЗаполнитьДанныеВыбораПоИнвентарномуНомеру(ПараметрыПодбора, ДанныеВыбора) Экспорт
	
	ОписаниеЗапроса = ТМЦВЭксплуатацииКлиентСервер.ОписаниеЗапросаДляВыбораПартииТМЦ(ПараметрыПодбора);
	
	Запрос = Новый Запрос(ОписаниеЗапроса.ТекстЗапроса);
	Для Каждого ОписаниеПараметра Из ОписаниеЗапроса.ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ОписаниеПараметра.Ключ, ОписаниеПараметра.Значение);
	КонецЦикла;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ВыбранноеЗначение = ТМЦВЭксплуатацииКлиентСервер.НовоеЗначениеВыбора();
		ЗаполнитьЗначенияСвойств(ВыбранноеЗначение, Выборка);
		
		Представление = ОбщегоНазначенияУТ.ПредставлениеРезультатаПоискаПоСтроке(Выборка.ИнвентарныйНомер, ПараметрыПодбора.ИнвентарныйНомерСодержит);
		
		ДанныеВыбора.Добавить(ВыбранноеЗначение, Представление);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
