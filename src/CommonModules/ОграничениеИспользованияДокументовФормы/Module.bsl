////////////////////////////////////////////////////////////////////////////////
// Подсистема "Ограничение использования документов".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает доступность документа для редактирования и располагает команду ограничения/освобождения документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа.
//  ИмяДействия - Строка - имя действия команды ограничения/освобождения.
//
Процедура ПриПолученииДанныхНаСервере(Форма, ИмяДействия = "ОграничитьДокумент") Экспорт
	
	ДополнитьФормуДокумента(Форма, ИмяДействия);
	УстановитьДоступностьДокументаДляРедактирования(Форма);
	
КонецПроцедуры

// Из формы ограничивает/освобождает документ для редактирования и выполнения прочих команд.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа.
//
Процедура ОграничитьДокумент(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Элементы.ОграничитьДокумент.Пометка = Не Элементы.ОграничитьДокумент.Пометка;
	
	ОграничитьОсвободитьДокумент(Форма, Элементы.ОграничитьДокумент.Пометка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьДоступностьДокументаДляРедактирования(Форма)
	
	Если Форма.Команды.Найти("ОграничитьДокумент") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ДокументОграничен = РегистрыСведений.ОграниченныеДокументы.ДокументОграничен(Объект.Ссылка);
	ОграничениеИспользованияДокументовКлиентСервер.УстановитьДоступностьДанныхФормы(Форма, ДокументОграничен);
	
КонецПроцедуры

Процедура ДополнитьФормуДокумента(Форма, ИмяДействия)
	
	Если Не ОграничениеИспользованияДокументов.АвтоматическиОграничиватьИспользованиеДокументов() Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РежимТолькоПросмотр", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РежимТолькоПросмотрПрежняя", Новый ОписаниеТипов("Булево")));
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
	ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	ИмяКоманды = "ОграничитьДокумент";
	
	ПредставлениеПометки = ОграничениеИспользованияДокументов.ПредставлениеПометкиОграниченияДокумента(Форма.Объект.Ссылка);
	
	КомандаЗакрытияДокумента = Форма.Команды.Найти(ИмяКоманды);
	
	Если КомандаЗакрытияДокумента = Неопределено Тогда
		
		НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "Подключаемый_" + ИмяДействия;
		
		ЭлементКнопкаПодменю = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Форма.КоманднаяПанель);
		ЭлементКнопкаПодменю.Заголовок				= ПредставлениеПометки;
		ЭлементКнопкаПодменю.Вид					= ВидКнопкиФормы.КнопкаКоманднойПанели;
		ЭлементКнопкаПодменю.ИмяКоманды				= ИмяКоманды;
		ЭлементКнопкаПодменю.ТолькоВоВсехДействиях 	= Истина;
		
		КомандаЗакрытияДокумента = НоваяКоманда;
		
	КонецЕсли;
	
	КомандаЗакрытияДокумента.Заголовок = ПредставлениеПометки;
	
	ИмяЭлементаНадпись = "СообщениеОНевозможностиИзмененияНадпись";
	ЭлементНадпись = Форма.Элементы.Найти(ИмяЭлементаНадпись);
	
	Если ЭлементНадпись = Неопределено Тогда
		
		ГруппаСообщения = Форма.Элементы.Найти("СообщениеОНевозможностиИзмененияГруппа");
		
		Если ГруппаСообщения <> Неопределено Тогда
			
			ЭлементКартинка = Форма.Элементы.Добавить("СообщениеОНевозможностиИзмененияКартинка", Тип("ДекорацияФормы"), ГруппаСообщения);
			ЭлементКартинка.Вид = ВидДекорацииФормы.Картинка;
			ЭлементКартинка.Заголовок = НСтр("ru = 'Сообщение о невозможности изменения (картинка)';
											|en = 'Message about change impossibility (picture)'");
			ЭлементКартинка.Картинка = БиблиотекаКартинок.Предупреждение;
			
			ЭлементНадпись = Форма.Элементы.Добавить(ИмяЭлементаНадпись, Тип("ДекорацияФормы"), ГруппаСообщения);
			ЭлементНадпись.Вид = ВидДекорацииФормы.Надпись;
			ЭлементНадпись.АвтоМаксимальнаяШирина = Ложь;
			ЭлементНадпись.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			ЭлементНадпись.Подсказка = НСтр("ru = 'Для снятия запрета редактирования, снимите пометку в меню «Еще».';
											|en = 'To unlock editing, clear the mark in the More menu.'");
			ЭлементНадпись.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗаголовкаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Документ недоступен для редактирования, т.к. он %1';
			|en = 'Document is not available for editing as it is %1'"),
		НРег(ПредставлениеПометки));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлементаНадпись,
		"Заголовок",
		ТекстЗаголовкаСообщения);
	
КонецПроцедуры

Процедура ОграничитьОсвободитьДокумент(Форма, Ограничить = Истина)
	
	ДокументСсылка = Форма.Объект.Ссылка;
	
	Если Ограничить Тогда
		РегистрыСведений.ОграниченныеДокументы.ОграничитьДокумент(ДокументСсылка);
	Иначе
		РегистрыСведений.ОграниченныеДокументы.ОсвободитьДокумент(ДокументСсылка);
	КонецЕсли;
	
	УстановитьДоступностьДокументаДляРедактирования(Форма);
	
КонецПроцедуры

#КонецОбласти
