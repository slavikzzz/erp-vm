////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Оприходование ТМЦ в эксплуатацию".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";

	//++ Локализация
	ТекстыОтражения = Новый Массив;
	
	#Область ЗабалансовыйУчет // Дт МЦ.0Х :: Кт ---
	ТекстЗапроса =
	"// Оприходование ТМЦ в эксплуатации, забалансовый учет (Дт МЦ.0Х :: Кт ---)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,	
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом) КАК ВидСчетаДт,
	|	ТабличнаяЧасть.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ТабличнаяЧасть.Номенклатура КАК СубконтоДт1,
	|	ТабличнаяЧасть.Партия КАК СубконтоДт2,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	ТабличнаяЧасть.Количество КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Оприходование ТМЦ в эксплуатации, забалансовый учет"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеТМЦВЭксплуатации КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеТМЦВЭксплуатации.Товары КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизации
	|		ПО Операция.Ссылка = УчетнаяПолитикаОрганизации.Ссылка
	|			И Операция.Организация = УчетнаяПолитикаОрганизации.Организация
	|	
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизации.ЗабалансовыйУчетТМЦВЭксплуатации, ЛОЖЬ)
	|	И ТабличнаяЧасть.КатегорияЭксплуатации.ИмущественныйУчет
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// Ведомость учета выдачи спецодежды (МБ-7)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФормТМЦВЭксплуатации";
	КомандаПечати.Идентификатор = "МБ7";
	КомандаПечати.Представление = НСтр("ru = 'Ведомость учета выдачи спецодежды (МБ-7)';
										|en = 'Overalls issue accounting list (MB-7)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	// Акт о приемке-передаче оборудования (ОС-15)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФормТМЦВЭксплуатации";
	КомандаПечати.Идентификатор = "ОС15";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приемке-передаче оборудования (ОС-15)';
										|en = 'Certificate of equipment receiving and handover (FA-15)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Получает данные для печатной формы МБ-7.
// 
// Параметры:
//  МассивДокументов - Массив - Массив документов
// 
// Возвращаемое значение:
//  Структура - Содержит:
// 		* РезультатПоШапке - РезультатЗапроса -
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса -
Функция ДанныеДляПечатнойФормыМБ7(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Ссылка.Дата КАК Дата,
	|	ТаблицаТовары.Ссылка.Организация КАК Организация,
	|	ТаблицаТовары.ФизическоеЛицо КАК ФизическоеЛицо
	|
	|ИЗ
	|	Документ.ОприходованиеТМЦВЭксплуатации.Товары КАК ТаблицаТовары
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивДокументов)
	|
	|ИТОГИ
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Организация)
	|ПО
	|	Ссылка";
	
	ДанныеДокументов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТолькоРазрешенные = Истина;
	ОсновныеСотрудникиФизическихЛиц = Неопределено;
	Для Каждого Документ Из ДанныеДокументов.Строки Цикл
		
		СписокФизическихЛиц = Документ.Строки.ВыгрузитьКолонку("ФизическоеЛицо");
		КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(
			Запрос.МенеджерВременныхТаблиц, 
			ТолькоРазрешенные, 
			СписокФизическихЛиц, 
			Документ.Организация, 
			Документ.Дата);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Документ КАК Документ,
		|	ТаблицаСотрудники.Сотрудник КАК Сотрудник,
		|	ТаблицаСотрудники.Сотрудник.Код КАК ТабельныйНомер,
		|	ТаблицаСотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТОсновныеСотрудникиФизическихЛиц КАК ТаблицаСотрудники";
		Запрос.УстановитьПараметр("Документ", Документ.Ссылка);
		
		СотрудникиФизическихЛиц = Запрос.Выполнить().Выгрузить();
		Если ОсновныеСотрудникиФизическихЛиц = Неопределено Тогда
			ОсновныеСотрудникиФизическихЛиц = СотрудникиФизическихЛиц;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СотрудникиФизическихЛиц,ОсновныеСотрудникиФизическихЛиц)
		КонецЕсли;
		
		Запрос.Текст = "УНИЧТОЖИТЬ ВТОсновныеСотрудникиФизическихЛиц";
		Запрос.Выполнить();
		
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Документ КАК Документ,
	|	Т.Сотрудник КАК Сотрудник,
	|	Т.ТабельныйНомер КАК ТабельныйНомер,
	|	Т.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ втОсновныеСотрудникиФизическихЛиц
	|ИЗ
	|	&ОсновныеСотрудникиФизическихЛиц КАК Т";
	Запрос.УстановитьПараметр("ОсновныеСотрудникиФизическихЛиц", ОсновныеСотрудникиФизическихЛиц);
	Запрос.Выполнить();

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.Дата КАК ДатаСоставления,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Организация) КАК ПредставлениеОрганизации,
	|	ДанныеДокумента.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Подразделение) КАК ПредставлениеПодразделения,
	|	ДанныеДокумента.Подразделение.ТекущийРуководитель КАК РуководительПодразделения,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК МОЛФИО,
	|	"""" КАК МОЛДолжность
	|
	|ИЗ
	|	Документ.ОприходованиеТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Ссылка.Дата КАК ДатаДокумента,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТовары.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|
	|	ТаблицаТовары.ФизическоеЛицо КАК Сотрудник,
	|	Сотрудники.ТабельныйНомер КАК ТабельныйНомер,
	|
	|	ТаблицаТовары.СрокЭксплуатации КАК СрокСлужбы,
	|	ТаблицаТовары.Количество КАК Количество
	|
	|ИЗ
	|	Документ.ОприходованиеТМЦВЭксплуатации.Товары КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОсновныеСотрудникиФизическихЛиц КАК Сотрудники
	|		ПО Сотрудники.Документ = ТаблицаТовары.Ссылка
	|			И Сотрудники.ФизическоеЛицо = ТаблицаТовары.ФизическоеЛицо 
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивДокументов)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Цены.Упаковка",
		"ТаблицаТовары.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
		
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакет();
	// Пакет запросов:
	// 		[0] - Выборка по шапкам документов
	// 		[1] - Выборка по табличным частям документов
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПакетаЗапросов[0],
		РезультатПакетаЗапросов[1]);
	
КонецФункции

// Получает данные для печатной формы ОС-15.
// 
// Параметры:
//  МассивДокументов - Массив - Массив документов
// 
// Возвращаемое значение:
//  Структура - Содержит:
// 		* РезультатПоШапке - РезультатЗапроса -
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса -
Функция ДанныеДляПечатнойФормыОС15(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена
	|
	|ПОМЕСТИТЬ Товары
	|
	|ИЗ
	|	Документ.ОприходованиеТМЦВЭксплуатации.Товары КАК Товары
	|
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|	И Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.Дата КАК ДатаСоставление,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Организация) КАК ОрганизацияЗаказчикНаименование,
	|	ДанныеДокумента.Организация.КодПоОКПО КАК ОрганизацияЗаказчикКодПоОКПО,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДанныеДокумента.Подразделение) КАК ПодразделениеНаименование,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК КладовщикОтправитель,
	|	"""" КАК ДолжностьКладовщикаОтправителя
	|
	|ИЗ
	|	Документ.ОприходованиеТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	Товары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	Товары.Количество КАК Количество,
	|
	|	ВЫРАЗИТЬ((Товары.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)) * Товары.Количество КАК ЧИСЛО(31,2)) КАК СтоимостьВсего,
	|
	|	ВЫРАЗИТЬ(Товары.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(31,2)) КАК СтоимостьЕдиницы,
	|
	|	Товары.НомерСтроки КАК НомерСтроки
	|
	|ИЗ
	|	Товары КАК Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПакетаЗапросов[РезультатПакетаЗапросов.ВГраница()-1],
		РезультатПакетаЗапросов[РезультатПакетаЗапросов.ВГраница()]);
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ Локализация

#Область Прочее

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- Локализация

#КонецОбласти
