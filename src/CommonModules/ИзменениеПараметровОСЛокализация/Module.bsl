////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Изменение параметров ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Добавляет в текст запроса реквизиты шапки документа.
//  Реквизиты добавляются вместо параметра "&ИзменениеПараметровОС_РеквизитыДокумента".
//
// Параметры:
//  ТекстЗапроса - Строка	 - Исходный текст запроса.
//  ИмяТаблицы	 - Строка	 - Синоним таблицы документа в запросе.
//
Процедура ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, ИмяТаблицы) Экспорт

	ПоляЛокализации = "NULL";
	
	//++ Локализация
	
	ПоляЛокализации = 
	"	ДанныеДокумента.ОтражатьВНУ КАК ОтражатьВНУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУФлаг КАК НачислятьАмортизациюНУФлаг,
	|	ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг КАК ОтражениеАмортизационнойПремииФлаг,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициентНУ,
	|	ДанныеДокумента.СпециальныйКоэффициентНУФлаг КАК СпециальныйКоэффициентНУФлаг,
	|	ДанныеДокумента.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|	ДанныеДокумента.СрокИспользованияНУФлаг КАК СрокИспользованияНУФлаг,
	|	ДанныеДокумента.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.АмортизационнаяГруппаФлаг КАК АмортизационнаяГруппаФлаг,
	|	ДанныеДокумента.НедвижимоеИмущество КАК НедвижимоеИмущество,
	|	ДанныеДокумента.НедвижимоеИмуществоФлаг КАК НедвижимоеИмуществоФлаг";
	
	ПоляЛокализации = СтрЗаменить(ПоляЛокализации, "ДанныеДокумента", ИмяТаблицы);
	
	//-- Локализация

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИзменениеПараметровОС_РеквизитыДокумента", ПоляЛокализации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	
	МассивНепроверяемыхРеквизитов = Новый Массив;

	ПроверитьЗаполнениеПараметров(Объект, Отказ);
	ПроверитьТЧАмортизация(Объект, МассивНепроверяемыхРеквизитов, Отказ);
	ПроверитьТЧАмортизационнаяПремия(Объект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  ТекстЗаполнения - Строка, Неопределено - Текст, используемый для заполнения документа. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	
	РассчитатьСлужебныеРеквизитыДляРасчетаАмортизации(Объект);
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеПараметровОС2_4 - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.ИзменениеПараметровОС2_4 - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	
	Объект.ОтражениеАмортизационнойПремииФлаг = Ложь;
	Объект.АмортизационнаяПремия.Очистить();
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	ТекстЗапроса = "";
		
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПеремещенияСтоимостиОС(СписокЗапросов);
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПеремещенияАмортизацииОС(СписокЗапросов);
	
	#Область ПеремещениеДоходыОтЦелевогоФинансирования // Дт 98 :: Кт 98
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение доходов от целевого финансирования (Дт 98 :: Кт 98)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ 
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ 
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0)
	|		- ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Перемещение доходов от целевого финансирования"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО ДокументыКОтражению.Ссылка = Строки.Регистратор
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|	ПО АмортизацияОС.Регистратор = Строки.Регистратор
	|		И АмортизацияОС.ОсновноеСредство = Строки.ОсновноеСредство
	|		И АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|ГДЕ
	|	(Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)
	|	И Строки.НаправлениеДеятельности <> Строки.КорНаправлениеДеятельности";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПеремещениеИнвестицииВАренду // Дт 58 :: Кт 58
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение арендных платежей (Дт АрендныеПлатежи :: Кт АрендныеПлатежи)
	|ВЫБРАТЬ
	|	
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.АрендныеПлатежиРегл КАК Сумма,
	|	СтрокиДт.АрендныеПлатежиУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеПлатежи) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	СтрокиДт.Договор.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	СтрокиДт.ОсновноеСредство КАК СубконтоДт1,
	|	СтрокиДт.Договор.Контрагент КАК СубконтоДт2,
	|	СтрокиДт.Договор КАК СубконтоДт3,
	|	
	|	СтрокиДт.АрендныеПлатежи КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	СтрокиДт.АрендныеПлатежиРегл КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеПлатежи) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	СтрокиКт.Договор.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	СтрокиКт.Договор.Контрагент КАК СубконтоКт2,
	|	СтрокиКт.Договор КАК СубконтоКт3,
	|	
	|	СтрокиКт.АрендныеПлатежи КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиКт.АрендныеПлатежиРегл КАК СуммаВРКт,
	|	""Перемещение арендных платежей"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиДт
	|		ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС)
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиКт
	|		ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|";

	СписокЗапросов.Добавить(ТекстЗапроса);

	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение НГЛС (Дт НГЛС :: Кт НГЛС)
	|ВЫБРАТЬ
	|	
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.НГЛСРегл КАК Сумма,
	|	СтрокиДт.НГЛСУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НГЛС) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	СтрокиДт.Договор.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	СтрокиДт.ОсновноеСредство КАК СубконтоДт1,
	|	СтрокиДт.Договор.Контрагент КАК СубконтоДт2,
	|	СтрокиДт.Договор КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	СтрокиДт.НГЛСРегл КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НГЛС) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Договор.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	СтрокиКт.Договор.Контрагент КАК СубконтоКт2,
	|	СтрокиКт.Договор КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиКт.НГЛСРегл КАК СуммаВРКт,
	|	""Перемещение НГЛС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиДт
	|		ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС)
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиКт
	|		ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|";

	СписокЗапросов.Добавить(ТекстЗапроса);

	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение процентного дисконта (Дт ПроцентныйДисконт :: Кт ПроцентныйДисконт)
	|ВЫБРАТЬ
	|	
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	-СтрокиДт.ПроцентныйДисконтРегл КАК Сумма,
	|	-СтрокиДт.ПроцентныйДисконтУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПроцентныйДисконт) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	СтрокиДт.Договор.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	СтрокиДт.ОсновноеСредство КАК СубконтоДт1,
	|	СтрокиДт.Договор.Контрагент КАК СубконтоДт2,
	|	СтрокиДт.Договор КАК СубконтоДт3,
	|	
	|	-СтрокиДт.ПроцентныйДисконт КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-СтрокиДт.ПроцентныйДисконтРегл КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПроцентныйДисконт) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	СтрокиКт.Договор.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	СтрокиКт.Договор.Контрагент КАК СубконтоКт2,
	|	СтрокиКт.Договор КАК СубконтоКт3,
	|	
	|	-СтрокиКт.ПроцентныйДисконт КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-СтрокиКт.ПроцентныйДисконтРегл КАК СуммаВРКт,
	|	""Перемещение процентного дисконта"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиДт
	|		ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС)
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИнвестицииВАренду КАК СтрокиКт
	|		ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|";

	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#Область Проведение
 
// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

//
// Параметры:
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
Процедура ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса) Экспорт
	
	//++ Локализация
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Неопределено);
	
	//-- Локализация
	
КонецПроцедуры

Функция ПараметрыВыбраны(Объект) Экспорт

	ПараметрыВыбраны = Ложь;
	
	//++ Локализация
	
	ПараметрыВыбраны =
		Объект.НедвижимоеИмуществоФлаг
		ИЛИ Объект.ГрафикАмортизацииФлаг 
		ИЛИ Объект.СпециальныйКоэффициентНУФлаг 
		ИЛИ Объект.СрокИспользованияНУФлаг 
		ИЛИ Объект.НачислятьАмортизациюНУФлаг 
		ИЛИ Объект.АмортизационнаяГруппаФлаг 
		ИЛИ Объект.ОтражениеАмортизационнойПремииФлаг;

	//-- Локализация
	
	Возврат ПараметрыВыбраны;
	
КонецФункции

Функция ТекстЗапросаПроверитьЧтоИзменяемыеПараметрыИспользуются() Экспорт
	
	ТекстЗапроса = Неопределено;

	//++ Локализация
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ) КАК НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ,
	|	МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаУскоренияБУ) КАК НеДопускаетсяИзменениеКоэффициентаУскоренияБУ,
	|	МАКСИМУМ(РезультатПроверки.НеИспользуетсяМетодНачисленияПоНаработке) КАК НеИспользуетсяМетодНачисленияПоНаработке,
	|	МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеГрафикаНачисления) КАК НеДопускаетсяИзменениеГрафикаНачисления,
	|	МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаУскоренияУУ) КАК НеДопускаетсяИзменениеКоэффициентаУскоренияУУ
	|ИЗ
	|	(ВЫБРАТЬ
	|
			// НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ
	|		&КоэффициентАмортизацииБУФлаг
	|		И (НЕ &МетодНачисленияАмортизацииБУФлаг
	|					И НЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (&СпособыПоЕНАОФ)
	|			ИЛИ &МетодНачисленияАмортизацииБУФлаг
	|					И НЕ &МетодНачисленияАмортизацииБУ В (&СпособыПоЕНАОФ)) КАК НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ,
	|
			// НеДопускаетсяИзменениеКоэффициентаУскоренияБУ
	|		&КоэффициентУскоренияБУФлаг
	|		И (НЕ &МетодНачисленияАмортизацииБУФлаг
	|					И ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|			ИЛИ &МетодНачисленияАмортизацииБУФлаг
	|					И &МетодНачисленияАмортизацииБУ <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)) КАК НеДопускаетсяИзменениеКоэффициентаУскоренияБУ,
	|		
			// НеИспользуетсяМетодНачисленияПоНаработке
	|		(&ОбъемНаработкиФлаг ИЛИ &ПоказательНаработкиФлаг)
	|		И НЕ (НЕ &МетодНачисленияАмортизацииУУФлаг
	|					И ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации В (&СпособыПоНаработке)
	|				ИЛИ &МетодНачисленияАмортизацииУУФлаг
	|					И &МетодНачисленияАмортизацииУУ В (&СпособыПоНаработке)
	|				ИЛИ НЕ &МетодНачисленияАмортизацииБУФлаг
	|					И ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (&СпособыПоНаработке)
	|				ИЛИ &МетодНачисленияАмортизацииБУФлаг
	|					И &МетодНачисленияАмортизацииБУ В (&СпособыПоНаработке)) КАК НеИспользуетсяМетодНачисленияПоНаработке,
	|
			// НеДопускаетсяИзменениеГрафикаНачисления
	|		&ГрафикАмортизацииФлаг
	|		И (НЕ &МетодНачисленияАмортизацииБУФлаг
	|					И ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (&СпособыПоНаработке)
	|				ИЛИ &МетодНачисленияАмортизацииБУФлаг
	|					И &МетодНачисленияАмортизацииБУ В (&СпособыПоНаработке)) КАК НеДопускаетсяИзменениеГрафикаНачисления,
	|
			// НеДопускаетсяИзменениеКоэффициентаУскоренияУУ
	|		&КоэффициентУскоренияУУФлаг
	|		И (НЕ &МетодНачисленияАмортизацииУУФлаг
	|					И ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|			ИЛИ &МетодНачисленияАмортизацииУУФлаг
	|					И &МетодНачисленияАмортизацииУУ <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)) КАК НеДопускаетсяИзменениеКоэффициентаУскоренияУУ
	|	ИЗ
	|		Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&Дата, 
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|			И ПервоначальныеСведенияОС.Организация = &Организация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Дата, 
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСУУ
	|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|			И ПараметрыАмортизацииОСУУ.Организация = &Организация
	|	ГДЕ
	|		ОбъектыЭксплуатации.Ссылка В (&СписокОС)
	|
	|	) КАК РезультатПроверки
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ) = ИСТИНА
	|		ИЛИ МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаУскоренияБУ) = ИСТИНА
	|		ИЛИ МАКСИМУМ(РезультатПроверки.НеИспользуетсяМетодНачисленияПоНаработке) = ИСТИНА
	|		ИЛИ МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеГрафикаНачисления) = ИСТИНА
	|		ИЛИ МАКСИМУМ(РезультатПроверки.НеДопускаетсяИзменениеКоэффициентаУскоренияУУ) = ИСТИНА";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыЗапросаДляПроверкиЧтоИзменяемыеПараметрыИспользуются(Объект, Запрос) Экспорт
	
	//++ Локализация
	
	// БУ
	Запрос.УстановитьПараметр("НедвижимоеИмуществоФлаг", Объект.НедвижимоеИмуществоФлаг);
	Запрос.УстановитьПараметр("ГрафикАмортизацииФлаг", Объект.ГрафикАмортизацииФлаг);
	
	// Прочее
	СпособыПоНаработке = Новый Массив;
	СпособыПоНаработке.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	СпособыПоНаработке.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега);
	Запрос.УстановитьПараметр("СпособыПоНаработке", СпособыПоНаработке);
	
	СпособыПоЕНАОФ = Новый Массив;
	СпособыПоЕНАОФ.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФ);
	СпособыПоЕНАОФ.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега);
	Запрос.УстановитьПараметр("СпособыПоЕНАОФ", СпособыПоЕНАОФ);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ОбработатьРезультатПроверкиИспользованияИзменяемыхПараметров(Выборка, Объект, Отказ) Экспорт

	//++ Локализация
	
	Если Выборка.НеДопускаетсяИзменениеКоэффициентаАмортизацииБУ Тогда
		ТекстСообщения = НСтр("ru = 'Для изменения параметра ""Годовая норма амортизации"" у основных средств должен быть метод начисления амортизации ""По ЕНАОФ"" или ""По ЕНАОФ на 1000 км пробега"".';
								|en = 'Fixed assets must have the ""By UFADR"" or ""By UFADR per 1000 km of mileage"" depreciation accrual method to change the ""Annual depreciation rate"" parameter.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "КоэффициентАмортизацииБУФлаг",, Отказ);
	КонецЕсли;

	Если Выборка.НеДопускаетсяИзменениеГрафикаНачисления Тогда
		ТекстСообщения = НСтр("ru = 'Для изменения параметра ""График амортизации"" у основных средств должен быть метод начисления амортизации ""Линейный"", ""Уменьшаемого остатка"".';
								|en = 'Fixed assets must have the ""Linear"", ""Decreasing balance"" depreciation accrual method to change the ""Depreciation schedule"" parameter.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "ГрафикАмортизацииФлаг",, Отказ);
	КонецЕсли;

	Если Выборка.НеИспользуетсяМетодНачисленияПоНаработке 
		И Объект.ПоказательНаработкиФлаг Тогда
		ТекстСообщения = НСтр("ru = 'Для изменения параметра ""Показатель наработки"" у основных средств должен быть метод начисления амортизации ""Пропорционально объему"".';
								|en = 'Fixed asset must have ""Units-of-activity"" method of depreciation charge to change the ""Asset activity measure"" parameter.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "ПоказательНаработкиФлаг",, Отказ);
	КонецЕсли;
	
	Если Выборка.НеИспользуетсяМетодНачисленияПоНаработке 
		И Объект.ОбъемНаработкиФлаг Тогда
		ТекстСообщения = НСтр("ru = 'Для изменения параметра ""Предполагаемый объем наработки"" у основных средств должен быть метод начисления амортизации ""Пропорционально объему"".';
								|en = 'Fixed asset must have ""Units-of-activity"" method of depreciation charge to change the ""Estimated asset activity value"" parameter.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "ОбъемНаработкиФлаг",, Отказ);
	КонецЕсли;

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокумента

Процедура ПриЧтенииСозданииНаСервере(Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	Форма.ЕстьСвязанныеОрганизации = Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация);
	Форма.ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);

	ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
		"ПрименяетсяФСБУ6",
		НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()))),
		Форма.СлужебныеПараметрыФормы);
	
	//-- Локализация
	
КонецПроцедуры

Процедура НастроитьЗависимыеЭлементыФормы(Форма, СтруктураИзмененныхРеквизитов) Экспорт
	
	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.РегламентированномУчете"),
			НСтр("ru = 'Бухгалтерском и налоговом учете';
				|en = 'Accounting and tax accounting'"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.БухгалтерскомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.НалоговомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.УправленческомИБухгалтерскомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.УправленческомИНалоговомУчете"));
	
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры
 
Функция ТекстЗапросаДляЗаполненияТекущихЗначенийПараметров() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка КАК ОсновноеСредство,
	|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ГруппаФинансовогоУчета, НЕОПРЕДЕЛЕНО) КАК ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ПоказательНаработки, НЕОПРЕДЕЛЕНО) КАК ПоказательНаработки,
	|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ОбъемНаработки, НЕОПРЕДЕЛЕНО) КАК ОбъемНаработки,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.СрокИспользования, 0) КАК СрокИспользованияУУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) КАК СрокИспользованияДляВычисленияАмортизацииУУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.КоэффициентУскорения, 0) КАК КоэффициентУскоренияУУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.МетодНачисленияАмортизации, НЕОПРЕДЕЛЕНО) КАК МетодНачисленияАмортизацииУУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
	|	ЕСТЬNULL(ПорядокУчетаОСУУСрезПоследних.НачислятьАмортизациюУУ, ЛОЖЬ) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияОССрезПоследних.ДатаВводаВЭксплуатациюУУ, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1) КАК ПринятКУчетуУпр
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.СрокПолезногоИспользованияНУ, 0) КАК СрокИспользованияНУ
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) КАК СрокИспользованияДляВычисленияАмортизацииБУ
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.КоэффициентАмортизацииБУ, 0) КАК КоэффициентАмортизацииБУ
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.КоэффициентУскорения, 0) КАК КоэффициентУскоренияБУ
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.ГрафикАмортизации, НЕОПРЕДЕЛЕНО) КАК ГрафикАмортизации
	|	,ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПоследних.СпециальныйКоэффициент, 0) КАК СпециальныйКоэффициент
	|	,ЕСТЬNULL(ПорядокУчетаОСБУСрезПоследних.НачислятьАмортизациюБУ, ЛОЖЬ) КАК НачислятьАмортизациюБУ
	|	,ЕСТЬNULL(ПорядокУчетаОСБУСрезПоследних.НачислятьАмортизациюНУ, ЛОЖЬ) КАК НачислятьАмортизациюНУ
	|	,ЕСТЬNULL(ПорядокУчетаОСБУСрезПоследних.АмортизационнаяГруппа, ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка)) КАК АмортизационнаяГруппа
	|	,ЕСТЬNULL(ПорядокУчетаОСБУСрезПоследних.НедвижимоеИмущество, ЛОЖЬ) КАК НедвижимоеИмущество
	|	,ЕСТЬNULL(ПервоначальныеСведенияОССрезПоследних.МетодНачисленияАмортизацииБУ, НЕОПРЕДЕЛЕНО) КАК МетодНачисленияАмортизацииБУ
	|	,ЕСТЬNULL(ПервоначальныеСведенияОССрезПоследних.ДатаВводаВЭксплуатациюБУ, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1) КАК ПринятКУчетуРегл
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСУУСрезПоследних
	|		ПО (ПараметрыАмортизацииОСУУСрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОССрезПоследних
	|		ПО (ПорядокУчетаОССрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСУУСрезПоследних
	|		ПО (ПорядокУчетаОСУУСрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОССрезПоследних
	|		ПО (ПервоначальныеСведенияОССрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСБУСрезПоследних
	|		ПО (ПараметрыАмортизацииОСБУСрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСБУСрезПоследних
	|		ПО (ПорядокУчетаОСБУСрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ОрганизацияПриИзменении(Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;

	Форма.ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
	
	ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
		"ПрименяетсяФСБУ6",
		НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()))),
		Форма.СлужебныеПараметрыФормы);
		
	//-- Локализация
	
КонецПроцедуры

Процедура ДатаПриИзменении(Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
		"ИспользоватьРеглУчет",
		РеглУчетСервер.ВедетсяРеглУчет(Объект.Дата),
		Форма.СлужебныеПараметрыФормы);
	
	Форма.ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
	
	ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
		"ПрименяетсяФСБУ6",
		НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()))),
		Форма.СлужебныеПараметрыФормы);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПоДокументуОснованию(Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ИзменениеПараметровОС") Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	1
		|ИЗ
		|	Документ.ИзменениеПараметровОС КАК ИзменениеПараметровОС
		|ГДЕ
		|	ИзменениеПараметровОС.Ссылка = &ДокументОснование
		|	И ИзменениеПараметровОС.ДокументОснование ССЫЛКА Документ.ПринятиеКУчетуОС
		|	И ВЫРАЗИТЬ(ИзменениеПараметровОС.ДокументОснование КАК Документ.ПринятиеКУчетуОС).ВключитьАмортизационнуюПремиюВСоставРасходов";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДокументОснование", Объект.ДокументОснование);
		
		Форма.ВключитьАмортизационнуюПремиюВСоставРасходов = НЕ Запрос.Выполнить().Пустой();
			
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьИнформациюВПодвале(Объект, ЗаголовокНадписи) Экспорт

	//++ Локализация
	
	Если ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()) < ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4() Тогда
		Если ЗаголовокНадписи.Количество() <> 0 Тогда
			ЗаголовокНадписи.Добавить(Символы.ПС);
		КонецЕсли;
		ЗаголовокНадписи.Добавить(НСтр("ru = 'Документ введен для перехода на учет внеоборотных активов версии 2.4';
										|en = 'Document is entered to migrate to fixed assets accounting 2.4'"));
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#Область Прочее

Процедура ДополнитьВспомогательныеРеквизиты(Объект, ВспомогательныеРеквизиты) Экспорт

	//++ Локализация
	ВспомогательныеРеквизиты.Вставить("ЕстьСвязанныеОрганизации", Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация));
	ВспомогательныеРеквизиты.Вставить("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата));
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяФСБУ6", НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()))));
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПорядокУчетаОСБУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДАТАВРЕМЯ(1,1,1) КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОСБУ.СостояниеБУ КАК СостояниеБУ,
	|	ПорядокУчетаОСБУ.СостояниеНУ КАК СостояниеНУ,
	|	ПорядокУчетаОСБУ.НалогообложениеНДС КАК НалогообложениеНДС,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НачислятьАмортизациюБУФлаг
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюБУ
	|		ИНАЧЕ ПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НачислятьАмортизациюНУФлаг
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюНУ
	|		ИНАЧЕ ПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг 
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовБУ
	|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг 
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовНУ
	|	КОНЕЦ КАК СпособОтраженияРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СпособОтраженияРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовБУ
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовБУ
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовНУ
	|	КОНЕЦ КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовНУ
	|	КОНЕЦ КАК АналитикаРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СтатьяРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии
	|	КОНЕЦ КАК АналитикаРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ПорядокУчетаОСБУ.ПередаватьРасходыВДругуюОрганизацию
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ПорядокУчетаОСБУ.ОрганизацияПолучательРасходов
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационнаяГруппаФлаг
	|			ТОГДА ДанныеДокумента.АмортизационнаяГруппа
	|		ИНАЧЕ ПорядокУчетаОСБУ.АмортизационнаяГруппа
	|	КОНЕЦ КАК АмортизационнаяГруппа,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НедвижимоеИмуществоФлаг
	|			ТОГДА ДанныеДокумента.НедвижимоеИмущество
	|		ИНАЧЕ ПорядокУчетаОСБУ.НедвижимоеИмущество
	|	КОНЕЦ КАК НедвижимоеИмущество
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСБУ_СУчетомИсправлений КАК ДанныеРегистраПоследнее
	|		ПО ДанныеРегистраПоследнее.Ссылка = ТаблицаОС.Ссылка
	|			И ДанныеРегистраПоследнее.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|
	|	И (ДанныеДокумента.НачислятьАмортизациюБУФлаг
	|			ИЛИ ДанныеДокумента.НачислятьАмортизациюНУФлаг
	|			ИЛИ ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ИЛИ ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ИЛИ ДанныеДокумента.АмортизационнаяГруппаФлаг
	|			ИЛИ ДанныеДокумента.НедвижимоеИмуществоФлаг)
	|
	|	И (НЕ ДанныеДокумента.ПараметрыДействуютСПрошлойДаты
	|		ИЛИ ЕСТЬNULL(ДанныеРегистраПоследнее.Период, ДАТАВРЕМЯ(1,1,1)) < ДанныеДокумента.НачалоДействия)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.НачалоДействия, ДЕНЬ) КАК Период,
	|	ДанныеДокумента.Период КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОСБУ.СостояниеБУ КАК СостояниеБУ,
	|	ПорядокУчетаОСБУ.СостояниеНУ КАК СостояниеНУ,
	|	ПорядокУчетаОСБУ.НалогообложениеНДС КАК НалогообложениеНДС,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НачислятьАмортизациюБУФлаг
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюБУ
	|		ИНАЧЕ ПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НачислятьАмортизациюНУФлаг
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюНУ
	|		ИНАЧЕ ПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг 
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовБУ
	|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг 
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовНУ
	|	КОНЕЦ КАК СпособОтраженияРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ ПорядокУчетаОСБУ.СпособОтраженияРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СпособОтраженияРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовБУ
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовБУ
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовНУ
	|	КОНЕЦ КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|				И ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовНУ
	|	КОНЕЦ КАК АналитикаРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СтатьяРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии
	|	КОНЕЦ КАК АналитикаРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ПорядокУчетаОСБУ.ПередаватьРасходыВДругуюОрганизацию
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ПорядокУчетаОСБУ.ОрганизацияПолучательРасходов
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационнаяГруппаФлаг
	|			ТОГДА ДанныеДокумента.АмортизационнаяГруппа
	|		ИНАЧЕ ПорядокУчетаОСБУ.АмортизационнаяГруппа
	|	КОНЕЦ КАК АмортизационнаяГруппа,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НедвижимоеИмуществоФлаг
	|			ТОГДА ДанныеДокумента.НедвижимоеИмущество
	|		ИНАЧЕ ПорядокУчетаОСБУ.НедвижимоеИмущество
	|	КОНЕЦ КАК НедвижимоеИмущество
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСБУ_СУчетомИсправлений КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|
	|	И (ДанныеДокумента.НачислятьАмортизациюБУФлаг
	|			ИЛИ ДанныеДокумента.НачислятьАмортизациюНУФлаг
	|			ИЛИ ДанныеДокумента.ОтражениеАмортизационныхРасходовФлаг
	|			ИЛИ ДанныеДокумента.ОтражениеАмортизационнойПремииФлаг
	|			ИЛИ ДанныеДокумента.АмортизационнаяГруппаФлаг
	|			ИЛИ ДанныеДокумента.НедвижимоеИмуществоФлаг)
	|
	|	И ДанныеДокумента.ПараметрыДействуютСПрошлойДаты
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДАТАВРЕМЯ(1,1,1) КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ КАК ДатаВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ КАК ДокументВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ КАК ДокументСнятияСУчетаБУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаНУ КАК ДокументСнятияСУчетаНУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ КАК ДокументСнятияСУчетаУУ,
	|
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияОС_СУчетомИсправлений КАК ДанныеРегистраПоследнее
	|		ПО ДанныеРегистраПоследнее.Ссылка = ТаблицаОС.Ссылка
	|			И ДанныеРегистраПоследнее.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.МетодНачисленияАмортизацииБУФлаг
	|
	|	И (НЕ ДанныеДокумента.ПараметрыДействуютСПрошлойДаты
	|		ИЛИ ЕСТЬNULL(ДанныеРегистраПоследнее.Период, ДАТАВРЕМЯ(1,1,1)) < ДанныеДокумента.НачалоДействия)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.НачалоДействия, ДЕНЬ) КАК Период,
	|	ДанныеДокумента.Период КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ КАК ДатаВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ КАК ДокументВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ КАК ДокументСнятияСУчетаБУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаНУ КАК ДокументСнятияСУчетаНУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ КАК ДокументСнятияСУчетаУУ,
	|
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияОС_СУчетомИсправлений КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.МетодНачисленияАмортизацииБУФлаг
	|	И ДанныеДокумента.ПараметрыДействуютСПрошлойДаты";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДАТАВРЕМЯ(1,1,1) КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации, 0) КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ, 0) КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	// СрокПолезногоИспользованияБУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И ДанныеДокумента.УказанОстаточныйСрокБУ
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) + ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0)
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	// СрокПолезногоИспользованияНУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияНУФлаг
	|			ТОГДА ДанныеДокумента.СрокИспользованияНУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ, 0)
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	// КоэффициентУскорения
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоэффициентУскоренияБУФлаг
	|			ТОГДА ДанныеДокумента.КоэффициентУскоренияБУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.КоэффициентУскорения, 0)
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	// КоэффициентАмортизацииБУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоэффициентАмортизацииБУФлаг
	|			ТОГДА ДанныеДокумента.КоэффициентАмортизацииБУ
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ
	|	КОНЕЦ КАК КоэффициентАмортизацииБУ,
	|
	// СрокИспользованияДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ 
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ 
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) 
	|					+ ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|				И ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ
	|				И ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) 
	|					+ ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации 
	|					+ (ДанныеДокумента.СрокИспользованияБУ - ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ)
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|					+ РАЗНОСТЬДАТ(ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ)
	|
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// ОбъемПродукцииРаботДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|				И ПорядокУчетаОС.ОбъемНаработки = ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|
	|			ТОГДА ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации 
	|					+ (ДанныеДокумента.ОбъемНаработки - ПорядокУчетаОС.ОбъемНаработки)
	|
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// ГрафикАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ГрафикАмортизацииФлаг
	|			ТОГДА ДанныеДокумента.ГрафикАмортизации
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ГрафикАмортизации
	|	КОНЕЦ КАК ГрафикАмортизации,
	|
	// СпециальныйКоэффициент
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СпециальныйКоэффициентНУФлаг
	|			ТОГДА ДанныеДокумента.СпециальныйКоэффициентНУ
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент
	|	КОНЕЦ КАК СпециальныйКоэффициент,
	|
	// ЛиквидационнаяСтоимость
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
	|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО ПараметрыАмортизацииОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПорядокУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСБУ_СУчетомИсправлений КАК ДанныеРегистраПоследнее
	|		ПО ДанныеРегистраПоследнее.Ссылка = ТаблицаОС.Ссылка
	|			И ДанныеРегистраПоследнее.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И (ДанныеДокумента.СрокИспользованияБУФлаг
	|			ИЛИ ДанныеДокумента.СрокИспользованияНУФлаг
	|			ИЛИ ДанныеДокумента.КоэффициентУскоренияБУФлаг
	|			ИЛИ ДанныеДокумента.КоэффициентАмортизацииБУФлаг
	|			ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг
	|			ИЛИ ДанныеДокумента.ГрафикАмортизацииФлаг
	|			ИЛИ ДанныеДокумента.СпециальныйКоэффициентНУФлаг
	|			ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг)
	|
	|	И (НЕ ДанныеДокумента.ПараметрыДействуютСПрошлойДаты
	|		ИЛИ ЕСТЬNULL(ДанныеРегистраПоследнее.Период, ДАТАВРЕМЯ(1,1,1)) < ДанныеДокумента.НачалоДействия)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.НачалоДействия, ДЕНЬ) КАК Период,
	|	ДанныеДокумента.Период КАК ДатаИсправления,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации, 0) КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ, 0) КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	// СрокПолезногоИспользованияБУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И ДанныеДокумента.УказанОстаточныйСрокБУ
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) 
	|					+ ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0)
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	// СрокПолезногоИспользованияНУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияНУФлаг
	|			ТОГДА ДанныеДокумента.СрокИспользованияНУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ, 0)
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	// КоэффициентУскорения
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоэффициентУскоренияБУФлаг
	|			ТОГДА ДанныеДокумента.КоэффициентУскоренияБУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСБУ.КоэффициентУскорения, 0)
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	// КоэффициентАмортизацииБУ
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоэффициентАмортизацииБУФлаг
	|			ТОГДА ДанныеДокумента.КоэффициентАмортизацииБУ
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ
	|	КОНЕЦ КАК КоэффициентАмортизацииБУ,
	|
	// СрокИспользованияДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ 
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ 
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) 
	|					+ ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|				И ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ
	|				И ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|			ТОГДА РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ) 
	|					+ ДанныеДокумента.СрокИспользованияБУ
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И НЕ ДанныеДокумента.УказанОстаточныйСрокБУ
	|
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации 
	|					+ (ДанныеДокумента.СрокИспользованияБУ - ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ)
	|
	|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг 
	|				И ДанныеДокумента.УказанОстаточныйСрокБУ
	|
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|					+ РАЗНОСТЬДАТ(ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения, НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаОтражения, ДЕНЬ), МЕСЯЦ)
	|
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// ОбъемПродукцииРаботДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|				И ПараметрыАмортизацииОСБУ.ОсновноеСредство ЕСТЬ NULL
	|
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|				И ПорядокУчетаОС.ОбъемНаработки = ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|
	|			ТОГДА ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации 
	|					+ (ДанныеДокумента.ОбъемНаработки - ПорядокУчетаОС.ОбъемНаработки)
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// ГрафикАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ГрафикАмортизацииФлаг
	|			ТОГДА ДанныеДокумента.ГрафикАмортизации
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ГрафикАмортизации
	|	КОНЕЦ КАК ГрафикАмортизации,
	|
	// СпециальныйКоэффициент
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СпециальныйКоэффициентНУФлаг
	|			ТОГДА ДанныеДокумента.СпециальныйКоэффициентНУ
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент
	|	КОНЕЦ КАК СпециальныйКоэффициент,
	|
	// ЛиквидационнаяСтоимость
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
	|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСБУ_СУчетомИсправлений КАК ПараметрыАмортизацииОСБУ
	|		ПО ПараметрыАмортизацииОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОС_СУчетомИсправлений КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПорядокУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияОС_СУчетомИсправлений КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И (ДанныеДокумента.СрокИспользованияБУФлаг
	|			ИЛИ ДанныеДокумента.СрокИспользованияНУФлаг
	|			ИЛИ ДанныеДокумента.КоэффициентУскоренияБУФлаг
	|			ИЛИ ДанныеДокумента.КоэффициентАмортизацииБУФлаг
	|			ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг
	|			ИЛИ ДанныеДокумента.ГрафикАмортизацииФлаг
	|			ИЛИ ДанныеДокумента.СпециальныйКоэффициентНУФлаг
	|			ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг)
	|	И ДанныеДокумента.ПараметрыДействуютСПрошлойДаты";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ Локализация

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.СобытиеОС КАК Событие,
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер КАК НомерДокумента
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.НачислятьАмортизациюБУФлаг 
	|		ИЛИ ДанныеДокумента.НачислятьАмортизациюНУФлаг
	|		ИЛИ ДанныеДокумента.СобытиеОС <> ЗНАЧЕНИЕ(Справочник.СобытияОС.ПустаяСсылка))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПараметров(Объект, Отказ)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РезультатПроверки.Ссылка КАК Ссылка,
	|	РезультатПроверки.Наименование КАК Наименование,
	|	РезультатПроверки.ГруппаОС КАК ГруппаОС,
	|	РезультатПроверки.ОшибкаНедвижимоеИмущество КАК ОшибкаНедвижимоеИмущество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбъектыЭксплуатации.Ссылка КАК Ссылка,
	|		ОбъектыЭксплуатации.Наименование КАК Наименование,
	|		ОбъектыЭксплуатации.ГруппаОС КАК ГруппаОС,
	|
	|		(&НедвижимоеИмуществоФлаг
	|			И &ОтражатьВРеглУчете
	|			И ОбъектыЭксплуатации.ГруппаОС В (
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.РабочийСкот),
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПродуктивныйСкот), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки))) КАК ОшибкаНедвижимоеИмущество
	|
	|	ИЗ
	|		Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|	ГДЕ
	|		ОбъектыЭксплуатации.Ссылка В (&СписокОС)) КАК РезультатПроверки
	|ГДЕ
	|	РезультатПроверки.ОшибкаНедвижимоеИмущество";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("СписокОС", Объект.ОС.ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("НедвижимоеИмуществоФлаг", Объект.НедвижимоеИмуществоФлаг);
	Запрос.УстановитьПараметр("ОтражатьВРеглУчете", Объект.ОтражатьВРеглУчете);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонСообщенияНедвижимоеИмущество = НСтр("ru = 'Основное средство ""%1"" относится к группе ОС ""%2"", для которой не допускается изменение признака ""Недвижимое имущество""';
												|en = 'Fixed asset ""%1"" belongs to the FA group ""%2"", for which the change of the attribute ""Real estate"" is not allowed'");
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Объект.ОС.Найти(Выборка.Ссылка, "ОсновноеСредство");
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", ДанныеСтроки.НомерСтроки, "ОсновноеСредство");
		
		Если Выборка.ОшибкаНедвижимоеИмущество Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияНедвижимоеИмущество, СокрЛП(Выборка.Наименование), Строка(Выборка.ГруппаОС));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#Область Прочее

//++ Локализация

Процедура ПроверитьТЧАмортизация(Объект, МассивНепроверяемыхРеквизитов, Отказ)

	МассивНепроверяемыхРеквизитов.Добавить("Амортизация.ОрганизацияПолучательРасходов");
	
	ШаблонПолучатель = НСтр("ru = 'Не заполнено поле ""Получатель"" в строке ""%1"" табличной части ""Амортизация"".';
							|en = 'The ""Recipient"" field is blank in line ""%1"" of the ""Depreciation"" tabular section.'");
	Для каждого ДанныеСтроки Из Объект.Амортизация Цикл
		Если ДанныеСтроки.ПередаватьРасходыВДругуюОрганизацию
			И НЕ ЗначениеЗаполнено(ДанныеСтроки.ОрганизацияПолучательРасходов) Тогда
			
			Путь = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Амортизация", ДанныеСтроки.НомерСтроки, "ОрганизацияПолучательРасходов");
			ТекстСообщения = СтрШаблон(ШаблонПолучатель, ДанныеСтроки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, Путь,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьТЧАмортизационнаяПремия(Объект, МассивНепроверяемыхРеквизитов, Отказ)
	
	МассивНепроверяемыхРеквизитов.Добавить("АмортизационнаяПремия.ОрганизацияПолучательРасходов");
	
	Для каждого ДанныеСтроки Из Объект.АмортизационнаяПремия Цикл
		Если ДанныеСтроки.ПередаватьРасходыВДругуюОрганизацию
			И НЕ ЗначениеЗаполнено(ДанныеСтроки.ОрганизацияПолучательРасходов) Тогда
			
			Путь = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("АмортизационнаяПремия", ДанныеСтроки.НомерСтроки, "ОрганизацияПолучательРасходов");
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнено поле ""Получатель"" в строке ""%1"" табличной части ""Амортизационная премия"".';
											|en = 'The ""Recipient"" field is blank in line ""%1"" of the ""Bonus depreciation"" tabular section.'"), ДанныеСтроки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, Путь,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьСлужебныеРеквизитыДляРасчетаАмортизации(Объект)

	Объект.СуммаКоэффициентовАмортизационнойПремии = Объект.АмортизационнаяПремия.Итог("Коэффициент");
	
	СуммаКоэффициентовПредыдущихСтрок = 0;
	Для каждого ДанныеСтроки Из Объект.АмортизационнаяПремия Цикл
		ДанныеСтроки.СуммаКоэффициентовПредыдущихСтрок = СуммаКоэффициентовПредыдущихСтрок;
		СуммаКоэффициентовПредыдущихСтрок = СуммаКоэффициентовПредыдущихСтрок + ДанныеСтроки.Коэффициент;
	КонецЦикла; 
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти
