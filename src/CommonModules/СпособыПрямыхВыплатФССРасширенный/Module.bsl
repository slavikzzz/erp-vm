#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриОпределенииСпособовПрямыхВыплат(ТипыСпособов) Экспорт
	
	ОписаниеСпособа = ТипыСпособов.Добавить();
	ОписаниеСпособа.ИмяТипа   = "СправочникСсылка.БанковскиеСчетаКонтрагентов";
	ОписаниеСпособа.Заголовок = НСтр("ru = 'На банковский счет';
									|en = 'To bank account'");
	ОписаниеСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк;
	
КонецПроцедуры

Процедура ПриОпределенииЗарплатныхПроектовИБанковскихСчетов(ФизическиеЛица, БанковскиеРеквизиты) Экспорт
	СтандартнаяОбработка = Ложь;
	Разрешенные = Ложь;
	Организация = Неопределено;
	Дата = ТекущаяДатаСеанса();
	КадровыеДанные = "Организация, ФизическоеЛицо, МестоВыплаты";
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(МенеджерВТ, Разрешенные, ФизическиеЛица, Организация, Дата, Дата);
	ИмяВТ     = "ВТОсновныеСотрудникиФизическихЛиц";
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(МенеджерВТ, ИмяВТ);
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Разрешенные, КадровыеДанные);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.МестоВыплаты КАК МестоВыплаты,
	|	ЕСТЬNULL(Сотрудники.Организация, ЗарплатныеПроекты.Организация) КАК Организация,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТМестаВыплатыЗарплатныеПроекты
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО Сотрудники.МестоВыплаты = ЗарплатныеПроекты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.МестоВыплаты КАК МестоВыплаты,
	|	Сотрудники.Организация КАК Организация,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТМестаВыплатыБанковскиеСчета
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК Сотрудники
	|ГДЕ
	|	Сотрудники.МестоВыплаты ССЫЛКА Справочник.БанковскиеСчетаКонтрагентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарплатныеПроекты.Ссылка КАК Значение,
	|	ЗарплатныеПроекты.Представление КАК ПредставлениеЗначения,
	|	ЗарплатныеПроекты.ПометкаУдаления КАК ПометкаУдаления,
	|	ЗарплатныеПроекты.Банк КАК Банк,
	|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерСчета,
	|	ЛицевыеСчета.ДатаОткрытияЛицевогоСчета КАК ДатаОткрытияСчета,
	|	ЛицевыеСчета.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА МестаВыплаты.МестоВыплаты ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЯвляетсяМестомВыплатыЗарплаты
	|ПОМЕСТИТЬ ВТИтогиБезБИК
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО ЛицевыеСчета.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМестаВыплатыЗарплатныеПроекты КАК МестаВыплаты
	|		ПО ЛицевыеСчета.ФизическоеЛицо = МестаВыплаты.ФизическоеЛицо
	|			И ЛицевыеСчета.ЗарплатныйПроект = МестаВыплаты.МестоВыплаты
	|			И ЛицевыеСчета.Организация = МестаВыплаты.Организация
	|ГДЕ
	|	ЛицевыеСчета.ФизическоеЛицо В(&ФизическиеЛица)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчетаКонтрагентов.Ссылка,
	|	БанковскиеСчетаКонтрагентов.Представление,
	|	БанковскиеСчетаКонтрагентов.ПометкаУдаления,
	|	БанковскиеСчетаКонтрагентов.Банк,
	|	БанковскиеСчетаКонтрагентов.Владелец,
	|	БанковскиеСчетаКонтрагентов.НомерСчета,
	|	НЕОПРЕДЕЛЕНО,
	|	ЕСТЬNULL(МестаВыплаты.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА МестаВыплаты.МестоВыплаты ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМестаВыплатыБанковскиеСчета КАК МестаВыплаты
	|		ПО БанковскиеСчетаКонтрагентов.Владелец = МестаВыплаты.ФизическоеЛицо
	|			И БанковскиеСчетаКонтрагентов.Ссылка = МестаВыплаты.МестоВыплаты
	|ГДЕ
	|	БанковскиеСчетаКонтрагентов.Владелец В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИтогиБезБИК.Значение КАК Значение,
	|	ВТИтогиБезБИК.ПредставлениеЗначения КАК ПредставлениеЗначения,
	|	ВТИтогиБезБИК.ПометкаУдаления КАК ПометкаУдаления,
	|	ВТИтогиБезБИК.Банк КАК Банк,
	|	КлассификаторБанков.Наименование КАК НаименованиеБанка,
	|	КлассификаторБанков.Код КАК БИК,
	|	КлассификаторБанков.КоррСчет КАК КоррСчет,
	|	ВТИтогиБезБИК.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТИтогиБезБИК.НомерСчета КАК НомерСчета,
	|	ВТИтогиБезБИК.ДатаОткрытияСчета КАК ДатаОткрытияСчета,
	|	ЕСТЬNULL(Организации.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(Организации.ГоловнаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ГоловнаяОрганизация,
	|	ВТИтогиБезБИК.ЯвляетсяМестомВыплатыЗарплаты КАК ЯвляетсяМестомВыплатыЗарплаты
	|ИЗ
	|	ВТИтогиБезБИК КАК ВТИтогиБезБИК
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТИтогиБезБИК.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО ВТИтогиБезБИК.Банк = КлассификаторБанков.Ссылка";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	БанковскиеРеквизиты = Запрос.Выполнить().Выгрузить();
КонецПроцедуры

Процедура ПриОпределенииВидимостиСпособаВыплатыПособийЧерезИнуюОрганизацию(Видимость) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		МодульГосударственнаяСлужба.ПриОпределенииВидимостиСпособаВыплатыПособийЧерезИнуюОрганизацию(Видимость);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
