#Область СлужебныеПроцедурыИФункции

Функция ОсновнойСотрудникФизическогоЛица(ФизическоеЛицо, Организация, Период) Экспорт
	
	СотрудникиФизическихЛиц = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо), Истина, Организация, Период);
	
	Если СотрудникиФизическихЛиц.Количество() > 0 Тогда
		
		СтрокаСотрудника = СотрудникиФизическихЛиц.Найти(ФизическоеЛицо, "ФизическоеЛицо");
		Если СтрокаСотрудника <> Неопределено Тогда
			Возврат СтрокаСотрудника.Сотрудник;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ФизическоеЛицоСотрудника(Сотрудник) Экспорт
	
	ФизическиеЛица = КадровыйУчет.ФизическиеЛицаСотрудников(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник));
	
	Если ФизическиеЛица.Количество() > 0 Тогда
		Возврат ФизическиеЛица[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПросклоненноеФизическоеЛицо(ФизическоеЛицо, Падеж) Экспорт
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизическоеЛицо, "ФИО,Пол");
		Возврат СклонениеПредставленийОбъектов.ПросклонятьФИО(
			ЗначенияРеквизитов.ФИО, Падеж, ФизическоеЛицо,
			?(ЗначенияРеквизитов.Пол = Перечисления.ПолФизическогоЛица.Женский, 2, 1));
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция НастройкиКадровогоУчета() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураНастроек = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(РегистрыСведений.НастройкиКадровогоУчета.СоздатьМенеджерЗаписи(), Метаданные.РегистрыСведений.НастройкиКадровогоУчета);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиКадровогоУчета.*
		|ИЗ
		|	РегистрСведений.НастройкиКадровогоУчета КАК НастройкиКадровогоУчета";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураНастроек, Выборка);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтруктураНастроек;
	
КонецФункции

#КонецОбласти
