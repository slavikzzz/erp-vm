////////////////////////////////////////////////////////////////////////////////
// ЗАПОЛНЕНИЕ БУХГАЛТЕРСКОЙ ОТЧЕТНОСТИ.
// Модуль предназначен для заполнения и просмотра регламентированного отчета
// "Бухгалтерская отчетность организаций" и финансовой отчетности в банки.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ЕстьРасшифровкаПоПоказателям(АдресХранилищаРасшифровки, ИменаПоказателей) Экспорт
	
	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	ТаблицаРасшифровок.Индексы.Добавить("ИмяПоказателя");
	
	ЕстьПоказателиИзСписка = Ложь;
	
	Для Каждого ИмяПоказателя Из ИменаПоказателей Цикл
		Если ТаблицаРасшифровок.Найти(ИмяПоказателя, "ИмяПоказателя") <> Неопределено Тогда
			ЕстьПоказателиИзСписка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьПоказателиИзСписка;
	
КонецФункции

Функция ПолучитьДополнительныеПараметрыРасшифровкиПоказателя(АдресХранилищаРасшифровки, ИмяПоказателя) Экспорт
	
	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	
	ДополнительныеПараметры = Неопределено;
	
	Если ТаблицаРасшифровок <> Неопределено Тогда 
		Расшифровка = ТаблицаРасшифровок.Найти(ИмяПоказателя, "ИмяПоказателя");
		Если Расшифровка <> Неопределено Тогда
			ДополнительныеПараметры = Расшифровка.ДополнительныеПараметры;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ЕстьДетальныеЗаписиРасшифровки(АдресХранилищаРасшифровки, ИмяПоказателя) Экспорт
	
	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	
	ДополнительныеПараметры = Неопределено;
	
	Если ТаблицаРасшифровок <> Неопределено Тогда
		Расшифровка = ТаблицаРасшифровок.Найти(ИмяПоказателя, "ИмяПоказателя");
		Если Расшифровка <> Неопределено Тогда
			ДополнительныеПараметры = Расшифровка.ДополнительныеПараметры;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ДетальныеЗаписи");
		
	 КонецФункции

Функция ПолучитьДополнительныеПараметрыРасшифровкиПоПараметрам(АдресХранилищаРасшифровки, ИмяПоказателя, КоординатыСтраницыРаздела) Экспорт
	
	ДополнительныеПараметры = Неопределено;

	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	
	Если КоординатыСтраницыРаздела.Количество() = 0 Тогда
		Возврат ДополнительныеПараметры;
	КонецЕсли;
		
	Если ТаблицаРасшифровок <> Неопределено Тогда 
		
		ИмяРаздела = КоординатыСтраницыРаздела[0];
		Отбор = Новый Структура("ИмяПоказателя, ИмяРаздела", ИмяПоказателя, ИмяРаздела);

		ПараметрыРасшифровки = ТаблицаРасшифровок.НайтиСтроки(Отбор);
		Если ПараметрыРасшифровки.Количество() > 0 Тогда
			ДополнительныеПараметры = ПараметрыРасшифровки[0].ДополнительныеПараметры;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ПолучитьДополнительныеПараметрыРасшифровкиПоПоказателюСИерархиейСтраниц(АдресХранилищаРасшифровки, ИмяПоказателя, ИмяРаздела, КоординатыСтраницыРаздела) Экспорт
	
	ДополнительныеПараметры = Неопределено;

	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	
	Если КоординатыСтраницыРаздела.Количество() = 0 Тогда
		Возврат ДополнительныеПараметры;
	КонецЕсли;
		
	Если ТаблицаРасшифровок <> Неопределено Тогда 
		
		ИтоговыйНомерСтраницы = ИмяРаздела;
		Для каждого НомерСтраницы Из КоординатыСтраницыРаздела Цикл
			ИтоговыйНомерСтраницы = СтрШаблон("%1_%2", ИтоговыйНомерСтраницы, Формат(НомерСтраницы, "ЧН=0; ЧГ="));
		КонецЦикла;

		Отбор = Новый Структура("ИмяПоказателя, ИмяРаздела", ИмяПоказателя, ИтоговыйНомерСтраницы);

		ПараметрыРасшифровки = ТаблицаРасшифровок.НайтиСтроки(Отбор);
		Если ПараметрыРасшифровки.Количество() > 0 Тогда
			ДополнительныеПараметры = ПараметрыРасшифровки[0].ДополнительныеПараметры;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ПолучитьДополнительныеПараметрыРасшифровкиПоРазделуСИерархиейСтраниц(АдресХранилищаРасшифровки, ИмяРаздела, КоординатыСтраницыРаздела) Экспорт
	
	ДополнительныеПараметры = Неопределено;

	ТаблицаРасшифровок = ПолучитьИзВременногоХранилища(АдресХранилищаРасшифровки);
	
	Если КоординатыСтраницыРаздела.Количество() = 0 Тогда
		Возврат ДополнительныеПараметры;
	КонецЕсли;
		
	Если ТаблицаРасшифровок <> Неопределено Тогда 
		
		ИтоговыйНомерСтраницы = ИмяРаздела;
		Для каждого НомерСтраницы Из КоординатыСтраницыРаздела Цикл
			ИтоговыйНомерСтраницы = СтрШаблон("%1_%2", ИтоговыйНомерСтраницы, Формат(НомерСтраницы, "ЧН=0; ЧГ="));
		КонецЦикла;

		Отбор = Новый Структура("ИмяРаздела", ИтоговыйНомерСтраницы);

		ПараметрыРасшифровки = ТаблицаРасшифровок.НайтиСтроки(Отбор);
		Если ПараметрыРасшифровки.Количество() > 0 Тогда
			ДополнительныеПараметры = ПараметрыРасшифровки[0].ДополнительныеПараметры;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ДополнительныеПараметры;
	
КонецФункции

#КонецОбласти
