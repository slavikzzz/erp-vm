#Область СлужебныйПрограммныйИнтерфейс

// Возвращает максимальный размер принимаемого файла в байтах.
Функция МаксимальныйРазмерПринимаемогоФайла() Экспорт
	Возврат 5242880;
КонецФункции

Функция РезультатПроверкиДанныхОтветственного(Ответственный, Данные) Экспорт
	
	Результат = Новый Структура("ДанныеЗаполнены,ЕстьОшибки,СтрокаПояснение,СтрокаТелефонПочта");
	Результат.ДанныеЗаполнены = Ложь;
	Результат.ЕстьОшибки = Ложь;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Результат.СтрокаТелефонПочта = НСтр("ru = 'Укажите ответственное лицо';
											|en = 'Укажите ответственное лицо'");
		Результат.СтрокаПояснение = НСтр("ru = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты';
										|en = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты'");
	Иначе
		
		ТелефонУказан = Ложь;
		ТелефонСоответствуетТребованиям = Ложь;
		Если ЗначениеЗаполнено(Данные.МобильныйТелефон) Тогда
			СтрокаТелефон = СокрЛП(Данные.МобильныйТелефон);
			ТелефонУказан = Истина;
			ТелефонСоответствуетТребованиям = (Лев(СтрокаТелефон,1) = "+") И СтрНайти("123456789", Сред(СтрокаТелефон, 2, 1)) <> 0;
		Иначе
			СтрокаТелефон = НСтр("ru = '<телефон не указан>';
								|en = '<телефон не указан>'");
		КонецЕсли;
		
		ПочтаУказана = Ложь;
		ПочтаСоответствуетТребованиям = Ложь;
		Если ЗначениеЗаполнено(Данные.ЭлектроннаяПочта) Тогда
			СтрокаПочта = СОКРЛП(Данные.ЭлектроннаяПочта);
			ПочтаУказана = Истина;
			ПочтаСоответствуетТребованиям = ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Данные.ЭлектроннаяПочта);
		Иначе
			СтрокаПочта = НСтр("ru = '<Email не указан>';
								|en = '<Email не указан>'");
		КонецЕсли;
		
		Результат.СтрокаТелефонПочта = СтрШаблон(НСтр("ru = 'Мобильный телефон и почта: %1, %2';
														|en = 'Мобильный телефон и почта: %1, %2'"), СтрокаТелефон, СтрокаПочта);
		
		Если ТелефонУказан И ТелефонСоответствуетТребованиям И ПочтаУказана И ПочтаСоответствуетТребованиям И ЗначениеЗаполнено(Данные.Пол) Тогда
			Результат.СтрокаПояснение = НСтр("ru = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты';
											|en = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты'");
			Результат.ДанныеЗаполнены = Истина;
		Иначе
			
			Результат.ЕстьОшибки = Истина;
			Если ТелефонУказан И ТелефонСоответствуетТребованиям И ПочтаУказана И ПочтаСоответствуетТребованиям Тогда
				Результат.СтрокаПояснение = НСтр("ru = 'Заполните, пожалуйста, пол ответственного';
												|en = 'Заполните, пожалуйста, пол ответственного'");
			Иначе
				
				СообщениеНеУказанПол = "";
				Если Не ЗначениеЗаполнено(Данные.Пол) Тогда
					СообщениеНеУказанПол = НСтр("ru = ', не указан пол';
												|en = ', не указан пол'");
				КонецЕсли;
				
				СообщениеПочтаСоответствуетТребованиям = "";
				Если ПочтаУказана И Не ПочтаСоответствуетТребованиям Тогда
					СообщениеПочтаСоответствуетТребованиям = НСтр("ru = ', адрес электронной почты указан некорректно';
																	|en = ', адрес электронной почты указан некорректно'");
				КонецЕсли;
				
				СообщениеТелефонСоответствуетТребованиям = "";
				Если ТелефонУказан И Не ТелефонСоответствуетТребованиям Тогда
					Если (Лев(СтрокаТелефон,1) <> "+") Тогда
						СообщениеТелефонСоответствуетТребованиям = НСтр("ru = ', номер телефон должен начинаться с символа ""+""';
																		|en = ', номер телефон должен начинаться с символа ""+""'");
					Иначе
						СообщениеТелефонСоответствуетТребованиям = НСтр("ru = ', код страны в мобильном телефоне указан некорректно';
																		|en = ', код страны в мобильном телефоне указан некорректно'");
					КонецЕсли;
				КонецЕсли;
				
				Результат.СтрокаПояснение = СтрШаблон(
					НСтр("ru = 'Заполните, пожалуйста, контактные данные ответственного%1%2%3';
						|en = 'Заполните, пожалуйста, контактные данные ответственного%1%2%3'"),
					СообщениеТелефонСоответствуетТребованиям,
					СообщениеПочтаСоответствуетТребованиям,
					СообщениеНеУказанПол);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти
