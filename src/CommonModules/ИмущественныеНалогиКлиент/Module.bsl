////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы "Имущественные налоги".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ЗемельныйНалог

// Настройка льготы по земельному налогу.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ДанныеНастройки - ДокументОбъект.ПараметрыНачисленияЗемельногоНалога - Данные настройки.
//  ОбработкаНастройки - ОписаниеОповещения - Обработка настройки.
Процедура НастройкаЛьготыПоЗемельномуНалогу(Форма, ДанныеНастройки, ОбработкаНастройки) Экспорт

	Объект = Форма.Объект;
	
	ПараметрыЛьготы = Новый Структура(
	"НалоговаяСтавка,
	|ВариантУменьшенияСуммыНалога,
	|ЛьготнаяСтавка,
	|ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
	|ДоляНеОблагаемойНалогомПлощадиЧислитель,
	|КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|НалоговаяЛьготаПоНалоговойБазе,
	|НеОблагаемаяНалогомСумма,
	|ПроцентУменьшенияСуммыНалога,
	|СнижениеНалоговойСтавки,
	|СниженнаяНалоговаяСтавка,
	|СуммаУменьшенияСуммыНалога,
	|УменьшениеНалоговойБазыНаСумму,
	|УменьшениеНалоговойБазыПоСтатье391,
	|Период,
	|ОснованиеЛьготыПоНалоговойБазе,
	|НачалоДействияЛьготыПоНалоговойБазе,
	|ОкончаниеДействияЛьготыПоНалоговойБазе,
	|ОснованиеЛьготыСнижениеСтавки,
	|НачалоДействияЛьготыСнижениеСтавки,
	|ОкончаниеДействияЛьготыСнижениеСтавки,
	|ОснованиеЛьготыСнижениеСуммыНалога,
	|НачалоДействияЛьготыСнижениеСуммыНалога,
	|ОкончаниеДействияЛьготыСнижениеСуммыНалога");
	ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, ДанныеНастройки);
	
	ПараметрыЛьготы.Период = Объект.Дата;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПараметрыЛьготы);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ДанныеНастройки", ДанныеНастройки);
	ДопПараметры.Вставить("ОбработкаНастройки", ОбработкаНастройки);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НастройкаЛьготыПоЗемельномуНалогуЗавершение", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("ОбщаяФорма.НастройкаЛьготыПоЗемельномуНалогу", 
		ПараметрыФормы, Форма,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура НастройкаЛьготыПоЗемельномуНалогуЗавершение(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") 
		ИЛИ ДопПараметры.Форма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры.Форма.Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(ДопПараметры.ДанныеНастройки, Результат);
	
	ВыполнитьОбработкуОповещения(ДопПараметры.ОбработкаНастройки, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ТранспортныйНалог

Процедура ВыбратьКодВидаТранспортногоСредства(Форма, Элемент, Дата, ТекущийКод, ОбработчикВыбора) Экспорт
	
	СписокВыбораКодовТС = Новый СписокЗначений;
	СписокВыбораКодовТС.Добавить("51000", НСтр("ru = '51000 Автомобили легковые';
												|en = '51000 Passenger cars'"));
	СписокВыбораКодовТС.Добавить("51004", НСтр("ru = '51004 - прочие автомобили легковые (кроме включенных по кодам 56600, 56700)';
												|en = '51004 - other cars (except for included by 56600, 56700 codes)'"));
	СписокВыбораКодовТС.Добавить("52001", НСтр("ru = '52001 Автомобили грузовые (кроме включенных по коду 57000)';
												|en = '52001 Trucks (except for included by 57000 code)'"));
	СписокВыбораКодовТС.Добавить("ФормаВыбораКодаТС", НСтр("ru = 'Другие коды видов ТС...';
															|en = 'Other codes of vehicle kinds...'"));
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("Дата", Дата);
	ДопПараметры.Вставить("ТекущийКод", ТекущийКод);
	ДопПараметры.Вставить("ОбработчикВыбора", ОбработчикВыбора);
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьКодВидаТранспортногоСредстваЗавершение", ЭтотОбъект, ДопПараметры);
	Форма.ПоказатьВыборИзСписка(Оповещение, СписокВыбораКодовТС, Элемент);
	
КонецПроцедуры

Процедура ВыбратьКодВидаТранспортногоСредстваЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
			
	Если Результат.Значение = "ФормаВыбораКодаТС" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекущийКод", ДопПараметры.ТекущийКод);
		ПараметрыФормы.Вставить("Дата", ДопПараметры.Дата);
		
		ОткрытьФорму(
			"ОбщаяФорма.ВыборКодаВидаТранспортногоСредства",
			ПараметрыФормы,
			ДопПараметры.Форма,,,,
			ДопПараметры.ОбработчикВыбора,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		РезультатВыбора = Новый Структура("КодВидаТС, Наименование", Результат.Значение, Результат.Представление);
		ВыполнитьОбработкуОповещения(ДопПараметры.ОбработчикВыбора, РезультатВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаЛьготыПоТранспортномуНалогу(Форма, ДанныеНастройки, ОбработкаНастройки) Экспорт
	
	Объект = Форма.Объект;
	
	ПараметрыЛьготы = Новый Структура(
	"НалоговаяЛьгота,
	|ЛьготнаяСтавка,
	|ПроцентУменьшения,
	|СуммаУменьшения,
	|КодНалоговойЛьготы,
	|РегиональныйКодЛьготы,
	|Дата");
	ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, ДанныеНастройки);
	
	ПараметрыЛьготы.Дата = Объект.Дата;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПараметрыЛьготы);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ДанныеНастройки", ДанныеНастройки);
	ДопПараметры.Вставить("ОбработкаНастройки", ОбработкаНастройки);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НастройкаЛьготыПоТранспортномуНалогуЗавершение", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("ОбщаяФорма.НастройкаЛьготыПоТранспортномуНалогу", 
		ПараметрыФормы, Форма,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура НастройкаЛьготыПоТранспортномуНалогуЗавершение(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") 
		ИЛИ ДопПараметры.Форма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры.Форма.Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(ДопПараметры.ДанныеНастройки, Результат);
	
	ВыполнитьОбработкуОповещения(ДопПараметры.ОбработкаНастройки, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ВыбратьКодИзКлассификатора(ИмяКода, НазваниеМакета, НазваниеОбъекта, Дата, ТекущийКод, ОбработкаВыбора) Экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта", НазваниеОбъекта);
	ПараметрыФормы.Вставить("НазваниеМакета", НазваниеМакета);
	ПараметрыФормы.Вставить("ТекущийПериод", Дата);
	ПараметрыФормы.Вставить("ТекущийКод", ТекущийКод);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяКода", ИмяКода);
	ДопПараметры.Вставить("ТекущийКод", ТекущийКод);
	ДопПараметры.Вставить("ТекущийКод", ТекущийКод);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,, ОбработкаВыбора);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
