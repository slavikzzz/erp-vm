///////////////////////////////////////////////////////////
// Процедуры и функции подсистемы "Переработка на стороне"
// 
///////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область СозданиеНаОсновании

// Возвращает параметры открытия формы Документ.ПередачаТоваровХранителю при вводе на основании
//
// Параметры:
//  ПараметрКоманды - Структура
// Возвращаемое значение:
//  Структура - содержит:
//   * Основание - Структура - 
//
Функция ПередачаТоваровХранителюПараметрыОткрытияФормы(ПараметрКоманды) Экспорт
	
	ПараметрыОснования = Новый Структура;
	ПараметрыОснования.Вставить("СкладОтгрузки", Неопределено);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПараметрКоманды)) Тогда
		
		Если ТипЗнч(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() > 0 Тогда
			ДокументОснование = ПараметрКоманды[0];
		Иначе
			ДокументОснование = ПараметрКоманды;
		КонецЕсли;
		
		Если ОбщегоНазначенияУТ.ПроверитьОперациюРаспоряжения(
				ДокументОснование,
				"ПередачаТоваровХранителю",
				Перечисления.ХозяйственныеОперации.ПередачаПереработчику2_5) Тогда
			ПараметрыОснования.Вставить("ДокументОснование", ДокументОснование);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
		ПараметрыОснования.Вставить("ДокументОснование", ДокументОснование);
		
	Иначе
		
		РеквизитыШапки = Новый Структура;
		ИмяДокумента   = "ПередачаТоваровХранителю";
		
		Если Не СформироватьДанныеЗаполненияПередачиСырья(ПараметрКоманды, ИмяДокумента, РеквизитыШапки) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ПараметрыОснования.Вставить("РеквизитыШапки",    РеквизитыШапки);
		ПараметрыОснования.Вставить("ДокументОснование", ПараметрКоманды);
		
	КонецЕсли;
	
	Возврат Новый Структура("Основание", ПараметрыОснования);
	
КонецФункции

// Формирует структуру для создания передачи сырья по одному или нескольким заказам переработчикам.
// Если в переданных заказах отличаются реквизиты шапки, выдается сообщение об ошибке.
//
// Параметры:
//  МассивСсылок     - Массив из ДокументСсылка.ЗаказПереработчику2_5, ДокументСсылка.ЗаказПереработчику2_5 - заказы переработчику,
//                                                                 по которым необходимо ввести передачу сырья.
//  ИмяДокумента    - Строка - имя документа создаваемого на основании заказов.
//  РеквизитыШапки  - Структура - структура, в которую будут помещены реквизиты шапки из массива заказов.
//  СвойстваЗаказов - Структура, Неопределено - 
//
// Возвращаемое значение:
//   Булево - Ложь, если в переданных заказах отличаются реквизиты шапки.
//
Функция СформироватьДанныеЗаполненияПередачиСырья(МассивСсылок, ИмяДокумента, РеквизитыШапки, СвойстваЗаказов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Если ОбщегоНазначения.РежимОтладки() Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заказ.Ссылка                                                  КАК ЗаказКлиента,
	|	Заказ.Партнер                                                 КАК Партнер,
	|	Заказ.Контрагент                                              КАК Контрагент,
	|	Заказ.Договор                                                 КАК Договор,
	|	Заказ.Организация                                             КАК Организация,
	|	Заказ.Сделка                                                  КАК Сделка,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА Заказ.ГруппировкаЗатрат =
	|				ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|			ТОГДА ЕСТЬNULL(ОстаткиЗаказов.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА Заказ.СкладМатериалов
	|	КОНЕЦ                                                         КАК Склад,
	|	ЕСТЬNULL(ОстаткиЗаказов.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладОтгрузки,
	|	Заказ.Подразделение                                           КАК Подразделение,
	|	Заказ.Валюта                                                  КАК ВалютаВзаиморасчетов,
	|	Заказ.ЗакупкаПодДеятельность                                  КАК НалогообложениеНДС,
	|	Заказ.ЦенаВключаетНДС                                         КАК ЦенаВключаетНДС,
	|	Заказ.ПорядокРасчетов                                         КАК ПорядокРасчетов,
	|	Заказ.ОплатаВВалюте                                           КАК ОплатаВВалюте,
	|	Заказ.ФормаОплаты                                             КАК ФормаОплаты,
	|	Заказ.ГрафикОплаты                                            КАК ГрафикОплаты,
	|	Заказ.БанковскийСчет                                          КАК БанковскийСчет,
	|	Заказ.Касса                                                   КАК Касса,
	|	ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка) КАК БанковскийСчетКонтрагента,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)                 КАК Грузоотправитель,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)                 КАК Грузополучатель,
	|	ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка) КАК БанковскийСчетГрузоотправителя,
	|	ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка) КАК БанковскийСчетГрузополучателя,
	|	Заказ.СпособДоставки                                          КАК СпособДоставки,
	|	Заказ.СпособДоставки =
	|		ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)  КАК СпособДоставкиСиламиПеревозчика,
	|	Заказ.ПеревозчикПартнер                                       КАК ПеревозчикПартнер,
	|	Заказ.АдресДоставки                                           КАК АдресДоставки,
	|	Заказ.АдресДоставкиЗначенияПолей                              КАК АдресДоставкиЗначенияПолей,
	|	Заказ.АдресДоставкиПеревозчика                                КАК АдресДоставкиПеревозчика,
	|	Заказ.АдресДоставкиПеревозчикаЗначенияПолей                   КАК АдресДоставкиПеревозчикаЗначенияПолей,
	|	Заказ.ЗонаДоставки                                            КАК ЗонаДоставки,
	|	Заказ.ВремяДоставкиС                                          КАК ВремяДоставкиС,
	|	Заказ.ВремяДоставкиПо                                         КАК ВремяДоставкиПо,
	|	Заказ.ДополнительнаяИнформацияПоДоставке                      КАК ДополнительнаяИнформацияПоДоставке,
	|	Заказ.НаправлениеДеятельности                                 КАК НаправлениеДеятельности,
	|	Заказ.КонтактноеЛицо                                          КАК КонтактноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.КартыЛояльности.ПустаяСсылка)             КАК КартаЛояльности,
	|	ЛОЖЬ                                                          КАК ЕстьНедопустимаяОперация
	|ПОМЕСТИТЬ ВтЗаказы
	|ИЗ
	|	Документ.ЗаказПереработчику2_5 КАК Заказ
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыКлиентов.Остатки(, ЗаказКлиента В (&МассивСсылок) И (НЕ &ОтборПоСкладу ИЛИ Склад В (&Склад))) КАК ОстаткиЗаказов
	|	ПО Заказ.Ссылка = ОстаткиЗаказов.ЗаказКлиента
	|ГДЕ
	|	Заказ.Ссылка В (&МассивСсылок)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заказы.ЗаказКлиента    КАК ЗаказКлиента,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтЗаказы КАК Заказы
	|ГДЕ
	|	Заказы.ЕстьНедопустимаяОперация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(Заказы.Организация)                    КАК Организация,
	|	МИНИМУМ(Заказы.Партнер)                        КАК Партнер,
	|	МИНИМУМ(Заказы.Контрагент)                     КАК Контрагент,
	|	МИНИМУМ(Заказы.Договор)                        КАК Договор,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Подразделение) > 1
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заказы.Подразделение)
	|	КОНЕЦ                                          КАК Подразделение,
	|	МИНИМУМ(Заказы.Склад)                          КАК Склад,
	|	МИНИМУМ(Заказы.СкладОтгрузки)                  КАК СкладОтгрузки,
	|	МИНИМУМ(Заказы.КартаЛояльности)                КАК КартаЛояльности,
	|	МИНИМУМ(Заказы.Сделка)                         КАК Сделка,
	|	&ХозяйственнаяОперация                         КАК ХозяйственнаяОперация,
	|	МИНИМУМ(Заказы.ВалютаВзаиморасчетов)           КАК ВалютаВзаиморасчетов,
	|	МИНИМУМ(Заказы.НалогообложениеНДС)             КАК НалогообложениеНДС,
	|	МИНИМУМ(Заказы.ЦенаВключаетНДС)                КАК ЦенаВключаетНДС,
	|	МИНИМУМ(Заказы.ПорядокРасчетов)                КАК ПорядокРасчетов,
	|	МИНИМУМ(Заказы.ОплатаВВалюте)                  КАК ОплатаВВалюте,
	|	МИНИМУМ(Заказы.ГрафикОплаты)                   КАК ГрафикОплаты,
	|	МИНИМУМ(Заказы.НаправлениеДеятельности)        КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ФормаОплаты) > 1
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заказы.ФормаОплаты)
	|	КОНЕЦ                                          КАК ФормаОплаты,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.КонтактноеЛицо <> ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|						ТОГДА Заказы.КонтактноеЛицо
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.КонтактноеЛицо) = 1
	|			ТОГДА МАКСИМУМ(Заказы.КонтактноеЛицо)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|	КОНЕЦ                                          КАК КонтактноеЛицо,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|						ТОГДА Заказы.БанковскийСчет
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Организация) = 1
	|			ТОГДА МАКСИМУМ(Заказы.БанковскийСчет)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|	КОНЕЦ                                          КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|						ТОГДА Заказы.Касса
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Организация) = 1
	|			ТОГДА МАКСИМУМ(Заказы.Касса)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|	КОНЕЦ                                          КАК Касса,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.БанковскийСчетКонтрагента <>
	|							ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|						ТОГДА Заказы.БанковскийСчетКонтрагента
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Контрагент) = 1
	|			ТОГДА МАКСИМУМ(Заказы.БанковскийСчетКонтрагента)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ                                          КАК БанковскийСчетКонтрагента,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Грузоотправитель <>
	|							ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|						ТОГДА Заказы.Грузоотправитель
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			ТОГДА МАКСИМУМ(Заказы.Грузоотправитель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ                                          КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|						ТОГДА Заказы.Грузополучатель
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			ТОГДА МАКСИМУМ(Заказы.Грузополучатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ                                          КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.БанковскийСчетГрузоотправителя <>
	|							ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|						ТОГДА Заказы.БанковскийСчетГрузоотправителя
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|						ТОГДА Заказы.Грузоотправитель
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			ТОГДА МАКСИМУМ(Заказы.БанковскийСчетГрузоотправителя)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ                                          КАК БанковскийСчетГрузоотправителя,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.БанковскийСчетГрузополучателя <>
	|							ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|						ТОГДА Заказы.БанковскийСчетГрузополучателя
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|						ТОГДА Заказы.Грузополучатель
	|					ИНАЧЕ NULL
	|				КОНЕЦ) = 1
	|			ТОГДА МАКСИМУМ(Заказы.БанковскийСчетГрузополучателя)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ                                          КАК БанковскийСчетГрузополучателя,
	|	ВЫБОР
	|		КОГДА МИНИМУМ(Заказы.Склад.ЭтоГруппа) = ИСТИНА
	|			И МИНИМУМ(Заказы.Склад.ВыборГруппы) = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                          КАК ЗапрещеноВыбиратьГруппуСкладов,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.СпособДоставки) = 1
	|			ТОГДА МИНИМУМ(Заказы.СпособДоставки)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.Самовывоз)
	|	КОНЕЦ                                          КАК СпособДоставки,
	|	ВЫБОР
	|		КОГДА МИНИМУМ(Заказы.СпособДоставки) = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ПеревозчикПартнер) = 1
	|			ТОГДА МИНИМУМ(Заказы.ПеревозчикПартнер)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ                                          КАК ПеревозчикПартнер,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.АдресДоставки) = 1
	|			ТОГДА МИНИМУМ(Заказы.АдресДоставки)
	|		ИНАЧЕ """"
	|	КОНЕЦ                                          КАК АдресДоставки,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.АдресДоставки) = 1
	|			ТОГДА МИНИМУМ(ВЫРАЗИТЬ(Заказы.АдресДоставкиЗначенияПолей КАК СТРОКА(10))) = """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                          КАК АдресДоставкиЗначенияПолейПустой,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками
	|			И МИНИМУМ(Заказы.СпособДоставки) = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫРАЗИТЬ(Заказы.ДополнительнаяИнформацияПоДоставке КАК СТРОКА(10))) = 1
	|			ТОГДА МИНИМУМ(ВЫРАЗИТЬ(Заказы.ДополнительнаяИнформацияПоДоставке КАК СТРОКА(10))) = """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                          КАК ДополнительнаяИнформацияПоДоставкеПустой,
	|	НЕОПРЕДЕЛЕНО                                   КАК АдресДоставкиПеревозчика,
	|	НЕОПРЕДЕЛЕНО                                   КАК АдресДоставкиПеревозчикаЗначенияПолей,
	|	НЕОПРЕДЕЛЕНО                                   КАК ЗонаДоставки,
	|	НЕОПРЕДЕЛЕНО                                   КАК ВремяДоставкиС,
	|	НЕОПРЕДЕЛЕНО                                   КАК ВремяДоставкиПо,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Организация) > 1   КАК ЕстьОтличияОрганизация,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Партнер) > 1       КАК ЕстьОтличияПартнер,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Контрагент) > 1    КАК ЕстьОтличияКонтрагент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Договор) > 1       КАК ЕстьОтличияДоговор,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Сделка) > 1        КАК ЕстьОтличияСделка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.Склад) > 1         КАК ЕстьОтличияСклад,
	|	ЛОЖЬ                                           КАК ЕстьОтличияХозяйственнаяОперация,
	|	ЛОЖЬ                                           КАК ЕстьОтличияЭтоЗаказКакСчет,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ВалютаВзаиморасчетов) > 1    КАК ЕстьОтличияВалютаВзаиморасчетов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.НалогообложениеНДС) > 1      КАК ЕстьОтличияНалогообложениеНДС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ЦенаВключаетНДС) > 1         КАК ЕстьОтличияЦенаВключаетНДС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ПорядокРасчетов) > 1         КАК ЕстьОтличияПорядокРасчетов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.НаправлениеДеятельности) > 1 КАК ЕстьОтличияНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками
	|			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.СпособДоставки) > 1
	|			И МАКСИМУМ(Заказы.СпособДоставкиСиламиПеревозчика) = ИСТИНА
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                          КАК ЕстьНесовместимыеСпособыДоставки,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|						ТОГДА Заказы.БанковскийСчет
	|					ИНАЧЕ NULL
	|				КОНЕЦ) > 1                         КАК ЕстьОтличияБанковскийСчет,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|				ВЫБОР
	|					КОГДА Заказы.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|						ТОГДА Заказы.Касса
	|					ИНАЧЕ NULL
	|				КОНЕЦ) > 1                         КАК ЕстьОтличияКасса,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ФормаОплаты) > 1   КАК ЕстьОтличияФормаОплаты,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ОплатаВВалюте) > 1 КАК ЕстьОтличияОплатаВВалюте,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заказы.ГрафикОплаты) > 1  КАК ЕстьОтличияГрафикОплаты
	|ПОМЕСТИТЬ ВТРеквизитыШапки
	|ИЗ
	|	ВтЗаказы КАК Заказы
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ШапкаДокумента.Партнер                        КАК Партнер,
	|	ШапкаДокумента.Контрагент                     КАК Контрагент,
	|	ШапкаДокумента.Договор                        КАК Договор,
	|	ШапкаДокумента.Организация                    КАК Организация,
	|	ШапкаДокумента.КартаЛояльности                КАК КартаЛояльности,
	|	ШапкаДокумента.Сделка                         КАК Сделка,
	|	ШапкаДокумента.Склад                          КАК Склад,
	|	ШапкаДокумента.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	ШапкаДокумента.ВалютаВзаиморасчетов           КАК ВалютаВзаиморасчетов,
	|	ШапкаДокумента.НалогообложениеНДС             КАК НалогообложениеНДС,
	|	ШапкаДокумента.ЦенаВключаетНДС                КАК ЦенаВключаетНДС,
	|	ШапкаДокумента.ПорядокРасчетов                КАК ПорядокРасчетов,
	|	ШапкаДокумента.ФормаОплаты                    КАК ФормаОплаты,
	|	ШапкаДокумента.ОплатаВВалюте                  КАК ОплатаВВалюте,
	|	ШапкаДокумента.Касса                          КАК Касса,
	|	ШапкаДокумента.ГрафикОплаты                   КАК ГрафикОплаты,
	|	ЛОЖЬ                                          КАК ЕстьОтличияГруппаФинансовогоУчета,
	|	ШапкаДокумента.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	ШапкаДокумента.Подразделение                  КАК Подразделение,
	|	ШапкаДокумента.КонтактноеЛицо                 КАК КонтактноеЛицо,
	|	ШапкаДокумента.ЕстьОтличияПартнер             КАК ЕстьОтличияПартнер,
	|	ШапкаДокумента.ЕстьОтличияКонтрагент          КАК ЕстьОтличияКонтрагент,
	|	ШапкаДокумента.ЕстьОтличияДоговор             КАК ЕстьОтличияДоговор,
	|	ЛОЖЬ                                          КАК ЕстьОтличияСоглашение,
	|	ЛОЖЬ                                          КАК ЕстьОтличияВернутьМногооборотнуюТару,
	|	ЛОЖЬ                                          КАК ЕстьОтличияТребуетсяЗалогЗаТару,
	|	ЛОЖЬ                                          КАК ЕстьОтличияСрокВозвратаМногооборотнойТары,
	|	ШапкаДокумента.ЕстьОтличияОрганизация         КАК ЕстьОтличияОрганизация,
	|	ШапкаДокумента.ЕстьОтличияСделка              КАК ЕстьОтличияСделка,
	|	ШапкаДокумента.ЕстьОтличияСклад               КАК ЕстьОтличияСклад,
	|	ШапкаДокумента.ЕстьОтличияХозяйственнаяОперация   КАК ЕстьОтличияХозяйственнаяОперация,
	|	ШапкаДокумента.ЕстьОтличияВалютаВзаиморасчетов    КАК ЕстьОтличияВалютаВзаиморасчетов,
	|	ШапкаДокумента.ЕстьНесовместимыеСпособыДоставки   КАК ЕстьНесовместимыеСпособыДоставки,
	|	ШапкаДокумента.ЕстьОтличияНаправлениеДеятельности КАК ЕстьОтличияНаправлениеДеятельности,
	|	ШапкаДокумента.ЕстьОтличияНалогообложениеНДС  КАК ЕстьОтличияНалогообложениеНДС,
	|	ШапкаДокумента.ЕстьОтличияЦенаВключаетНДС     КАК ЕстьОтличияЦенаВключаетНДС,
	|	ШапкаДокумента.ЕстьОтличияПорядокРасчетов     КАК ЕстьОтличияПорядокРасчетов,
	|	ШапкаДокумента.ЕстьОтличияБанковскийСчет      КАК ЕстьОтличияБанковскийСчет,
	|	ШапкаДокумента.ЕстьОтличияКасса               КАК ЕстьОтличияКасса,
	|	ШапкаДокумента.ЕстьОтличияФормаОплаты         КАК ЕстьОтличияФормаОплаты,
	|	ШапкаДокумента.ЕстьОтличияОплатаВВалюте       КАК ЕстьОтличияОплатаВВалюте,
	|	ШапкаДокумента.ЕстьОтличияГрафикОплаты        КАК ЕстьОтличияГрафикОплаты,
	|	ШапкаДокумента.ЕстьОтличияЭтоЗаказКакСчет     КАК ЕстьОтличияЭтоЗаказКакСчет,
	|	ШапкаДокумента.БанковскийСчет                 КАК БанковскийСчет,
	|	ШапкаДокумента.БанковскийСчетКонтрагента      КАК БанковскийСчетКонтрагента,
	|	ШапкаДокумента.Грузоотправитель               КАК Грузоотправитель,
	|	ШапкаДокумента.Грузополучатель                КАК Грузополучатель,
	|	ШапкаДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ШапкаДокумента.БанковскийСчетГрузополучателя  КАК БанковскийСчетГрузополучателя,
	|	ШапкаДокумента.СкладОтгрузки                  КАК СкладОтгрузки,
	|	ШапкаДокумента.ЗапрещеноВыбиратьГруппуСкладов КАК ЗапрещеноВыбиратьГруппуСкладов,
	|	ШапкаДокумента.СпособДоставки                 КАК СпособДоставки,
	|	ШапкаДокумента.ПеревозчикПартнер              КАК ПеревозчикПартнер,
	|	ШапкаДокумента.АдресДоставки                  КАК АдресДоставки,
	|	ВЫБОР
	|		КОГДА ШапкаДокумента.АдресДоставкиЗначенияПолейПустой
	|		ТОГДА """" 
	|		ИНАЧЕ ЕСТЬNULL(ШапкаЗаказа.АдресДоставкиЗначенияПолей, """")
	|	КОНЕЦ                                                КАК АдресДоставкиЗначенияПолей,
	|	ВЫБОР
	|		КОГДА ШапкаДокумента.ДополнительнаяИнформацияПоДоставкеПустой
	|		ТОГДА """" 
	|		ИНАЧЕ ЕСТЬNULL(ШапкаЗаказа.ДополнительнаяИнформацияПоДоставке, """")
	|	КОНЕЦ                                                КАК ДополнительнаяИнформацияПоДоставке,
	|	ШапкаДокумента.АдресДоставкиПеревозчика              КАК АдресДоставкиПеревозчика,
	|	ШапкаДокумента.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
	|	ШапкаДокумента.ЗонаДоставки                          КАК ЗонаДоставки,
	|	ШапкаДокумента.ВремяДоставкиС                        КАК ВремяДоставкиС,
	|	ШапкаДокумента.ВремяДоставкиПо                       КАК ВремяДоставкиПо,
	|	0                                                    КАК СрокВозвратаМногооборотнойТары,
	|	ЛОЖЬ                                                 КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
	|	ЗНАЧЕНИЕ(Справочник.ПроизводственныеКалендари.ПустаяСсылка) КАК КалендарьВозвратаТары
	|ИЗ
	|	ВТРеквизитыШапки КАК ШапкаДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтЗаказы КАК ШапкаЗаказа
	|	ПО ИСТИНА
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заказ.Ссылка      КАК ЗаказКлиента,
	|	Заказ.Статус      КАК Статус,
	|	НЕ Заказ.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА
	|			Заказ.Статус В (&МассивСтатусовЗаказа)
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК ЕстьОшибкиСтатус
	|	
	|ИЗ
	|	Документ.ЗаказПереработчику2_5 КАК Заказ
	|ГДЕ
	|	Заказ.Ссылка В (&МассивСсылок)
	|	И (НЕ Заказ.Проведен
	|		ИЛИ НЕ Заказ.Статус В(&МассивСтатусовЗаказа))";

	Запрос.УстановитьПараметр("МассивСсылок" , МассивСсылок);
	
	Запрос.УстановитьПараметр(
		"ХозяйственнаяОперация" , ПереработкаНаСторонеКлиентСервер.ХозяйственнаяОперацияПоИмениДокумента(ИмяДокумента));
		
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами", Ложь);
	
	Запрос.УстановитьПараметр(
		"ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками",
		ПолучитьФункциональнуюОпцию("ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками"));
		
	Запрос.УстановитьПараметр(
		"МассивСтатусовЗаказа",
		Документы.ЗаказПереработчику2_5.ДопустимыеСтатусыВводаНаОсновании());
	
	ОтборПоСкладу = Ложь;
	Склад         = Неопределено;
	Если ТипЗнч(СвойстваЗаказов) = Тип("Структура")
		И СвойстваЗаказов.Свойство("Склад") Тогда
		ОтборПоСкладу = Истина;
		Склад         = СвойстваЗаказов.Склад;
	КонецЕсли;
	Запрос.УстановитьПараметр("ОтборПоСкладу", ОтборПоСкладу);
	Запрос.УстановитьПараметр("Склад",         Склад);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); // Массив из РезультатЗапроса - 
	
	РезультатРеквизитыШапки = РезультатЗапроса[3];
	ВыборкаРеквизитыШапки = РезультатРеквизитыШапки.Выбрать();
	ВыборкаРеквизитыШапки.Следующий();
	
	ИсключаемыеЗаказы = Новый Массив;
	ЗаказыСНедопустимойОперацией = РезультатЗапроса[1].Выбрать();
	
	Пока ЗаказыСНедопустимойОперацией.Следующий() Цикл
		
		Если МассивСсылок.Количество() > 1 Тогда
			ТекстСообщения = НСтр("ru = 'Распоряжение %Распоряжение% пропущено, т.к. невозможно оформить документ ""%ИмяДокумента%"" на основании распоряжения с операцией ""%ХозяйственнаяОперация%"".';
									|en = 'The %Распоряжение% reference is skipped as you cannot register the ""%ИмяДокумента%"" document based on the reference with the ""%ХозяйственнаяОперация%"" transaction.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Невозможно оформить документ ""%ИмяДокумента%"" на основании распоряжения с операцией ""%ХозяйственнаяОперация%"".';
									|en = 'Cannot register the ""%ИмяДокумента%"" document based on the reference with the ""%ХозяйственнаяОперация%"" operation.'");
		КонецЕсли;
		
		ТекстСообщения =
		 СтрЗаменить(ТекстСообщения, "%ИмяДокумента%", НСтр("ru = 'Передача сырья переработчику';
															|en = 'Goods issue — Subcontracting services received'"));
		 
		ТекстСообщения =
			СтрЗаменить(ТекстСообщения, "%ХозяйственнаяОперация%", ЗаказыСНедопустимойОперацией.ХозяйственнаяОперация);
			
		ТекстСообщения =
		 СтрЗаменить(ТекстСообщения, "%Распоряжение%", ЗаказыСНедопустимойОперацией.ЗаказКлиента);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЗаказыСНедопустимойОперацией.ЗаказКлиента);
		
		ИсключаемыеЗаказы.Добавить(ЗаказыСНедопустимойОперацией.ЗаказКлиента);
		
	КонецЦикла;
	
	Если ЗаказыСНедопустимойОперацией.Количество() = МассивСсылок.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МассивСсылок = ОбщегоНазначенияКлиентСервер.РазностьМассивов(МассивСсылок, ИсключаемыеЗаказы);
	
	Отказ = Ложь;
	
	Если ПродажиСервер.СообщитьОбОшибкахФормированияДанныхЗаполненияРеализации(ВыборкаРеквизитыШапки) Тогда
		ТекстОшибки = НСтр("ru = 'Ввод одной передачи сырья переработчику невозможен. Воспользуйтесь рабочим местом ""Документы к оформлению"".';
							|en = 'You cannot enter one customer stock fill-up. Use the ""Unregistered documents"" workplace.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	Иначе
		
		Если Не РезультатЗапроса[4].Пустой() Тогда

			ВыборкаЗаказы = РезультатЗапроса[4].Выбрать();
			
			Пока ВыборкаЗаказы.Следующий() Цикл
				
				Документы.ЗаказПереработчику2_5.ПроверитьВозможностьВводаНаОсновании(
					ВыборкаЗаказы.ЗаказКлиента, ВыборкаЗаказы.Статус, ВыборкаЗаказы.ЕстьОшибкиПроведен);
				
			КонецЦикла;
			
		КонецЕсли;
		
		РеквизитыШапки = Новый Структура;
		Для Каждого Колонка Из РезультатРеквизитыШапки.Колонки Цикл
			РеквизитыШапки.Вставить(Колонка.Имя, ВыборкаРеквизитыШапки[Колонка.Имя]);
		КонецЦикла;
		
		ДоставкаТоваров.ДополнитьРеквизитамиДоставкиДанныеЗаполнения(РеквизитыШапки,ВыборкаРеквизитыШапки);
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

#КонецОбласти