////////////////////////////////////////////////////////////////////////////////
// Управление производством: содержит процедуры для управления производством.
// Модуль входит в подсистему "УправлениеПредприятием".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОткрытиеФорм

// Открывает заказ на производство с выделением строки продукции
//
// Параметры:
//  Заказ		 - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство
//  ДанныеСтроки - см. УправлениеПроизводствомКлиентСервер.СтруктураПродукцииЗаказа
//  Форма		 - ФормаКлиентскогоПриложения						 - форма из которой выполняется переход.
//
Процедура ОткрытьСтрокуЗаказаНаПроизводства(Заказ, ДанныеСтроки, Форма) Экспорт
	
	Параметры = Новый Структура("Ключ, ВыбраннаяСтрока", Заказ, ДанныеСтроки);
	ОткрытьФорму("Документ.ЗаказНаПроизводство2_2.ФормаОбъекта", Параметры, Форма);
	
КонецПроцедуры

// Открывает очередь заказов
//
// Параметры:
//  СтруктураОтборов - Структура							 - устанавливаемые отборы
//  ТекущаяСтрока	 - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство
//  Форма			 - ФормаКлиентскогоПриложения						 - форма из которой выполняется переход.
//
Процедура ОткрытьОчередьЗаказов(СтруктураОтборов = Неопределено, ТекущаяСтрока = Неопределено, Форма = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	
	Если СтруктураОтборов <> Неопределено Тогда
		Параметры.Вставить("СтруктураОтборов", СтруктураОтборов);
	КонецЕсли;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Параметры.Вставить("ТекущаяСтрока", ТекущаяСтрока);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаказНаПроизводство2_2.Форма.Очередь", Параметры, Форма);
	
КонецПроцедуры

// Открывает форму редактирования цепочки этапов
//
// Параметры:
//  Распоряжение		 - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство
//  ПартияПроизводства	 - СправочникСсылка.ПартииПроизводства	 - партия производства
//  Продукция			 - Массив								 - массив структур, определяющих перечень дефицита продукции.
//  Форма				 - ФормаКлиентскогоПриложения						 - форма (владелец)
//
Процедура ОткрытьФормуРедактированияЦепочкиЭтапов(Распоряжение, ПартияПроизводства, Продукция = Неопределено, Форма = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Распоряжение", Распоряжение);
	Параметры.Вставить("ПартияПроизводства", ПартияПроизводства);
	Параметры.Вставить("Продукция", Продукция);
	
	ОткрытьФорму("Обработка.РедактированиеПоследовательностиЭтаповПроизводства.Форма", Параметры, Форма);
	
КонецПроцедуры

// Открывает форму создания новой цепочки этапов
//
// Параметры:
//  Распоряжение				 - ДокументСсылка.ЗаказНаПроизводство2_2			 - заказ на производство.
//  ТипПроизводственногоПроцесса - ПеречислениеСсылка.ТипыПроизводственныхПроцессов	 - тип производственного процесса.
//  Назначение					 - СправочникСсылка.Назначения						 - назначение продукции заказа.
//  Продукция					 - Массив											 - массив структур, определяющих перечень дефицита продукции.
//  Форма						 - ФормаКлиентскогоПриложения									 - форма (владелец).
//
Процедура ОткрытьФормуСозданияНовойЦепочкиЭтапов(Распоряжение,
												ТипПроизводственногоПроцесса,
												Назначение,
												Продукция = Неопределено,
												Форма = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Распоряжение", Распоряжение);
	Параметры.Вставить("ТипПроизводственногоПроцесса", ТипПроизводственногоПроцесса);
	Параметры.Вставить("Назначение", Назначение);
	Параметры.Вставить("Продукция", Продукция);
	
	ОткрытьФорму("Обработка.РедактированиеПоследовательностиЭтаповПроизводства.Форма", Параметры, Форма);
	
КонецПроцедуры

// Открывает рабочее место "Выполнение операций" с отбором по этапу производства
//
// Параметры:
//  Этап	 - ДокументСсылка.ЭтапПроизводства2_2, Массив	 - этап или список этапов
//  Форма	 - ФормаКлиентскогоПриложения								 - форма из которой выполняется переход.
//
Процедура ОткрытьВыполнениеОпераций(Этап, Форма) Экспорт
	
	МассивСсылок = ОбщегоНазначенияУТКлиентСервер.Массив(Этап);
	
	ВыбранноеПодразделение = Неопределено;
	Подразделения = УправлениеПроизводствомВызовСервера.ПодразделенияИсполнители(МассивСсылок);
	Для каждого Ссылка Из МассивСсылок Цикл
		
		Если ВыбранноеПодразделение = Неопределено Тогда
			ВыбранноеПодразделение = Подразделения[Ссылка];
		КонецЕсли;
		
		Если ВыбранноеПодразделение <> Подразделения[Ссылка] Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для выбранных объектов!';
											|en = 'Command cannot be executed for the selected objects.'"));
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыПодразделения = ПроизводствоВызовСервера.ПараметрыПроизводственногоПодразделения(ВыбранноеПодразделение);
	
	Если Не ПараметрыПодразделения.ИспользоватьПооперационноеУправление Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена, в подразделении не используется пооперационное управление!';
										|en = 'Command cannot be executed, operation management is not used in the business unit.'"));
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Подразделение, ОтборПоЭтапам", ВыбранноеПодразделение, МассивСсылок);
	
	Если ПараметрыПодразделения.ИспользоватьПооперационноеПланирование Тогда
		ИмяФормы = "Обработка.ВыполнениеОперацийПриПланировании.Форма.РабочееМесто";
	Иначе
		ИмяФормы = "Обработка.ВыполнениеОпераций2_2.Форма.РабочееМесто";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы, СтруктураПараметров, Форма);

КонецПроцедуры

// Открывает упрощенную форму отражения выполненного этапа производства
//
// Параметры:
//  ЗаказОбъект  - ДанныеФормыСтруктура	 - заказ на производство, содержит:
//                  * Ссылка                                     - ДокументСсылка.ЗаказНаПроизводство2_2 -
//                  * НазначениеПродукция                        - СправочникСсылка.Назначения -
//                  * ПартияПроизводства                         - СправочникСсылка.ПартииПроизводства -
//                  * СпособРаспределенияЗатратНаВыходныеИзделия - ПеречислениеСсылка.СпособыРаспределенияЗатратНаВыходныеИзделия -
//  Форма       - ФормаКлиентскогоПриложения - форма из которой выполняется переход.
//
Процедура ОткрытьФормуОтраженияВыполненногоЭтапаИзЗаказа(ЗаказОбъект, Форма) Экспорт
	
	ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.БезСпецификаций");
	ПустаяСпецификация = ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка");
	
	РеквизитыШапки = УправлениеПроизводствомВызовСервера.ЗначенияЗаполненияНовогоЭтапаПроизводства(
		ЗаказОбъект.Ссылка,
		ЗаказОбъект.НазначениеПродукция,
		ЗаказОбъект.ПартияПроизводства,
		ТипПроизводственногоПроцесса,
		ПустаяСпецификация);
	
	Дата = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Завершен");
	
	РеквизитыШапки.Вставить("Статус", Статус);
	РеквизитыШапки.Вставить("СпособРаспределенияЗатратНаВыходныеИзделия", ЗаказОбъект.СпособРаспределенияЗатратНаВыходныеИзделия);

	РеквизитыШапки.Вставить("НеОтгружатьЧастями",			Истина);
	РеквизитыШапки.Вставить("РасходОднойДатой",				Истина);
	РеквизитыШапки.Вставить("ПроизводствоОднойДатой",		Истина);
	РеквизитыШапки.Вставить("ВыполнениеРаботОднойДатой",	Истина);
	
	РеквизитыШапки.Вставить("Дата",							Дата);
	РеквизитыШапки.Вставить("ФактическоеНачалоЭтапа",		Дата);
	РеквизитыШапки.Вставить("ФактическоеОкончаниеЭтапа",	Дата);
	РеквизитыШапки.Вставить("ДатаОтгрузки",					Дата);
	РеквизитыШапки.Вставить("ДатаРасхода",					Дата);
	РеквизитыШапки.Вставить("ДатаПроизводства",				Дата);
	РеквизитыШапки.Вставить("ДатаВыполненияРабот",			Дата);
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("РеквизитыШапки", РеквизитыШапки);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОтражениеВыполненного", Истина);
	Параметры.Вставить("ЗначенияЗаполнения",    ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЭтапПроизводства2_2.ФормаОбъекта", Параметры, Форма);
	
КонецПроцедуры

// Открывает форму выбора этапа-потребителя
//
// Параметры:
//  Объект					 - ДокументОбъект.ЭтапПроизводства2_2	 - этап
//  ТекущаяСтрока			 - Структура							 - данные текущей строки
//  Владелец				 - ФормаКлиентскогоПриложения						 - форма или элемент управления в которой выполняется выбор
//  ОписаниеОповещения		 - ОписаниеОповещения					 - содержит описание процедуры, которая будет вызвана при закрытии формы
//
Процедура ОткрытьФормуВыбораЭтапаПотребителя(Объект, ТекущаяСтрока, Владелец, ОписаниеОповещения = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Распоряжение", Объект.Распоряжение);

	ПараметрыФормы.Вставить("Номенклатура", ТекущаяСтрока.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", ТекущаяСтрока.Характеристика);
	ПараметрыФормы.Вставить("Назначение", ТекущаяСтрока.Назначение);
	
	ПараметрыФормы.Вставить("Получатель", ТекущаяСтрока.Получатель);
	
	ПараметрыФормы.Вставить("Источник", Объект.Ссылка);
	
	ОткрытьФорму("РегистрНакопления.ОбеспечениеПроизводственныхПроцессов.Форма.ФормаВыбораЭтаповПроизводства",
		ПараметрыФормы,
		Владелец,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

// Открывает форму, детализирующую состояние выполнения производственных операций этапа производства.
//
// Параметры:
//  Этап	  - ДокументСсылка.ЭтапПроизводства2_2 - этап.
//  Состояние - ПеречислениеСсылка.СостоянияОперацийЭтапаПроизводства - состояние выполнения операций.
//  Форма	  - ФормаКлиентскогоПриложения - владелец открываемой формы.
//
Процедура РасшифроватьСостояниеОперацийЭтапа(Этап, Состояние, Форма) Экспорт
	
	Если Этап.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = ОбщегоНазначенияУТКлиентСервер.Массив(Этап);
	
	Подразделения = УправлениеПроизводствомВызовСервера.ПодразделенияИсполнители(МассивСсылок);
	Подразделение = Подразделения[Этап];
	
	ПараметрыПодразделения = ПроизводствоВызовСервера.ПараметрыПроизводственногоПодразделения(Подразделение);
	
	Если Не ПараметрыПодразделения.ИспользоватьПооперационноеУправление Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена, в подразделении не используется пооперационное управление!';
										|en = 'Command cannot be executed, operation management is not used in the business unit.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыПодразделения.ИспользоватьСменныеЗадания
		ИЛИ Состояние = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЭтапаПроизводства.Выполнено") Тогда
		
		ПараметрыФормы = Новый Структура("Подразделение, ОтборПоЭтапам", Подразделение, МассивСсылок);
		
		Если ПараметрыПодразделения.ИспользоватьПооперационноеПланирование Тогда
			ИмяФормы = "Обработка.ВыполнениеОперацийПриПланировании.Форма.РабочееМесто";
		Иначе
			ИмяФормы = "Обработка.ВыполнениеОпераций2_2.Форма.РабочееМесто";
		КонецЕсли;
		
		ОткрытьФорму(ИмяФормы, ПараметрыФормы, Форма);
		
	Иначе
		
		Если Состояние = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЭтапаПроизводства.ОжидаетЗавершения") Тогда
			
			ОткрытьФорму(
				"Документ.СменноеЗадание.ФормаСписка",
				Новый Структура("ОтборПоЭтапу", Этап),
				Форма); 
			
		ИначеЕсли Состояние = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЭтапаПроизводства.ОжидаетНазначения") Тогда
			
			ОткрытьФорму(
				"Обработка.ФормированиеСменныхЗаданий.Форма",
				Новый Структура("ОтборПоЭтапу", Этап),
				Форма); 
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму документа операции с замером времени.
// 
// Параметры:
// 	Ключ - ДокументСсылка.ПроизводственнаяОперация2_2 -
Процедура ОткрытьФормуДокументаОперации(Ключ) Экспорт
	
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПроизводственнаяОперация2_2.ФормаДокумента.ОткрытиеФормы",, Ложь);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", Ключ);
	
	ОткрытьФорму("Документ.ПроизводственнаяОперация2_2.Форма.ФормаДокумента", ПараметрыФормы);
	
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
	
КонецПроцедуры

#КонецОбласти

#Область СтатьиНаИТС

// Выполняет переход по ссылке на статью "5 шагов к производству версии 2.2"
//
Процедура ОткрытьСтатью5ШаговКПроизводству22() Экспорт
	
	ОбщегоНазначенияКлиент.ПерейтиПоСсылке("http://its.1c.ru/db/metod81#content:6511:hdoc");
	
КонецПроцедуры

#КонецОбласти

#Область ОповещенияПользователей

// Оповещает пользователя о завершении процесса передачи этапов к выполнению.
//
// Параметры:
//  Результат	 - Структура			 - результат передачи этапов к выполнению
//  Источник	 - УникальныйИдентификатор	 - идентификатор формы, инициировавшей создание документов.
//
Процедура ОповеститьПользователяОПередачиЭтаповКВыполнению(Результат, Источник = Неопределено) Экспорт
	
	Если Результат.Количество > 0 Тогда
		
		ТекстДокументов = ОбщегоНазначенияУТКлиентСервер.ФормаМножественногоЧисла(
				НСтр("ru = 'документ';
					|en = 'document'"), НСтр("ru = 'документа';
											|en = 'document'"), НСтр("ru = 'документов';
																	|en = 'documents'"), Результат.Количество);
		
		ТекстСообщения = НСтр("ru = 'Передано к выполнению %Количество% %ТекстДокументов%';
								|en = 'Released %Количество% %ТекстДокументов%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Результат.Количество);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстДокументов%", ТекстДокументов);
		ТекстЗаголовка = НСтр("ru = 'Этапы производства переданы к выполнению';
								|en = 'Production stages are released'");
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Ни один документ не передан к выполнению';
								|en = 'No document is released'");
		ТекстЗаголовка = НСтр("ru = 'Этапы производства не переданы к выполнению';
								|en = 'Production stages are not released'");
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Оповещает пользователя о завершении процесса создания этапов производства.
//
// Параметры:
//  РезультатФормирования	 - Структура - результат формирования этапов.
//  Источник				 - УникальныйИдентификатор - идентификатор формы, инициировавшей создание документов.
//
Процедура ОповеститьПользователяОФормированииЭтаповПроизводства(РезультатФормирования, Источник = Неопределено) Экспорт
	
	ТекстЗаголовка = НСтр("ru = 'Результат формирования этапов производства';
							|en = 'Production stage generation result'");
	
	Если РезультатФормирования.ЕстьОшибка Тогда
		СтатусОповещения = СтатусОповещенияПользователя.Важное;
	Иначе
		СтатусОповещения = СтатусОповещенияПользователя.Информация;
	КонецЕсли;
	
	Если РезультатФормирования.СформированоДокументов > 0 Тогда
		
		ТекстДокументов = ОбщегоНазначенияУТКлиентСервер.ФормаМножественногоЧисла(
				НСтр("ru = 'документ';
					|en = 'document'"),
				НСтр("ru = 'документа';
					|en = 'document'"),
				НСтр("ru = 'документов';
					|en = 'documents'"),
				РезультатФормирования.СформированоДокументов);
		
		ТекстСообщения = НСтр("ru = 'Сформировано %КоличествоОбработанных% %ТекстДокументов%';
								|en = 'Generated %КоличествоОбработанных% %ТекстДокументов%'");
		ТекстСообщения = СтрЗаменить(
			ТекстСообщения,
			"%КоличествоОбработанных%",
			РезультатФормирования.СформированоДокументов);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстДокументов%", ТекстДокументов);
		
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32, СтатусОповещения);
		
	ИначеЕсли НЕ РезультатФормирования.ЕстьОшибка Тогда
		
		ТекстСообщения = НСтр("ru = 'Не сформирован ни один документ';
								|en = 'No document is generated'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32, СтатусОповещения);
		
	КонецЕсли;
	
	Если РезультатФормирования.ЕстьОшибка Тогда
		
		Ссылка = ПолучитьНавигационнуюСсылку(РезультатФормирования.ОшибкаСсылка);
		
		ТекстСообщения = РезультатФормирования.ОшибкаТекст;
		ПоказатьОповещениеПользователя(ТекстЗаголовка, Ссылка, ТекстСообщения, БиблиотекаКартинок.Ошибка32, СтатусОповещения);
		
	КонецЕсли;
	
	Если РезультатФормирования.Свойство("Распоряжения") И ЗначениеЗаполнено(РезультатФормирования.Распоряжения) Тогда
		Оповестить("Запись_ЭтапыПроизводства", Новый Структура("Распоряжения", РезультатФормирования.Распоряжения));
	Иначе
		Оповестить("Запись_ЭтапыПроизводства");
	КонецЕсли;
	
КонецПроцедуры

// Оповещает пользователя о завершении процесса планирования графика производства.
//
// Параметры:
//  Результат	 - Структура - результат планирования графика производства.
//  Источник	 - УникальныйИдентификатор	 - идентификатор формы, инициировавшей создание документов.
//
Процедура ОповеститьПользователяОПланированииГрафикаПроизводства(Результат, Источник = Неопределено) Экспорт
	
	Если Результат.ЕстьОшибки Тогда
		
		Если Результат.КоличествоОбработанных > 0 Тогда
			ТекстЗаголовка = НСтр("ru = 'Запланировано %КоличествоОбработанных% из %КоличествоВсего% документов';
									|en = '%КоличествоОбработанных% documents planned out of %КоличествоВсего%'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%КоличествоОбработанных%", Результат.КоличествоОбработанных);
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%КоличествоВсего%",        Результат.КоличествоВсего);
		Иначе
			ТекстЗаголовка = НСтр("ru = 'График производства не запланирован';
									|en = 'Production schedule is not created'");
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'При планировании заказа %1 возникли ошибки. Подробнее см. в рабочем месте ""Планирование графика заказа"" (нажмите для перехода).';
				|en = 'Errors occurred when planning order %1. For more information, see the ""Order schedule planning"" workplace (click to go).'"),
			УправлениеПроизводствомВызовСервера.ПредставлениеЗаказа(Результат.РаспоряжениеОшибка, ""));
		
			
		ДополнительныеПараметры = Новый Структура("Распоряжение", Результат.РаспоряжениеОшибка);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПерейтиКПланированиюГрафикаПроизводстваЗаказа", 
			ЭтотОбъект,
			ДополнительныеПараметры);
	Иначе
		
		Если Результат.КоличествоОбработанных > 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'Запланировано %КоличествоОбработанных% из %КоличествоВсего% документов';
									|en = '%КоличествоОбработанных% documents planned out of %КоличествоВсего%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоОбработанных%", Результат.КоличествоОбработанных);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоВсего%",        Результат.КоличествоВсего);
			ТекстЗаголовка = НСтр("ru = 'График производства запланирован';
									|en = 'Production schedule is created'");
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Не запланирован ни один документ';
									|en = 'No document is planned'");
			ТекстЗаголовка = НСтр("ru = 'График производства не запланирован';
									|en = 'Production schedule is not created'");
			
		КонецЕсли;
		
		ОписаниеОповещения = Неопределено;
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка, ОписаниеОповещения, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Оповещает пользователя о завершении процесса пометки на удаление этапов к выполнению.
//
// Параметры:
//  Результат - Структура - результат выполнения операции.
//
Процедура ОповеститьПользователяОПометкеНаУдалениеЭтаповПроизводства(Результат) Экспорт
	
	ОшибкаСсылка = Неопределено;
	
	Если Результат.КоличествоОбработано > 0 ИЛИ Результат.ЕстьОшибки Тогда
		
		ТекстДокументов = ОбщегоНазначенияУТКлиентСервер.ФормаМножественногоЧисла(
				НСтр("ru = 'документ';
					|en = 'document'"),
				НСтр("ru = 'документа';
					|en = 'document'"),
				НСтр("ru = 'документов';
					|en = 'documents'"),
				Результат.КоличествоОбработано);
	
		Если Результат.ЕстьОшибки Тогда
			ТекстЗаголовка = НСтр("ru = 'Ошибка установки пометки на удаления этапов!';
									|en = 'An error occurred when marking stages for deletion.'");
			ТекстШаблон = НСтр("ru = 'При пометке на удаление цепочки этапов %1 возникла ошибка! Помечено на удаление %2 %3.';
								|en = 'An error occurred when marking stage chain %1 for deletion. Marked for deletion %2 %3.'");
			ТекстСообщения = СтрШаблон(ТекстШаблон,Результат.ОшибкаСсылка,Формат(Результат.КоличествоОбработано,"ЧН=; ЧГ="),ТекстДокументов);
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Этапы производства помечены на удаление.';
									|en = 'Production stages are marked for deletion.'");
			ТекстШаблон = НСтр("ru = 'Помечено на удаление %1 %2';
								|en = 'Marked for deletion %1 %2'");
			ТекстСообщения = СтрШаблон(ТекстШаблон,Формат(Результат.КоличествоОбработано,"ЧН=; ЧГ="),ТекстДокументов);
		КонецЕсли;
		
		Если Результат.ЕстьОшибки Тогда 
			ОшибкаСсылка = ПолучитьНавигационнуюСсылку(Результат.ОшибкаСсылка);
		КонецЕсли;
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Ни один документ не помечен на удаление';
								|en = 'No document is marked for deletion'");
		ТекстЗаголовка = НСтр("ru = 'Не удалось пометить на удаление этапы производства';
								|en = 'Cannot mark production stages for deletion'");
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка, ОшибкаСсылка, ТекстСообщения, БиблиотекаКартинок.Информация32);	
	
КонецПроцедуры

// Оповещает пользователя об изменении заказа на производство.
//
// Параметры:
//  Результат	 - Структура				 - результат полного расчета (или отмены расчета) структуры заказа
//  Источник	 - УникальныйИдентификатор	 - идентификатор формы, инициировавшей создание документов.
//
Процедура ОповеститьПользователяОбИзмененииЗаказовНаПроизводство(Результат, Источник = Неопределено) Экспорт
	
	КоличествоОбработанных = Результат.ОбработанныеДокументы.Количество();
	
	Если Результат.ВсегоДокументов > КоличествоОбработанных Тогда
		
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Изменение (%1 из %2)';
										|en = 'Change (%1 out of %2)'"), Формат(КоличествоОбработанных,"ЧН=; ЧГ="), Формат(Результат.ВсегоДокументов,"ЧН=; ЧГ="));
		ТекстСообщения = НСтр("ru = 'Управление очередью заказов на производство';
								|en = 'Management of production order queue'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Внимание32);
		
		
	Иначе
	
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Изменение (%1)';
										|en = 'Change (%1)'"), Формат(КоличествоОбработанных,"ЧН=; ЧГ="));
		ТекстСообщения = НСтр("ru = 'Управление очередью заказов на производство';
								|en = 'Management of production order queue'");
		ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(Результат.ОбработанныеДокументы, Новый Структура("ЗаказыНаПроизводство", Результат.ОбработанныеДокументы));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РедактированиеЭтапаПроизводства

#Область ВыходныеИзделия

// Событие при начале редактирования таблицы ВыходныеИзделия.
//
// Параметры:
//  Форма         - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции - текущие данные.
//  ИмяТЧ         - Строка - имя табличной части.
//  НоваяСтрока   - Булево - признак новой строки.
//  Копирование   - Булево - признак копирование.
//
Процедура ВыходныеИзделияЭтапаПриНачалеРедактирования(Форма, ТекущиеДанные, ИмяТЧ, НоваяСтрока, Копирование) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные.КодСтроки = 0;
		
		СкладыКлиентСервер.ЗаполнитьСкладПоУмолчанию(
				Форма.ИспользоватьНесколькоСкладов, 
				Форма.СкладПоУмолчанию, 
				ТекущиеДанные,
				"Получатель");
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораНаправлениеВыпуска(ТекущиеДанные, ИмяТЧ, Форма);
	
	Если НоваяСтрока И Не Копирование Тогда
		ДоходыИРасходыКлиентСервер.ПриДобавленииСтрокиВТаблицу(Форма, ТекущиеДанные, "Объект." + ИмяТЧ);
	КонецЕсли;
	
	Если НоваяСтрока И НЕ ЗначениеЗаполнено(ТекущиеДанные.НаправлениеВыпуска) Тогда
		ТекущиеДанные.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Получатель) Тогда
		Если ТекущиеДанные.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение")
			ИЛИ ТекущиеДанные.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию") Тогда
			ТекущиеДанные.Получатель = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
		Иначе
			ТекущиеДанные.Получатель = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	ОграничениеТипаПолучатель(Элементы[ИмяТЧ + "Получатель"], ТекущиеДанные.НаправлениеВыпуска);
	
КонецПроцедуры

// Событие при изменении поля НаправленияВыпуска.
//
// Параметры:
//  ТекущиеДанные        - ДанныеФормыЭлементКоллекции - текущие данные.
//  ИмяТЧ                - Строка - имя табличной части.
//  Форма                - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  Объект               - ДанныеФормыСтруктура - обрабатываемый объект.
//  КэшированныеЗначения - Структура - переменная модуля формы, в которой хранятся кешируемые значения.
//
Процедура ВыходныеИзделияЭтапаНаправленияВыпускаПриИзменении(ТекущиеДанные, ИмяТЧ, Форма, Объект, КэшированныеЗначения) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.НаправлениеВыпуска) Тогда
		ТекущиеДанные.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад");
	КонецЕсли;
	
	ТекущиеДанные.СписатьНаРасходы = (ТекущиеДанные.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию"));
	ДоходыИРасходыКлиентСервер.ПриИзмененииРеквизитаДоступностиСтатьиВСтроке(Форма, ТекущиеДанные, "Объект." + ИмяТЧ);
	
	ОграничениеТипаПолучатель(Элементы[ИмяТЧ + "Получатель"], ТекущиеДанные.НаправлениеВыпуска);
	ТекущиеДанные.Получатель = Элементы[ИмяТЧ + "Получатель"].ОграничениеТипа.ПривестиЗначение(ТекущиеДанные.Получатель);
	
	Если ТекущиеДанные.НаправлениеВыпуска <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад") Тогда
		ТекущиеДанные.ОбработатьПоСпецификации = Ложь;
		ТекущиеДанные.Спецификация = Неопределено;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	
	НазначениеДавальческогоВыпуска =
		?(ТекущиеДанные.СписатьНаРасходы, Неопределено, Форма.НазначениеДавальческогоВыпуска);
	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение", НазначениеДавальческогоВыпуска);
	
	//++ Устарело_Переработка24
	ДавальческаяСхемаКлиентСервер.ДобавитьДействиеЗаполнитьПризнакДоступноОформлениеОтчетаДавальцу(
		Форма, СтруктураДействий);
	//-- Устарело_Переработка24
	
	Если ИмяТЧ = "ПобочныеИзделия" Тогда
		ВнутренняяПереработкаКлиентСервер.ДобавитьДействиеЗаполнитьВладелецИзделияДоступен(Форма, СтруктураДействий);
	КонецЕсли;
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
	ВыходныеИзделияЭтапаПолучательПриИзменении(ТекущиеДанные, ИмяТЧ, Форма, Объект, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ВыходныеИзделияЭтапаПолучательПриИзменении(ТекущиеДанные, ИмяТЧ, Форма, Объект, КэшированныеЗначения) Экспорт
	
	ПараметрыУказанияСерий = Форма[Форма.ПараметрыРедактированияЭтапа.ИмяРеквизитаПараметрыУказанияСерий];
	
	Если ТекущиеДанные.Получатель = Неопределено Тогда
		ТекущиеДанные.Получатель = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", 
		Новый Структура("Склад, ПараметрыУказанияСерий", ТекущиеДанные.Получатель, ПараметрыУказанияСерий[ИмяТЧ]));
		
	СтруктураДействий.Вставить("ЗаполнитьПризнакЦеховаяКладовая", Новый Структура("ИмяПоляСклад", "Получатель"));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

// Заполняет список выбора направление выпуска.
//
// Параметры:
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции - текущие данные.
//  ИмяТЧ         - Строка - имя табличной части.
//  Форма         - ФормаКлиентскогоПриложения - обрабатываемая форма.
//
// Возвращаемое значение:
//	СписокЗначений.
// 	
Функция ЗаполнитьСписокВыбораНаправлениеВыпуска(ТекущиеДанные, ИмяТЧ, Форма) Экспорт

	Элементы = Форма.Элементы;
	
	СписокВыбораЭлемента = Элементы[ИмяТЧ + "НаправлениеВыпуска"].СписокВыбора;
	СписокВыбораЭлемента.Очистить();
	
	Для каждого Элемент Из УправлениеПроизводствомКлиентСервер.СписокВыбораНаправлениеВыпуска(ТекущиеДанные, Форма) Цикл
		СписокВыбораЭлемента.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;	

	Возврат СписокВыбораЭлемента;
	
КонецФункции

#КонецОбласти

Процедура ИзделияЭтапаСуммаПриИзменении(ТекущиеДанные, КэшированныеЗначения) Экспорт
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуПоСумме");
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ИзделияЭтапаЦенаПриИзменении(ТекущиеДанные, КэшированныеЗначения) Экспорт

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

#Область ЭкономияМатериалов

// Событие при начале редактирования таблицы ЭкономияМатериалов.
//
// Параметры:
//  Форма         - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  Объект        - ДанныеФормыСтруктура - обрабатываемый объект.
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции - текущие данные.
//  НоваяСтрока   - Булево - признак новой строки.
//  Копирование   - Булево - признак копирование.
//
Процедура ЭкономияМатериаловПриНачалеРедактирования(Форма, Объект, ТекущиеДанные, НоваяСтрока, Копирование) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные.КодСтроки = 0;
		
		СкладыКлиентСервер.ЗаполнитьСкладПоУмолчанию(
				Форма.ИспользоватьНесколькоСкладов, 
				Форма.СкладПоУмолчанию, 
				ТекущиеДанные,
				"Получатель");
		
		Если Объект.РасходОднойДатой И Объект.ДатаРасхода <> '000101010000' Тогда
			ТекущиеДанные.ДатаОперации = Объект.ДатаРасхода;
		КонецЕсли;
		
	КонецЕсли;
	
	ЭкономияЗаполнитьСписокВыбораДействия(ТекущиеДанные, Форма);
	
	Если НоваяСтрока И Не Копирование Тогда
		ДоходыИРасходыКлиентСервер.ПриДобавленииСтрокиВТаблицу(Форма, ТекущиеДанные, "Объект.ЭкономияМатериалов");
	КонецЕсли;
	
	Если НоваяСтрока И Не Копирование Тогда
		ТекущиеДанные.Действия = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратИзПроизводства");
	КонецЕсли;
	
	ОграничениеТипаПолучатель(Элементы.ЭкономияМатериаловПолучатель, ТекущиеДанные.Действия);
	
КонецПроцедуры

// Событие при изменении поля ЭкономияМатериаловДействия.
//
// Параметры:
//  ТекущиеДанные        - ДанныеФормыЭлементКоллекции - текущие данные.
//  Форма                - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  Объект               - ДанныеФормыСтруктура - обрабатываемый объект.
//  КэшированныеЗначения - Структура - переменная модуля формы, в которой хранятся кешируемые значения.
//
Процедура ЭкономияМатериаловЭтапаДействиеПриИзменении(ТекущиеДанные, Форма, Объект, КэшированныеЗначения) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Действия) Тогда
		ТекущиеДанные.Действия = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратИзПроизводства");
	КонецЕсли;
	
	ТекущиеДанные.СписатьНаРасходы = (ТекущиеДанные.Действия = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию"));
	ДоходыИРасходыКлиентСервер.ПриИзмененииРеквизитаДоступностиСтатьиВСтроке(Форма, ТекущиеДанные, "Объект.ЭкономияМатериалов");
	
	Если ТекущиеДанные.СписатьНаРасходы Тогда
		ТекущиеДанные.Назначение = ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка");
	КонецЕсли;
	
	ОграничениеТипаПолучатель(Элементы.ЭкономияМатериаловПолучатель, ТекущиеДанные.Действия);
	
	ТекущиеДанные.Получатель = Элементы.ЭкономияМатериаловПолучатель.ОграничениеТипа.ПривестиЗначение(
												ТекущиеДанные.Получатель);
		
	ПолучательПриИзменении(ТекущиеДанные, "ЭкономияМатериалов", Форма, Объект, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ЭкономияМатериаловЗаполнитьПризнакОбособленно(ТекущиеДанные, ОбеспечениеМатериаламиИРаботами) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	Отбор.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	Отбор.Вставить("Обособленно", Истина);
	
	Если ОбеспечениеМатериаламиИРаботами.НайтиСтроки(Отбор).ВГраница() = -1 Тогда
		ТекущиеДанные.Назначение = ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

Функция ЭкономияЗаполнитьСписокВыбораДействия(ТекущиеДанные, Форма)

	Элементы = Форма.Элементы;
	
	СписокВыбораЭлемента = Элементы.ЭкономияМатериаловДействия.СписокВыбора;
	СписокВыбораЭлемента.Очистить();
	
	СписокВыбораЭлемента.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратИзПроизводства"),
									НСтр("ru = 'Передать на склад';
										|en = 'Transfer to warehouse'"));
	
	Если Форма.ИспользоватьСписаниеНаРасходы Тогда
		СписокВыбораЭлемента.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию"),
									НСтр("ru = 'Списать на расходы';
										|en = 'Expense as'"));
	КонецЕсли;
	
	Возврат СписокВыбораЭлемента;
	
КонецФункции

#КонецОбласти

#Область ОбеспечениеМатериалами

// Обеспечение материалами и работами этапа при начале редактирования.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции
//  НоваяСтрока - Булево
//  Копирование - Булево
//
Процедура ОбеспечениеМатериаламиИРаботамиЭтапаПриНачалеРедактирования(Форма, ТекущиеДанные, НоваяСтрока, Копирование) Экспорт
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные.КодСтроки = 0;
		ТекущиеДанные.КлючНоменклатура = Неопределено;
		
		СкладыКлиентСервер.ЗаполнитьСкладПоУмолчанию(
				Форма.ИспользоватьНесколькоСкладов,
				Форма.СкладПоУмолчанию,
				ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбеспечениеМатериаламиИРаботамиЭтапаСпецификацияПриИзменении(Форма, Объект, ТекущиеДанные, ПараметрыУказанияСерий, КэшированныеЗначения) Экспорт
	
	СтруктураДействий = Новый Структура;
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("ЗаказНаПроизводство",           Объект.Распоряжение);
	ПараметрыМетода.Вставить("Подразделение",                 Объект.Подразделение);
	ПараметрыМетода.Вставить("Ссылка",                        Объект.Ссылка);
	ПараметрыМетода.Вставить("Статус",                        Объект.Статус);
	ПараметрыМетода.Вставить("ЗаказПереработчику",            Объект.ЗаказПереработчику);
	ПараметрыМетода.Вставить("ПараметрыУказанияСерий",        ПараметрыУказанияСерий);
	ПараметрыМетода.Вставить("Форма",                         Форма);
	
	УправлениеПроизводствомКлиентСервер.ДобавитьВСтруктуруДействияПроверитьЗаполнитьОбеспечениеВЭтапеПроизводства(
		СтруктураДействий,
		ПараметрыМетода,
		ТекущиеДанные);
		
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

Функция ОбеспечениеМатериаламиИРаботамиЭтапаСкладПриИзменении(Форма, ТекущиеДанные, ПараметрыУказанияСерий, КэшированныеЗначения) Экспорт
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
		Новый Структура("Склад, ПараметрыУказанияСерий", ТекущиеДанные.Склад, ПараметрыУказанияСерий));
		
	ОбеспечениеВДокументахКлиентСервер.ДобавитьДействияОбеспечения(
		СтруктураДействий,
		"ДоступноВДругихСтроках",
		Новый Структура("Форма", Форма));
		
		
	СтруктураДействий.Вставить("ЗаполнитьПризнакЦеховаяКладовая");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
	Возврат СтруктураДействий;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Переходит к строке с автовыбором которую требуется уточнить.
//
// Параметры:
//  Объект             - ДанныеФормыСтруктура - обрабатываемый объект.
//  Форма              - ФормаКлиентскогоПриложения - обрабатываемая форма.
//  ИмяТЧ              - Строка - имя табличной части.
//  ТекущийНомерСтроки - Число - номер сторки.
//  Направление        - Число - направление.
//
Процедура ПерейтиКСтрокеСАвтовыборомКоторуюТребуетсяУточнить(Объект, Форма, ИмяТЧ, ТекущийНомерСтроки, Направление) Экспорт
	
	Если Объект[ИмяТЧ].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	Если Направление = 1 Тогда
		Для каждого Строка из Объект[ИмяТЧ] Цикл
			
			Если Строка.Номенклатура.Пустая()
					ИЛИ Строка.Характеристика.Пустая() И Строка.ХарактеристикиИспользуются Тогда
				
				Если Строка.НомерСтроки > ТекущийНомерСтроки Тогда
					
					Таблица = Элементы[ИмяТЧ];//ТаблицаФормы
					Таблица.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
	Иначе
		
		Сч = Объект[ИмяТЧ].Количество() - 1;
		
		Пока Сч >= 0 Цикл
			
			Строка = Объект[ИмяТЧ][Сч];
			
			Если Строка.Номенклатура.Пустая()
					ИЛИ Строка.Характеристика.Пустая() И Строка.ХарактеристикиИспользуются Тогда
				
				Если Строка.НомерСтроки < ТекущийНомерСтроки Тогда
					
					Таблица = Элементы[ИмяТЧ];//ТаблицаФормы
					Таблица.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Сч = Сч - 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОграничениеТипаПолучатель(Элемент, ХозяйственнаяОперация)
	
	Если ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение")
		ИЛИ ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию") Тогда
		
		ОграничениеТипаПолучатель = Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия");
		
	Иначе
		
		ОграничениеТипаПолучатель = Новый ОписаниеТипов("СправочникСсылка.Склады");
		
	КонецЕсли;
	
	Элемент.ОграничениеТипа = ОграничениеТипаПолучатель;

КонецПроцедуры

Процедура ПолучательПриИзменении(СтрокаТабличнойЧасти, ИмяТЧ, Форма, Объект, КэшированныеЗначения) Экспорт
	
	ПараметрыУказанияСерий = Форма[Форма.ПараметрыРедактированияЭтапа.ИмяРеквизитаПараметрыУказанияСерий];
	
	СтруктураДействий = Новый Структура;
	
	ПараметрыПроверкиСерий = Новый Структура;
	ПараметрыПроверкиСерий.Вставить("Склад", СтрокаТабличнойЧасти.Получатель);
	ПараметрыПроверкиСерий.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий[ИмяТЧ]);
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", ПараметрыПроверкиСерий);
	
	СтруктураДействий.Вставить("ЗаполнитьПризнакЦеховаяКладовая", Новый Структура("ИмяПоляСклад", "Получатель"));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ПерейтиКПланированиюГрафикаПроизводстваЗаказа(ДополнительныеПараметры) Экспорт
	
	ИмяФормы = "Обработка.ПланированиеГрафикаПроизводства2_2.Форма.ПланированиеГрафикаЗаказа";
	
	ОткрытьФорму(ИмяФормы, ДополнительныеПараметры,, ДополнительныеПараметры.Распоряжение);
	
КонецПроцедуры

// Проверяет, удаляет неполные цепочки этапов из списка.
//
// Параметры:
//  Список - ТаблицаФормы - обрабатываемый список.
//
Процедура ПроверитьУдалитьНеполныеЦепочкиЭтаповИзСписка(Список) Экспорт
	
	ВыделенныеСтроки = Список.ВыделенныеСтроки;
	
	МассивЭтапов = Новый Массив;
	СоответствиеСтрок = Новый Соответствие;
	
	Для каждого ТекущаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = Список.ДанныеСтроки(ТекущаяСтрока);//ДокументСсылка - тип ДанныеФормыЭлементКоллекции
		Ссылка = ДанныеСтроки.Ссылка; 
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
			МассивЭтапов.Добавить(Ссылка);
			СоответствиеСтрок.Вставить(Ссылка, ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	МассивЭтапов = УправлениеПроизводствомВызовСервера.СписокДоступныхДляУдаленияЭтаповПроизводства(МассивЭтапов);
	
	Если СоответствиеСтрок.Количество() <> МассивЭтапов.Количество() Тогда
	
		Для каждого КлючИЗначение Из СоответствиеСтрок Цикл
			Если МассивЭтапов.Найти(КлючИЗначение.Ключ) = Неопределено Тогда
				ВыделенныеСтроки.Удалить(ВыделенныеСтроки.Найти(КлючИЗначение.Значение));
			КонецЕсли;
		КонецЦикла;
		
		ТекстСообщения = НСтр("ru = 'Частичное удаление этапов при многоэтапном производственном процессе запрещено, воспользуйтесь рабочим местом ""Структура заказа на производство""';
								|en = 'Partial deletion of stages in a multi-stage production process is prohibited. Use the ""Production order structure"" workplace'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет поиск этапа производства по переданному штрихкоду.
//
// Параметры:
//  Штрихкод - Строка - штрихкод.
// 
// Возвращаемое значение:
//  ДокументСсылка.ЭтапПроизводства2_2, Неопределено - ссылка на найденный этап, Неопределено если значение не найдено.
//
Функция СсылкаНаЭтапПроизводстваПоШтрихкоду(Штрихкод) Экспорт
	
	Результат = Неопределено;
	
	Менеджеры = Новый Массив();
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ЭтапПроизводства2_2.ПустаяСсылка"));
	Менеджеры.Добавить(ПредопределенноеЗначение("Справочник.ПартииПроизводства.ПустаяСсылка"));
	МассивСсылок = ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);
	
	Если МассивСсылок.Количество() > 0 Тогда
		Если ТипЗнч(МассивСсылок[0]) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
			Результат = МассивСсылок[0];
		ИначеЕсли ТипЗнч(МассивСсылок[0]) = Тип("СправочникСсылка.ПартииПроизводства") Тогда
			Результат = УправлениеПроизводствомВызовСервера.ЭтапПоШтрихкодуПартииПроизводства(МассивСсылок[0]);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Прочее

// Процедура сдвигает только что отредактированную строку в коллекции 
// таким образом, чтобы строки коллекции оставались упорядоченными.
//
// Параметры:
//	КоллекцияСтрок - ДанныеФормыКоллекция - коллекция строк.
//	ПолеУпорядочивания - Строка - имя поля элемента коллекции, 
//		по которому производится упорядочивание.
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - отредактированная строка коллекции.
//
Процедура ВосстановитьПорядокСтрокКоллекцииПослеРедактирования(КоллекцияСтрок, ПолеУпорядочивания, ТекущаяСтрока) Экспорт
	
	Если КоллекцияСтрок.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущаяСтрока[ПолеУпорядочивания]) <> Тип("Дата") 
		И Не ЗначениеЗаполнено(ТекущаяСтрока[ПолеУпорядочивания]) Тогда
		Возврат;
	КонецЕсли;
	
	ИндексИсходный = КоллекцияСтрок.Индекс(ТекущаяСтрока);
	ИндексРезультат = ИндексИсходный;
	
	// Выбираем направление, в котором нужно сдвинуть.
	Направление = 0;
	Если ИндексИсходный = 0 Тогда
		// вниз
		Направление = 1;
	КонецЕсли;
	Если ИндексИсходный = КоллекцияСтрок.Количество() - 1 Тогда
		// вверх
		Направление = -1;
	КонецЕсли;
	
	Если Направление = 0 Тогда
		Если КоллекцияСтрок[ИндексИсходный][ПолеУпорядочивания] > КоллекцияСтрок[ИндексРезультат + 1][ПолеУпорядочивания] Тогда
			// вниз
			Направление = 1;
		КонецЕсли;
		Если КоллекцияСтрок[ИндексИсходный][ПолеУпорядочивания] < КоллекцияСтрок[ИндексРезультат - 1][ПолеУпорядочивания] Тогда
			// вверх
			Направление = -1;
		КонецЕсли;
	КонецЕсли;
	
	Если Направление = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Направление = 1 Тогда
		// Сдвигать нужно пока значение в текущей строке больше, чем в следующей.
		Пока ИндексРезультат < КоллекцияСтрок.Количество() - 1 
			И КоллекцияСтрок[ИндексИсходный][ПолеУпорядочивания] > КоллекцияСтрок[ИндексРезультат + 1][ПолеУпорядочивания] Цикл
			ИндексРезультат = ИндексРезультат + 1;
		КонецЦикла;
	Иначе
		// Сдвигать нужно пока значение в текущей строке меньше, чем в предыдущей.
		Пока ИндексРезультат > 0 
			И КоллекцияСтрок[ИндексИсходный][ПолеУпорядочивания] < КоллекцияСтрок[ИндексРезультат - 1][ПолеУпорядочивания] Цикл
			ИндексРезультат = ИндексРезультат - 1;
		КонецЦикла;
	КонецЕсли;
	
	КоллекцияСтрок.Сдвинуть(ИндексИсходный, ИндексРезультат - ИндексИсходный);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти