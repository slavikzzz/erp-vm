////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Принятие к учету ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ИмущественныеНалоги");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

// Добавляет в текст запроса реквизиты шапки документа.
//  Реквизиты добавляются вместо параметра "&ПринятиеКУчетуОС_РеквизитыДокумента".
//
// Параметры:
//  ТекстЗапроса - Строка				 - Исходный текст запроса.
//  ИмяТаблицы	 - Строка, Неопределено	 - Синоним таблицы документа в запросе.
//  	Для получения пустых значений нужно передать "Неопределено".
//
Процедура ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, ИмяТаблицы = Неопределено) Экспорт
	
	ПоляЛокализации = "NULL";
	
	//++ Локализация
	
	ПоляЛокализации =
	"
	|	ДанныеДокумента.ОтражатьВНУ                                       КАК ОтражатьВНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ИНАЧЕ ДанныеДокумента.НалогообложениеНДС
	|	КОНЕЦ                                                             КАК НалогообложениеНДС,
	|
	|	ДанныеДокумента.ВариантРаздельногоУчетаНДС                        КАК ВариантРаздельногоУчетаНДС,
	|	ДанныеДокумента.ГруппаОС                                          КАК ГруппаОС,
	|	ДанныеДокумента.НедвижимоеИмущество                               КАК НедвижимоеИмущество,
	|	ДанныеДокумента.АмортизационнаяГруппа                             КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.ПорядокУчетаНУ                                    КАК ПорядокУчетаНУ,
	|	ДанныеДокумента.СпособПоступления                                 КАК СпособПоступления,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовНУ = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|				И (ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации)
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена))
	|
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|
	|		ИНАЧЕ ДанныеДокумента.СтатьяРасходовНУ 
	|	КОНЕЦ                                                             КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовНУ = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|				И (ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации)
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена))
	|
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходовНУ 
	|	КОНЕЦ                                                             КАК АналитикаРасходовНУ,
	|
	|	ДанныеДокумента.НачислятьАмортизациюНУ                            КАК НачислятьАмортизациюНУ,
	|
	|	ДанныеДокумента.СрокИспользованияНУ                               КАК СрокИспользованияНУ,
	|	ДанныеДокумента.ГрафикАмортизации                                 КАК ГрафикАмортизации,
	|	ДанныеДокумента.ГодоваяНормаАмортизацииБУ                         КАК ГодоваяНормаАмортизацииБУ,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ                          КАК СпециальныйКоэффициентНУ,
	|	ДанныеДокумента.ВключитьАмортизационнуюПремиюВСоставРасходов      КАК ВключитьАмортизационнуюПремиюВСоставРасходов,
	|	ДанныеДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ    КАК ПроцентКапитальныхВложенийВключаемыхВРасходыНУ,
	|	ДанныеДокумента.СтатьяРасходовАмортизационнойПремии               КАК СтатьяРасходовАмортизационнойПремии,
	|	ДанныеДокумента.АналитикаРасходовАмортизационнойПремии            КАК АналитикаРасходовАмортизационнойПремии,
	|	ДанныеДокумента.СтатьяРасходовНалог                               КАК СтатьяРасходовНалог,
	|	ДанныеДокумента.АналитикаРасходовНалог                            КАК АналитикаРасходовНалог,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию               КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов                     КАК ОрганизацияПолучательРасходов,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования           КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.СтатьяДоходов                                     КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                                  КАК АналитикаДоходов";

	ПоляЛокализации = СтрЗаменить(ПоляЛокализации, "ДанныеДокумента", ИмяТаблицы);
	
	//-- Локализация
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПринятиеКУчетуОС_РеквизитыДокумента", ПоляЛокализации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
// 
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект
//  Отказ - Булево - 
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//  ВспомогательныеРеквизиты - Структура -
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты, ВспомогательныеРеквизиты) Экспорт
	
	//++ Локализация
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды Тогда
		ОбработкаПроверкиЗаполненияПринятияКУчетуПредметовАренды(Объект, ВспомогательныеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  ТекстЗаполнения - Строка, Неопределено - Текст, используемый для заполнения документа. 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		
		ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	
	Если НЕ ЗначениеЗаполнено(Объект.ВариантПримененияЦелевогоФинансирования) Тогда
		Объект.ВариантПримененияЦелевогоФинансирования = Перечисления.ВариантыПримененияЦелевогоФинансирования.НеИспользуется;
	КонецЕсли;
	
	ВнеоборотныеАктивыЛокализация.ЗаполнитьСуммыЦелевыхСредств(Объект.ОС, Объект.ЦелевоеФинансирование, Объект.ВариантПримененияЦелевогоФинансирования);
	ВнеоборотныеАктивыЛокализация.ЦелевоеФинансированиеОчиститьСубконто(Объект.ЦелевоеФинансирование);
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
		Объект.Организация,
		Объект.Дата);
	РаздельныйУчетВНАПоНалогообложениюНДС = ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.РаздельныйУчетВНАПоНалогообложениюНДС;
	
	Если НЕ РаздельныйУчетВНАПоНалогообложениюНДС Тогда
		Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
		Объект.НалогообложениеНДС = ВнеоборотныеАктивы.НалогообложениеНДС(Объект.Организация, Объект.Дата);
	КонецЕсли;
	
	//-- Локализация	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	
	Если Не Отказ И ПроведениеДокументов.СвойстваДокумента(Объект).РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗаполнитьРеквизитыОСПриПроведении(Объект, Отказ);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.ПринятиеКУчетуОС2_4 - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// ОС-1 (Акт о приеме-передаче ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС1";
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'ОС-1 (Акт о приеме-передаче ОС)';
										|en = 'FA-1 (Certificate of FA handover)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьРеглУчет";
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	ДанныеДляПечатнойФормы = Неопределено;
	
	//++ Локализация
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Организация,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр(
		"ИспользоватьУчетДрагоценныхМатериалов",
		ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов"));
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивыЛокализация.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	Шапка.Номер КАК НомерДокумента,
	|	Шапка.Дата КАК ДатаДокумента,
	|	Шапка.Дата КАК ДатаВвода,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПередачи,
	|	Шапка.Организация.Префикс КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Сдатчик,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеСдатчика,
	|	Шапка.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	Шапка.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегл,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетСдатчика,
	|	Шапка.Организация КАК Получатель,
	|	Шапка.Местонахождение КАК ПодразделениеПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК Руководитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ГлавныйБухгалтер,
	|	НЕОПРЕДЕЛЕНО КАК СдалМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьСдалМОЛ,
	|	Шапка.МОЛ КАК ПринялМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьПринялМОЛ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО Шапка.Ссылка = ТаблицаДанныхДокументов.Ссылка
	|ГДЕ
	|	Шапка.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОсновныеСредства.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА втОсновныеСредства.ОсновноеСредство.НаименованиеПолное = """"
	|			ТОГДА втОсновныеСредства.ОсновноеСредство
	|		ИНАЧЕ втОсновныеСредства.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаименованиеОС,
	|	втСведенияОбъектовДокументов.ГруппаОС КАК ГруппаОС,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.АмортизационнаяГруппа.Порядок + 1, 0) КАК АмортизационнаяГруппа,
	|	втСведенияОбъектовДокументов.КодПоОКОФ.Код КАК КодОКОФ,
	|	втСведенияОбъектовДокументов.ИнвентарныйНомер КАК НомерИнвентарный,
	|	ОбъектыЭксплуатации.ЗаводскойНомер КАК НомерЗаводской,
	|	НЕОПРЕДЕЛЕНО КАК ДатаНачалаСтроительства,
	|	ОбъектыЭксплуатации.ДатаВыпуска КАК ДатаВыпуска,
	|	ОбъектыЭксплуатации.Изготовитель КАК Изготовитель,
	|	втСведенияОбъектовДокументов.ДатаПринятияКУчету КАК ДатаВводаВЭксплуатацию,
	|	НЕОПРЕДЕЛЕНО КАК ДатаМодернизации,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКапитальногоРемонта,
	|	0 КАК СрокИспользованияФакт,
	|	0 КАК СрокИспользования,
	|	втСведенияОбъектовДокументов.СрокПолезногоИспользования КАК СрокИспользованияРаздел2,
	|	втСведенияОбъектовДокументов.ПервоначальнаяСтоимостьБУ КАК СтоимостьПервоначальная,
	|	0 КАК СтоимостьОстаточная,
	|	втСведенияОбъектовДокументов.СчетУчета КАК СчетУчетаОС,
	|	0 КАК ЦенаПродажи,
	|	втСведенияОбъектовДокументов.НакопленнаяАмортизация КАК АмортизацияСумма,
	|	втСведенияОбъектовДокументов.СпособНачисленияАмортизации КАК АмортизацияСпособ,
	|	втСведенияОбъектовДокументов.КоэффициентАмортизации КАК АмортизацияНорма,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны
	|ИЗ
	|	втОбъектыДокументов КАК втОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО втОсновныеСредства.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО втОсновныеСредства.Ссылка = втСведенияОбъектовДокументов.Ссылка
	|			И втОсновныеСредства.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	втОсновныеСредства.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал КАК ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ДрагоценныеМатериалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4.ОС КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации.ДрагоценныеМатериалы КАК ДрагоценныеМатериалы
	|		ПО ТабличнаяЧасть.ОсновноеСредство = ДрагоценныеМатериалы.Ссылка
	|ГДЕ
	|	&ИспользоватьУчетДрагоценныхМатериалов
	|	И ТабличнаяЧасть.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧасть.Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечатнойФормы = Новый Структура;
	ДанныеДляПечатнойФормы.Вставить("РезультатПоШапке", РезультатыЗапроса[0]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоТабличнойЧасти", РезультатыЗапроса[1]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоДрагоценнымМатериалам", РезультатыЗапроса[2]);
		
	//-- Локализация
	
	Возврат ДанныеДляПечатнойФормы;
	
КонецФункции

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеНачисленияАмортизацииОС(СписокЗапросов);
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеАмортизационнойПремии(СписокЗапросов);
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПринятияКУчету(СписокЗапросов);
	
	#Область ПринятиеКУчетуОС_Дт_СчетУчета__Кт_08_04_2_или_08_03
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоКт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ 
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьРегл - Строки.КорПостояннаяРазница - Строки.КорВременнаяРазница КАК СуммаНУКт,
	|	Строки.КорПостояннаяРазница КАК СуммаПРКт,
	|	Строки.КорВременнаяРазница КАК СуммаВРКт,
	|	""Принятие к учету ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Строки.КорСтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = Строки.КорАналитикаРасходов
	|
	|ГДЕ
	|	Строки.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды))
	|
	|	И (Операция.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|		ИЛИ Операция.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена))
	|
	|	И Строки.РасчетСтоимости
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область НеУчитываемаяСтоимостьНУ_Дт_01_К__Кт_08_04_2_или_08_03
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Не учитываемая часть стоимости предметов аренды в налоговом учете (Дт 01.К:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.НеУчитываемаяСтоимостьНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-Строки.НеУчитываемаяСтоимостьНУ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	Строки.ОсновноеСредство КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Не учитываемая часть стоимости предметов аренды в налоговом учете"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Строки.НеУчитываемаяСтоимостьНУ <> 0
	|	И Строки.КорСтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизационнаяПремия_Дт_КВ__Кт_ПустаяСсылка
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизационная премия (Дт КВ :: Кт <ПустаяСсылка>)
	|//
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Регистратор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	Строки.АмортизационнаяПремия КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Амортизационная премия"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|		ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Строки.АмортизационнаяПремия <> 0";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПринятиеКУчетуОСЦелевыеСредства_Дт_СчетУчета__Кт_08_04_2_или_08_03
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС за счет средств целевого финансирования (Дт СчетУчета:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоКт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ 
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	Строки.СтоимостьЦФ - Строки.КорПостояннаяРазница - Строки.КорВременнаяРазница КАК СуммаНУКт,
	|	Строки.КорПостояннаяРазница КАК СуммаПРКт,
	|	Строки.КорВременнаяРазница КАК СуммаВРКт,
	|
	|	""Принятие к учету ОС за счет средств целевого финансирования"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|		ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|		ПО Строки.КорСтатьяРасходов = СтатьиСтроительства.Ссылка
	|			И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|		ПО ОбъектыСтроительства.Ссылка = Строки.КорАналитикаРасходов
	|
	|ГДЕ
	|	Строки.КорСтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И (Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0
	|		ИЛИ Операция.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное))";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти

	#Область СписаниеПриПринятииКУчетуНУ_Дт_СчетЗатрат__Кт_СчетУчетаОС
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы при принятии к учету(Дт СчетЗатрат:: СчетУчетаОС)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.КорСтатьяРасходов КАК СубконтоДт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|				ТОГДА ОбъектыСтроительства.СпособСтроительства
	|			ИНАЧЕ 
	|				ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|		КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ Строки.СтоимостьНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА Строки.СтоимостьНУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаПРДт,
	|	- Строки.СтоимостьНУ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	- Строки.СтоимостьНУ КАК СуммаВРКт,
	|
	|	""Включение в расходы при принятии к учету"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО Строки.КорСтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.КорАналитикаРасходов
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПринятиеКУчетуАмортизированногоОС_Дт_СчетУчета__Кт_СчетАмортизации
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету самортизированного ОС (Дт СчетУчета:: Кт СчетАмортизации)
	|//
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьБУ КАК Сумма,
	|	Строки.СтоимостьУУ КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Операция.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Местонахождение КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаКт,
	|	Операция.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Местонахождение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Принятие к учету самортизированного ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Операция.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|	ИЛИ Операция.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПринятиеКУчетуПоРезультатамИнвентаризации_Дт_СчетУчета__Кт_СчетДоходов
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету по результатам инвентаризации ОС (Дт СчетУчета:: Кт СчетДоходов)
	|//
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьБУ КАК Сумма,
	|	Строки.СтоимостьУУ КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Операция.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Местонахождение КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Операция.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Операция.Местонахождение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Местонахождение КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Операция.СтатьяДоходов КАК СубконтоКт1,
	|	Операция.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Принятие к учету по результатам инвентаризации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации)
	|	И Операция.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|	И Операция.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПризнаниеДоходовБудущихПериодов_Дт_86_Кт_98
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов будущих периодов (Дт 86:: Кт 98)
	|//
	|ВЫБРАТЬ
	|	
	|	втЦелевоеФинансирование.Ссылка КАК Ссылка,
	|	втЦелевоеФинансирование.Дата КАК Период,
	|	втЦелевоеФинансирование.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втЦелевоеФинансирование.Сумма КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втЦелевоеФинансирование.Подразделение КАК ПодразделениеДт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втЦелевоеФинансирование.Счет КАК СчетДт,
	|	
	|	втЦелевоеФинансирование.Субконто1 КАК СубконтоДт1,
	|	втЦелевоеФинансирование.Субконто2 КАК СубконтоДт2,
	|	втЦелевоеФинансирование.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втЦелевоеФинансирование.Подразделение ПодразделениеКт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетКт,
	|	
	|	втЦелевоеФинансирование.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Признание доходов будущих периодов"" КАК Содержание
	|ИЗ
	|	втЦелевоеФинансированиеИтог КАК втЦелевоеФинансирование";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВТНачислениеАмортизацииОС(СписокЗапросов);
	
	#Область втЦелевоеФинансированиеИтог
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.Счет,
	|	ТабличнаяЧасть.Субконто1,
	|	ТабличнаяЧасть.Субконто2,
	|	ТабличнаяЧасть.Субконто3,
	|	ТабличнаяЧасть.Сумма,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Местонахождение КАК Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Дата,
	|	ТабличнаяЧасть.НомерСтроки
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансирования
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ДокументыКОтражению.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ЦелевоеФинансирование КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = ТаблицаДокумента.Ссылка)
	|ГДЕ
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное), 
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсточникиЦелевогоФинансирования.Ссылка,
	|	СУММА(втИсточникиЦелевогоФинансирования.Сумма) КАК Сумма,
	|	КОЛИЧЕСТВО(втИсточникиЦелевогоФинансирования.Счет) КАК Количество
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансированияСгруппированные
	|ИЗ
	|	втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсточникиЦелевогоФинансирования.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Ссылка,
	|	СтоимостьОС.ОсновноеСредство,
	|	СУММА(СтоимостьОС.СтоимостьЦФ) КАК Сумма
	|ПОМЕСТИТЬ втЦелевоеФинансированиеОС
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4 КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ДокументыКОтражению.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ПО (СтоимостьОС.Регистратор = ДокументыКОтражению.Ссылка)
	|			И (СтоимостьОС.СтоимостьЦФ <> 0)
	|ГДЕ
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное), 
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтражению.Ссылка,
	|	СтоимостьОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансированиеОС.Ссылка КАК Ссылка,
	|	втЦелевоеФинансированиеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	втИсточникиЦелевогоФинансирования.Счет КАК Счет,
	|	втИсточникиЦелевогоФинансирования.Субконто1 КАК Субконто1,
	|	втИсточникиЦелевогоФинансирования.Субконто2 КАК Субконто2,
	|	втИсточникиЦелевогоФинансирования.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА втИсточникиЦелевогоФинансированияСгруппированные.Сумма = 0
	|			ТОГДА ВЫРАЗИТЬ(втЦелевоеФинансированиеОС.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Количество КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(втЦелевоеФинансированиеОС.Сумма * (втИсточникиЦелевогоФинансирования.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Сумма) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	втИсточникиЦелевогоФинансирования.Организация,
	|	втИсточникиЦелевогоФинансирования.Подразделение,
	|	втИсточникиЦелевогоФинансирования.НаправлениеДеятельности,
	|	втИсточникиЦелевогоФинансирования.ВариантПримененияЦелевогоФинансирования,
	|	втИсточникиЦелевогоФинансирования.Ссылка КАК Ссылка1,
	|	втИсточникиЦелевогоФинансирования.Дата,
	|	втИсточникиЦелевогоФинансирования.НомерСтроки,
	|	втЦелевоеФинансированиеОС.Сумма КАК ПолнаяСуммаПоОС
	|ПОМЕСТИТЬ втЦелевоеФинансирование
	|ИЗ
	|	втЦелевоеФинансированиеОС КАК втЦелевоеФинансированиеОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|		ПО втЦелевоеФинансированиеОС.Ссылка = втИсточникиЦелевогоФинансирования.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансированияСгруппированные КАК втИсточникиЦелевогоФинансированияСгруппированные
	|		ПО втЦелевоеФинансированиеОС.Ссылка = втИсточникиЦелевогоФинансированияСгруппированные.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.Ссылка КАК Ссылка,
	|	втЦелевоеФинансирование.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(втЦелевоеФинансирование.Сумма) КАК Сумма,
	|	МАКСИМУМ(втЦелевоеФинансирование.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ втЦелевоеФинансированиеСгруппированное
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|
	|СГРУППИРОВАТЬ ПО
	|	втЦелевоеФинансирование.Ссылка,
	|	втЦелевоеФинансирование.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.ОсновноеСредство,
	|	втЦелевоеФинансирование.Счет,
	|	втЦелевоеФинансирование.Субконто1,
	|	втЦелевоеФинансирование.Субконто2,
	|	втЦелевоеФинансирование.Субконто3,
	|	ВЫБОР
	|		КОГДА НЕ втЦелевоеФинансированиеСгруппированное.ОсновноеСредство ЕСТЬ NULL
	|				И втЦелевоеФинансированиеСгруппированное.Сумма <> втЦелевоеФинансирование.ПолнаяСуммаПоОС
	|			ТОГДА втЦелевоеФинансирование.Сумма + втЦелевоеФинансирование.ПолнаяСуммаПоОС - втЦелевоеФинансированиеСгруппированное.Сумма
	|		ИНАЧЕ втЦелевоеФинансирование.Сумма
	|	КОНЕЦ КАК Сумма,
	|	втЦелевоеФинансирование.Организация,
	|	втЦелевоеФинансирование.Подразделение,
	|	втЦелевоеФинансирование.НаправлениеДеятельности,
	|	втЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования,
	|	втЦелевоеФинансирование.Ссылка,
	|	втЦелевоеФинансирование.Дата
	|ПОМЕСТИТЬ втЦелевоеФинансированиеИтог
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦелевоеФинансированиеСгруппированное КАК втЦелевоеФинансированиеСгруппированное
	|		ПО втЦелевоеФинансирование.Ссылка = втЦелевоеФинансированиеСгруппированное.Ссылка
	|			И втЦелевоеФинансирование.ОсновноеСредство = втЦелевоеФинансированиеСгруппированное.ОсновноеСредство
	|			И втЦелевоеФинансирование.НомерСтроки = втЦелевоеФинансированиеСгруппированное.НомерСтроки";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	СписокЗапросов.Добавить(Символы.ПС);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());

	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#Область Проведение

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра, ПереопределениеРасчетаПараметров, СинонимТаблицыДокумента) Экспорт

	ТекстЗапроса = Неопределено;
	ТекстыЗапроса = Новый СписокЗначений;
	
	//++ Локализация
	
	Если ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Неопределено);
		СинонимТаблицыДокумента = "ТаблицаОС";
		
	КонецЕсли;
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ЗначенияПараметровПроведения(ЗначенияПараметровПроведения) Экспорт

	//++ Локализация
	
	ЗначенияПараметровПроведения.Вставить("ПредельнаяСтоимостьСписанияНаРасходы", ВнеоборотныеАктивыЛокализация.ПредельнаяСтоимостьСписанияНаРасходы());

	//-- Локализация
	
КонецПроцедуры
 
// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ТаблицыДвиженийРасчетаСтоимости(Запрос, ТекстыЗапроса) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Неопределено);
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(СписокЗапросовОбъединение) Экспорт

	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяДоходов                КАК Статья,
	|	ДанныеДокумента.АналитикаДоходов             КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                 КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Местонахождение              КАК КорПодразделение,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	&СтатьяАП                                    КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство                   КАК КорАналитикаАктивовПассивов,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация                  КАК КорОрганизация,
	|
	|	ТаблицаОС.СтоимостьУУ                        КАК Сумма,
	|	ТаблицаОС.СтоимостьБУ                        КАК СуммаРегл,
	|	ТаблицаОС.СтоимостьУУ                        КАК СуммаУпр
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|";
	
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПроверитьРеквизитыШапки(Объект, Отказ) Экспорт

	//++ Локализация
	
	Если Объект.ПорядокУчетаУУ = Перечисления.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислитьАмортизациюПриПринятииКУчету 
		И Объект.ГруппаОС <> Перечисления.ГруппыОС.ПроизводственныйИХозяйственныйИнвентарь
		И ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
		
		ТекстСообщения = НСтр("ru = 'Единовременно начислять амортизацию при принятии к управленческому учету можно только по производственному и хозяйственному инвентарю.';
								|en = 'You can accrue depreciation once recognition for management accounting only by production and maintenance tools.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "ПорядокУчетаУУ", "", Отказ);
	КонецЕсли; 
	
	Если Объект.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету
		И Объект.ГруппаОС <> Перечисления.ГруппыОС.ПроизводственныйИХозяйственныйИнвентарь 
		И ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
		
		ТекстСообщения = НСтр("ru = 'Единовременно начислять амортизацию при принятии к бухгалтерскому учету можно только по производственному и хозяйственному инвентарю.';
								|en = 'You can accrue depreciation once recognition only by production and maintenance tools.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "ПорядокУчетаБУ", "", Отказ);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Функция ПроверитьПорядокНеПриниматьКУчетуВключитьВРасходы(Объект, Отказ) Экспорт

	ПроверкаВыполнена = Ложь;
	
	//++ Локализация
	
	ПроверкаВыполнена = Истина;
	
	Если (Объект.ПорядокУчетаУУ = Перечисления.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы
			ИЛИ НЕ Объект.ОтражатьВУпрУчете)
		И (Объект.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы
			ИЛИ НЕ Объект.ОтражатьВБУ) 
		И (Объект.ПорядокУчетаНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы
			ИЛИ НЕ Объект.ОтражатьВНУ)
		И (Объект.ОтражатьВУпрУчете
			ИЛИ Объект.ОтражатьВБУ
			ИЛИ Объект.ОтражатьВНУ) Тогда
		
		Если Объект.ОтражатьВБУ Тогда
			Поле = "ПорядокУчетаБУ";
		ИначеЕсли Объект.ОтражатьВНУ Тогда
			Поле = "ПорядокУчетаНУ";
		Иначе
			Поле = "ПорядокУчетаУУ";
		КонецЕсли;
		
		ТекстСообщения = НСтр("ru = 'Не поддерживается выбор порядка учета ""Не принимать к учету, включить в расходы"" сразу во всех учетах. В одном из учетов должен быть другой порядок учета.';
								|en = 'Cannot select the ""Do not recognize, add to expenses"" accounting procedure in all accountings at the same time. One of the accountings must have a different accounting procedure.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЕсли;
	
	//-- Локализация
	
	Возврат ПроверкаВыполнена;
	
КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса);
	
	ВнеоборотныеАктивы.ТекстЗапросаСрезПоследнихРегистра(
		"НастройкиУчетаНалогаНаПрибыль", "ВтНастройкиУчетаНалогаНаПрибыль", "ДанныеДокументаРеквизиты", ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДАТАВРЕМЯ(1,1,1)                  КАК ДатаИсправления,
	|	ТаблицаОС.ОсновноеСредство        КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.СпособПоступления КАК СпособПоступления,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ 
	|			ТОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ) <= &ПредельнаяСтоимостьСписанияНаРасходы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету) 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ) > &ПредельнаяСтоимостьСписанияНаРасходы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации) 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаНУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаНУ
	|	КОНЕЦ КАК ПорядокУчетаНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ
	|		КОГДА ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды) 
	|					И ЕСТЬNULL(ВтНастройкиУчетаНалогаНаПрибыль.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи, ЛОЖЬ)
	|				ИЛИ ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьНУ)
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл + РасчетСтоимости.СтоимостьЦФ + РасчетСтоимости.НДСРегл, ТаблицаОС.СтоимостьБУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.Стоимость + РасчетСтоимости.НДС, ТаблицаОС.СтоимостьУУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНастройкиУчетаНалогаНаПрибыль КАК ВтНастройкиУчетаНалогаНаПрибыль
	|		ПО ВтНастройкиУчетаНалогаНаПрибыль.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|			И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.ДатаПринятияКУчету КАК Период,
	|	ДанныеДокумента.Период            КАК ДатаИсправления,
	|	ТаблицаОС.ОсновноеСредство        КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.СпособПоступления КАК СпособПоступления,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|				И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|				И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ 
	|			ТОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ) <= &ПредельнаяСтоимостьСписанияНаРасходы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету) 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|				И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ) > &ПредельнаяСтоимостьСписанияНаРасходы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации) 
	|		КОГДА ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаНУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаНУ
	|	КОНЕЦ КАК ПорядокУчетаНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ 
	|			ТОГДА ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ
	|		КОГДА ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды) 
	|					И ЕСТЬNULL(ВтНастройкиУчетаНалогаНаПрибыль.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи, ЛОЖЬ)
   	|				ИЛИ ДанныеДокумента.ПринятиеКУчетуПрошлойДатой 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьНУ)
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл + РасчетСтоимости.СтоимостьЦФ + РасчетСтоимости.НДСРегл, ТаблицаОС.СтоимостьБУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.Стоимость + РасчетСтоимости.НДС, ТаблицаОС.СтоимостьУУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНастройкиУчетаНалогаНаПрибыль КАК ВтНастройкиУчетаНалогаНаПрибыль
	|		ПО ВтНастройкиУчетаНалогаНаПрибыль.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|	И (ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|			И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы))
	|";

	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса);
	
	СписокЗапросов = Новый Массив;
	
	#Область ПринятиеКУчету // Дт 01 - Кт 08
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуСамортизированногоОС)
	|		ИНАЧЕ ДанныеДокумента.ХозяйственнаяОперация 
	|	КОНЕЦ                                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования     КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ВидАналитикиКапитализацииРасходов           КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	ДанныеДокумента.НалогообложениеНДС                          КАК НалогообложениеНДС,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВидАналитикиКапитализацииРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОбъектСтроительства) 
	|			ТОГДА ДанныеДокумента.ОбъектСтроительства 
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство 
	|	КОНЕЦ                                        КАК АналитикаКапитализацииРасходов,
	|
	|	ЕСТЬNULL(РасчетСтоимости.КорПодразделение, НЕОПРЕДЕЛЕНО)                КАК КорПодразделение,
	|	ЕСТЬNULL(РасчетСтоимости.КорНаправлениеДеятельности, НЕОПРЕДЕЛЕНО)      КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(РасчетСтоимости.КорСтатьяРасходов, НЕОПРЕДЕЛЕНО)               КАК КорСтатьяРасходов,
	|	ЕСТЬNULL(РасчетСтоимости.КорАналитикаРасходов, НЕОПРЕДЕЛЕНО)            КАК КорАналитикаРасходов,
	|	НЕ РасчетСтоимости.ОбъектУчета ЕСТЬ NULL                                КАК РасчетСтоимости,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВУпрУчете
	|				ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.Стоимость, ТаблицаОС.СтоимостьУУ)
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды) 
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьНУ)
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьПР, ТаблицаОС.СтоимостьПР)
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств)
	|	КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|					ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды) 
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьНУ)
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьНУ, ТаблицаОС.СтоимостьБУ)
	|		КОНЕЦ
	|		- ВЫБОР
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьПР, ТаблицаОС.СтоимостьПР)
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА НЕ РасчетСтоимости.ОбъектУчета ЕСТЬ NULL
	|			ТОГДА РасчетСтоимости.СтоимостьЦФ 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СуммаЦелевыхСредств 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	ЕСТЬNULL(РасчетСтоимости.СтоимостьНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ЕСТЬNULL(РасчетСтоимости.СтоимостьПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ЕСТЬNULL(РасчетСтоимости.СтоимостьВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|					И ЕСТЬNULL(ВтРасчетСтоимостиСгруппированная.СтоимостьНУ, 0) <= &ПредельнаяСтоимостьСписанияНаРасходы
	|			ТОГДА 0
	|		КОГДА НЕ РасчетСтоимости.ОбъектУчета ЕСТЬ NULL
	|			ТОГДА РасчетСтоимости.СтоимостьНУ
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ * ДанныеДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ / 100 КАК АмортизационнаяПремия,
	|
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.КорПостояннаяРазница, 0)
	|	КОНЕЦ КАК КорПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл + РасчетСтоимости.СтоимостьЦФ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ 
	|		- ВЫБОР 
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|					ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.КорСуммаНУ, ТаблицаОС.СтоимостьНУ)
	|		КОНЕЦ
	|		- ВЫБОР
	|			КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.КорПостояннаяРазница, 0)
	|		КОНЕЦ КАК КорВременнаяРазница,
	|
	|	ЕСТЬNULL(РасчетСтоимости.НеУчитываемаяСтоимостьНУ, 0) КАК НеУчитываемаяСтоимостьНУ,
	|	ЕСТЬNULL(РасчетСтоимости.ИдентификаторСтроки, &ИдентификаторНеиспользуемойФинЗаписи) КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОбъектУчета
	|			И РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК ВтРасчетСтоимостиСгруппированная
	|		ПО ВтРасчетСтоимостиСгруппированная.ОбъектУчета = ТаблицаОС.ОбъектУчета
	|			И ВтРасчетСтоимостиСгруппированная.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|
	|	И (ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|			И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы))
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПринятиеКУчетуИнвентаризация // Дт 01 - Кт 91
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуСамортизированногоОС)
	|		ИНАЧЕ ДанныеДокумента.ХозяйственнаяОперация 
	|	КОНЕЦ                                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования     КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ВидАналитикиКапитализацииРасходов           КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	ДанныеДокумента.НалогообложениеНДС                          КАК НалогообложениеНДС,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВидАналитикиКапитализацииРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОбъектСтроительства) 
	|			ТОГДА ДанныеДокумента.ОбъектСтроительства 
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство 
	|	КОНЕЦ                                        КАК АналитикаКапитализацииРасходов,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаРасходов,
	|	ЛОЖЬ         КАК РасчетСтоимости,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВУпрУчете
	|				ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьУУ
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьНУ
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьПР
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|				ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|					ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьНУ
	|		КОНЕЦ
	|		- ВЫБОР
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|					ИЛИ ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьПР
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СуммаЦелевыхСредств 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ * ДанныеДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ / 100 КАК АмортизационнаяПремия,
	|
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|
	|	0 КАК КорПостояннаяРазница,
	|	0 КАК КорВременнаяРазница,
	|
	|	0 КАК НеУчитываемаяСтоимостьНУ,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|
	|	И (ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|			И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы))
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПредварительнаяСтоимость
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуСамортизированногоОС)
	|		ИНАЧЕ ДанныеДокумента.ХозяйственнаяОперация 
	|	КОНЕЦ                                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ВидАналитикиКапитализацииРасходов КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	ДанныеДокумента.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВидАналитикиКапитализацииРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОбъектСтроительства) 
	|			ТОГДА ДанныеДокумента.ОбъектСтроительства 
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство 
	|	КОНЕЦ                                        КАК АналитикаКапитализацииРасходов,
	|
	|	НЕОПРЕДЕЛЕНО                                 КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ЛОЖЬ                                         КАК РасчетСтоимости,
	|
	|	0                                            КАК Стоимость,
	|	0                                            КАК СтоимостьРегл,
	|	0                                            КАК СтоимостьНУ,
	|	0                                            КАК СтоимостьПР,
	|	0                                            КАК СтоимостьВР,
	|	0                                            КАК СтоимостьЦФ,
	|	0                                            КАК СтоимостьНУЦФ,
	|	0                                            КАК СтоимостьПРЦФ,
	|	0                                            КАК СтоимостьВРЦФ,
	|	0                                            КАК АмортизационнаяПремия,
	|
	|	ТаблицаОС.СтоимостьУУ                        КАК ПредварительнаяСтоимость,
	|	ТаблицаОС.СтоимостьБУ                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                            КАК КорПостояннаяРазница,
	|	0                                            КАК КорВременнаяРазница,
	|	0                                            КАК НеУчитываемаяСтоимостьНУ,
	|	&ИдентификаторНеиспользуемойФинЗаписи        КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                 КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СписаниеНаРасходы // Дт 91 - Кт 01
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ВидАналитикиКапитализацииРасходов КАК ВидАналитикиКапитализацииРасходов,
	|	РасчетСтоимости.ОбъектУчета                  КАК ОсновноеСредство,
	|	ДанныеДокумента.НалогообложениеНДС           КАК НалогообложениеНДС,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВидАналитикиКапитализацииРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОбъектСтроительства) 
	|			ТОГДА ДанныеДокумента.ОбъектСтроительства 
	|		ИНАЧЕ РасчетСтоимости.ОбъектУчета 
	|	КОНЕЦ                                        КАК АналитикаКапитализацииРасходов,
	|
	|	ДанныеДокумента.Местонахождение              КАК КорПодразделение,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходовНУ             КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНУ          КАК КорАналитикаРасходов,
	|	ИСТИНА                          КАК РасчетСтоимости,
	|
	|	0                               КАК Стоимость,
	|	0                               КАК СтоимостьРегл,
	|	РасчетСтоимости.СтоимостьНУ     КАК СтоимостьНУ,
	|	0                               КАК СтоимостьПР,
	|	-РасчетСтоимости.СтоимостьНУ    КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	0 КАК АмортизационнаяПремия,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|	0 КАК КорПостояннаяРазница,
	|	0 КАК КорВременнаяРазница,
	|	0 КАК НеУчитываемаяСтоимостьНУ,
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОбъектУчета
	|			И РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК ВтРасчетСтоимостиСгруппированная
	|		ПО ВтРасчетСтоимостиСгруппированная.ОбъектУчета = РасчетСтоимости.ОбъектУчета
	|			И ВтРасчетСтоимостиСгруппированная.Ссылка = РасчетСтоимости.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|	И (ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ВтРасчетСтоимостиСгруппированная.СтоимостьНУ <= &ПредельнаяСтоимостьСписанияНаРасходы)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СписаниеНаРасходыИнвентаризация // Дт 91 - Кт 91
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ВидАналитикиКапитализацииРасходов КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	ДанныеДокумента.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	ТаблицаОС.ОсновноеСредство                   КАК АналитикаКапитализацииРасходов,
	|
	|	ДанныеДокумента.Местонахождение              КАК КорПодразделение,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходовНУ             КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНУ          КАК КорАналитикаРасходов,
	|	ЛОЖЬ                            КАК РасчетСтоимости,
	|
	|	0                               КАК Стоимость,
	|	0                               КАК СтоимостьРегл,
	|	ТаблицаОС.СтоимостьНУ           КАК СтоимостьНУ,
	|	0                               КАК СтоимостьПР,
	|	-ТаблицаОС.СтоимостьНУ          КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	0 КАК АмортизационнаяПремия,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|	0 КАК КорПостояннаяРазница,
	|	0 КАК КорВременнаяРазница,
	|	0 КАК НеУчитываемаяСтоимостьНУ,
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ТаблицаОС.СтоимостьНУ <> 0
	|	И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапросаТаблицаВтНачисленнаяАмортизация(Запрос, ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	НачисленнаяАмортизация.Ссылка                   КАК Регистратор,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ                                           КАК ХозяйственнаяОперация,
	|
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОС)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                          КАК Регистратор,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Период                          КАК Период,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Местонахождение                 КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|			ТОГДА ТаблицаОС.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК АмортизацияПР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|			ТОГДА ТаблицаОС.СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК АмортизацияВР,
	|
	|	0                                 КАК АмортизацияЦФ,
	|	0                                 КАК АмортизацияНУЦФ,
	|	0                                 КАК АмортизацияПРЦФ,
	|	0                                 КАК АмортизацияВРЦФ,
	|
	|	НЕОПРЕДЕЛЕНО                      КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                      КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                      КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ЛОЖЬ                              КАК НачислятьИзнос,
	|
	|	ТаблицаОС.ИдентификаторСтроки     КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена))
	|";
	
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса, ДопПоля) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ЗапросыРегистра = Новый Массив;
	
	ЗапросыРегистра.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДопПоля));
	ЗапросыРегистра.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах(ДопПоля));
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПрочиеРасходыПринятияКУчету(ЗапросыРегистра);
	
	#Область ВключениеВРасходыПриПринятииКУчету // Дт 91 - Кт 01
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|   ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходовНУ             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНУ          КАК АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	0                                            КАК СуммаСНДС,
	|	0                                            КАК СуммаБезНДС,
	|	0                                            КАК СуммаБезНДСУпр,
	|
	|	0                                            КАК СуммаСНДСРегл,
	|	0                                            КАК СуммаБезНДСРегл,
	|	0                                            КАК ПостояннаяРазница,
	|	-РасчетСтоимости.СтоимостьНУ                 КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаУчетаНоменклатуры,
	|	                                                                             
	|	&ИдентификаторНеиспользуемойФинЗаписи        КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                 КАК НастройкаХозяйственнойОперации,
	|
	|	ДанныеДокумента.Ссылка                       КАК Регистратор
	|
	//++ НЕ УТКА
	|	,0 КАК СуммаНДД
	//-- НЕ УТКА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК ВтРасчетСтоимостиСгруппированная
	|		ПО ВтРасчетСтоимостиСгруппированная.ОбъектУчета = РасчетСтоимости.ОбъектУчета
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|	И РасчетСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|	И (ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(ВтРасчетСтоимостиСгруппированная.СтоимостьНУ, 0) <= &ПредельнаяСтоимостьСписанияНаРасходы)
	|";
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	#КонецОбласти
		
	#Область ПринятиеКУчетуОСпоИнвентаризации // Дт 91 - Кт 01
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|   ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходовНУ             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНУ          КАК АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	0                                            КАК СуммаСНДС,
	|	0                                            КАК СуммаБезНДС,
	|	0                                            КАК СуммаБезНДСУпр,
	|
	|	0                                            КАК СуммаСНДСРегл,
	|	0                                            КАК СуммаБезНДСРегл,
	|	0                                            КАК ПостояннаяРазница,
	|	-ТаблицаОС.СтоимостьНУ                       КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаУчетаНоменклатуры,
	|	&ИдентификаторНеиспользуемойФинЗаписи        КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                 КАК НастройкаХозяйственнойОперации,
	|
	|	ДанныеДокумента.Ссылка                       КАК Регистратор
	|
	//++ НЕ УТКА
	|	,0                                           КАК СуммаНДД
	//-- НЕ УТКА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|	И ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|";
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	//++ НЕ УТКА
	#Область РеклассификацияСуммыНДД
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|   ВтРасчетСтоимости.КорПодразделение           КАК Подразделение,
	|	ВтРасчетСтоимости.КорСтатьяРасходов          КАК СтатьяРасходов,
	|	ВтРасчетСтоимости.КорАналитикаРасходов       КАК АналитикаРасходов,
	|	ВтРасчетСтоимости.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	                                                                             
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
	|
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|	,ВтРасчетСтоимости.СуммаНДД КАК СуммаНДД
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК ВтРасчетСтоимости
	|		ПО ВтРасчетСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК ВтРасчетСтоимостиСгруппированная
	|		ПО ВтРасчетСтоимостиСгруппированная.Ссылка = ВтРасчетСтоимости.Ссылка
	|			И ВтРасчетСтоимостиСгруппированная.ОбъектУчета = ВтРасчетСтоимости.ОбъектУчета
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|
	|	И (ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(ВтРасчетСтоимостиСгруппированная.СтоимостьНУ, 0) <= &ПредельнаяСтоимостьСписанияНаРасходы)
	|
	|	И ВтРасчетСтоимости.СуммаНДД <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|   ДанныеДокумента.Местонахождение              КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходовНУ             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНУ          КАК АналитикаРасходов,
	|	ДанныеДокумента.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	                                                                             
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
	|
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|	,ВтРасчетСтоимости.СуммаНДД КАК СуммаНДД
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК ВтРасчетСтоимости
	|		ПО ВтРасчетСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК ВтРасчетСтоимостиСгруппированная
	|		ПО ВтРасчетСтоимостиСгруппированная.ОбъектУчета = ВтРасчетСтоимости.ОбъектУчета
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|
	|	И (ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизацииИлиСписаниеНаРасходы)
	|				И ЕСТЬNULL(ВтРасчетСтоимостиСгруппированная.СтоимостьНУ, 0) <= &ПредельнаяСтоимостьСписанияНаРасходы)
	|
	|	И ВтРасчетСтоимости.СуммаНДД <> 0
	|";
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	#КонецОбласти
	//-- НЕ УТКА

	ТекстЗапроса = СтрСоединить(ЗапросыРегистра, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ИмяТаблицы = "ВтРасчетСтоимостиСгруппированная";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	РасчетСтоимостиВНА.ТекстЗапросаПустаяВтРасчетСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетСтоимости.Ссылка                    КАК Ссылка,
	|	РасчетСтоимости.ОбъектУчета               КАК ОбъектУчета,
	|
	|	СУММА(РасчетСтоимости.Стоимость)          КАК Стоимость,
	|	СУММА(РасчетСтоимости.СтоимостьРегл)      КАК СтоимостьРегл,
	|	СУММА(РасчетСтоимости.СтоимостьНУ)        КАК СтоимостьНУ,
	|	СУММА(РасчетСтоимости.СтоимостьПР)        КАК СтоимостьПР,
	|	СУММА(РасчетСтоимости.СтоимостьВР)        КАК СтоимостьВР,
	|	СУММА(РасчетСтоимости.СтоимостьЦФ)        КАК СтоимостьЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьНУЦФ)      КАК СтоимостьНУЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьПРЦФ)      КАК СтоимостьПРЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьВРЦФ)      КАК СтоимостьВРЦФ,
	|
	|	0 КАК НДС,
	|	0 КАК НДСРегл
	|
	|ПОМЕСТИТЬ ВтРасчетСтоимостиСгруппированная
	| 
	|ИЗ
	|	ВтРасчетСтоимости КАК РасчетСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетСтоимости.Ссылка,
	|	РасчетСтоимости.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОбъектУчета
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокУчетаОСБУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                                 КАК Период,
	|	ДАТАВРЕМЯ(1,1,1)                        КАК ДатаИсправления,
	|	ДанныеДокумента.Организация                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА втПорядокУчетаОСБУ.СостояниеБУ
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	КОНЕЦ КАК СостояниеБУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА втПорядокУчетаОСБУ.СостояниеНУ
	|		КОГДА ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	КОНЕЦ КАК СостояниеНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовБУ
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовБУ
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовНУ
	|	КОНЕЦ КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовНУ
	|	КОНЕЦ КАК АналитикаРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовАмортизационнойПремии
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СтатьяРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовАмортизационнойПремии
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии
	|	КОНЕЦ КАК АналитикаРасходовАмортизационнойПремии,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаОСБУ.СпособОтраженияРасходовБУ
	|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаОСБУ.СпособОтраженияРасходовНУ
	|	КОНЕЦ КАК СпособОтраженияРасходовНУ,
	|
	|	ДанныеДокумента.АмортизационнаяГруппа  КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.НедвижимоеИмущество    КАК НедвижимоеИмущество,
	|	ДанныеДокумента.НалогообложениеНДС     КАК НалогообложениеНДС,
	|	ДанныеДокумента.ГрафикАмортизации      КАК ГрафикАмортизации
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
	|		ПО втПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|			И ДанныеДокумента.ПорядокУчетаНУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ДатаПринятияКУчету       КАК Период,
	|	ДанныеДокумента.Период                   КАК ДатаИсправления,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ТаблицаОС.ОсновноеСредство               КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА втПорядокУчетаОСБУ.СостояниеБУ
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	КОНЕЦ КАК СостояниеБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|		ИНАЧЕ втПорядокУчетаОСБУ.СостояниеНУ
	|	КОНЕЦ КАК СостояниеНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.НачислятьАмортизациюНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовБУ
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовБУ
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовНУ
	|	КОНЕЦ КАК СтатьяРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовНУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовНУ
	|	КОНЕЦ КАК АналитикаРасходовНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СтатьяРасходовАмортизационнойПремии
	|		ИНАЧЕ втПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии
	|	КОНЕЦ КАК СтатьяРасходовАмортизационнойПремии,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.АналитикаРасходовАмортизационнойПремии
	|		ИНАЧЕ втПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии
	|	КОНЕЦ КАК АналитикаРасходовАмортизационнойПремии,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаОСБУ.СпособОтраженияРасходовБУ
	|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаОСБУ.СпособОтраженияРасходовНУ
	|	КОНЕЦ КАК СпособОтраженияРасходовНУ,
	|
	|	ДанныеДокумента.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	ДанныеДокумента.НалогообложениеНДС                     КАК НалогообложениеНДС,
	|	ДанныеДокумента.ГрафикАмортизации                      КАК ГрафикАмортизации
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
	|		ПО втПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|	И (ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ)
	|";
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокУчетаОСУУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                                 КАК Период,
	|	ДАТАВРЕМЯ(1,1,1)                                       КАК ДатаИсправления,
	|	ДанныеДокумента.Организация                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)       КАК Состояние,
	|	ДанныеДокумента.НачислятьАмортизациюУУ                 КАК НачислятьАмортизациюУУ,
	|	ДанныеДокумента.СтатьяРасходовУУ                       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовУУ                    КАК АналитикаРасходов,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ДатаПринятияКУчету                     КАК Период,
	|	ДанныеДокумента.Период                                 КАК ДатаИсправления,
	|	ДанныеДокумента.Организация                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)       КАК Состояние,
	|	ДанныеДокумента.НачислятьАмортизациюУУ                 КАК НачислятьАмортизациюУУ,
	|	ДанныеДокумента.СтатьяРасходовУУ                       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовУУ                    КАК АналитикаРасходов,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|	И ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|";
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПорядокУчетаОС() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДАТАВРЕМЯ(1,1,1)                        КАК ДатаИсправления,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.ПоказательНаработки           КАК ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки                КАК ОбъемНаработки,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|		ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ДатаПринятияКУчету      КАК Период,
	|	ДанныеДокумента.Период                  КАК ДатаИсправления,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.ПоказательНаработки           КАК ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки                КАК ОбъемНаработки,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|	И (ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ
	|		ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы))
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка       КАК Регистратор,
	|	ДанныеДокумента.Период       КАК Период,
	|	ДАТАВРЕМЯ(1,1,1)             КАК ДатаИсправления,
	|	ДанныеДокумента.Организация  КАК Организация,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения
	|	КОНЕЦ КАК ДатаПоследнегоИзменения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияНУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ГодоваяНормаАмортизацииБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ
	|	КОНЕЦ КАК КоэффициентАмортизацииБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.КоэффициентУскоренияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентУскорения
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ГрафикАмортизации
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ГрафикАмортизации
	|	КОНЕЦ КАК ГрафикАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СпециальныйКоэффициентНУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СпециальныйКоэффициент
	|	КОНЕЦ КАК СпециальныйКоэффициент,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				ИЛИ ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|			ТОГДА ТаблицаОС.ОбъемНаработки
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьПР + РасчетСтоимости.СтоимостьПРЦФ, ТаблицаОС.СтоимостьПР)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации
	|	КОНЕЦ КАК ПРДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл 
	|				+ РасчетСтоимости.СтоимостьЦФ 
	|				+ РасчетСтоимости.НДСРегл, ТаблицаОС.СтоимостьБУ)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьЦФ, 0)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ТаблицаОС.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК втПараметрыАмортизацииОСБУ
	|		ПО втПараметрыАмортизацииОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка       КАК Регистратор,
	|	ДанныеДокумента.ДатаПринятияКУчету КАК Период,
	|	ДанныеДокумента.Период       КАК ДатаИсправления,
	|	ДанныеДокумента.Организация  КАК Организация,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ДатаПринятияКУчету
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения
	|	КОНЕЦ КАК ДатаПоследнегоИзменения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияНУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ГодоваяНормаАмортизацииБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ
	|	КОНЕЦ КАК КоэффициентАмортизацииБУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.КоэффициентУскоренияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентУскорения
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.ГрафикАмортизации
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ГрафикАмортизации
	|	КОНЕЦ КАК ГрафикАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВНУ
	|			ТОГДА ДанныеДокумента.СпециальныйКоэффициентНУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СпециальныйКоэффициент
	|	КОНЕЦ КАК СпециальныйКоэффициент,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				ИЛИ ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|			ТОГДА ТаблицаОС.ОбъемНаработки
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьПР + РасчетСтоимости.СтоимостьПРЦФ, ТаблицаОС.СтоимостьПР)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации
	|	КОНЕЦ КАК ПРДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл 
	|				+ РасчетСтоимости.СтоимостьЦФ 
	|				+ РасчетСтоимости.НДСРегл, ТаблицаОС.СтоимостьБУ)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьЦФ, 0)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВБУ
	|			ТОГДА ТаблицаОС.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК втПараметрыАмортизацииОСБУ
	|		ПО втПараметрыАмортизацииОСБУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ПринятиеКУчетуПрошлойДатой
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Параметры:
// 	Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - 
// 	Отказ - Булево
Процедура ПроверитьПринятиеКУчетуПоДоговоруАрендыПередЗаписью(Объект, Отказ) Экспорт
	
	//++ Локализация
	
	Если Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды Тогда
		Возврат;
	КонецЕсли;
	
	СписокОС = Новый Массив;
	Для Каждого ДанныеСтроки Из Объект.ОС Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокОС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.ОсновноеСредство.Представление КАК ОсновноеСредствоПредставление,
	|	АрендованныеОС.Договор.Представление КАК ДоговорПредставление,
	|	АрендованныеОС.Договор.Балансодержатель КАК Балансодержатель,
	|	ЕСТЬNULL(АрендованныеОС.Договор.УчитыватьПредметыАрендыВРеглУчете, ЛОЖЬ) КАК УчитыватьПредметыАрендыВРеглУчете,
	|	ЕСТЬNULL(АрендованныеОС.Договор.УчитыватьПредметыАрендыВУпрУчете, ЛОЖЬ) КАК УчитыватьПредметыАрендыВУпрУчете
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(
	|		&Дата, 
	|		ОсновноеСредство В (&СписокОС)) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Состояние В (
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды),
	|			ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("СписокОС", СписокОС);
	
	Результат = Запрос.Выполнить();

	ШаблонСообщенияУпр = НСтр("ru = 'Основное средство ""%1"" необходимо принять к учету только в управленческом учете в соответствии с порядком учета, указанным в договоре аренды ""%2"".';
								|en = 'The fixed asset ""%1"" must be recognized in management accounting only in accordance with the accounting procedure specified in the lease agreement ""%2"".'");
	ШаблонСообщенияРегл = НСтр("ru = 'Основное средство ""%1"" необходимо принять к учету только в регламентированном учете в соответствии с порядком учета, указанным в договоре аренды ""%2"".';
								|en = 'The fixed asset ""%1"" must be recognized in local accounting only in accordance with the accounting procedure specified in the lease agreement ""%2"".'");
	ШаблонСообщенияНУ = НСтр("ru = 'Основное средство ""%1"" учитывается на балансе арендодателя, в налоговом учете допускается только порядок учета ""Стоимость не включается в расходы"".';
							|en = 'Fixed asset ""%1"" is recognized on the lessor''s balance sheet, only the ""Cost is not included in expenses"" accounting procedure is allowed in tax accounting.'");
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Объект.ОС.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
		
		Если Выборка.Балансодержатель = Перечисления.БалансодержательПредметовАренды.Арендодатель
			И Объект.ОтражатьВРеглУчете
			И Объект.ПорядокУчетаНУ <> Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.СтоимостьНеВключаетсяВРасходы
			И ЗначениеЗаполнено(Объект.ПорядокУчетаНУ) Тогда
				
			ТекстСообщения = СтрШаблон(ШаблонСообщенияНУ, Выборка.ОсновноеСредствоПредставление);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", ДанныеСтроки.НомерСтроки, "ОсновноеСредство");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		КонецЕсли;
		
		Если НЕ Выборка.УчитыватьПредметыАрендыВРеглУчете
			И Объект.ОтражатьВРеглУчете Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщенияУпр, Выборка.ОсновноеСредствоПредставление, Выборка.ДоговорПредставление);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", ДанныеСтроки.НомерСтроки, "ОсновноеСредство");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		КонецЕсли;
		
		Если НЕ Выборка.УчитыватьПредметыАрендыВУпрУчете
			И Объект.ОтражатьВУпрУчете Тогда
				
			ТекстСообщения = СтрШаблон(ШаблонСообщенияРегл, Выборка.ОсновноеСредствоПредставление, Выборка.ДоговорПредставление);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", ДанныеСтроки.НомерСтроки, "ОсновноеСредство");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		КонецЕсли;
		
	КонецЦикла;

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокумента
 
Процедура ПриЧтенииСозданииНаСервере(Форма) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
		Элементы.ХозяйственнаяОперация.СписокВыбора,
		Истина,
		Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации,
		НСтр("ru = 'Принятие к учету на основании инвентаризации';
			|en = 'Recognition based on stocktaking'"));
		
	Элементы.ХозяйственнаяОперация.Видимость = (Элементы.ХозяйственнаяОперация.СписокВыбора.Количество() > 1);
		
	ЦелевоеФинансированиеЗаполнитьСлужебныеРеквизиты(Объект);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт

	//++ Локализация
	ЦелевоеФинансированиеЗаполнитьСлужебныеРеквизиты(Форма.Объект);
	//-- Локализация
	
КонецПроцедуры

Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ИмяЭлемента = Элементы.ЦелевоеФинансированиеСчет.Имя Тогда
		
		ЦелевоеФинансированиеЗаполнитьСлужебныеРеквизиты(Объект, Элементы.ЦелевоеФинансирование.ТекущаяСтрока);
		
	ИначеЕсли ИмяЭлемента = Элементы.АмортизационнаяГруппа.Имя Тогда
		
		АмортизационнаяГруппаПриИзменении(Объект, ДополнительныеПараметры);
		
	ИначеЕсли ИмяЭлемента = Элементы.ДокументНаОсновании.Имя Тогда
		
		ЗаполнитьПоДаннымОснованияНаСервере(Форма);
		
	ИначеЕсли ИмяЭлемента = Элементы.ВключитьАмортизационнуюПремиюВСоставРасходов.Имя Тогда
		
		ВключитьАмортизационнуюПремиюВСоставРасходовПриИзменении(Объект, ДополнительныеПараметры);
		
	ИначеЕсли ИмяЭлемента = Элементы.ОССтоимостьБУ.Имя Тогда
		
		ОССтоимостьБУПриИзменении(Форма, ДополнительныеПараметры);
		
	ИначеЕсли ИмяЭлемента = Элементы.ПорядокУчетаНУ.Имя Тогда
		
		ПорядокУчетаНУПриИзменении(Форма, ДополнительныеПараметры);
		
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПриВыполненииКоманды(ИмяКоманды, Форма) Экспорт
	
	//++ Локализация
	
	Объект = Форма.Объект;
	
	Если ИмяКоманды = Форма.Команды.ЗаполнитьИсточникиФинансирования.Имя Тогда
		ЗаполнитьИсточникиФинансирования(Объект);
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

Процедура ОСОсновноеСредствоПриИзменении(Форма, ИзмененныеРеквизиты) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	Если ЗаполнитьГруппуОС(Объект) Тогда
		
		Если Объект.ВключитьАмортизационнуюПремиюВСоставРасходов Тогда
			ЗаполнитьПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект);
		КонецЕсли;
		
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",АмортизационнаяГруппа,ГруппаОС,НедвижимоеИмущество";
		
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

Процедура ОССтоимостьБУПриИзменении(Форма, ТекущиеДанные) Экспорт

	//++ Локализация
	Если ТекущиеДанные.СтоимостьНУ = Форма.ЗначенияРеквизитовОСДоИзменения.СтоимостьБУ И Форма.РасширеннаяСтоимостьРегл Тогда
		ТекущиеДанные.СтоимостьНУ = ТекущиеДанные.СтоимостьБУ;
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

Процедура ОбработкаВыбораЭлемента(Форма, ИзмененныеРеквизиты) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	Если ЗаполнитьГруппуОС(Объект) Тогда
		
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",АмортизационнаяГруппа,ГруппаОС,НедвижимоеИмущество";
		
		Если Объект.ВключитьАмортизационнуюПремиюВСоставРасходов Тогда
			ЗаполнитьПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект);
		КонецЕсли;
		
	КонецЕсли; 
	
	//-- Локализация

КонецПроцедуры

Процедура НастроитьЗависимыеЭлементыФормы(Форма, СтруктураИзмененныхРеквизитов) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
		
	Если ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.РегламентированномУчете"),
			НСтр("ru = 'Бухгалтерском и налоговом учете';
				|en = 'Accounting and tax accounting'"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.БухгалтерскомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.НалоговомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.УправленческомИБухгалтерскомУчете"));
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ВариантОтраженияВУчете.СписокВыбора,
			Истина,
			ПредопределенноеЗначение("Перечисление.ВариантыОтраженияВУчетеВнеоборотныхАктивов.УправленческомИНалоговомУчете"));
			
		//		
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаБУ.СписокВыбора, 
			Истина, 
			Перечисления.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету,,
			2);
				
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаБУ.СписокВыбора, 
			Истина, 
			Перечисления.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы);
								
		//
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаУУ.СписокВыбора, 
			Истина, 
			Перечисления.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы);
				
	КонецЕсли;
		
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ГруппаОС")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("АмортизационнаяГруппа")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НедвижимоеИмущество")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОсновноеСредство")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Договор")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВБУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ")
		ИЛИ ОбновитьВсе Тогда
		
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = Ложь;
		Если Объект.ОтражатьВБУ
			И Объект.ПорядокУчетаБУ <> Перечисления.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы Тогда
			
			СписокОС = Новый Массив;
			Для Каждого ДанныеСтроки Из Объект.ОС Цикл
				СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
			КонецЦикла;
			ОСУчитываютсяНаБалансе = Истина;
			Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды
				И СписокОС.Количество() > 0 Тогда
					
				ОСУчитываютсяНаБалансе = УчетАрендованныхОС.ЕстьПредметыАрендыНаБалансеАрендатора(СписокОС, Объект.Дата);
			КонецЕсли;
			
			Если ОСУчитываютсяНаБалансе Тогда
			
				ТребуетсяНастройкаОтраженияРасходовПоНалогам = 
					ВнеоборотныеАктивыЛокализация.ТребуетсяНастройкаОтраженияРасходовПоНалогам(
							Объект.Организация, 
							Объект.Дата, 
							СписокОС,
							Объект.ГруппаОС,
							Объект.АмортизационнаяГруппа,
							Объект.НедвижимоеИмущество);
			КонецЕсли;
			
		КонецЕсли;
				
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ТребуетсяНастройкаОтраженияРасходовПоНалогам",
			ТребуетсяНастройкаОтраженияРасходовПоНалогам,
			Форма.СлужебныеПараметрыФормы);
				
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") Тогда
		ВнеоборотныеАктивыСлужебный.ПроверитьСоответствиеСтатьиРасходовПорядкуУчетаНУ(Форма);
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПлательщикНалогаНаПрибыль",
			УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ВедетсяУчетПостоянныхИВременныхРазниц",
			УчетнаяПолитика.ВедетсяУчетПостоянныхИВременныхРазниц(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПрименяетсяУСНДоходыМинусРасходы",
			УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Объект.Организация, Объект.Дата),
			Форма.СлужебныеПараметрыФормы);
			
		//
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"НастройкиУчетаНДС",
			Объект.Организация,
			Объект.Дата);
		
		РаздельныйУчетВНАПоНалогообложениюНДС = 
			ПараметрыУчетнойПолитики <> Неопределено
			И ПараметрыУчетнойПолитики.РаздельныйУчетВНАПоНалогообложениюНДС;
			
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"РаздельныйУчетВНАПоНалогообложениюНДС",
			РаздельныйУчетВНАПоНалогообложениюНДС,
			Форма.СлужебныеПараметрыФормы);
			
		//
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"НастройкиУчетаНалогаНаПрибыль",
			Объект.Организация,
			?(Объект.Дата <> '000101010000', КонецМесяца(Объект.Дата), '000101010000'));
			
		ВключатьВСоставНалоговыхРасходовАрендныеПлатежи = 
			ПараметрыУчетнойПолитики <> Неопределено 
			И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи;
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ВключатьВСоставНалоговыхРасходовАрендныеПлатежи",
			ВключатьВСоставНалоговыхРасходовАрендныеПлатежи,
			Форма.СлужебныеПараметрыФормы);
			
		//
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ПрименяетсяФСБУ6",
			НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()))),
			Форма.СлужебныеПараметрыФормы);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыСлужебный.УстановитьСвойствоСтруктуры(
			"ЕстьСвязанныеОрганизации",
			Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация),
			Форма.СлужебныеПараметрыФормы);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ ОбновитьВсе Тогда
		
		ЗначенияПараметровВыбора = ОбщегоНазначенияУТКлиентСервер.ПараметрыПодбора(Элементы.СтатьяРасходовНУ, Форма);

		ВнеоборотныеАктивы.УстановитьПараметрыВыбораСтатьиПоПорядкуУчета(
			Элементы.СтатьяРасходовНУ.ПараметрыВыбора,
			ЗначенияПараметровВыбора);
		
	КонецЕсли;

	//-- Локализация
	
КонецПроцедуры
 
// 
// Параметры:
// 	УсловноеОформление - УсловноеОформление - 
// 	Элементы - ЭлементыФормы - 
Процедура УстановитьУсловноеОформление(УсловноеОформление, Элементы) Экспорт
	
	//++ Локализация
	
	#Область ОССтоимостьНУ_ТолькоПросмотр
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОССтоимостьНУ.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ХозяйственнаяОперация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтражатьВРеглУчете");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОС.ПредметАрендыУчитываетсяВНУ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокумента

Процедура ЗаполнитьДокументПоОтбору(Объект, Основание) Экспорт

	//++ Локализация
	
	Если Основание.Свойство("ДокументОснование")
		И ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, Основание);
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

// Заполняет документ на основании объекта эксплуатации.
// 
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - 
//  Основание - СправочникСсылка.ОбъектыЭксплуатации
Процедура ЗаполнитьНаОснованииОбъектаЭксплуатации(Объект, Основание) Экспорт

	//++ Локализация
	ЗаполнитьГруппуОС(Объект);
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииПринятияКУчетуИлиВводаОстатков(Объект, Основание) Экспорт

	//++ Локализация
	ЗаполнитьГруппуОС(Объект);
	//-- Локализация
	
КонецПроцедуры

// 
// Параметры:
// 	Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - 
// 	Основание - ДокументСсылка.ЗаключениеДоговораАренды - 
Процедура ЗаполнитьНаОснованииЗаключенияДоговораАренды(Объект, Основание) Экспорт

	//++ Локализация
	ЗаполнитьПринятиеКУчетуПредметовАренды(Объект);
	//-- Локализация
	
КонецПроцедуры

// 
// Параметры:
// 	Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - 
// 	Основание - ДокументСсылка.ИзменениеУсловийДоговораАренды - 
Процедура ЗаполнитьНаОснованииИзмененияУсловийДоговораАренды(Объект, Основание) Экспорт

	//++ Локализация
	ЗаполнитьПринятиеКУчетуПредметовАренды(Объект);
	//-- Локализация
	
КонецПроцедуры

// 
// Параметры:
// 	Объект - ДокументОбъект.ПринятиеКУчетуОС2_4 - 
//	СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов -
Процедура ЗаполнитьНаОснованииГрупповогоОС(Объект, СтатьяРасходов) Экспорт
	//++ Локализация
	Если Объект.ОтражатьВНУ Тогда
		Объект.ПорядокУчетаНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НеПриниматьКУчетуВключитьВРасходы;
		Объект.СтатьяРасходовНУ = СтатьяРасходов;
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ДополнитьВспомогательныеРеквизиты(Объект, ВспомогательныеРеквизиты) Экспорт

	//++ Локализация
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());
	
	ВспомогательныеРеквизиты.Вставить("ЕстьСвязанныеОрганизации", Справочники.Организации.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация));
	ВспомогательныеРеквизиты.Вставить("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, ДатаДок));
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяФСБУ6", НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, КонецМесяца(ДатаДок)));
	
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Объект.Организация, ДатаДок);
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяУСНДоходыМинусРасходы", ПрименяетсяУСНДоходыМинусРасходы);
	
	ВедетсяУчетПостоянныхИВременныхРазниц = УчетнаяПолитика.ВедетсяУчетПостоянныхИВременныхРазниц(Объект.Организация, ДатаДок);
	ВспомогательныеРеквизиты.Вставить("ВедетсяУчетПостоянныхИВременныхРазниц", ВедетсяУчетПостоянныхИВременныхРазниц);

	СписокОС = Объект.ОС.ВыгрузитьКолонку("ОсновноеСредство");
	ОСУчитываютсяНаБалансе = Истина;
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды
		И СписокОС.Количество() > 0 Тогда
			
		ОСУчитываютсяНаБалансе = УчетАрендованныхОС.ЕстьПредметыАрендыНаБалансеАрендатора(СписокОС, ДатаДок);
	КонецЕсли;
	
	Если ОСУчитываютсяНаБалансе
		И Объект.ОтражатьВБУ
		И Объект.ПорядокУчетаБУ <> Перечисления.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы Тогда
	
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = ВнеоборотныеАктивыЛокализация.ТребуетсяНастройкаОтраженияРасходовПоНалогам(
														Объект.Организация, 
														ДатаДок, 
														СписокОС, 
														Объект.ГруппаОС, 
														Объект.АмортизационнаяГруппа,
														Объект.НедвижимоеИмущество);
	Иначе
		ТребуетсяНастройкаОтраженияРасходовПоНалогам = Ложь;
	КонецЕсли;
	ВспомогательныеРеквизиты.Вставить("ТребуетсяНастройкаОтраженияРасходовПоНалогам", ТребуетсяНастройкаОтраженияРасходовПоНалогам);

	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНалогаНаПрибыль",
			Объект.Организация,
			ДатаДок);
	
	ВспомогательныеРеквизиты.Вставить("ВключатьВСоставНалоговыхРасходовАрендныеПлатежи", 
		ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи);
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
			Объект.Организация,
			ДатаДок);
	ВспомогательныеРеквизиты.Вставить("РаздельныйУчетВНАПоНалогообложениюНДС", 
		ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.РаздельныйУчетВНАПоНалогообложениюНДС);
	
	ВспомогательныеРеквизиты.Вставить("ВариантПримененияЦелевогоФинансирования", Объект.ВариантПримененияЦелевогоФинансирования);
		
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьСтоимость(Объект, СписокСтрок, ТаблицаОС) Экспорт

	//++ Локализация
	
	Если НЕ Объект.ОтражатьВРеглУчете
		ИЛИ Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды Тогда
		Возврат;
	КонецЕсли;
	
	СтоимостьНУ = Неопределено;
		
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНалогаНаПрибыль",
			Объект.Организация,
			?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	
	Если ПараметрыУчетнойПолитики <> Неопределено
		И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи Тогда
			
		СтоимостьНУ = Документы.ЗаключениеДоговораАренды.СтоимостьПредметовАренды(
						ТаблицаОС.ВыгрузитьКолонку("ОсновноеСредство"), 
						Объект.Организация, 
						Объект.Дата);
	КонецЕсли;
	
	Для каждого ДанныеСтроки Из СписокСтрок Цикл
		
		ДанныеСтроки.СтоимостьНУ = ?(СтоимостьНУ <> Неопределено, СтоимостьНУ.Получить(ДанныеСтроки.ОсновноеСредство), 0);
		
	КонецЦикла;
	
	//-- Локализация
	
КонецПроцедуры

// Дополняет параметры выбора статей и аналитик.
// 
// Параметры:
// 	ПараметрыВыбораСтатьиИАналитики - Массив из см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики -
Процедура ДополнитьПараметрыВыбораСтатейИАналитик(ПараметрыВыбораСтатьиИАналитики) Экспорт

	//++ Локализация
	
	// СтатьяРасходовНУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовНУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовНУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовНУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовНУ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяРасходовАмортизационнойПремии
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовАмортизационнойПремии";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовАмортизационнойПремии";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовАмортизационнойПремии");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовАмортизационнойПремии");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяРасходовНалог
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовНалог";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовНалог";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовНалог");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовНалог");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходовПоИнвентаризации");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходовПоИнвентаризации");
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходовЦФ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходовЦФ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

//++ Локализация

Процедура ОбработкаПроверкиЗаполненияПринятияКУчетуПредметовАренды(Объект, ВспомогательныеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ)
	
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СтоимостьНУ");
	
	Если НЕ ВспомогательныеРеквизиты.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи
		ИЛИ НЕ Объект.ОтражатьВНУ Тогда
		Возврат;
	КонецЕсли;
	
	СписокОС = Новый Массив;
	Для Каждого ДанныеСтроки Из Объект.ОС Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство)
			И НЕ ЗначениеЗаполнено(ДанныеСтроки.СтоимостьНУ) Тогда
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
		КонецЕсли;
	КонецЦикла;

	Если СписокОС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(
	|		&Дата, 
	|		ОсновноеСредство В (&СписокОС)) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Договор.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендатор)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("СписокОС", СписокОС);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Объект.ОС.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
		
		ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Стоимость (НУ)"" в строке %1 списка ""Основные средства""';
								|en = 'The ""Cost (TA)"" column in line %1 of the ""Fixed assets"" list is not filled in'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Формат(ДанныеСтроки.НомерСтроки, "ЧГ=;"));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", ДанныеСтроки.НомерСтроки, "СтоимостьНУ");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Местонахождение         КАК Подразделение,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ХозяйственнаяОперация   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяДоходов           КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов        КАК АналитикаДоходов,
	|
	|	ТаблицаОС.СтоимостьУУ                   КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыПартионногоУчета.УправленческийУчетОрганизаций, ЛОЖЬ)
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыПартионногоУчета.ИспользоватьУчетПрочихДоходовРасходовРегл, ЛОЖЬ)
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК ПараметрыПартионногоУчета
	|		ПО ПараметрыПартионногоУчета.Период = НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, МЕСЯЦ)
	|		 И ПараметрыПартионногоУчета.Организация = ДанныеДокумента.Организация
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации)
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период      КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.СобытиеОС   КАК Событие,
	|	ТаблицаОС.ОсновноеСредство  КАК ОсновноеСредство,
	|	&НазваниеДокумента          КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер       КАК НомерДокумента
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВБУ
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВНУ)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) КАК ВидНалога,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ГруппаОС <> ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|	И (ДанныеДокумента.Период < ДАТАВРЕМЯ(2019, 1, 1)
	|		ИЛИ ДанныеДокумента.НедвижимоеИмущество)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог) КАК ВидНалога,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ГруппаОС В (
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МашиныИОборудование)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог) КАК ВидНалога,
	|	ДанныеДокумента.СтатьяРасходовНалог       КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовНалог    КАК АналитикаРасходов
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период           КАК Период,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство,
	|	ИСТИНА                           КАК ПрименениеЦелевогоФинансирования,
	|	ДанныеДокумента.СтатьяДоходов    КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, ДЕНЬ) КАК ДатаОтражения,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, ДЕНЬ) КАК ДатаОтражения,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	(ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|		ИЛИ ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислитьАмортизациюПриПринятииКУчету))
	|	И ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию
	|	И ДанныеДокумента.ОрганизацияПолучательРасходов <> ДанныеДокумента.Организация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыОСПриПроведении(Объект, Отказ)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ОбъектыЭксплуатации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ОсновноеСредство");
	ЭлементБлокировки.ИсточникДанных = Объект.ОС;
	Блокировка.Заблокировать();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|ГДЕ
	|	ОбъектыЭксплуатации.Ссылка В(&ОбъектыЭксплуатации)
	|	И ОбъектыЭксплуатации.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПустаяСсылка)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбъектыЭксплуатации", Объект.ОС.ВыгрузитьКолонку("ОсновноеСредство"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектСправочника = Выборка.Ссылка.ПолучитьОбъект();
		Если Не ЗначениеЗаполнено(ОбъектСправочника.ГруппаОС) Тогда
			ОбъектСправочника.ГруппаОС = Объект.ГруппаОС;
		КонецЕсли;
		Если ОбъектСправочника.Модифицированность() Тогда
			ОбъектСправочника.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтНачисленнаяАмортизация(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСвДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСизДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтНачисленнаяАмортизация(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "НачисленнаяАмортизация";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса);
	
	ТекстПодзапроса =
	// Амортизация по предварительной стоимости
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Период                          КАК ПериодРасчета,
	|	ДанныеДокумента.Период                          КАК ПериодНачисления,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Местонахождение                 КАК Подразделение,
	|	ДанныеДокумента.Местонахождение                 КАК КорПодразделение,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.СтатьяРасходовБУ                КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовБУ             КАК АналитикаРасходов,
	|
	|	0                                               КАК ОбесценениеУУ,
	|	0                                               КАК ОбесценениеБУ,
	|	0                                               КАК ОбесценениеЦФ,
	|
	// СуммаУУ
	|	0                                               КАК СуммаУУ,
	|
	// СуммаБУ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаБУ,
	|
	|	0                                               КАК СуммаНУ,
	|	0                                               КАК СуммаПР,
	// СуммаВР
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаВР,
	|
	// СуммаЦФ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаЦФ,
	|
	|	0                                               КАК СуммаНУЦФ,
	|	0                                               КАК СуммаПРЦФ,
	|
	// СуммаВРЦФ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаВРЦФ,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходов,
	|
	|	ЛОЖЬ                                            КАК НачислятьИзнос,
	|	0                                               КАК НомерПакета,
	|	0                                               КАК ВыработкаБУ,
	|	ЛОЖЬ                                            КАК ЭтоАмортизационнаяПремия,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериод,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|	0                                               КАК СтоимостьДляВычисленияАмортизации,
	|	0                                               КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0                                               КАК ОстатокОбесцененияРегл,
	|	0                                               КАК ОстатокОбесцененияУпр,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВариантПроводокПоОбесценениюВНА,
	|	ЛОЖЬ                                            КАК ЭтоДоначисление,
	|	ЛОЖЬ                                            КАК ЭтоДоначислениеБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ЕГДС,
	|
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Период                          КАК ПериодРасчета,
	|	ДанныеДокумента.Период                          КАК ПериодНачисления,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Местонахождение                 КАК Подразделение,
	|	ДанныеДокумента.Местонахождение                 КАК КорПодразделение,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.СтатьяРасходовУУ                КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовУУ             КАК АналитикаРасходов,
	|
	|	0                                               КАК ОбесценениеУУ,
	|	0                                               КАК ОбесценениеБУ,
	|	0                                               КАК ОбесценениеЦФ,
	|
	|	ТаблицаОС.СтоимостьУУ                           КАК СуммаУУ,
	|
	|	0                                               КАК СуммаБУ,
	|	0                                               КАК СуммаНУ,
	|	0                                               КАК СуммаПР,
	|	0                                               КАК СуммаВР,
	|
	|	0                                               КАК СуммаЦФ,
	|	0                                               КАК СуммаНУЦФ,
	|	0                                               КАК СуммаПРЦФ,
	|	0                                               КАК СуммаВРЦФ,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходов,
	|
	|	ЛОЖЬ                                            КАК НачислятьИзнос,
	|	0                                               КАК НомерПакета,
	|	0                                               КАК ВыработкаБУ,
	|	ЛОЖЬ                                            КАК ЭтоАмортизационнаяПремия,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериод,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|	0                                               КАК СтоимостьДляВычисленияАмортизации,
	|	0                                               КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0                                               КАК ОстатокОбесцененияРегл,
	|	0                                               КАК ОстатокОбесцененияУпр,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВариантПроводокПоОбесценениюВНА,
	|	ЛОЖЬ                                            КАК ЭтоДоначисление,
	|	ЛОЖЬ                                            КАК ЭтоДоначислениеБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ЕГДС,
	|
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислитьАмортизациюПриПринятииКУчету)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Доначисление амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Период                          КАК ПериодРасчета,
	|	ДанныеДокумента.Период                          КАК ПериодНачисления,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Местонахождение                 КАК Подразделение,
	|	ДанныеДокумента.Местонахождение                 КАК КорПодразделение,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантРаспределенияРасходовРегл В (&СписокРазрешенныхВариантовРаспределенияРасходов)
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации)
	|	КОНЕЦ                                           КАК СтатьяРасходов,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантРаспределенияРасходовРегл В (&СписокРазрешенныхВариантовРаспределенияРасходов)
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                           КАК АналитикаРасходов,
	|
	|	0                                               КАК ОбесценениеУУ,
	|	0                                               КАК ОбесценениеБУ,
	|	0                                               КАК ОбесценениеЦФ,
	|
	// СуммаУУ
	|	0                                               КАК СуммаУУ,
	|
	// СуммаБУ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетСтоимости.СтоимостьРегл - (ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств)
	|	КОНЕЦ                                           КАК СуммаБУ,
	|
	|	0                                               КАК СуммаНУ,
	|	0                                               КАК СуммаПР,
	// СуммаВР
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетСтоимости.СтоимостьРегл - (ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств)
	|	КОНЕЦ                                           КАК СуммаВР,
	|
	// СуммаЦФ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА РасчетСтоимости.СтоимостьЦФ - ТаблицаОС.СтоимостьБУ 
	|		ИНАЧЕ РасчетСтоимости.СтоимостьЦФ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаЦФ,
	|
	|	0                                               КАК СуммаНУЦФ,
	|	0                                               КАК СуммаПРЦФ,
	|
	// СуммаВРЦФ
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА РасчетСтоимости.СтоимостьЦФ - ТаблицаОС.СтоимостьБУ 
	|		ИНАЧЕ РасчетСтоимости.СтоимостьЦФ - ТаблицаОС.СуммаЦелевыхСредств
	|	КОНЕЦ                                           КАК СуммаВРЦФ,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию            КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов                  КАК ОрганизацияПолучательРасходов,
	|	ЛОЖЬ                                            КАК НачислятьИзнос,
	|	0                                               КАК НомерПакета,
	|	0                                               КАК ВыработкаБУ,
	|	ЛОЖЬ                                            КАК ЭтоАмортизационнаяПремия,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериод,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|	0                                               КАК СтоимостьДляВычисленияАмортизации,
	|	0                                               КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0                                               КАК ОстатокОбесцененияРегл,
	|	0                                               КАК ОстатокОбесцененияУпр,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВариантПроводокПоОбесценениюВНА,
	|	ЛОЖЬ                                            КАК ЭтоДоначисление,
	|	ЛОЖЬ                                            КАК ЭтоДоначислениеБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ЕГДС,
	|
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|	И (РасчетСтоимости.СтоимостьРегл + РасчетСтоимости.СтоимостьЦФ) > ТаблицаОС.СтоимостьБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Период                          КАК ПериодРасчета,
	|	ДанныеДокумента.Период                          КАК ПериодНачисления,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Местонахождение                 КАК Подразделение,
	|	ДанныеДокумента.Местонахождение                 КАК КорПодразделение,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантРаспределенияРасходовУпр В (&СписокРазрешенныхВариантовРаспределенияРасходов)
	|			ТОГДА ДанныеДокумента.СтатьяРасходовУУ
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации)
	|	КОНЕЦ                                           КАК СтатьяРасходов,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ВариантРаспределенияРасходовУпр В (&СписокРазрешенныхВариантовРаспределенияРасходов)
	|			ТОГДА ДанныеДокумента.АналитикаРасходовУУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                           КАК АналитикаРасходов,
	|
	|	0                                               КАК ОбесценениеУУ,
	|	0                                               КАК ОбесценениеБУ,
	|	0                                               КАК ОбесценениеЦФ,
	|
	|	РасчетСтоимости.Стоимость - ТаблицаОС.СтоимостьУУ КАК СуммаУУ,
	|
	|	0                                               КАК СуммаБУ,
	|	0                                               КАК СуммаНУ,
	|	0                                               КАК СуммаПР,
	|	0                                               КАК СуммаВР,
	|
	|	0                                               КАК СуммаЦФ,
	|	0                                               КАК СуммаНУЦФ,
	|	0                                               КАК СуммаПРЦФ,
	|	0                                               КАК СуммаВРЦФ,
	|
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходов,
	|
	|	ЛОЖЬ                                            КАК НачислятьИзнос,
	|	0                                               КАК НомерПакета,
	|	0                                               КАК ВыработкаБУ,
	|	ЛОЖЬ                                            КАК ЭтоАмортизационнаяПремия,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериод,
	|	0                                               КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	0                                               КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|	0                                               КАК СтоимостьДляВычисленияАмортизации,
	|	0                                               КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0                                               КАК ОстатокОбесцененияРегл,
	|	0                                               КАК ОстатокОбесцененияУпр,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокУчетаБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииУУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ПорядокНачисленияАмортизацииБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВариантПроводокПоОбесценениюВНА,
	|	ЛОЖЬ                                            КАК ЭтоДоначисление,
	|	ЛОЖЬ                                            КАК ЭтоДоначислениеБУ,
	|	НЕОПРЕДЕЛЕНО                                    КАК ЕГДС,
	|
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислитьАмортизациюПриПринятииКУчету)
	|	И РасчетСтоимости.Стоимость > ТаблицаОС.СтоимостьУУ
	|";
	
	Документы.АмортизацияОС2_4.ТекстЗапросаВтНачисленнаяАмортизация(ТекстыЗапроса, ТекстПодзапроса, Истина);
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#Область ЗаполнениеДокумента

//++ Локализация

Процедура ЗаполнитьПринятиеКУчетуПредметовАренды(Объект)
	
	СписокОС = Новый Массив;
	Для Каждого ДанныеСтроки Из Объект.ОС Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
		КонецЕсли;
	КонецЦикла;
	
	Объект.СпособПоступления = Перечисления.СпособыПоступленияАктивов.ПоДоговоруАренды;
	Объект.ГруппаОС = Перечисления.ГруппыОС.ПустаяСсылка();
	ЗаполнитьГруппуОС(Объект);

	Если Объект.ОтражатьВРеглУчете Тогда
		
		ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
		
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНалогаНаПрибыль",
			Объект.Организация,
			ДатаДокумента);
			
		Если ПараметрыУчетнойПолитики <> Неопределено
			И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи Тогда
			
			СтоимостьОС = Документы.ЗаключениеДоговораАренды.СтоимостьПредметовАренды(СписокОС, Объект.Организация, ДатаДокумента);
			Для каждого ДанныеСтроки Из Объект.ОС Цикл
				ДанныеСтроки.СтоимостьНУ = СтоимостьОС.Получить(ДанныеСтроки.ОсновноеСредство);
			КонецЦикла;
		
		КонецЕсли;
		 
		//	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	РегистрСведений.АрендованныеОС.СрезПоследних(
		|		&Дата, 
		|		ОсновноеСредство В (&СписокОС)) КАК АрендованныеОС
		|ГДЕ
		|	АрендованныеОС.Договор.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата", Объект.Дата);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("СписокОС", СписокОС);
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Объект.ПорядокУчетаНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.СтоимостьНеВключаетсяВРасходы;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, Знач ДанныеЗаполнения)
	
	МассивНомеровСтрок = Неопределено;
	СообщатьОбОшибках = Истина;
	
	Объект.ДокументОснование = Неопределено;
	Объект.ДокументНаОсновании = Истина;
	
	Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Объект.ДокументОснование = ДанныеЗаполнения.ДокументОснование;
		МассивНомеровСтрок = ДанныеЗаполнения.МассивНомеровСтрок;
		СообщатьОбОшибках = ДанныеЗаполнения.СообщатьОбОшибках;
	Иначе
		Объект.ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Если НЕ ЗаполнитьНаОснованииИнвентаризацииОС(Объект, МассивНомеровСтрок)
		И СообщатьОбОшибках Тогда
		ТекстОшибки = НСтр("ru = 'В документе %1 отсутствуют строки требующие создания принятия к учету';
							|en = 'There are no lines which require recognition in the %1 document'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Объект.ДокументОснование);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "Объект.ДокументОснование");
	КонецЕсли; 
	
КонецПроцедуры

Функция ЗаполнитьНаОснованииИнвентаризацииОС(Объект, МассивНомеровСтрок = Неопределено)

	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	РезультатЗапроса = Документы.ИнвентаризацияОС.ДанныеЗаполненияНаОснованииИнвентаризации24(
							Объект.ДокументОснование, 
							Объект.Дата, 
							"ПринятиеКУчету", 
							МассивНомеровСтрок);
							
	Если РезультатЗапроса.ТабличнаяЧасть = Неопределено 
		ИЛИ РезультатЗапроса.ТабличнаяЧасть.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Реквизиты.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	Если Не ЗначениеЗаполнено(Объект.СпособПоступления) Тогда
		Объект.СпособПоступления = Перечисления.СпособыПоступленияАктивов.Иное;
	КонецЕсли;
	
	Объект.ОтражатьВРеглУчете = Истина;
	Объект.ОтражатьВУпрУчете  = Истина;
	
	КоэффициентПересчетаИзВалютыВВалюту = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
											ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация),
											Константы.ВалютаУправленческогоУчета.Получить(),
											?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса()));
	
	Выборка = РезультатЗапроса.ТабличнаяЧасть.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
			Объект.ГруппаОС = Выборка.ГруппаОС;
		КонецЕсли;
		
		Если Объект.ГруппаОС = Выборка.ГруппаОС Тогда
			
			НоваяСтрока = Объект.ОС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			Если Объект.ОтражатьВРеглУчете Тогда
				НоваяСтрока.СтоимостьБУ = Выборка.СтоимостьФактическая;
				НоваяСтрока.СтоимостьНУ = НоваяСтрока.СтоимостьБУ;
			КонецЕсли; 
			
			НоваяСтрока.СтоимостьУУ = Выборка.СтоимостьФактическая * КоэффициентПересчетаИзВалютыВВалюту;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ЗаполнитьГруппуОС(Объект)
	
	Если ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СписокОС = Новый Массив;
	Для каждого ДанныеСтроки Из Объект.ОС Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
		КонецЕсли; 
	КонецЦикла; 
	
	Если СписокОС.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	РеквизитыЗаполнены = Ложь;
	ЗначенияРеквизитовОбъектов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокОС, "ГруппаОС");
	Для каждого ЗначенияРеквизитов Из ЗначенияРеквизитовОбъектов Цикл
		Если НЕ ЗначениеЗаполнено(Объект.ГруппаОС)
			И ЗначениеЗаполнено(ЗначенияРеквизитов.Значение.ГруппаОС) Тогда
			Объект.ГруппаОС = ЗначенияРеквизитов.Значение.ГруппаОС;
			РеквизитыЗаполнены = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
		Объект.НедвижимоеИмущество = (ВнеоборотныеАктивыКлиентСерверЛокализация.ГруппыНедвижимогоИмущества().Найти(Объект.ГруппаОС) <> Неопределено);
	КонецЕсли; 

	Возврат РеквизитыЗаполнены;
	
КонецФункции

//-- Локализация

#КонецОбласти

#Область ФормаДокумента

//++ Локализация

Процедура ЦелевоеФинансированиеЗаполнитьСлужебныеРеквизиты(Объект, Строка = Неопределено)
	ВнеоборотныеАктивыЛокализация.ЗаполнитьСлужебныеРеквизитыВТаблицеЦелевогоФинансирования(Объект, Строка);
КонецПроцедуры

Процедура ЗаполнитьПоДаннымОснованияНаСервере(Форма)

	Объект = Форма.Объект;
	
	Объект.ОС.Очистить();
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОСпоИнвентаризации Тогда
		ЗаполнитьНаОснованииИнвентаризацииОС(Объект);
	КонецЕсли; 

КонецПроцедуры

Процедура АмортизационнаяГруппаПриИзменении(Объект, ДополнительныеПараметры)

	Если Объект.ВключитьАмортизационнуюПремиюВСоставРасходов Тогда
		ЗаполнитьПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "АмортизационнаяГруппа");
	
КонецПроцедуры

Процедура ВключитьАмортизационнуюПремиюВСоставРасходовПриИзменении(Объект, ДополнительныеПараметры)

	Если Объект.ВключитьАмортизационнуюПремиюВСоставРасходов Тогда
		ЗаполнитьПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ВключитьАмортизационнуюПремиюВСоставРасходов");

КонецПроцедуры

Процедура ЗаполнитьПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект)

	ПроцентКапитальныхВложенийВключаемыхВРасходыНУ = ВнеоборотныеАктивыЛокализация.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ(Объект.АмортизационнаяГруппа);
	Если ПроцентКапитальныхВложенийВключаемыхВРасходыНУ <> Объект.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ
		ИЛИ Объект.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ = 0 Тогда
		Объект.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ = ПроцентКапитальныхВложенийВключаемыхВРасходыНУ;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИсточникиФинансирования(Объект)
	
	Объект.ЦелевоеФинансирование.Загрузить(ВнеоборотныеАктивыЛокализация.ДанныеЗаполненияЦелевогоФинансирования(Объект));
	
КонецПроцедуры

Процедура ПорядокУчетаНУПриИзменении(Форма, ДополнительныеПараметры)

	Объект = Форма.Объект;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ПорядокУчетаНУ");
	
	Если НЕ ЗначениеЗаполнено(Объект.СтатьяРасходовНУ) Тогда
		Возврат;
	КонецЕсли;
	
	МассивВариантовРаспределения = ВнеоборотныеАктивыКлиентСервер.ДоступныеВариантыРаспределенияРасходов(Объект.ПорядокУчетаНУ);
	
	Если МассивВариантовРаспределения.Количество() <> 0 Тогда
		ВариантРаспределенияРасходовРегл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтатьяРасходовНУ, "ВариантРаспределенияРасходовНУ");
		Если МассивВариантовРаспределения.Найти(ВариантРаспределенияРасходовРегл) = Неопределено Тогда
			Объект.СтатьяРасходовНУ = Неопределено;
			Объект.АналитикаРасходовНУ = Неопределено;
			Форма.АналитикаРасходовНУОбязательна = Ложь;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры
 
//-- Локализация

#КонецОбласти

#Область РабочееМесто

// 
// Возвращаемое значение:
//  Строка - Текст запроса принятые к учету ОС
Функция ТекстЗапросаПринятыеКУчетуОС() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПорядокУчетаОСУУ
	|
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Дата,
	|			ОсновноеСредство В (
	|				ВЫБРАТЬ
	|					ПрочиеРасходы.ОсновноеСредство
	|				ИЗ
	|					ПрочиеРасходы КАК ПрочиеРасходы)) КАК ПорядокУчетаОСУУ
	|ГДЕ
	|	ПорядокУчетаОСУУ.Состояние В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПорядокУчетаОСБУ
	|
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Дата,
	|			ОсновноеСредство В (
	|				ВЫБРАТЬ
	|					ПрочиеРасходы.ОсновноеСредство
	|				ИЗ
	|					ПрочиеРасходы КАК ПрочиеРасходы)) КАК ПорядокУчетаОСБУ
	|
	|ГДЕ
	|	ПорядокУчетаОСБУ.СостояниеБУ В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|
	|	И ПорядокУчетаОСБУ.СостояниеНУ В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектыЭксплуатации.Ссылка КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПринятыеКУчетуОС
	|
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
