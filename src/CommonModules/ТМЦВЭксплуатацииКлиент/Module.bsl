////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы ТМЦ в эксплуатации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// 
// Параметры:
//  ТекущаяСтрока - Структура - 
//  СтруктураДействий - Структура - 
//  КэшированныеЗначения - Структура - 
// 
// Возвращаемое значение:
//  Булево -
Функция НеобходимВызовСервераДляОбработкиСтрокиТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		Если ЗначениеЗаполнено(ТекущаяСтрока.КатегорияЭксплуатации)
			И КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Получить(ТекущаяСтрока.КатегорияЭксплуатации) = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// См. ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ
Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, ДополнительныеПараметрыЗаполнения = Неопределено) Экспорт
	
	ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

// Обрабатывает строку при изменении категории эксплуатации.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыСтруктура, Массив из ДанныеФормыСтруктура - Текущая строка.
//  ТабличнаяЧасть - ДанныеФормыКоллекция - Табличная часть.
//  СтруктураДействий - Структура - Структура действий.
//	ПолеКоличество - Строка - Поле "Количество".
Процедура ОбработатьСтрокуПриИзмененииКатегорииЭксплуатации(ДанныеСтроки, ТабличнаяЧасть, СтруктураДействий = Неопределено, ПолеКоличество = "") Экспорт
	
	Если ПолеКоличество = "" Тогда
		ПолеКоличество = "КоличествоУпаковок";
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеСтроки", ДанныеСтроки);
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
	ДополнительныеПараметры.Вставить("СтруктураДействий", СтруктураДействий);
	ДополнительныеПараметры.Вставить("ПолеКоличество", ПолеКоличество);

	Если ТипЗнч(ДанныеСтроки) = Тип("Массив") Тогда
		
		ТребуетсяРазбитьСтроки = Ложь;
		
		Для Каждого ИДСтроки Из ДанныеСтроки Цикл
			ТекущаяСтрока = ТабличнаяЧасть.НайтиПоИдентификатору(ИДСтроки);
		 	Если ЗначениеЗаполнено(ТекущаяСтрока.КатегорияЭксплуатации) 
				И ТекущаяСтрока.ИнвентарныйУчет
				И ТекущаяСтрока[ПолеКоличество] > 1 Тогда
		 		ТребуетсяРазбитьСтроки = Истина;
		 		Прервать
		 	КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ТребуетсяРазбитьСтроки = 
			ЗначениеЗаполнено(ДанныеСтроки.КатегорияЭксплуатации) 
			И ДанныеСтроки.ИнвентарныйУчет
			И ДанныеСтроки[ПолеКоличество] > 1;
			
	КонецЕсли;
	
	Если ТребуетсяРазбитьСтроки Тогда

		Если ТипЗнч(ДанныеСтроки) = Тип("Массив")
			И ДанныеСтроки.Количество() > 1 Тогда
			
			ТекстВопроса = НСтр("ru = 'В категории эксплуатации включен инвентарный учет, количество должно быть равным 1.
								|Разбить строки или установить количество равным 1?';
								|en = 'The inventory accounting is enabled in the operation category, the quantity must be equal to 1.
								|Do you want to split the lines or set the quantity to 1?'");
								
			ТекстКнопки = НСтр("ru = 'Разбить строки';
								|en = 'Split lines'");
			
		Иначе
			
			ТекстВопроса = НСтр("ru = 'В категории эксплуатации включен инвентарный учет, количество должно быть равным 1.
								|Разбить строку или установить количество равным 1?';
								|en = 'The inventory accounting is enabled in the operation category, the quantity must equal 1.
								|Do you want to split the line or set the quantity to 1?'");
								
			ТекстКнопки = НСтр("ru = 'Разбить строку';
								|en = 'Split line'");
			
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьСтрокуПриИзмененииКатегорииЭксплуатацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, ТекстКнопки);
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Изменить количество';
															|en = 'Change quantity'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок);
		
	Иначе
		
		ОбработатьСтрокуПриИзмененииКатегорииЭксплуатацииЗавершение(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// 
// Параметры:
//  СтруктураПолейТЧ - Структура - Структура полей ТЧ
//  ТекущаяСтрока - Структура - Текущая строка
//  СтруктураДействий - Структура - Структура действий
//  ДополнительныеПараметрыЗаполнения - Структура - Дополнительные параметры заполнения
Процедура ДополнитьТекущуюСтрокуСтруктурой(СтруктураПолейТЧ, ТекущаяСтрока, СтруктураДействий, ДополнительныеПараметрыЗаполнения) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		СтруктураПолейТЧ.Вставить("КатегорияЭксплуатации");
		СтруктураПолейТЧ.Вставить("ИнвентарныйУчет");
		СтруктураПолейТЧ.Вставить("УчетПоФизЛицам");
		СтруктураПолейТЧ.Вставить("УчитыватьВВидеГрупповогоОС");
		СтруктураПолейТЧ.Вставить("СпособПогашенияСтоимостиБУ");
		СтруктураПолейТЧ.Вставить("СтатьяРасходов");
		СтруктураПолейТЧ.Вставить("СрокЭксплуатации");
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команды заполнения категории эксплуатации в выделенных строках таблицы.
// 
// Параметры:
// 	ТабличнаяЧасть - ДанныеФормыКоллекция - Табличная часть.
// 	ВыделенныеСтроки - Массив из Число - Выделенные строки таблицы формы.
// 	ПараметрыВыбора - Структура - Параметры отбора.
// 	ОписаниеОповещенияПослеЗаполнения - ОписаниеОповещения - Описания оповещения, которое необходимо вызвать после заполнения.
//
Процедура ЗаполнитьКатегориюЭксплуатации(ТабличнаяЧасть, ВыделенныеСтроки, ПараметрыВыбора = Неопределено, ОписаниеОповещенияПослеЗаполнения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	ДополнительныеПараметры.Вставить("ОписаниеОповещенияПослеЗаполнения", ОписаниеОповещенияПослеЗаполнения);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьКатегориюЭксплуатацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Справочник.КатегорииЭксплуатации.ФормаВыбора", ПараметрыВыбора,,,,, ОписаниеОповещения);
	
КонецПроцедуры

//++ НЕ УТ

// Заполняет список выбора при авто подборе по инвентарному номеру.
// 
// Параметры:
//  Текст - Строка - Введенный текст.
//  ПараметрыПодбора - см. ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации
//  ДанныеВыбора - СписокЗначений - Данные выбора.
//  СтандартнаяОбработка - Булево - Признак стандартной обработки события.
//
Процедура АвтоПодборПоИнвентарномуНомеру(Текст, ПараметрыПодбора, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ТМЦВЭксплуатацииКлиент.АвтоПодборПоИнвентарномуНомеру");
	
	Если ПараметрыПодбора = Неопределено Тогда
		ПараметрыПодбора = ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации();
	КонецЕсли;
	
	ПараметрыПодбора.ИнвентарныйНомерСодержит = Текст;
	ПараметрыПодбора.ВыбратьПервые = 10;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ТМЦВЭксплуатацииВызовСервера.ЗаполнитьДанныеВыбораПоИнвентарномуНомеру(
		ПараметрыПодбора, 
		ДанныеВыбора);
	
КонецПроцедуры

// Получает данные для печати и открывает форму для печати этикеток.
//
// Параметры:
//	ОписаниеКоманды - Структура - структура с описанием команды:
//	                    * Идентификатор - Строка - Может принимать значения "Этикетки";
//	                    * ОбъектыПечати - Массив - Объекты печати.
//
// Возвращаемое значение:
//	Неопределено - Ничего не возвращает.
//
Функция ПечатьЭтикетокТМЦВЭксплуатацииИзДокумента(ОписаниеКоманды) Экспорт
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("Идентификатор", ОписаниеКоманды.Идентификатор);
	ПараметрыПечати.Вставить("ОбъектыПечати", ОписаниеКоманды.ОбъектыПечати);
	ПараметрыПечати.Вставить("ДополнительныеПараметры", Новый Структура("МенеджерПечати", ОписаниеКоманды.МенеджерПечати));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПечатьИзДокумента", ПараметрыПечати);
	
	ОткрытьФорму(
		"Обработка.ПечатьЭтикетокТМЦВЭксплуатации.Форма",
		ПараметрыФормы,
		ОписаниеКоманды.Форма,
		Новый УникальныйИдентификатор);
	
	Возврат Неопределено;
	
КонецФункции

// Обработчик команды заполнения физ. лица выделенных строках таблицы.
// 
// Параметры:
// 	ТабличнаяЧасть - ДанныеФормыКоллекция - Табличная часть.
// 	ВыделенныеСтроки - Массив из Число - Выделенные строки таблицы формы.
// 	ИмяПоляФизическоеЛицо - Строка - Имя поля содержащего физическое лицо.
//
Процедура ЗаполнитьФизическоеЛицо(ТабличнаяЧасть, ВыделенныеСтроки, ИмяПоляФизическоеЛицо = "ФизическоеЛицо") Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	ДополнительныеПараметры.Вставить("ИмяПоляФизическоеЛицо", ИмяПоляФизическоеЛицо);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьФизическоеЛицоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыПодбора.Вставить("МножественныйВыбор", Ложь);

	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыПодбора,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открывает додобор ТМЦ в эксплуатации.
// 
// Параметры:
//  ПараметрыФормы - Структура - Параметры формы
//	Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПодобратьТМЦВЭксплуатации(ПараметрыФормы, Форма) Экспорт
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Документ.ПрочееОприходованиеТоваров.ФормаДокумента.Команда.ПодобратьТМЦВЭксплуатации");
	
	ИмяФормы = "Справочник.ПартииТМЦВЭксплуатации.Форма.ФормаПодбораУТ";
	//++ НЕ УТ
	ИмяФормы = "Справочник.ПартииТМЦВЭксплуатации.Форма.ФормаПодбора";
	//-- НЕ УТ
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураПризнаков = КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Получить(
		ТекущаяСтрока.КатегорияЭксплуатации);
		
	Если СтруктураПризнаков <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПризнаков);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуПриИзмененииКатегорииЭксплуатацииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да 
		И РезультатВопроса <> КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ДанныеСтроки = ДополнительныеПараметры.ДанныеСтроки;
	ТабличнаяЧасть = ДополнительныеПараметры.ТабличнаяЧасть; // ТабличнаяЧасть
	ПолеКоличество = ДополнительныеПараметры.ПолеКоличество;
	Разбить = (РезультатВопроса = КодВозвратаДиалога.Да);
	СтруктураДействий = ДополнительныеПараметры.СтруктураДействий; // Структура
	
	ЕстьУпаковка = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСтроки, "Упаковка");
	
	Если ТипЗнч(ДанныеСтроки) = Тип("Массив") Тогда
		
		СписокСтрок = Новый Массив;
		Для Каждого ИДСтроки Из ДанныеСтроки Цикл
			ТекущаяСтрока = ТабличнаяЧасть.НайтиПоИдентификатору(ИДСтроки);
			СписокСтрок.Добавить(ТекущаяСтрока);
		КонецЦикла;
		
	Иначе
		СписокСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСтроки);
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из СписокСтрок Цикл
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.КатегорияЭксплуатации) 
			И ТекущаяСтрока.ИнвентарныйУчет Тогда
				
			КоличествоВСтроке = ТекущаяСтрока.Количество - 1;
			
			ТекущаяСтрока[ПолеКоличество] = 1;
			ТекущаяСтрока.Количество = 1;
			
			Если ЕстьУпаковка Тогда
				ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
			КонецЕсли;
			
			Если СтруктураДействий <> Неопределено Тогда
				КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
				ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
			КонецЕсли;
			
			Если Разбить Тогда
				
				Пока КоличествоВСтроке > 0 Цикл
					
					НоваяСтрока = ТабличнаяЧасть.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
					НоваяСтрока.ИнвентарныйНомер = "";
					
					Если СтруктураДействий <> Неопределено Тогда
						ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
					КонецЕсли;
					
					КоличествоВСтроке = КоличествоВСтроке - 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			Если НЕ ТекущаяСтрока.ИнвентарныйУчет Тогда
				ТекущаяСтрока.ИнвентарныйНомер = "";
			КонецЕсли;
	
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьКатегориюЭксплуатацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧасть = ДополнительныеПараметры.ТабличнаяЧасть;
	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТабличнаяЧасть.НайтиПоИдентификатору(ИдентификаторСтроки);
		ДанныеСтроки.КатегорияЭксплуатации = Результат;
		
	КонецЦикла;
	
	Если ДополнительныеПараметры.ОписаниеОповещенияПослеЗаполнения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияПослеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

Процедура ЗаполнитьФизическоеЛицоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧасть = ДополнительныеПараметры.ТабличнаяЧасть;
	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	ИмяПоляФизическоеЛицо = ДополнительныеПараметры.ИмяПоляФизическоеЛицо;
	
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТабличнаяЧасть.НайтиПоИдентификатору(ИдентификаторСтроки);
		ДанныеСтроки[ИмяПоляФизическоеЛицо] = Результат;
		
	КонецЦикла;
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти