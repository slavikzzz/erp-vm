#Область СлужебныеПроцедурыИФункции

Функция ПараметрыБлокировкиИзмененияОбъекта(СсылкаНаОбъект, УправляемаяФорма) Экспорт
	
	КоллекцияБлокировок = Новый Массив;
	
	МетаданныеОбъекта = СсылкаНаОбъект.Метаданные();
	Если Не ПравоДоступа("Изменение", МетаданныеОбъекта) Тогда
		
		БлокировкаИзмененияОбъекта = БлокировкаИзмененияОбъектов.ОписаниеБлокировкиИзмененияОбъекта("ПраваИзмененияОбъекта",
			НСтр("ru = 'Нет прав на изменение';
				|en = 'You do not have permissions to change'"), Ложь);
		
		КоллекцияБлокировок.Добавить(БлокировкаИзмененияОбъекта);
		
	Иначе
		
		ОписаниеРезультата = "";
		ИзменениеЗапрещено = ДатыЗапретаИзменения.ИзменениеЗапрещено(МетаданныеОбъекта.ПолноеИмя(), СсылкаНаОбъект , ОписаниеРезультата);
		Если ИзменениеЗапрещено Тогда
			Если Не ПустаяСтрока(ОписаниеРезультата) Тогда
				
				ОписаниеРезультата = СокрЛП(ОписаниеРезультата);
				ОписаниеРезультата = СтрЗаменить(ОписаниеРезультата, Символы.ПС + Символы.ПС, Символы.ПС);
				
				БлокировкаИзмененияОбъекта = БлокировкаИзмененияОбъектов.ОписаниеБлокировкиИзмененияОбъекта("ДатыЗапретаИзменения", ОписаниеРезультата,
					Ложь, НСтр("ru = 'Необходимо изменить или совсем отключить дату запрета изменения данных, обратитесь к Администратору.';
								|en = 'Change or fully disable the period-end closing date. Contact the administrator.'"));
				
				КоллекцияБлокировок.Добавить(БлокировкаИзмененияОбъекта);
				
			КонецЕсли;
		КонецЕсли;
		
		ЭлектронныеТрудовыеКнижки.ДобавитьБлокировкуИзмененияОбъекта(КоллекцияБлокировок, СсылкаНаОбъект);
		КадровыйЭДО.ДобавитьБлокировкуИзмененияОбъекта(КоллекцияБлокировок, СсылкаНаОбъект);
		
	КонецЕсли;
	
	Возврат КоллекцияБлокировок;
	
КонецФункции

#КонецОбласти

