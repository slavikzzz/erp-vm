#Область СлужебныеПроцедурыИФункции

Функция ОписаниеКлиентскогоПриложения() Экспорт

	СистемнаяИнформация = Новый СистемнаяИнформация; 
	ВерсияПлатформы = СтрШаблон("%1%2", "1C/", СистемнаяИнформация.ВерсияПриложения);
	
	ВариантРаботы = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая(), "File", "ClientServer");
	РежимРазделения = ?(ОбщегоНазначения.РазделениеВключено(),"; Splitted","");
	РежимРаботы = СтрШаблон("%1%2%3%4", "(", ВариантРаботы, РежимРазделения, ")");
	
	ВерсияПодсистемы = "";
	УстановитьПривилегированныйРежим(Истина);
	ВерсииПодсистем = ОбновлениеИнформационнойБазы.ВерсииПодсистем();
	УстановитьПривилегированныйРежим(Ложь);
	СтрокаТЗ = ВерсииПодсистем.Найти("ЗарплатаКадрыБазовая", "ИмяПодсистемы");
	Если СтрокаТЗ <> Неопределено Тогда
		ВерсияПодсистемы = СтрокаТЗ.Версия;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ВерсияПодсистемы) Тогда
		Приложение = СтрШаблон("%1/%2 HRLib/%3", ОбщегоНазначения.ИдентификаторИнтернетПоддержкиКонфигурации(), Метаданные.Версия, ВерсияПодсистемы);
	Иначе
		Приложение = СтрШаблон("%1/%2", ОбщегоНазначения.ИдентификаторИнтернетПоддержкиКонфигурации(), Метаданные.Версия);
	КонецЕсли;
	
	Возврат СтрШаблон("%1 %2 %3 %4", ВерсияПлатформы, РежимРаботы, Приложение, "(1.0)");

КонецФункции

#КонецОбласти
