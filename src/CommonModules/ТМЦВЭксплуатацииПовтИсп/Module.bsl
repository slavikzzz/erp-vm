////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы ТМЦ в эксплуатации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет возможен ли учет серий в эксплуатации.
// 
// Возвращаемое значение:
//  Булево - Истина, если есть учетная политика с включенным учетом серий в эксплуатации
Функция ВозможенУчетСерийВЭксплуатации() Экспорт
	
	Возврат ТМЦВЭксплуатацииСервер.ВозможенУчетСерийВЭксплуатации();
	
КонецФункции

//++ НЕ УТ

//++ Локализация

// Возвращает год списания стоимости ТМЦ в эксплуатации.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется настройка.
//
// Возвращаемое значение:
//	Дата - Год списания стоимости.
//
Функция ГодСписанияСтоимости(Организация) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеРегистра.ГодСписанияСтоимости КАК ГодСписанияСтоимости
	|ИЗ
	|	РегистрСведений.НастройкаПереходаНаФСБУ5 КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.Организация = ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.ГодСписанияСтоимости КАК ГодСписанияСтоимости
	|ИЗ
	|	РегистрСведений.НастройкаПереходаНаФСБУ5 КАК ДанныеРегистра
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК ДанныеРегистраПоОрганизации
	|		ПО ДанныеРегистраПоОрганизации.Организация = ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|ГДЕ
	|	ДанныеРегистра.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ДанныеРегистраПоОрганизации.Организация ЕСТЬ NULL
	|";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		ГодСписанияСтоимости = Выборка.ГодСписанияСтоимости;
	Иначе
		ГодСписанияСтоимости = Год(РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019()) - 1;
	КонецЕсли;
	
	Возврат ГодСписанияСтоимости;
	
КонецФункции

// Возвращает необходимость списания стоимости ТМЦ в эксплуатации.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется настройка.
//	Период - Дата - дата, на которую определяется настройка.
//
// Возвращаемое значение:
//	Булево - Истина, если стоимость списывается.
//
Функция СписатьСтоимостьТМЦВЭксплуатации(Организация, Период) Экспорт
	
	Если КонецМесяца(Период) <> КонецГода(Период) Тогда
		Возврат Ложь; // Списание выполняется всегда в конце года.
	КонецЕсли;
	
	ГодСписанияСтоимости = ГодСписанияСтоимости(Организация);
	
	Возврат Год(Период) = ГодСписанияСтоимости;
	
КонецФункции

// Определяет списана ли стоимость ТМЦ в эксплуатации.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется настройка.
//	Период - Дата - дата, на которую определяется настройка.
//
// Возвращаемое значение:
//	Булево - Истина, если стоимость списана.
//
Функция СтоимостьТМЦВЭксплуатацииСписана(Организация, Период) Экспорт
	
	ГодСписанияСтоимости = ГодСписанияСтоимости(Организация);

	Возврат ГодСписанияСтоимости < Год(?(Период <> '000101010000', Период, ТекущаяДатаСеанса()));
	
КонецФункции

//-- Локализация

//-- НЕ УТ

#КонецОбласти
