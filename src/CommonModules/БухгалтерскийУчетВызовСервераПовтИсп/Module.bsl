#Область ПрограммныйИнтерфейс

#Область СвойстваСчетов

// Возвращает структуру со свойствами счета.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Счет, свойства которого необходимо вернуть.
//
// Возвращаемое значение:
//	Структура - См. БухгалтерскийУчетПереопределяемый.ПолучитьСвойстваСчета().
//
Функция ПолучитьСвойстваСчета(Знач Счет) Экспорт

	Возврат БухгалтерскийУчетПереопределяемый.ПолучитьСвойстваСчета(Счет);
	
КонецФункции

// Функция возвращает типы значений субконто, связанных с именами реквизитов.
//
// Возвращаемое значение:
//   Соответствие   - ключ - имя реквизита, значение - описание типов связанных значений субконто.
//
Функция ВсеТипыСвязанныхСубконто() Экспорт
	
	СвязанныеСубконто = Новый Соответствие;
	
	БухгалтерскийУчетПереопределяемый.УстановитьТипыСвязанныхСубконто(СвязанныеСубконто);
	
	СвязанныеСубконто.Вставить("Организация", БухгалтерскийУчетПереопределяемый.ТипыСвязанныеСОрганизацией());
	
	Возврат СвязанныеСубконто;
	
КонецФункции

#КонецОбласти

// Функция возвращает признак использования одной номенклатурной группы.
//
// Возвращаемое значение:
//	Булево - Признак использования одной номенклатурной группы.
//
Функция ИспользоватьОднуНоменклатурнуюГруппу() Экспорт

	Возврат БухгалтерскийУчетПереопределяемый.ИспользоватьОднуНоменклатурнуюГруппу();

КонецФункции

// Возвращает Истина, если переданный счет является счетом доходов, не учитываемых для целей налогообложения.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетЗабалансовогоУчетаДоходов(Знач Счет) Экспорт
	
	Возврат (Счет = ПланыСчетов.Хозрасчетный.ДоходыРасходыНеУчитываемые);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом расходов, не учитываемых для целей налогообложения.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетЗабалансовогоУчетаРасходов(Знач Счет) Экспорт
	
	Возврат (Счет = ПланыСчетов.Хозрасчетный.ВнереализационныеРасходыНеУчитываемые)
				ИЛИ (Счет = ПланыСчетов.Хозрасчетный.ВыплатыВпользуФизЛицПоП_1_48)
				ИЛИ (Счет = ПланыСчетов.Хозрасчетный.ДругиеВыплатыПоП_1_48);
	
КонецФункции

// Возвращает Истина, если переданный счет является забалансовым счетом учета материалов в эксплуатации.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетУчетаВЭксплуатации(Знач Счет) Экспорт
	
	Возврат (Счет = ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный)
				ИЛИ (Счет = ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета НДС.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетУчетаНДС(Знач Счет) Экспорт
	
	Возврат БухгалтерскийУчетПовтИсп.СчетВИерархии(Счет, ПланыСчетов.Хозрасчетный.НДС)
				ИЛИ (Счет = ПланыСчетов.Хозрасчетный.НДСНачисленныйПоОтгрузке);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета целевого финансирования.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетЦелевоеФинансирование(Знач Счет) Экспорт
	
	Возврат БухгалтерскийУчетПовтИсп.СчетВИерархии(Счет, ПланыСчетов.Хозрасчетный.ЦелевоеФинансирование);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета прочих расходов.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетПрочиеРасходы(Знач Счет) Экспорт
	
	Возврат БухгалтерскийУчетПовтИсп.СчетВИерархии(Счет, ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета прочих доходов.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетПрочиеДоходы(Знач Счет) Экспорт
	
	Возврат БухгалтерскийУчетПовтИсп.СчетВИерархии(Счет, ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета расчетов с персоналом по оплате труда.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетРасчетыСПерсоналомПоОплатеТруда(Знач Счет) Экспорт
	
	Возврат (Счет = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	
КонецФункции

// Возвращает Истина, если переданный вид субконто является прочим доходом и расходом.
//
// Параметры:
//	ВидСубконто - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные - Проверяемый вид субконто.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСубконтоПрочиеДоходыИРасходы(Знач ВидСубконто) Экспорт
	
	Возврат (ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	
КонецФункции

// Возвращает Истина, если переданный вид субконто является статьей затрат.
//
// Параметры:
//	ВидСубконто - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные - Проверяемый вид субконто.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСубконтоСтатьиЗатрат(Знач ВидСубконто) Экспорт
	
	Возврат (ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);

КонецФункции

// Возвращает счет внереализационных расходов, не учитываемых для целей налогообложения.
//
// Возвращаемое значение:
//	ПланСчетовСсылка.Хозрасчетный - Счет.
//
Функция СчетВнереализационныеРасходыНеУчитываемые() Экспорт
	
	Возврат ПланыСчетов.Хозрасчетный.ВнереализационныеРасходыНеУчитываемые;
	
КонецФункции

// Возвращает счет выплат в пользу физических лиц, не учитываемых для целей налогообложения.
//
// Возвращаемое значение:
//	ПланСчетовСсылка.Хозрасчетный - Счет.
//
Функция СчетВыплатыВпользуФизЛицПоП_1_48() Экспорт
	
	Возврат ПланыСчетов.Хозрасчетный.ВыплатыВпользуФизЛицПоП_1_48;
	
КонецФункции

// Возвращает счет других выплат, не учитываемых для целей налогообложения.
//
// Возвращаемое значение:
//	ПланСчетовСсылка.Хозрасчетный - Счет.
//
Функция СчетДругиеВыплатыПоП_1_48() Экспорт
	
	Возврат ПланыСчетов.Хозрасчетный.ДругиеВыплатыПоП_1_48;
	
КонецФункции

// Возвращает счет доходов, не учитываемых для целей налогообложения.
//
// Возвращаемое значение:
//	ПланСчетовСсылка.Хозрасчетный - Счет.
//
Функция СчетДоходыНеУчитываемые() Экспорт
	
	Возврат ПланыСчетов.Хозрасчетный.ДоходыНеУчитываемые;
	
КонецФункции

// Возвращает Истина, если переданный счет является счетом учета внеоборотных активов.
//
// Параметры:
//	Счет - ПланСчетовСсылка.Хозрасчетный - Проверяемый счет.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСчетКапитализацииРасходовПоВНА(Счет) Экспорт
	
	Возврат БухгалтерскийУчетПереопределяемый.ЭтоСчетКапитализацииРасходовПоВНА(Счет);

КонецФункции

// Возвращает Истина, если переданный вид субконто используется для учета внеоборотных активов.
//
// Параметры:
//	ВидСубконто - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные - Проверяемый вид субконто.
//
// Возвращаемое значение:
//	Булево - Признак.
//
Функция ЭтоСубконтоОбъектВНА(Знач ВидСубконто) Экспорт
	
	Возврат (ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства
				ИЛИ ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства
				ИЛИ ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	
КонецФункции

// Возвращает описание типов для суммового показателя.
//
// Возвращаемое значение:
//	ОписаниеТипов - Описание типов для суммового показателя.
//
Функция ТипСумма() Экспорт
	
	Возврат БухгалтерскийУчетПереопределяемый.ТипСумма();
	
КонецФункции

Функция НаСчетеВедетсяУчетПоРаботникам(Счет) Экспорт
	
	СвойстваСчета = ПолучитьСвойстваСчета(Счет);
	
	УчетПоРаботникам = СвойстваСчета.ВидСубконто1 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций
		ИЛИ СвойстваСчета.ВидСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций
		ИЛИ СвойстваСчета.ВидСубконто3 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций;
	
	Возврат УчетПоРаботникам;
	
КонецФункции

#КонецОбласти

#Область ПервичныеДокументы

// Определяет в целом для сеанса работы, может ли потребоваться рассчитывать, заполнять и отображать в проводках
// пользователю суммы налогового учета и постоянных/временных разниц.
// 
// Возвращаемое значение:
//  Строка - варианты использования разниц:
//         * "ПоддержкаПБУ18", если может потребоваться использовать как НУ, так и суммы ПР/ВР в проводках;
//         * "ПлательщикНалогаНаПрибыль", если потребуются суммы НУ;
//         * "НеИспользовать", если ничего кроме сумм БУ не требуется использовать.
//
Функция ПользователюДоступныСуммыНалогНаПрибыль() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидИспользованияСумм = "ПоддержкаПБУ18";
	Если Не БухгалтерскийУчет.ПользователюДоступныСуммыНУ() Тогда // только БУ
		ВидИспользованияСумм = "НеИспользовать";
	ИначеЕсли Не НалогНаПрибыльБухгалтерскийУчет.ПользователюДоступныСуммыРазниц() Тогда
		ВидИспользованияСумм = "ПлательщикНалогаНаПрибыль";
	КонецЕсли;
	
	Возврат ВидИспользованияСумм;
	
КонецФункции

#КонецОбласти
