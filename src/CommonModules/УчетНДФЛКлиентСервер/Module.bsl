
#Область СлужебныйПрограммныйИнтерфейс

Процедура НазначитьИдентификаторСтрокеНДФЛ(СтрокаНДФЛ, МаксимальныйИдентификаторСтрокиНДФЛ, НоваяСтрока) Экспорт
	
	Если Не НоваяСтрока Или СтрокаНДФЛ.ИдентификаторСтрокиНДФЛ > 0 Тогда
		Возврат;
	КонецЕсли;
	
	МаксимальныйИдентификаторСтрокиНДФЛ = МаксимальныйИдентификаторСтрокиНДФЛ + 1;
	СтрокаНДФЛ.ИдентификаторСтрокиНДФЛ = МаксимальныйИдентификаторСтрокиНДФЛ;
	
КонецПроцедуры

Функция РесурсыИсчисленногоНалогаСтрокой(ИмяПервойКолонки, ТолькоПревышение, БезПревышения) Экспорт

	Возврат СтрСоединить(РесурсыИсчисленногоНалогаВМассиве(ИмяПервойКолонки, ТолькоПревышение, БезПревышения), ",")

КонецФункции 

Функция РесурсыИсчисленногоНалогаВМассиве(ИмяПервойКолонки, ТолькоПревышение, БезПревышения) Экспорт

	МассивСтрок = Новый Массив;
	Если ТолькоПревышение И БезПревышения Тогда
		Возврат Новый ФиксированныйМассив(МассивСтрок)
	КонецЕсли;
	Если Не ТолькоПревышение Тогда
		МассивСтрок.Добавить(ИмяПервойКолонки)
	КонецЕсли;
	Если Не БезПревышения Тогда
		МассивСтрок.Добавить(ИмяПервойКолонки + "СПревышения");
		МассивСтрок.Добавить(ИмяПервойКолонки + "СПревышенияПоСтавке18");
		МассивСтрок.Добавить(ИмяПервойКолонки + "СПревышенияПоСтавке20");
		МассивСтрок.Добавить(ИмяПервойКолонки + "СПревышенияПоСтавке22");
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(МассивСтрок)
	
КонецФункции 

Функция СоответствиеКолонокНалогаЗначениямВидовОсобыхНачисленийИУдержаний(ИмяПервойКолонки = "Сумма") Экспорт 

	ИменаРесурсовНалога = РесурсыИсчисленногоНалогаВМассиве(ИмяПервойКолонки, Ложь, Ложь);
	ИменаЗначенийПеречисления = РесурсыИсчисленногоНалогаВМассиве("Перечисление.ВидыОсобыхНачисленийИУдержаний.НДФЛ", Ложь, Ложь);
	КоличествоКолонок = ИменаРесурсовНалога.Количество();
	ПереченьСтрок = Новый Соответствие;
	Для СчКолонок = 1 По КоличествоКолонок Цикл
		ПереченьСтрок.Вставить(ПредопределенноеЗначение(ИменаЗначенийПеречисления[СчКолонок - 1]), ИменаРесурсовНалога[СчКолонок - 1]);
	КонецЦикла; 
	
    Возврат ПереченьСтрок
	
 КонецФункции 

Функция ИтоговаяСуммаНДФЛпоДокументу(Источник, ИмяПервойКолонки = "Сумма") Экспорт
	
	ИменаНалоговыхРесурсов = РесурсыИсчисленногоНалогаВМассиве(ИмяПервойКолонки, Ложь, Ложь);
	ИтогНДФЛ = 0;
	Для каждого ИмяРесурса Из ИменаНалоговыхРесурсов Цикл
		ИтогНДФЛ = ИтогНДФЛ + Источник.НДФЛ.Итог(ИмяРесурса);
	КонецЦикла;
	Возврат ИтогНДФЛ;
	
КонецФункции

Функция ИтоговаяСуммаНДФЛпоСтроке(Источник, ИмяПервойКолонки = "Сумма") Экспорт
	
	ИменаНалоговыхРесурсов = РесурсыИсчисленногоНалогаВМассиве(ИмяПервойКолонки, Ложь, Ложь);
	ИтогНДФЛ = 0;
	Для каждого ИмяРесурса Из ИменаНалоговыхРесурсов Цикл
		ИтогНДФЛ = ИтогНДФЛ + Источник[ИмяРесурса];
	КонецЦикла;
	Возврат ИтогНДФЛ;
	
КонецФункции

#Область ПанельПримененныеВычеты

// См. функцию УчетНДФЛКлиентСерверВнутренний.ОписаниеПанелиВычеты.
//
Функция ОписаниеПанелиВычеты() Экспорт
	
	Возврат УчетНДФЛКлиентСерверВнутренний.ОписаниеПанелиВычеты();
	
КонецФункции

// См. функцию УчетНДФЛКлиентСерверВнутренний.ОписаниеТабличнойЧастиНДФЛ.
//
Функция ОписаниеТабличнойЧастиНДФЛ() Экспорт
	
	Возврат УчетНДФЛКлиентСерверВнутренний.ОписаниеТабличнойЧастиНДФЛ();
	
КонецФункции

Функция ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ() Экспорт
	
	Возврат УчетНДФЛКлиентСерверВнутренний.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ();
	
КонецФункции

Функция НДФЛТекущиеДанные(Форма, ОписаниеПанелиВычеты) Экспорт

	Возврат УчетНДФЛКлиентСерверВнутренний.НДФЛТекущиеДанные(Форма, ОписаниеПанелиВычеты);
	
КонецФункции

Процедура ДополнитьПредставлениеВычетов(ПредставлениеВычетов, КодВычета, СуммаВычета) Экспорт
	
	Если ЗначениеЗаполнено(КодВычета) И ЗначениеЗаполнено(СуммаВычета) Тогда
		ПредставлениеВычетов = ПредставлениеВычетов + СуммаВычета;
	КонецЕсли; 
					
КонецПроцедуры

Процедура ЗаполнитьПредставлениеВычетовЛичныхСтрокиНДФЛ(Форма, СтрокаНДФЛ, ОписаниеПанелиВычеты) Экспорт
	
	ВычетыЛичные = ОписаниеПанелиВычеты.НастраиваемыеПанели.Получить("ВычетыЛичные");
	Если ВычетыЛичные <> Неопределено Тогда
	
		СтрокаНДФЛ.ПредставлениеВычетовЛичных = 0;
		ДополнитьПредставлениеВычетов(СтрокаНДФЛ.ПредставлениеВычетовЛичных, СтрокаНДФЛ.ПримененныйВычетЛичныйКодВычета, СтрокаНДФЛ.ПримененныйВычетЛичный);
		ДополнитьПредставлениеВычетов(СтрокаНДФЛ.ПредставлениеВычетовЛичных, СтрокаНДФЛ.ПримененныйВычетЛичныйКЗачетуВозвратуКодВычета, СтрокаНДФЛ.ПримененныйВычетЛичныйКЗачетуВозврату);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция МесяцНалоговогоПериодаСтрокой(НомерМесяца) Экспорт
	Если НомерМесяца >= 1 И НомерМесяца <= 12 Тогда
		Возврат Формат(Дата(2012, НомерМесяца, 1), "ДФ=ММММ");
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

Функция СправкиНДФЛИмяПоляИтоговПоСтавке(ИмяПоля, Ставка) Экспорт
	Если Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка13") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке13";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка30") Тогда
		ИмяПоляСправки =  ИмяПоля + "ПоСтавке30";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка09") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке9";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка15") Тогда
		ИмяПоляСправки =  ИмяПоля + "ПоСтавке15";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка18") Тогда
		ИмяПоляСправки =  ИмяПоля + "ПоСтавке18";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка20") Тогда
		ИмяПоляСправки =  ИмяПоля + "ПоСтавке20";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка22") Тогда
		ИмяПоляСправки =  ИмяПоля + "ПоСтавке22";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка35") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке35";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка05") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке5";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка10") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке10";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка03") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке3";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка06") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке6";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка07") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке7";
	ИначеЕсли Ставка = ПредопределенноеЗначение("Перечисление.НДФЛСтавки.Ставка12") Тогда
		ИмяПоляСправки = ИмяПоля + "ПоСтавке12";
	КонецЕсли;
	
	Возврат ИмяПоляСправки;
КонецФункции

Функция СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, ИмяПоля, Ставка) Экспорт 
	ИмяПоляИтоговПоСтавке = СправкиНДФЛИмяПоляИтоговПоСтавке(ИмяПоля, Ставка);
	
	Возврат ДанныеСправки[ИмяПоляИтоговПоСтавке];
КонецФункции

Функция СправкиНДФЛЕстьИтогиПоСтавке(ДанныеСправки, Ставка) Экспорт
	Возврат СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "ОбщаяСуммаДохода", Ставка) <> 0 
		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "ОблагаемаяСуммаДохода", Ставка) <> 0
		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "Исчислено", Ставка) <> 0
		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "Удержано", Ставка) <> 0
  		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "Перечислено", Ставка) <> 0
		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "Задолженность", Ставка) <> 0
		Или СправкиНДФЛЗначениеИтоговПоСтавке(ДанныеСправки, "ИзлишнеУдержано", Ставка) <> 0;
	
КонецФункции	

Функция СправкиНДФЛДокументИспользуетКодОКТМО(ДанныеДокумента) Экспорт
	Если ДанныеДокумента.НалоговыйПериод >= 2013 
		И Не ДанныеДокумента.СпециальныйДокумент2013года Тогда 
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции	

#КонецОбласти