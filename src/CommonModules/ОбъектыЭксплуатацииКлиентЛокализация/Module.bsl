////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации справочника "Объекты эксплуатации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
// 	ИмяЭлемента - Строка - 
// 	Форма - ФормаКлиентскогоПриложения -
//  ДополнительныеПараметры - Структура - 
Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры = Неопределено) Экспорт

	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	//++ Локализация
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	Если ИмяЭлемента = Элементы.ГруппаОС.Имя Тогда	
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", ИмяЭлемента);
		
	ИначеЕсли ИмяЭлемента = Элементы.ДрагоценныеМатериалыДрагоценныйМатериал.Имя Тогда	
		
		ТребуетсяВызовСервера = Истина;
		
	ИначеЕсли ИмяЭлемента = Элементы.КодПоОКОФ.Имя Тогда	
		
		КодПоОКОФПриИзменении(Объект, Элементы, ТребуетсяВызовСервера);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			ИмяЭлемента, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура НачалоВыбораРеквизита(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент.Имя = Элементы.КодПоОКОФ.Имя Тогда	
		
		КодПоОКОФНачалоВыбора(Форма, СтандартнаяОбработка);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт

	//++ Локализация
	
	Если ИмяСобытия = "ВыборАмортизационнойГруппыОС" 
		И Источник = Форма.УникальныйИдентификатор Тогда
		
		ОбработатьВыборКодаПоОКОФ(Форма, Параметр);
		
	КонецЕсли;
	
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры

Процедура ПриВыполненииКоманды(Команда, Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Команда.Имя = Элементы.НедвижимоеИмуществоИстория.ИмяКоманды Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОсновноеСредство", Объект.Ссылка);
		ПараметрыФормы.Вставить("ИмяРеквизита", "НедвижимоеИмущество");
		ОткрытьФорму("Справочник.ОбъектыЭксплуатации.Форма.ИсторияИзменений", ПараметрыФормы, Форма);
		
	ИначеЕсли Команда.Имя = Элементы.АмортизационнаяГруппаИстория.ИмяКоманды Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОсновноеСредство", Объект.Ссылка);
		ПараметрыФормы.Вставить("ИмяРеквизита", "АмортизационнаяГруппа");
		ОткрытьФорму("Справочник.ОбъектыЭксплуатации.Форма.ИсторияИзменений", ПараметрыФормы, Форма);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры
 
#КонецОбласти

//++ Локализация

#Область СлужебныеПроцедурыИФункции

Процедура КодПоОКОФПриИзменении(Объект, Элементы, ТребуетсяВызовСервера)
	
	Если ЗначениеЗаполнено(Объект.КодПоОКОФ) Тогда
		ТребуетсяВызовСервера = Истина;
	Иначе
		Элементы.КодПоОКОФ.Подсказка = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура КодПоОКОФНачалоВыбора(Форма, СтандартнаяОбработка)
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("КодПоОКОФ", Объект.КодПоОКОФ);
	ПараметрыОткрытия.Вставить("АмортизационнаяГруппа", Форма.АмортизационнаяГруппа);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораАмортизационнойГруппы", ПараметрыОткрытия, Форма);
	
КонецПроцедуры

Процедура ОбработатьВыборКодаПоОКОФ(Форма, ВыбранноеЗначение)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
		Форма, 
		"ОбработатьВыборКодаПоОКОФ", 
		Истина,
		ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

//-- Локализация
