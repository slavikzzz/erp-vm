#Область СлужебныеПроцедурыИФункции

Функция ИзменитьПометкуУдаления(Знач МассивСсылок, Знач ПометкаУдаления) Экспорт
	Возврат СЭДОФССРасширенный.ИзменитьПометкуУдаления(МассивСсылок, ПометкаУдаления);
КонецФункции

Функция ЗначенияЗаполненияСведенийОЗастрахованномЛицеФСС(Ссылка) Экспорт
	
	СтруктураРезультат = Новый Структура("АдресХранилища,ЗначенияЗаполнения", "", Неопределено);
	ПолноеИмя = Ссылка.Метаданные().ПолноеИмя();
	ДатаСобытия = "ДатаЗамены";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	СписокСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СписокСотрудники.Ссылка.Организация КАК Организация,
		|	СписокСотрудники.ФизическоеЛицо.Наименование КАК ФИО,
		|	СписокСотрудники.ДатаЗамены КАК ДатаСобытия,
		|	СписокСотрудники.Ссылка.Дата КАК Дата
		|ПОМЕСТИТЬ СоставДокумента
		|ИЗ
		|	&СписокСотрудники КАК СписокСотрудники
		|ГДЕ
		|	СписокСотрудники.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СоставДокумента.Организация КАК Организация,
		|	СведенияОЗастрахованномЛицеФСС.Ссылка КАК Ссылка,
		|	СведенияОЗастрахованномЛицеФСС.ДатаСоздания КАК ДатаСоздания,
		|	СведенияОЗастрахованномЛицеФСС.ДатаОтправки <> ДАТАВРЕМЯ(1, 1, 1) КАК Отправлен,
		|	ВЫБОР
		|		КОГДА СведенияОЗастрахованномЛицеФСС.ПометкаУдаления
		|			ТОГДА 3
		|		КОГДА СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.ОшибкаЛогическогоКонтроля)
		|				ИЛИ СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.ОшибкаПриОтправке)
		|			ТОГДА 4
		|		КОГДА СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.Несоответствие)
		|			ТОГДА 5
		|		КОГДА СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.ПринятСЗамечаниями)
		|			ТОГДА 6
		|		КОГДА СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.Принят)
		|				ИЛИ СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.ОтправленОператору)
		|				ИЛИ СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.Отправлен)
		|			ТОГДА 7
		|		КОГДА СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.НеПринят)
		|			ТОГДА 8
		|		КОГДА СведенияОЗастрахованномЛицеФСС.Проведен
		|			ТОГДА 2
		|		КОГДА НЕ СведенияОЗастрахованномЛицеФСС.Проведен
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексКартинки,
		|	СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.Состояние КАК Состояние,
		|	СведенияОЗастрахованномЛицеФСС.РегистрацияСведений КАК РегистрацияСведений,
		|	СведенияОЗастрахованномЛицеФСС.РегистрацияСведений.ТекстОшибки КАК ОписаниеОшибок
		|ПОМЕСТИТЬ СозданныеДокументыСведений
		|ИЗ
		|	СоставДокумента КАК СоставДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СведенияОЗастрахованномЛицеФСС КАК СведенияОЗастрахованномЛицеФСС
		|		ПО СоставДокумента.ФизическоеЛицо = СведенияОЗастрахованномЛицеФСС.ФизическоеЛицо
		|			И СоставДокумента.Организация = СведенияОЗастрахованномЛицеФСС.Организация
		|			И (НАЧАЛОПЕРИОДА(СоставДокумента.Дата, ДЕНЬ) <= СведенияОЗастрахованномЛицеФСС.ДатаСоздания)
		|			И (НЕ СведенияОЗастрахованномЛицеФСС.ПометкаУдаления)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СозданныеДокументыСведений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СозданныеДокументыСведений.Организация КАК Организация,
		|	МАКСИМУМ(СозданныеДокументыСведений.ДатаСоздания) КАК ДатаСоздания
		|ПОМЕСТИТЬ МаксДатаСоздания
		|ИЗ
		|	СозданныеДокументыСведений КАК СозданныеДокументыСведений
		|
		|СГРУППИРОВАТЬ ПО
		|	СозданныеДокументыСведений.ФизическоеЛицо,
		|	СозданныеДокументыСведений.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СозданныеДокументыСведений.Ссылка КАК Документ,
		|	СозданныеДокументыСведений.Ссылка ЕСТЬ NULL КАК Флаг,
		|	СоставДокумента.ДатаСобытия КАК ДатаСобытия,
		|	СозданныеДокументыСведений.ДатаСоздания КАК ДатаСоздания,
		|	СозданныеДокументыСведений.Отправлен КАК Отправлен,
		|	СозданныеДокументыСведений.ИндексКартинки КАК ИндексКартинки,
		|	СоставДокумента.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА СозданныеДокументыСведений.ИндексКартинки = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.ПодготовленКОтправке)
		|		ИНАЧЕ СозданныеДокументыСведений.Состояние
		|	КОНЕЦ КАК Состояние,
		|	СозданныеДокументыСведений.РегистрацияСведений КАК РегистрацияСведений,
		|	СозданныеДокументыСведений.ОписаниеОшибок КАК ОписаниеОшибок
		|ИЗ
		|	СоставДокумента КАК СоставДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ СозданныеДокументыСведений КАК СозданныеДокументыСведений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксДатаСоздания КАК МаксДатаСоздания
		|			ПО СозданныеДокументыСведений.ФизическоеЛицо = МаксДатаСоздания.ФизическоеЛицо
		|				И СозданныеДокументыСведений.Организация = МаксДатаСоздания.Организация
		|				И СозданныеДокументыСведений.ДатаСоздания = МаксДатаСоздания.ДатаСоздания
		|		ПО СоставДокумента.ФизическоеЛицо = СозданныеДокументыСведений.ФизическоеЛицо
		|			И СоставДокумента.Организация = СозданныеДокументыСведений.Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	СоставДокумента.ФИО";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокСотрудники", ПолноеИмя + ".Сотрудники");
	Если ПолноеИмя = "Документ.УвольнениеСписком" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ДатаСобытия, "ДатаУвольнения");
	ИначеЕсли ПолноеИмя = "Документ.ПриемНаРаботуСписком" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ДатаСобытия, "ДатаПриема");
	Иначе
		Возврат СтруктураРезультат;
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		ЗначенияЗаполнения = Новый Структура("ФизическоеЛицо,Организация");
		ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, Выборка);
		СтруктураРезультат.ЗначенияЗаполнения = ЗначенияЗаполнения;
	Иначе
		СтруктураРезультат.АдресХранилища = ПоместитьВоВременноеХранилище(Результат.Выгрузить());
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции

#КонецОбласти
