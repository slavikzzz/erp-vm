////////////////////////////////////////////////////////////////////////////////
// Подсистема "Расчет имущественных налогов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс 

#Область НалогНаИмущество

// Возвращает результат расчета налога (или аванса по налогу) на имущество.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
//  ПериодРасчета - Дата - Дата расчета.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
// 
// Возвращаемое значение:
//  Структура - Результат расчета налога. Имена свойств соответствуют наименованиям регистров сведений, куда записывается
//  результат, а значения свойств - таблицы записей этих регистров (ТаблицаЗначений)
//
Функция ПолучитьРасчетПоНалогуНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт

	РезультатыРасчетаПоНалогу = Новый Структура;

	ПараметрыРасчета = ПараметрыРасчетаНалогаНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметры);
	
	РасчетИмущественныхНалоговПереопределяемый.ПередРасчетомНалогаНаИмущество(ПараметрыРасчета, ДополнительныеПараметры);
	
	Если ПараметрыРасчета.мДатаКонцаПериодаОтчета < НачалоПримененияНовогоРасчетаНалогаНаИмущество() Тогда
		РезультатыРасчетаПоНалогу.Вставить("РасчетНалогаНаИмущество", ПустаяСправкаРасчет("РасчетНалогаНаИмущество"));
	Иначе
		РезультатыРасчетаПоНалогу.Вставить("РасчетНалогаНаИмуществоПоСреднегодовойСтоимости", ПустаяСправкаРасчет("РасчетНалогаНаИмуществоПоСреднегодовойСтоимости"));		
		РезультатыРасчетаПоНалогу.Вставить("РасчетНалогаНаИмуществоПоКадастровойСтоимости", ПустаяСправкаРасчет("РасчетНалогаНаИмуществоПоКадастровойСтоимости"));
	КонецЕсли;

	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат РезультатыРасчетаПоНалогу;
	КонецЕсли;
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		ПараметрыРасчета.МенеджерВременныхТаблиц, 
		Организация, 
		ПериодРасчета, 
		"НалогНаИмущество");
	
	Если ПараметрыРасчета.мДатаКонцаПериодаОтчета < НачалоПримененияНовогоРасчетаНалогаНаИмущество() Тогда
		// По среднегодовой стоимости
		ТаблицаРасчетНалога = РезультатыРасчетаПоНалогу.РасчетНалогаНаИмущество;
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимостиДо2022(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимостиДо2022(ТаблицаРасчетНалога, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры);
		
		// По кадастровой стоимости
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимостиДо2022(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимостиДо2022(ТаблицаРасчетНалога, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры);
	
		ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.мДатаКонцаПериодаОтчета, "ПериодРасчета");
		ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");
	
	Иначе
		// По среднегодовой стоимости
		ТаблицаРасчетНалогаПоСреднегодовойСтоимости = РезультатыРасчетаПоНалогу.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости;
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимости(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимости(ТаблицаРасчетНалогаПоСреднегодовойСтоимости, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры);
		
		ТаблицаРасчетНалогаПоСреднегодовойСтоимости.ЗаполнитьЗначения(ПараметрыРасчета.мДатаКонцаПериодаОтчета, "ПериодРасчета");
		ТаблицаРасчетНалогаПоСреднегодовойСтоимости.ЗаполнитьЗначения(Организация, "Организация");
		
		// По кадастровой стоимости
		ТаблицаРасчетНалогаПоКадастровойСтоимости = РезультатыРасчетаПоНалогу.РасчетНалогаНаИмуществоПоКадастровойСтоимости;
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимости(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимости(ТаблицаРасчетНалогаПоКадастровойСтоимости, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры);
	
		ТаблицаРасчетНалогаПоКадастровойСтоимости.ЗаполнитьЗначения(ПараметрыРасчета.мДатаКонцаПериодаОтчета, "ПериодРасчета");
		ТаблицаРасчетНалогаПоКадастровойСтоимости.ЗаполнитьЗначения(Организация, "Организация");
	
	КонецЕсли;
	
	РасчетИмущественныхНалоговПереопределяемый.ПослеРасчетаНалогаНаИмущество(ПараметрыРасчета, ДополнительныеПараметры);
	
	Возврат РезультатыРасчетаПоНалогу;

КонецФункции

// Формирует временную таблицу, содержащую срез последних на дату ставок налога на имущество по отдельным ОС.
//
// Параметры:
//  ПараметрыРасчета - 	см. ПараметрыРасчетаНалогаНаИмущество 
//  Период - Дата - Дата, на которую нужно получить срез последних.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних"
//
Процедура СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(ПараметрыРасчета, Период, ДополнительныеПараметры = Неопределено, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Формирует временную таблицу, содержащую записи ставок налога на имущество по отдельным ОС до определенной даты.
//
// Параметры:
//  ПараметрыРасчета - 	см. ПараметрыРасчетаНалогаНаИмущество 
//  НачалоПериода - Дата - Дата, с которой нужно получить записи.
//  КонецПериода - Дата - Дата, до которой нужно получить записи.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи"
//
Процедура СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(ПараметрыРасчета, НачалоПериода, КонецПериода, ДополнительныеПараметры = Неопределено, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(
		ПараметрыРасчета, НачалоПериода, КонецПериода, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Формирует временную таблицу, содержащую срез последних на дату ставок налога на имущество.
//
// Параметры:
//  ПараметрыРасчета - 	см. ПараметрыРасчетаНалогаНаИмущество 
//  Период - Дата - Дата, на которую нужно получить срез последних.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "СтавкиНалогаНаИмущество_СрезПоследних"
//
Процедура СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(ПараметрыРасчета, Период, ДополнительныеПараметры = Неопределено, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Возвращает параметры расчета налога на имущество
//
// Параметры:
//  Организация				 - СправочникСсылка.Организации - организация, по которовый выполняется расчет налога на имущество
//  ПериодРасчета			 - Дата - окончание периода, за который рассчитывается налог
//  ДополнительныеПараметры	 - Структура, Неопределено - дополнительные параметры расчета
// 
// Возвращаемое значение:
//  Структура - параметры расчета налога 
//
Функция ПараметрыРасчетаНалогаНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт 

	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("Организации", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация));
	ПараметрыРасчета.Вставить("ИностраннаяОрганизация", РасчетИмущественныхНалоговПереопределяемый.ЭтоИностраннаяОрганизация(Организация, ПериодРасчета));
	ПараметрыРасчета.Вставить("ПериодРасчета", ПериодРасчета);
	ПараметрыРасчета.Вставить("ГодовойРасчет", Месяц(ПериодРасчета) = 12);
	ПараметрыРасчета.Вставить("мДатаНачалаПериодаОтчета", НачалоГода(ПериодРасчета));
	ПараметрыРасчета.Вставить("мДатаКонцаПериодаОтчета", КонецКвартала(ПериодРасчета));
	ПараметрыРасчета.Вставить("Делитель", Месяц(ПериодРасчета) + 1);
	
	СистемаНалогообложения = УчетнаяПолитика.СистемаНалогообложения(Организация, ПериодРасчета);
	ПараметрыРасчета.Вставить("ПрименениеОСНО", СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая);
	ПараметрыРасчета.Вставить("ПрименениеУСН", СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная);
	ПараметрыРасчета.Вставить("ПлательщикЕНВД", УчетнаяПолитика.ПлательщикЕНВД(Организация, ПериодРасчета));
	ПараметрыРасчета.Вставить("ПлательщикЕНВД_НаКонецКвартала", УчетнаяПолитика.ПлательщикЕНВД(Организация, КонецКвартала(ПериодРасчета)));
	
	ПараметрыРегистрацииВФНС = Новый Структура;
	ПараметрыРегистрацииВФНС.Вставить("ОсновнойНалоговыйОрган", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ПараметрыРегистрацииВФНС.Вставить("КодПоОКТМО", "");
	
	РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыРегистрацииОрганизацииВФНС(
		ПараметрыРегистрацииВФНС,
		Организация,
		ПараметрыРасчета.мДатаКонцаПериодаОтчета);
			
	ПараметрыРасчета.Вставить("ОсновнойНалоговыйОрган", ПараметрыРегистрацииВФНС.ОсновнойНалоговыйОрган);
	ПараметрыРасчета.Вставить("КодПоОКТМО", ПараметрыРегистрацииВФНС.КодПоОКТМО);
		
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	РасчетИмущественныхНалоговПереопределяемый.ДополнитьПараметрыРасчетаНалогаНаИмущество(
		ПараметрыРасчета, Организация, ПериодРасчета, ДополнительныеПараметры);
		
	Возврат ПараметрыРасчета;

КонецФункции

Функция НачалоПримененияНовогоРасчетаНалогаНаИмущество() Экспорт
	
	Возврат '20230101';
	
КонецФункции

#КонецОбласти

#Область ТранспортныйНалог

// Возвращает результат расчета транспортного налога (или аванса по налогу).
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
//  ПериодРасчета - Дата - Дата закрытия месяца.
//  ДополнительныеПараметры - Структура - произвольные параметры расчета.
// 
// Возвращаемое значение:
//  Структура - Содержит:
//		* Налог - ТаблицаЗначений - Результат расчета налога.
//		* Платон - ТаблицаЗначений - Данные по платону.
//
Функция ПолучитьРасчетПоТранспортномуНалогу(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт
			
	ПараметрыРасчета = ПараметрыРасчетаТранспортногоНалога(Организация, ПериодРасчета, ДополнительныеПараметры);
	
	РасчетИмущественныхНалоговПереопределяемый.ПередРасчетомТранспортногоНалога(ПараметрыРасчета, ДополнительныеПараметры);

	ТаблицаРасчетНалога = ПустаяСправкаРасчет("РасчетТранспортногоНалога");
	
	// Если для каких-то ТС в отчетном периоде отсутствуют ставки налога 
	// (не НалоговаяСтавка = 0, а именно отсутствует подходящая запись в регистре ставок),
	// то ниже отметим такие строки расчета.
	// Обработка таких ошибок выполняется вне данной процедуры.
	ТаблицаРасчетНалога.Колонки.Добавить("ОтсутствуетСтавка", Новый ОписаниеТипов("Булево"));
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат Новый Структура("Налог, Платон", ТаблицаРасчетНалога, Неопределено);
	КонецЕсли;
	
	ДанныеДляРасчета = ПолучитьДанныеДляРасчетаТранспортногоНалога(ПараметрыРасчета, ДополнительныеПараметры);
	РезультатРасчета = ВыполнитьРасчетТранспортногоНалога(ТаблицаРасчетНалога, ПараметрыРасчета, ДанныеДляРасчета, ДополнительныеПараметры);

	РасчетИмущественныхНалоговПереопределяемый.ПослеРасчетаТранспортногоНалога(ПараметрыРасчета, ДополнительныеПараметры);

	Возврат РезультатРасчета;

КонецФункции

// Возвращает результат авансового расчета транспортного налога.
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
//  ПериодРасчета - Дата - Дата закрытия месяца.
//  ДополнительныеПараметры - Структура - произвольные параметры расчета.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Авансовый расчет налога.
//
Функция ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, ПериодРасчета, ДополнительныеПараметры);
	
КонецФункции

// Формирует временную таблицу, содержащую срез последних на дату регистрации транспортных средств.
//
// Параметры:
//  ПараметрыРасчета - см. ПараметрыРасчетаТранспортногоНалога 
//  Период - Дата - Дата, на которую нужно получить срез последних.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "РегистрацияТранспортныхСредств_СрезПоследних"
//
Процедура СформироватьТаблицуРегистрацияТранспортныхСредствСрезПоследних(ПараметрыРасчета, Период, ДополнительныеПараметры = Неопределено, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуРегистрацияТранспортныхСредствСрезПоследних(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Формирует временную таблицу, содержащую записи регистрации транспортных средств.
//
// Параметры:
//  ПараметрыРасчета - см. ПараметрыРасчетаТранспортногоНалога 
//  Период - Дата - Дата, до которой нужно получить записи.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "РегистрацияТранспортныхСредств_Записи"
//
Процедура СформироватьТаблицуРегистрацияТранспортныхСредствЗаписи(ПараметрыРасчета, Период, ДополнительныеПараметры = Неопределено, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуРегистрацияТранспортныхСредствЗаписи(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Возвращает параметры расчета транспортного налога
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - организация, по которовый выполняется расчет налога.
//  ПериодРасчета - Дата - окончание периода, за который рассчитывается налог.
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры расчета.
// 
// Возвращаемое значение:
//  Структура - параметры расчета налога:
//  	* Организация - СправочникСсылка.Организации -
//  	* ПериодРасчета - Дата -
//  	* ДатаНачалаПериодаОтчета - Дата -
//  	* ДатаКонцаПериодаОтчета - Дата -
//  	* ГодовойРасчет - Булево -
//  	* МенеджерВременныхТаблиц - МенеджерВременныхТаблиц -
//
Функция ПараметрыРасчетаТранспортногоНалога(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт 

	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("ПериодРасчета", ПериодРасчета);
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	ПараметрыРасчета.Вставить("ГодовойРасчет", КонецКвартала(ПараметрыРасчета.ПериодРасчета) = КонецГода(ПараметрыРасчета.ПериодРасчета));
	
	ПараметрыРасчета.Вставить(
		"ДатаНачалаПериодаОтчета", 
		?(ПараметрыРасчета.ГодовойРасчет, НачалоГода(ПараметрыРасчета.ПериодРасчета), НачалоКвартала(ПараметрыРасчета.ПериодРасчета)));
		
	ПараметрыРасчета.Вставить("ДатаКонцаПериодаОтчета", КонецКвартала(ПараметрыРасчета.ПериодРасчета));
                      
	
	Возврат ПараметрыРасчета;

КонецФункции

// Возвращает коды видов и категории транспортных средств.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Коды видов и категорий транспортных средств.
Функция КодыВидовИКатегорииТранспортныхСредств() Экспорт
	Возврат РасчетИмущественныхНалоговПереопределяемый.КодыВидовИКатегорииТранспортныхСредств();
КонецФункции

#КонецОбласти

#Область ЗемельныйНалог

// Возвращает результат расчета земельного налога (или аванса по налогу).
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
//  ПериодРасчета - Дата - Дата закрытия месяца.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
// 
// Возвращаемое значение:
//  Структура - Результат расчета налога. Имена свойств соответствуют наименованиям регистров сведений, куда записывается
//  результат, а значения свойств - таблицы записей этих регистров (ТаблицаЗначений)
//
Функция ПолучитьРасчетПоЗемельномуНалогу(Организация, ПериодРасчета, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатыРасчетаПоНалогу = Новый Структура;
	
	ПараметрыРасчета = ПараметрыРасчетаЗемельногоНалога(Организация, ПериодРасчета, ДополнительныеПараметры);
	
	РасчетИмущественныхНалоговПереопределяемый.ПередРасчетомЗемельногоНалога(ПараметрыРасчета, ДополнительныеПараметры);

	Если ПараметрыРасчета.ДатаКонцаПериодаОтчета < РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаЗемельногоНалога() Тогда
		// Устаревший расчет оставлен для совместимости с периодами до 2020 года
		РезультатыРасчетаПоНалогу.Вставить("РасчетЗемельногоНалогаДо2020", ПустаяСправкаРасчет("РасчетЗемельногоНалогаДо2020"));
	Иначе
		РезультатыРасчетаПоНалогу.Вставить("РасчетЗемельногоНалога", ПустаяСправкаРасчет("РасчетЗемельногоНалога"));		
	КонецЕсли;

	ЭтоНеЮрЛицо = Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
		
	Если Месяц(ПериодРасчета)%3 <> 0 Или (ЭтоНеЮрЛицо И ПараметрыРасчета.ПериодРасчета >= '20150101') Тогда
		Возврат РезультатыРасчетаПоНалогу;
	КонецЕсли;

	Если ПараметрыРасчета.ДатаКонцаПериодаОтчета < РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаЗемельногоНалога() Тогда
		ТаблицаРасчетНалога = РезультатыРасчетаПоНалогу.РасчетЗемельногоНалогаДо2020;
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаЗемельногоНалогаДо2020(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетЗемельногоНалогаДо2020(
			ТаблицаРасчетНалога,
			ПараметрыРасчета,
			ДанныеДляРасчета,
			ДополнительныеПараметры);
	Иначе
		ТаблицаРасчетНалога = РезультатыРасчетаПоНалогу.РасчетЗемельногоНалога;
		ДанныеДляРасчета = ПолучитьДанныеДляРасчетаЗемельногоНалога(ПараметрыРасчета, ДополнительныеПараметры);
		ВыполнитьРасчетЗемельногоНалога(
			ТаблицаРасчетНалога,
			ПараметрыРасчета,
			ДанныеДляРасчета,
			ДополнительныеПараметры);
	КонецЕсли;

	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПериодРасчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");

	РасчетИмущественныхНалоговПереопределяемый.ПослеРасчетаЗемельногоНалога(ПараметрыРасчета, ДополнительныеПараметры);

	Возврат РезультатыРасчетаПоНалогу;
	
КонецФункции

// Формирует временную таблицу, содержащую срез последних на дату регистрации земельных участков.
//
// Параметры:
//  ПараметрыРасчета - см. ПараметрыРасчетаЗемельногоНалога 
//  Период - Дата - Дата, на которую нужно получить срез последних.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "РегистрацияЗемельныхУчастков_СрезПоследних".
//  УсловияВиртуальнойТаблицы - Строка - Условия виртуальной таблицы.
//
Процедура СформироватьТаблицуРегистрацияЗемельныхУчастковСрезПоследних(ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы = "", УсловияЗапроса = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуРегистрацияЗемельныхУчастковСрезПоследних(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы, УсловияЗапроса);
	
КонецПроцедуры

// Формирует временную таблицу, содержащую записи регистрации земельных участков до определенной даты.
//
// Параметры:
//  ПараметрыРасчета - см. ПараметрыРасчетаЗемельногоНалога 
//  Период - Дата - Дата, до которой нужно получить записи.
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры расчета.
//  ИмяТаблицы - Строка	- Имя временной таблицы, по умолчанию "РегистрацияЗемельныхУчастков_Записи"
//
Процедура СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи(ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы = "") Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи(
		ПараметрыРасчета, Период, ДополнительныеПараметры, ИмяТаблицы);
	
КонецПроцедуры

// Возвращает параметры расчета земельного налога
//
// Параметры:
//  Организация	- СправочникСсылка.Организации - организация, по которовый выполняется расчет налога.
//  ПериодРасчета - Дата - окончание периода, за который рассчитывается налог.
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры расчета.
// 
// Возвращаемое значение:
//  Структура - параметры расчета налога:
//  	* Организация - СправочникСсылка.Организации -
//  	* ПериодРасчета - Дата -
//  	* ДатаНачалаПериодаОтчета - Дата -
//  	* ДатаКонцаПериодаОтчета - Дата -
//  	* ГодовойРасчет - Булево -
//  	* МенеджерВременныхТаблиц - МенеджерВременныхТаблиц -
//
Функция ПараметрыРасчетаЗемельногоНалога(Организация, ПериодРасчета, ДополнительныеПараметры) Экспорт 

	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("ПериодРасчета", ПериодРасчета);
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	ПараметрыРасчета.Вставить("ДатаКонцаПериодаОтчета", КонецКвартала(ПараметрыРасчета.ПериодРасчета));
	ПараметрыРасчета.Вставить("ГодовойРасчет", КонецКвартала(ПараметрыРасчета.ПериодРасчета) = КонецГода(ПараметрыРасчета.ПериодРасчета));
	
	ПараметрыРасчета.Вставить(
		"ДатаНачалаПериодаОтчета", 
		?(ПараметрыРасчета.ГодовойРасчет, НачалоГода(ПараметрыРасчета.ПериодРасчета), НачалоКвартала(ПараметрыРасчета.ПериодРасчета)));
	
	Возврат ПараметрыРасчета;

КонецФункции

#КонецОбласти

#Область Прочее

Функция ОрганизацииУплачивающиеАвансы(Организация, Период, Налог, ОднаОрганизация = Истина) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ОрганизацииУплачивающиеАвансы(Организация, Период, Налог, ОднаОрганизация);
	
КонецФункции

// Возвращает таблицы выполнения регламентных операций расчета имущественных налогов.
//
// Параметры:
//  ТаблицаРеквизиты		 - ТаблицаЗначений	 - Таблица реквизитов документа.
//  Отказ					 - Булево			 - Флаг отказа проведения.
//  ДополнительныеПараметры	 - Структура		 - Дополнительные параметры.
// 
// Возвращаемое значение:
//  Структура - Таблицы расчета налога:
//  				* ТаблицаСправкиРасчета - ТаблицаЗначений - Справка-расчет налога.
//  				* ТаблицаРасходыПоНалогу - ТаблицаЗначений - Отражение расходов по налогу.
//
Функция ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ, ДополнительныеПараметры = Неопределено) Экспорт
	
	РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыОсвобожденияОтУплатыНалогов(ДополнительныеПараметры, ТаблицаРеквизиты);
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ, ДополнительныеПараметры);
	
КонецФункции

// Формирует движения регистров по данным расчета налога.
//
// Параметры:
//  ТаблицыНачислениеНалога	 - Структура - Содержит таблицы с результатами расчета налога.
//  ТаблицаРеквизитов		 - ТаблицаЗначений	 - Таблица реквизитов документа.
//  Движения				 - КоллекцияДвижений - Движения документа.
//  Отказ					 - Булево			 - Признак отказа от формирования движений.
//
Процедура СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ, ДополнительныеПараметры = Неопределено) Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ, ДополнительныеПараметры);
	
КонецПроцедуры

// Добавляет в состав параметров таблицу значений со структурой колонок для справки - расчета.
//
// Параметры:
//  Параметры				 - Структура	 - Содержит параметры формирования движений для справки-расчета.
//  Реквизиты				 - СтрокаТаблицыЗначений - Содержит поля:
//                                 * ВидНалога - ПеречислениеСсылка.ВидыИмущественныхНалогов - Вид налога
//  ТаблицаСправкиРасчета	 - ТаблицаЗначений		 - По структуре соответствует регистру сведений для соответствующего налога
//  		(РасчетТранспортногоНалога и др.).
//
Процедура ДополнитьПараметрыДвиженийСправкиРасчеты(Параметры, Реквизиты, ТаблицыСправокРасчетов) Экспорт
	
	ДополненныеСправкиРасчеты = Новый Структура;
	
	Для Каждого ТаблицаСправкаРасчет Из ТаблицыСправокРасчетов Цикл
		
		СписокОбязательныхКолонок = "";
	
		Если ТаблицаСправкаРасчет.Ключ = "РасчетНалогаНаИмущество" Тогда
			// До 2022 года используется общая таблица расчета налога по среднегодовой и по кадастровой стоимости
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"										// <Дата> - период расчета (конец квартала)
			+ "ДатаСведений,"										// <Дата>
			+ "Организация,"										// <СправочникСсылка.Организации> - организация
			+ "ВидНалоговойБазы,"									// <ПеречислениеСсылка.НалоговаяБазаПоНалогуНаИмущество>
			+ "ИФНС,"												// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "КодПоОКТМО,"											// <Строка, 11> 
			+ "КодВидаИмущества,"									// <Строка, 2>
			+ "ОсновноеСредство,"									// <СправочникСсылка.ОсновныеСредства>
			+ "ДоляВПравеОбщейСобственностиЧислитель,"				// <Число, 10, 0>
			+ "ДоляВПравеОбщейСобственностиЗнаменатель,"			// <Число, 10, 0>
			+ "ДоляПлощадиЧислитель,"								// <Число, 10, 0>
			+ "ДоляПлощадиЗнаменатель,"								// <Число, 10, 0>
			+ "ДоляСтоимостиЧислитель,"								// <Число, 10, 0>
			+ "ДоляСтоимостиЗнаменатель,"							// <Число, 10, 0>
			+ "КБК,"												// <Строка, 20>
			+ "КадастровыйНомер,"									// <Строка, 100>
			+ "КадастровыйНомерПомещения,"							// <Строка, 100>
			+ "УсловныйНомер,"										// <Строка, 100>
			+ "КадастроваяСтоимость,"								// <Число, 17, 2>
			+ "НеоблагаемаяКадастроваяСтоимость,"					// <Число, 17, 2>
			+ "ОстаточнаяСтоимостьНа0101,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0102,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0103,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0104,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0105,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0106,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0107,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0108,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0109,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0110,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0111,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0112,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа3112,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНедвижимости,"					// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьОсновныхСредств,"					// <Число, 15, 2>
			+ "СредняяСтоимость,"									// <Число, 15, 2>
			+ "НеоблагаемаяСредняяСтоимость,"						// <Число, 15, 2>
			+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"	// <Строка, 7>
			+ "НалоговаяБаза,"										// <Число, 17, 2>
			+ "ОснованиеЛьготы,"									// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "ПонижениеНалоговойСтавки,"							// <Булево>
			+ "НалоговаяСтавка,"									// <Число, 3, 2>
			+ "СуммаНалога,"										// <Число, 15, 2>
			+ "СуммаУменьшенияСуммыНалога," 						// <Число, 15, 2>
			+ "СуммаНалогаКУплате,"									// <Число, 15, 2>
			+ "СуммаАвансовыхПлатежей,"								// <Число, 15, 2>
			+ "КоличествоМесяцевВладения,"							// <Число, 2, 0>
			+ "КоличествоМесяцевИспользования,"						// <Число, 2, 0>
			+ "СуммаОсвобожденияОтУплатыНалога";					// <Число, 15, 2>
		
		ИначеЕсли ТаблицаСправкаРасчет.Ключ = "РасчетНалогаНаИмуществоПоСреднегодовойСтоимости" Тогда
		
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"										// <Дата> - период расчета (конец квартала)
			+ "ДатаСведений,"										// <Дата>
			+ "Организация,"										// <СправочникСсылка.Организации> - организация
			+ "ИФНС,"												// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "КодПоОКТМО,"											// <Строка, 11> 
			+ "КодВидаИмущества,"									// <Строка, 2>
			+ "ОсновноеСредство,"									// <СправочникСсылка.ОсновныеСредства>
			+ "ДоляСтоимостиЧислитель,"								// <Число, 10, 0>
			+ "ДоляСтоимостиЗнаменатель,"							// <Число, 10, 0>
			+ "КБК,"												// <Строка, 20>
			+ "КадастровыйНомер,"									// <Строка, 100>
			+ "КадастровыйНомерПомещения,"							// <Строка, 100>
			+ "УсловныйНомер,"										// <Строка, 100>
			+ "ОстаточнаяСтоимостьНа0101,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0102,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0103,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0104,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0105,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0106,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0107,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0108,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0109,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0110,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0111,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа0112,"							// <Число, 15, 2>
			+ "ОстаточнаяСтоимостьНа3112,"							// <Число, 15, 2>
			+ "СреднегодоваяСтоимость,"								// <Число, 15, 2>
			+ "НеоблагаемаяСреднегодоваяСтоимость,"					// <Число, 15, 2>
			+ "НалоговаяБаза,"										// <Число, 17, 2>
			+ "ОснованиеЛьготы,"									// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "НалоговаяСтавка,"									// <Число, 3, 2>
			+ "СуммаНалогаСУчетомНеоблагаемойБазы,"					// <Число, 15, 0>
			+ "СуммаУменьшенияСуммыНалога,"							// <Число, 15, 0>
			+ "СуммаНалога,"										// <Число, 15, 0>
			+ "СуммаНалогаКУплате,"									// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей";								// <Число, 15, 0>
			
		ИначеЕсли ТаблицаСправкаРасчет.Ключ = "РасчетНалогаНаИмуществоПоКадастровойСтоимости" Тогда 
		
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"										// <Дата> - период расчета (конец квартала)
			+ "Организация,"										// <СправочникСсылка.Организации> - организация
			+ "ИФНС,"												// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "КодПоОКТМО,"											// <Строка, 11> 
			+ "ОсновноеСредство,"									// <СправочникСсылка.ОсновныеСредства>
			+ "ДатаСведений,"										// <Дата>
			+ "КадастровыйНомер,"									// <Строка, 100>
			+ "КадастровыйНомерПомещения,"							// <Строка, 100>
			+ "КадастроваяСтоимость,"								// <Число, 17, 2>
			+ "КодВидаИмущества,"									// <Строка, 2>
			+ "НеоблагаемаяКадастроваяСтоимость,"					// <Число, 17, 2>
			+ "ДоляВПравеОбщейСобственностиЧислитель,"				// <Число, 10, 0>
			+ "ДоляВПравеОбщейСобственностиЗнаменатель,"			// <Число, 10, 0>
			+ "ДоляПлощадиЧислитель,"								// <Число, 10, 0>
			+ "ДоляПлощадиЗнаменатель,"								// <Число, 10, 0>
			+ "ДоляСтоимостиЧислитель,"								// <Число, 10, 0>
			+ "ДоляСтоимостиЗнаменатель,"							// <Число, 10, 0>
			+ "НалоговаяБаза,"										// <Число, 15, 0>
			+ "НалоговаяСтавка,"									// <Число, 3, 2>
			+ "ОснованиеЛьготы,"									// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "КоличествоМесяцевВладения,"							// <Число, 2, 0>
			+ "КоличествоМесяцевИспользования,"						// <Число, 2, 0>
			+ "СуммаНалогаБезЛьгот,"								// <Число, 15, 0>
			+ "СуммаЛьгот," 										// <Число, 15, 0>
			+ "СуммаНалогаСУчетомНеоблагаемойБазы,"					// <Число, 15, 0>
			+ "СуммаУменьшенияСуммыНалога," 						// <Число, 15, 0>
			+ "СуммаНалога,"										// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей,"								// <Число, 15, 0>
			+ "СуммаНалогаКУплате";									// <Число, 15, 0>
		
		ИначеЕсли ТаблицаСправкаРасчет.Ключ = "РасчетТранспортногоНалога" Тогда 
		
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"							// <Дата> - период расчета (конец квартала)
			+ "ДатаРегистрационныхДанных,"				// <Дата>
			+ "Организация,"							// <СправочникСсылка.Организации> - организация
			+ "ИФНС,"									// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "КодПоОКТМО,"								// <Строка, 11> 
			+ "ОсновноеСредство,"						// <СправочникСсылка.ОсновныеСредства>
			+ "КодВидаТранспортногоСредства,"			// <Строка, 100>
			+ "ИдентификационныйНомер,"					// <Строка, 50>	
			+ "Марка,"									// <Строка, 25>
			+ "РегистрационныйЗнак,"					// <Строка, 25>
			+ "НалоговаяБаза,"							// <Число, 15, 2>
			+ "ЕдиницаИзмерения,"						// <Строка, 5>
			+ "КоличествоМесяцевВладения,"				// <Число, 2, 0>
			+ "НалоговаяСтавка,"						// <Число, 3, 2>
			+ "СуммаНалога,"							// <Число, 15, 0>
			+ "КоличествоМесяцевЛьготы,"				// <Число, 2, 0>
			+ "КодНалоговойЛьготы,"						// <Строка, 6>
			+ "РегиональныйКодЛьготы,"					// <Строка, 12>
			+ "ПроцентУменьшения,"						// <Число, 4, 2>
			+ "ЛьготнаяСтавка,"							// <Число, 8, 2>
			+ "СуммаНалоговойЛьготы,"					// <Число, 15, 0>
			+ "СуммаНалогаКУплате,"						// <Число, 15, 0>
			+ "ЭкологическийКласс,"						// <Строка, 1>
			+ "ПовышающийКоэффициент,"					// <Число, 3, 1>
			+ "КоличествоЛетПрошедшихСГодаВыпускаТС,"	// <Число, 3, 0>
			+ "ДоляВПравеОбщейСобственностиЧислитель,"	// <Число, 10, 0>
			+ "ДоляВПравеОбщейСобственностиЗнаменатель,"// <Число, 10, 0>
			+ "СуммаАвансовыхПлатежей1Кв,"				// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей2Кв,"				// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей3Кв,"				// <Число, 15, 0>
			+ "ЗарегистрированоВРеестреСистемыПлатон,"	// <Булево>
			+ "СуммаНалоговогоВычета,"					// <Число, 15, 0>
			+ "ДатаРегистрации,"						// <Дата>
			+ "ДатаПрекращенияРегистрации,"				// <Дата>
			+ "СуммаОсвобожденияОтУплатыНалога";		// <Число, 15, 0>

		ИначеЕсли ТаблицаСправкаРасчет.Ключ = "РасчетЗемельногоНалога" Тогда 
			
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"												// <Дата> - период расчета (конец квартала)
			+ "Организация,"												// <СправочникСсылка.Организации> - организация
			+ "ОсновноеСредство,"											// <СправочникСсылка.ОсновныеСредства>
			+ "ДатаСведений,"												// <Дата>
			+ "КадастровыйНомер,"											// <Строка, 100>
			+ "КБК,"														// <Строка, 20>	
			+ "ИФНС,"														// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "КодПоОКТМО,"													// <Строка, 11> 
			+ "КодКатегорииЗемель,"											// <Строка, 12>
			+ "КадастроваяСтоимость,"										// <Число, 17, 2>
			+ "НеоблагаемаяКадастроваяСтоимость,"							// <Число, 17, 2>
			+ "ПовышающийКоэффициент,"										// <Число, 1, 0>
			+ "ДоляВПравеОбщейСобственностиЧислитель,"						// <Число, 10, 0>
			+ "ДоляВПравеОбщейСобственностиЗнаменатель,"					// <Число, 10, 0>
			+ "ОснованиеЛьготыПоНалоговойБазе,"								// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "ОснованиеЛьготыСнижениеСуммыНалога,"							// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "ОснованиеЛьготыСнижениеСтавки,"								// <СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам>
			+ "ПредставлениеНалоговойЛьготы," 								// <Строка, 250>
			+ "КоличествоМесяцевИспользования,"								// <Число, 2, 0>
			+ "НалоговаяСтавка,"											// <Число, 5, 4>
			+ "СуммаНалогаБезЛьгот,"										// <Число, 15, 0>
			+ "СуммаЛьгот,"													// <Число, 15, 0>
			+ "СуммаНалога,"												// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей,"										// <Число, 15, 0>
			+ "СуммаНалогаКУплате,"											// <Число, 15, 0>
			+ "ДатаРегистрации,"											// <Дата>
			+ "ДатаСнятияСУчета,"											// <Дата>
			+ "ДатаРегистрацииПравНаОбъектНедвижимости";					// <Дата>
		
		ИначеЕсли ТаблицаСправкаРасчет.Ключ = "РасчетЗемельногоНалогаДо2020" Тогда 
			// Устаревший расчет оставлен для совместимости с периодами до 2020 года
			СписокОбязательныхКолонок = СписокОбязательныхКолонок
			+ "ПериодРасчета,"												// <Дата> - период расчета (конец квартала)
			+ "Организация,"												// <СправочникСсылка.Организации> - организация
			+ "ИФНС,"														// <СправочникСсылка.РегистрацииВНалоговомОргане>
			+ "ОсновноеСредство,"											// <СправочникСсылка.ОсновныеСредства>
			+ "КадастровыйНомер,"											// <Строка, 100>
			+ "КБК,"														// <Строка, 20>	
			+ "КодПоОКТМО,"													// <Строка, 11> 
			+ "КодКатегорииЗемель,"											// <Строка, 12>
			+ "ПериодСтроительства,"										// <Строка, 1>	
			+ "КадастроваяСтоимость,"										// <Число, 17, 2>
			+ "ДоляВПравеОбщейСобственностиЧислитель,"						// <Число, 10, 0>
			+ "ДоляВПравеОбщейСобственностиЗнаменатель,"					// <Число, 10, 0>
			+ "УменьшениеНалоговойБазыНаСумму,"								// <Булево>
			+ "НеОблагаемаяНалогомСумма,"									// <Число, 17, 2>
			+ "УменьшениеНалоговойБазыПоСтатье391,"							// <Булево>
			+ "КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391," 		// <Строка, 7>
			+ "НалоговаяЛьготаПоНалоговойБазе," 							// <ПеречислениеСсылка.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогуДо2020>
			+ "ДоляНеоблагаемойПлощадиЧислитель,"							// <Число, 10, 0>
			+ "ДоляНеоблагаемойПлощадиЗнаменатель,"							// <Число, 10, 0>
			+ "НалоговаяБаза,"												// <Число, 17, 2>
			+ "КоличествоМесяцевВладения,"									// <Число, 2, 0>
			+ "КоличествоМесяцевИспользования,"								// <Число, 2, 0>
			+ "КоличествоМесяцевПримененияЛьготы,"							// <Число, 2, 0>	
			+ "НалоговаяСтавка,"											// <Число, 3, 2>
			+ "СуммаНалога,"												// <Число, 15, 0>
			+ "КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395," // <Строка, 7>	
			+ "СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога,"				// <Число, 15, 0>
			+ "СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки,"			// <Число, 15, 0>
			+ "ПредставлениеНалоговойЛьготы," 								// <Строка, 250>
			+ "СниженнаяНалоговаяСтавка," 									// <Число, 5, 4>
			+ "ПроцентУменьшенияСуммыНалога," 								// <Число, 7, 4>
			+ "СуммаНалогаКУплате,"											// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей1Кв,"									// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей2Кв,"									// <Число, 15, 0>
			+ "СуммаАвансовыхПлатежей3Кв,"									// <Число, 15, 0>
			+ "СуммаОсвобожденияОтУплатыНалога";							// <Число, 15, 0>
			
		КонецЕсли;
	
		РасчетИмущественныхНалоговПереопределяемый.ДополнитьПараметрыДвиженийСправкаРасчет(Реквизиты, ТаблицаСправкаРасчет.Ключ, СписокОбязательныхКолонок);
		
		ДополненныеСправкиРасчеты.Вставить(
			ТаблицаСправкаРасчет.Ключ,
			ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
				ТаблицаСправкаРасчет.Значение, СписокОбязательныхКолонок));
	
	КонецЦикла;
	
	Параметры.Вставить("ТаблицыСправокРасчетов", ДополненныеСправкиРасчеты);
	
КонецПроцедуры

// Возвращает результат расчета среднегодовой стоимости движмого имущества.
//
// Параметры:
//  Организации		- Массив, Фиксированный массив элементов типа СправочникСсылка.Организации - 
//					  Организация и ее подразделения на выделенном балансе, 
//					  по которым нужно рассчитать среднегодовую стоимость движимого имущества .
//  ДатаНачала		- Дата - Начало отчетного периода (года).
//  ДатаОкончания	- Дата - Окончание отчетного периода (года).
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат расчета среднегодовой стоимости имущества.
//
Функция ДанныеПоСреднейСтоимостиДвижимогоИмущества(Организации, ДатаНачала, ДатаОкончания) Экспорт 
	
	Период = ДатаНачала;
	Счетчик = 0;
	
	ПараметрыРасчета = ПараметрыРасчетаНалогаНаИмущество(Организации[0], ДатаОкончания);
	ПараметрыРасчета.Вставить("Организации", Организации);
	
	РасчетИмущественныхНалоговПереопределяемый.ДополнитьПараметрыРасчетаСтоимостиДвижимогоИмущества(
		ПараметрыРасчета, 
		Организации[0], 
		ДатаОкончания);
	
	РасчетИмущественныхНалоговПереопределяемый.ПередРасчетомНалогаНаИмущество(ПараметрыРасчета, Неопределено);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организации", Организации);
	
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	ТекстОбъединяющегоЗапроса = "";
	
	Пока Период <= НачалоМесяца(ДатаОкончания) Цикл
		
		Период = ДобавитьМесяц(ДатаНачала, Счетчик);
		Счетчик = Счетчик + 1;
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН=");
		ИспользуемыеВременныеТаблицы.Добавить(ИмяВременнойТаблицы);
		
		РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуОССостоящиеНаУчете(
			ПараметрыРасчета, 
			?(Год(Период) = Год(ДатаОкончания), Период, ДатаОкончания));
		
		СписокЗапросов = Новый Массив;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ СписокРазрешенныхОС
		|ИЗ
		|	ВТ_ОССостоящиеНаУчете КАК ОС
		|ГДЕ
		|	НЕ ОС.НедвижимоеИмущество
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСписокОС(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСчетаБухгалтерскогоУчета(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСтоимостьИАмортизация(Период, ПараметрыРасчета, СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаУчетОсновныхСредств(Период, ПараметрыРасчета, СписокЗапросов);
		
		ШаблонЗапроса = 
		"ВЫБРАТЬ
		|	УчетОсновныхСредств.Организация КАК Организация,
		|	УчетОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	&Счетчик КАК Период,
		|
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(ВЫБОР
		|				КОГДА УчетОсновныхСредств.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
		|						ИЛИ УчетОсновныхСредств.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
		|					ТОГДА АмортизацияОС.СуммаОстатокДт
		|				ИНАЧЕ АмортизацияОС.СуммаОстатокКт
		|			КОНЕЦ, 0) - ЕСТЬNULL(ОбесценениеОС.СуммаОстатокКт, 0) КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость,
		|
		|	УчетОсновныхСредств.КодРегиона КАК Регион
		|
		|ПОМЕСТИТЬ ИмяВременнойТаблицы
		|
		|ИЗ
		|	УчетОсновныхСредств КАК УчетОсновныхСредств
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьОС КАК ПервоначальнаяСтоимостьОС
		|		ПО УчетОсновныхСредств.Организация = ПервоначальнаяСтоимостьОС.Организация
		|			И УчетОсновныхСредств.ОсновноеСредство = ПервоначальнаяСтоимостьОС.ОсновноеСредство
		|			И (УчетОсновныхСредств.СчетУчета = ПервоначальнаяСтоимостьОС.Счет
		|				ИЛИ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) = ПервоначальнаяСтоимостьОС.Счет)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК АмортизацияОС
		|		ПО УчетОсновныхСредств.Организация = АмортизацияОС.Организация
		|			И УчетОсновныхСредств.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
		|			И УчетОсновныхСредств.СчетНачисленияАмортизации = АмортизацияОС.Счет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбесценениеОС КАК ОбесценениеОС
		|		ПО УчетОсновныхСредств.Организация = ОбесценениеОС.Организация
		|			И УчетОсновныхСредств.ОсновноеСредство = ОбесценениеОС.ОсновноеСредство
		|			И УчетОсновныхСредств.СчетОбесценения = ОбесценениеОС.Счет";
							
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&Счетчик", Счетчик); // значение Счетчик от 1 до 13
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		
		// На последнем цикле Период равен 1 января следующего года после налогового периода.
		// Поэтому вместо этого выбираем данные на конец налогового периода.
		Запрос.УстановитьПараметр("Период", ?(Год(Период) = Год(ДатаОкончания), Период, ДатаОкончания));
		Запрос.УстановитьПараметр("ПериодГраница", ?(Год(Период) = Год(ДатаОкончания),
			Новый Граница(Период, ВидГраницы.Исключая), // выборка на момент начала месяца
			Новый Граница(ДатаОкончания, ВидГраницы.Включая))); // выборка на конец периода включая проводки регламентных операций на 23:59:59 31.12
			
		РасчетИмущественныхНалоговПереопределяемый.ДополнитьПараметрыЗапросаДвижимогоИмущества(Запрос, Период);
			
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ ДанныеДляРасчета
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
		                            |ГДЕ
		                            |	ОстаточнаяСтоимость > 0
									|";
		
	КонецЦикла;
	
	Если ТекстОбъединяющегоЗапроса = "" Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляРасчета.Организация КАК Организация,
	|	ДанныеДляРасчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДляРасчета.ОсновноеСредство) КАК ПредставлениеОС,
	|	ДанныеДляРасчета.Регион КАК Регион,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 1
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0101,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 2
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0102,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 3
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0103,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 4
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0104,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 5
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0105,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 6
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0106,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 7
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0107,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 8
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0108,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 9
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0109,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 10
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0110,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 11
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0111,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 12
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0112,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеДляРасчета.Период = 13
	|				ТОГДА ДанныеДляРасчета.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа3112,
	|	СУММА(ДанныеДляРасчета.ОстаточнаяСтоимость) / 13 КАК СреднегодоваяСтоимость
	|
	|ИЗ
	|	ДанныеДляРасчета КАК ДанныеДляРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляРасчета.Организация,
	|	ДанныеДляРасчета.ОсновноеСредство,
	|	ДанныеДляРасчета.Регион
	|
	|УПОРЯДОЧИТЬ ПО
	|	СреднегодоваяСтоимость УБЫВ";
	
	Результат = Запрос.Выполнить();
		
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выгрузить();
	
КонецФункции	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НалогНаИмущество

Процедура УстановитьПараметрыЗапросаДляРасчетаНалогаНаИмущество(Запрос, ПараметрыРасчета) Экспорт

	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	
	// Некоторые запросы переиспользуются для расчета средней стоимости движимого имущества.
	// Для этого требуется передавать массив организаций (для движимого имущества - 
	// головную и ее обособленные подразделения на выделенном балансе). 
	// Но здесь расчет налога выполняется по каждой организации (в т.ч. подразделениям на выделенном балансе) отдельно.
	// Поэтому параметр устанавливается только для совместимости (массив из одной организации).
	Запрос.УстановитьПараметр("Организации", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыРасчета.Организация));
	
	Запрос.УстановитьПараметр("Период", ПараметрыРасчета.мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Год", Год(ПараметрыРасчета.ПериодРасчета));
	Запрос.УстановитьПараметр("ТолькоНедвижимость", ПараметрыРасчета.ПериодРасчета >= '20190101');
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРасчета.ОсновнойНалоговыйОрган);
	Запрос.УстановитьПараметр("КодПоОКТМО", ПараметрыРасчета.КодПоОКТМО);
	Запрос.УстановитьПараметр("ПрименениеУСН", ПараметрыРасчета.ПрименениеУСН);
	
	НачалоПериода = ?(ПараметрыРасчета.ГодовойРасчет Или ПараметрыРасчета.ПериодРасчета < '20190101', 
						НачалоГода(ПараметрыРасчета.ПериодРасчета), 
						НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(ПараметрыРасчета.ПериодРасчета));
	
КонецПроцедуры

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимости(ПараметрыРасчета, ДополнительныеПараметры)
	
	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	УстановитьПараметрыЗапросаДляРасчетаНалогаНаИмущество(Запрос, ПараметрыРасчета);
			
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(
		ПараметрыРасчета,
		'00010101',
		КонецМесяца(КонецМесяца(ПараметрыРасчета.мДатаКонцаПериодаОтчета)+1),
		ДополнительныеПараметры);
		
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Период = ПараметрыРасчета.мДатаНачалаПериодаОтчета;
	Счетчик = 0;
	
	ТекстОбъединяющегоЗапроса = "";
	Пока Период <= НачалоМесяца(ПараметрыРасчета.мДатаКонцаПериодаОтчета) Цикл
		
		Период = ДобавитьМесяц(ПараметрыРасчета.мДатаНачалаПериодаОтчета, Счетчик);
		ПериодДляВыборкиДанных = ?(Год(Период) = Год(ПараметрыРасчета.ПериодРасчета), Период, ПараметрыРасчета.мДатаКонцаПериодаОтчета);
		
		Счетчик = Счетчик + 1;
		
		Если УчетнаяПолитика.ПрименяетсяУСН(ПараметрыРасчета.Организация, ПериодДляВыборкиДанных) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН=");
		ИспользуемыеВременныеТаблицы.Добавить(ИмяВременнойТаблицы);
		
		СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
			ПараметрыРасчета,
			ПериодДляВыборкиДанных,
			ДополнительныеПараметры,
			"СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца");
		
		СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(
			ПараметрыРасчета,
			ПериодДляВыборкиДанных,
			ДополнительныеПараметры);
		
		РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуОССостоящиеНаУчете(
			ПараметрыРасчета, 
			ПериодДляВыборкиДанных, 
			ДополнительныеПараметры);
		
		СписокЗапросов = Новый Массив;
		
		// Срез последних записей по основному средству в регистре "Ставки налога на имущество по отдельным основным средствам".
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ПериодСреза
		|
		|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	ПериодСреза
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ОСИсключаемыеИзРасчетаПоСреднейСтоимости
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов КАК СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ОсновноеСредство
		|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ПериодСреза
		|
		|ГДЕ
		|	(СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ СписокРазрешенныхОС
		|
		|ИЗ
		|	ВТ_ОССостоящиеНаУчете КАК ОС
		|
		|ГДЕ
		|
		|	ОС.НедвижимоеИмущество
		|
		|	И НЕ ОС.ГруппаОС В (
		|		ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), 
		|		ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
		|
		|	И НЕ ОС.ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					ОСИсключаемыеИзРасчетаПоСреднейСтоимости.ОсновноеСредство
		|				ИЗ
		|					ОСИсключаемыеИзРасчетаПоСреднейСтоимости)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСписокОС(СписокЗапросов);
		
		// На начало каждого месяца определяем параметры расчета налога по объекту (основному средству). В набор таких параметров
		// входит ставка, код ОКТМО по месту регистрации, код вида имущества, КБК, льгота и т.п. Рассчитанная далее остаточная 
		// стоимость объекта будет привязана именно к текущему набору параметров расчета налога. Если в каком-то другом месяце
		// налогового периода действующий набор характеристик объекта будет иной, то остаточная стоимость объекта в том месяце
		// не будет учтена при расчете среднегодовой стоимости ОС для текущего набора харакетеристик (остаточная стоимость принимается за 0). 
		// В отношении периода действия льгот подход следующий: месяц начала и месяц окончания применения льготы считаются месяцами,
		// когда льгота отсутствует (абц. 5 п. 8 ст. 382 НК). При этом льготу, у которой начало периода применения установлено на 1 число месяца
		// (окончание периода установлено на последнее число месяца) считаем действующей в данном месяце. Настройки, на основании которых вычисляется
		// применение льготы в конкретном месяце, также считываем на начало этого месяца.
		// Для записи отдельных расчетов по одному ОС (за разные периоды в рамках одного отчетного или налогового периода)
		// используется дополнительное измерение ДатаСведений - это дата, с которой применяется соответствующий набор параметров
		// расчета налога (см. РС РасчетНалогаНаИмуществоПоСреднегодовойСтоимости). Стоит заметить, что даты начала и окончания периода применения льготы
		// также являются "граничными": даже при отсутствии отдельной записи в регистре сведений эти значения, взятые из действующей записи регистра,
		// влияют на расчет льготы в каждом из последующих периодах по-разному, и поэтому должны быть учтены при расчете значения ДатаСведений
		// (см. вспомогательное поле ДатаСведенийЛьготы).
		// Для определения фактически применяющегося в конкретном месяце набора параметров могут использоваться как настройки по конкретному ОС
		// (РС СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам) - место регистрации, льгота, ставка и т.п., - так и общие настройки по организации
		// (РС СтавкиНалогаНаИМущество) - ставка, льгота. Поэтому при расчете ДатаСведений и ДатаСведенийЛьготы учитываем, какие записи этих регистров
		// фактически повлияли на значения применеямых параметров расчета.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ПериодАктуальныхНастроекПоОС
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
		|		(ВЫБРАТЬ
		|			СписокОС.ОсновноеСредство
		|		ИЗ
		|			СписокОС КАК СписокОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
		|
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган КАК НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|	
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы > СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
		|				И СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы > 
		|					НАЧАЛОПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы > СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.Период > 
		|				НАЧАЛОПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период, МЕСЯЦ, 1), МЕСЯЦ)
		|		ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
		|	КОНЕЦ КАК НачалоДействияЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы
		|	КОНЕЦ КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы <= &НачалоМесяца
		|		И (СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СуммаУменьшения КАК СуммаУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаПоОтдельнымОССрезПоследних
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган КАК НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота КАК ДействуетЛьгота,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной)) КАК ОблагаетсяПоСниженнойСтавке,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение) КАК ОсвобождениеОтНалогообложения,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент) КАК УменьшениеСуммыНалогаНаПроцент,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму) КАК УменьшениеСуммыНалогаНаСумму,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.РегиональнаяЛьгота КАК РегиональнаяЛьгота,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы КАК ВидЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодЛьготы КАК КодЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПроцентУменьшения КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СуммаУменьшения КАК СуммаУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ИЗ
		|	ВТ_СтавкиНалогаПоОтдельнымОССрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодАктуальныхНастроекПоОС КАК ПериодАктуальныхНастроекПоОС
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ПериодАктуальныхНастроекПоОС.ОсновноеСредство
		|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период = ПериодАктуальныхНастроекПоОС.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмущество.Период КАК Период,
		|	СтавкиНалогаНаИмущество.Организация КАК Организация,
		|	СтавкиНалогаНаИмущество.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмущество.НачалоДействияЛьготы > СтавкиНалогаНаИмущество.Период
		|				И СтавкиНалогаНаИмущество.НачалоДействияЛьготы > 
		|					НАЧАЛОПЕРИОДА(СтавкиНалогаНаИмущество.НачалоДействияЛьготы, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмущество.НачалоДействияЛьготы, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА СтавкиНалогаНаИмущество.НачалоДействияЛьготы > СтавкиНалогаНаИмущество.Период
		|			ТОГДА СтавкиНалогаНаИмущество.НачалоДействияЛьготы
		|		ИНАЧЕ СтавкиНалогаНаИмущество.Период
		|	КОНЕЦ КАК НачалоДействияЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы
		|	КОНЕЦ КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И СтавкиНалогаНаИмущество.НачалоДействияЛьготы <= &НачалоМесяца
		|		И (СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
		|
		|ИЗ
		|	СтавкиНалогаНаИмущество_СрезПоследних КАК СтавкиНалогаНаИмущество";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСчетаБухгалтерскогоУчета(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСтоимостьИАмортизация(Период, ПараметрыРасчета, СписокЗапросов, ДополнительныеПараметры);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаДанныеБухУчетаОС(СписокЗапросов);
					
		ШаблонЗапроса = 
		"ВЫБРАТЬ
		|	СписокОС.ОсновноеСредство,
		|
		// ДатаСведений
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке, ЛОЖЬ)
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.Период
		|		КОГДА ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане, ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПустаяСсылка)) =
		|				ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.Период
		|		КОГДА НЕ СтавкиНалогаПоОтдельнымОС.Период ЕСТЬ NULL
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.Период
		|		КОГДА НЕ СтавкиНалогаНаИмуществоСрезПоследних.Период ЕСТЬ NULL
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.Период
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ДатаСведений,
		|
		// РегистрацияВНалоговомОргане
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговыйОрган
		|		ИНАЧЕ 
		|			&ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|
		// КодПоОКТМО
		|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодПоОКТМО, &КодПоОКТМО) КАК КодПоОКТМО,
		|
		// НалоговаяСтавка
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.СниженнаяНалоговаяСтавка
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговаяСтавка
		|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
		|	КОНЕЦ КАК НалоговаяСтавка,
		|
		// ОснованиеЛьготы
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.ОснованиеЛьготы
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы 
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка) 
		|	КОНЕЦ КАК ОснованиеЛьготы,
		|
		// ДатаСведенийЛьготы
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.НачалоДействияЛьготы
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы 
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ОкончаниеДействияЛьготы > СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы
		|				И СтавкиНалогаПоОтдельнымОС.ОкончаниеДействияЛьготы <= &КонецМесяца
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.ОкончаниеДействияЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы <= &КонецМесяца
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы
		|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаСведенийЛьготы,
		|
		// СнижениеНалоговойСтавки
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота 
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ЛОЖЬ
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы В (
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент),
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной))
		|			ТОГДА ИСТИНА 
		|		ИНАЧЕ ЛОЖЬ 
		|	КОНЕЦ КАК СнижениеНалоговойСтавки,
		|
		// ОсвобождениеОтНалогообложения
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.ОсвобождениеОтНалогообложения
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ЛОЖЬ
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|			ТОГДА ИСТИНА 
		|		ИНАЧЕ ЛОЖЬ 
		|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
		|
		// УменьшениеСуммыНалогаНаПроцент
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ЛОЖЬ
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			ТОГДА ИСТИНА 
		|		ИНАЧЕ ЛОЖЬ 
		|	КОНЕЦ КАК УменьшениеСуммыНалогаНаПроцент,
		|
		// ПроцентУменьшенияСуммыНалога
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.ПроцентУменьшения
		|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА 0
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
		|
		// УменьшениеСуммыНалогаНаСумму
		|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму, ЛОЖЬ) КАК УменьшениеСуммыНалогаНаСумму,
		|
		// СуммаУменьшения
		|	ВЫБОР
		|		КОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму
		|			ТОГДА СтавкиНалогаПоОтдельнымОС.СуммаУменьшения
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаУменьшения,
		|
		|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
		|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаПоОтдельнымОС.КБК КАК КБК,
		|	&Счетчик КАК Период,
		|
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ДанныеБухУчетаОС.ПервоначальнаяСтоимость, 0) 
		|				- ЕСТЬNULL(ВЫБОР 
		|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
		|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
		|									ТОГДА ДанныеБухУчетаОС.АмортизацияДт 
		|								ИНАЧЕ ДанныеБухУчетаОС.АмортизацияКт 
		|							КОНЕЦ, 0) - ЕСТЬNULL(ДанныеБухУчетаОС.Обесценение, 0) КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
		|
		|ПОМЕСТИТЬ ИмяВременнойТаблицы
		|
		|ИЗ
		|	СписокОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеБухУчетаОС КАК ДанныеБухУчетаОС
		|		ПО СписокОС.ОсновноеСредство = ДанныеБухУчетаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС КАК СтавкиНалогаПоОтдельнымОС
		|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС.ОсновноеСредство,
		|
		|		ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
								
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&Счетчик", Счетчик); // значение Счетчик от 1 до 13
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		
		// При расчете налога за год на последнем цикле Период равен 1 января следующего года.
		// В этом случае подменяем период: выбираем данные на конец налогового периода.
		Запрос.УстановитьПараметр("Период", ПериодДляВыборкиДанных);
		Запрос.УстановитьПараметр("ПериодГраница", ?(Год(Период) = Год(ПараметрыРасчета.ПериодРасчета),
			Новый Граница(Период, ВидГраницы.Исключая), // выборка на момент начала месяца
			Новый Граница(ПараметрыРасчета.мДатаКонцаПериодаОтчета, ВидГраницы.Включая))); // выборка на конец периода включая проводки регламентных операций на 23:59:59 31.12
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПериодДляВыборкиДанных));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодДляВыборкиДанных));
				
		РасчетИмущественныхНалоговПереопределяемый.УстановитьПараметрыЗапросаПриРасчетеНалогаЗаПериод(
			Запрос,
			ПараметрыРасчета.Организация,
			Период);
		
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ НалоговаяБазаПоНалогуНаИмущество
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
									|";
		
	КонецЦикла;
	
	Если ТекстОбъединяющегоЗапроса = "" Тогда
		// Удалим все таблицы, которые были созданы во время выполнения процедуры.
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
		Возврат Неопределено;
	КонецЕсли;

	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("КонечныйПериод", ПараметрыРасчета.Делитель);

	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
		ПараметрыРасчета,
		ПараметрыРасчета.мДатаКонцаПериодаОтчета,
		ДополнительныеПараметры);
	
	// УсловныйНомер, КадастровыйНомер и КадастровыйНомерПомещения определяются из настроек, действующих на конец отчетного периода.
	// ДатаСведений - это самая ранняя дата, с которой применяется конкретный набор параметров расчета налога по объекту.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
	|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|		ИНАЧЕ &КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО,
	|
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи
	|
	|ИЗ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
	|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК Период
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период
	|
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета
	|
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период КАК ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|		ПО ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
	|			И ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.Период = СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|			И ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.КодПоОКТМО = СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговаяБазаПоНалогуНаИмущество.ДатаСведений КАК ДатаСведений,
	|	НалоговаяБазаПоНалогуНаИмущество.ДатаСведенийЛьготы КАК ДатаСведенийЛьготы,
	|	НалоговаяБазаПоНалогуНаИмущество.Период КАК Период,
	|	НалоговаяБазаПоНалогуНаИмущество.КодВидаИмущества КАК КодВидаИмущества,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЧислитель, 0) КАК ДоляСтоимостиЧислитель,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЗнаменатель, 0) КАК ДоляСтоимостиЗнаменатель,
	|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.УсловныйНомер, """") КАК УсловныйНомер,
	|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомер, """") КАК КадастровыйНомер,
	|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомерПомещения, """") КАК КадастровыйНомерПомещения,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КБК, """") КАК КБК,
	|	НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане КАК ИФНС,
	|	НалоговаяБазаПоНалогуНаИмущество.КодПоОКТМО КАК КодПоОКТМО,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
	|	НалоговаяБазаПоНалогуНаИмущество.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	НалоговаяБазаПоНалогуНаИмущество.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	НалоговаяБазаПоНалогуНаИмущество.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	НалоговаяБазаПоНалогуНаИмущество.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	НалоговаяБазаПоНалогуНаИмущество.СуммаУменьшения КАК СуммаУменьшения,
	|	НалоговаяБазаПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
	|	НЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL КАК УплачиваютсяАвансы,
	|	НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость
	|
	|ПОМЕСТИТЬ ВТ_НалоговаяБаза
	|
	|ИЗ
	|	НалоговаяБазаПоНалогуНаИмущество КАК НалоговаяБазаПоНалогуНаИмущество
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета КАК СтавкиНалогаПоОтдельнымОС_НаДатуРасчета
	|		ПО НалоговаяБазаПоНалогуНаИмущество.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.ОсновноеСредство
	|			И НалоговаяБазаПоНалогуНаИмущество.КодПоОКТМО = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КодПоОКТМО
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ВТ_НалоговаяБаза.ДатаСведенийЛьготы > ВТ_НалоговаяБаза.ДатаСведений
	|			ТОГДА ВТ_НалоговаяБаза.ДатаСведенийЛьготы
	|		ИНАЧЕ ВТ_НалоговаяБаза.ДатаСведений
	|	КОНЕЦ) КАК ДатаСведений,
	|	ВТ_НалоговаяБаза.КодВидаИмущества КАК КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС КАК ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	ВТ_НалоговаяБаза.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	ВТ_НалоговаяБаза.СуммаУменьшения КАК СуммаУменьшения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер КАК УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК КАК КБК,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 1
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0101,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 2
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0102,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 3
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0103,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 4
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0104,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 5
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0105,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 6
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0106,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 7
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0107,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 8
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0108,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 9
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0109,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 10
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0110,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 11
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0111,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 12
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0112,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 13
	|					И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа3112
	|
	|ИЗ
	|	ВТ_НалоговаяБаза КАК ВТ_НалоговаяБаза
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НалоговаяБаза.КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ОснованиеЛьготы,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаПроцент,
	|	ВТ_НалоговаяБаза.ПроцентУменьшенияСуммыНалога,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаСумму,
	|	ВТ_НалоговаяБаза.СуммаУменьшения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК";
	
	Результат = Запрос.Выполнить();
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Процедура ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимости(ТаблицаРасчетНалога, РасчетНалога, ПараметрыРасчета, ДополнительныеПараметры)

	Если РасчетНалога = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	КолонкиГруппировок = ""
	+ "УплачиваютсяАвансы,"
	+ "ИФНС,"
	+ "КодПоОКТМО,"
	+ "КодВидаИмущества,"
	+ "КБК,"
	+ "ДоляСтоимостиЧислитель,"
	+ "ДоляСтоимостиЗнаменатель,"
	+ "НалоговаяСтавка,"
	+ "ОснованиеЛьготы,"
	+ "ПонижениеНалоговойСтавки,"
	+ "ОсвобождениеОтНалогообложения,"
	+ "УменьшениеСуммыНалогаНаПроцент,"
	+ "ПроцентУменьшенияСуммыНалога,"
	+ "УменьшениеСуммыНалогаНаСумму,"
	+ "СуммаУменьшения";
	
	КолонкиСуммирования = ""
	+ "ОстаточнаяСтоимостьНа0101,"
	+ "ОстаточнаяСтоимостьНа0102,"
	+ "ОстаточнаяСтоимостьНа0103,"
	+ "ОстаточнаяСтоимостьНа0104,"
	+ "ОстаточнаяСтоимостьНа0105,"
	+ "ОстаточнаяСтоимостьНа0106,"
	+ "ОстаточнаяСтоимостьНа0107,"
	+ "ОстаточнаяСтоимостьНа0108,"
	+ "ОстаточнаяСтоимостьНа0109,"
	+ "ОстаточнаяСтоимостьНа0110,"
	+ "ОстаточнаяСтоимостьНа0111,"
	+ "ОстаточнаяСтоимостьНа0112,"
	+ "ОстаточнаяСтоимостьНа3112";
	
	КолонкиРасчета = "" 
	+ "СреднегодоваяСтоимость,"
	+ "НеоблагаемаяСреднегодоваяСтоимость,"
	+ "НалоговаяБаза,"
	+ "СуммаНалогаСУчетомНеоблагаемойБазы,"
	+ "СуммаУменьшенияСуммыНалога,"
	+ "СуммаНалога,"
	+ "СуммаАвансовыхПлатежей1Кв,"
	+ "СуммаАвансовыхПлатежей2Кв,"
	+ "СуммаАвансовыхПлатежей3Кв,"
	+ "СуммаНалогаКУплате";
	
	РасчетНалогаПоЛистам = РасчетНалога.Скопировать();
	РасчетНалогаПоЛистам.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	ДопКолонки = Новый Структура(КолонкиРасчета);
	Для каждого Колонка Из ДопКолонки Цикл
		// Суммы по листам округлены до целых, т.к. декларация заполняется в целых рублях
		РасчетНалогаПоЛистам.Колонки.Добавить(Колонка.Ключ,
			ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный));
	КонецЦикла;

	// С целью оптимизации производительности список имён полей-периодов формируется заранее, а не динамически в циклах.
	КолонкиПериоды = Новый ТаблицаЗначений;
	КолонкиПериоды.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
	КолонкиПериоды.Колонки.Добавить("ОстаточнаяСтоимостьИмя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(25, ДопустимаяДлина.Фиксированная)));
	Для Счетчик = 1 По ПараметрыРасчета.Делитель Цикл
		
		КолонкаПериод = КолонкиПериоды.Добавить();
		КолонкаПериод.Счетчик = Счетчик;
		КолонкаПериод.ОстаточнаяСтоимостьИмя = "ОстаточнаяСтоимостьНа" + ?(Счетчик = 13, "3112", "01" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	КонецЦикла;

	Отбор = Новый Структура(КолонкиГруппировок);
	РасчетНалогаПоЛистам.Индексы.Добавить(КолонкиГруппировок);
	
	КэшУчетнойПолитики = Новый Соответствие;

	Для Каждого СтрокаТаблицы Из РасчетНалогаПоЛистам Цикл
		
		Если Не ПараметрыРасчета.ГодовойРасчет И Не СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПараметрыРасчета.ГодовойРасчет И (СтрокаТаблицы.КодВидаИмущества = "04" Или СтрокаТаблицы.КодВидаИмущества = "07") Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
			
		ДоляСтоимости = 1;
		
		КодВидаИмущества = ?(ПустаяСтрока(СтрокаТаблицы.КодВидаИмущества), "03", СтрокаТаблицы.КодВидаИмущества);
		Если КодВидаИмущества = "04" Тогда
			ДоляСтоимости = 0;
		ИначеЕсли КодВидаИмущества = "02" Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;
		КонецЕсли;
		
		ДелимоеСреднегодоваяСтоимость = 0;
		ДелимоеНеоблагаемаяСреднегодоваяСтоимость = 0;
		Для каждого КолонкаПериод Из КолонкиПериоды Цикл
			Счетчик = КолонкаПериод.Счетчик;
			
			СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя]);
			ДелимоеСреднегодоваяСтоимость = ДелимоеСреднегодоваяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			
			Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
				ДелимоеНеоблагаемаяСреднегодоваяСтоимость = ДелимоеНеоблагаемаяСреднегодоваяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			КонецЕсли;
			
			Месяц = Счетчик - 1;
			
			// В годовом расчете учитываются суммы авансовых платежей, если организация платит авансы в текущей ИФНС.
			Если УчитыватьАвансы И Месяц <> 0 И Месяц % 3 = 0 Тогда
				
				ПериодУчетнойПолитики = Дата(Год(ПараметрыРасчета.ПериодРасчета), Счетчик - 3, 1); // начало отчетного квартала
				НастройкиУчетнойПолитики = КэшУчетнойПолитики[ПериодУчетнойПолитики];
				
				Если НастройкиУчетнойПолитики = Неопределено Тогда
					ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(ПараметрыРасчета.Организация, ПериодУчетнойПолитики);
					КэшУчетнойПолитики.Вставить(ПериодУчетнойПолитики, Новый Структура("ПрименяетсяУСН", ПрименяетсяУСН));
				Иначе
					ПрименяетсяУСН = НастройкиУчетнойПолитики.ПрименяетсяУСН;
				КонецЕсли;	
				
				СреднегодоваяСтоимость = ?(ПрименяетсяУСН, 0, ДелимоеСреднегодоваяСтоимость / Счетчик);
				НеоблагаемаяСреднегодоваяСтоимость = ?(ПрименяетсяУСН, 0, ДелимоеНеоблагаемаяСреднегодоваяСтоимость / Счетчик);
				
				Если Месяц = 3 Тогда
					СреднегодоваяСтоимость1Кв = СреднегодоваяСтоимость;
					НеоблагаемаяСреднегодоваяСтоимость1Кв = НеоблагаемаяСреднегодоваяСтоимость;
				ИначеЕсли Месяц = 6 Тогда
					СреднегодоваяСтоимость2Кв = СреднегодоваяСтоимость;
					НеоблагаемаяСреднегодоваяСтоимость2Кв = НеоблагаемаяСреднегодоваяСтоимость;
				ИначеЕсли Месяц = 9 Тогда
					СреднегодоваяСтоимость3Кв = СреднегодоваяСтоимость;
					НеоблагаемаяСреднегодоваяСтоимость3Кв = НеоблагаемаяСреднегодоваяСтоимость;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Ко всем колонкам расчета неявно применяется округление до целых, т.к. формат этих чисел (15,0) - см. выше 
		СтрокаТаблицы.СреднегодоваяСтоимость = ДелимоеСреднегодоваяСтоимость / ПараметрыРасчета.Делитель;
		СтрокаТаблицы.НеоблагаемаяСреднегодоваяСтоимость = ДелимоеНеоблагаемаяСреднегодоваяСтоимость / ПараметрыРасчета.Делитель;
		
		СтоимостьОблагаемогоНалогомИмущества = Макс(СтрокаТаблицы.СреднегодоваяСтоимость - СтрокаТаблицы.НеоблагаемаяСреднегодоваяСтоимость, 0);
		
		Если УчитыватьАвансы Тогда
			СтоимостьОблагаемогоНалогомИмущества1Кв = Макс(СреднегодоваяСтоимость1Кв - НеоблагаемаяСреднегодоваяСтоимость1Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества2Кв = Макс(СреднегодоваяСтоимость2Кв - НеоблагаемаяСреднегодоваяСтоимость2Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества3Кв = Макс(СреднегодоваяСтоимость3Кв - НеоблагаемаяСреднегодоваяСтоимость3Кв, 0);
		КонецЕсли;
		
		СтрокаТаблицы.НалоговаяБаза = СтоимостьОблагаемогоНалогомИмущества * ДоляСтоимости;
		
		Если ПараметрыРасчета.ГодовойРасчет Тогда
			СтрокаТаблицы.СуммаНалогаСУчетомНеоблагаемойБазы = СтрокаТаблицы.НалоговаяБаза * СтрокаТаблицы.НалоговаяСтавка / 100;
		Иначе
			// При расчете аванса не используется промежуточный показатель НалоговаяБаза, т.к. последний используется
			// только для заполнения декларации за год
			СтрокаТаблицы.СуммаНалогаСУчетомНеоблагаемойБазы = СтоимостьОблагаемогоНалогомИмущества * ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4;	
		КонецЕсли;
		
		СтрокаТаблицы.СуммаНалога = СтрокаТаблицы.СуммаНалогаСУчетомНеоблагаемойБазы; // ниже сумма налога может быть уменьшена на сумму льгот
			
		Если УчитыватьАвансы Тогда
			
			СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = СтоимостьОблагаемогоНалогомИмущества1Кв 
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4;
			СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = СтоимостьОблагаемогоНалогомИмущества2Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4;
			СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = СтоимостьОблагаемогоНалогомИмущества3Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4;
				
		КонецЕсли;
		
		Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
			
			СуммаЛьготы = Окр(СтрокаТаблицы.СуммаНалогаСУчетомНеоблагаемойБазы * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			СтрокаТаблицы.СуммаНалога = СтрокаТаблицы.СуммаНалога - СуммаЛьготы;
			СтрокаТаблицы.СуммаУменьшенияСуммыНалога = СуммаЛьготы;
			
			Если УчитыватьАвансы Тогда
				СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаТаблицы.СуммаНалогаКУплате = СтрокаТаблицы.СуммаНалога; // сумма авансов вычитается ниже после распределения сумм по объектам
		
		// Распределим полученные суммы по объектам ОС
		
		// Суммы по объектам округлены до сотых, т.к. эти данные отражены только в справке-расчете. При этом суммарно
		// они дают целые значения на листах декларации (см. выше расчет по листам).
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		РасчетНалогаПоОбъектамОС = РасчетНалога.Скопировать(Отбор);
		РасчетНалогаПоОбъектамОС.Сортировать("ОсновноеСредство", Новый СравнениеЗначений);
		
		КоэффициентыРаспределения = Новый Массив;
		КоэффициентыРаспределения1Кв = Новый Массив;
		КоэффициентыРаспределения2Кв = Новый Массив;
		КоэффициентыРаспределения3Кв = Новый Массив;
		
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			ДелимоеСреднегодоваяСтоимость = 0;
			Для каждого КолонкаПериод Из КолонкиПериоды Цикл
				Счетчик = КолонкаПериод.Счетчик;
				
				СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя], 2);
				ДелимоеСреднегодоваяСтоимость = ДелимоеСреднегодоваяСтоимость + СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя];
				
				Если УчитыватьАвансы Тогда
					Если Счетчик = 4 Тогда
						СреднегодоваяСтоимость1Кв = Окр(ДелимоеСреднегодоваяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 7 Тогда
						СреднегодоваяСтоимость2Кв = Окр(ДелимоеСреднегодоваяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 10 Тогда
						СреднегодоваяСтоимость3Кв = Окр(ДелимоеСреднегодоваяСтоимость / Счетчик, 2);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			СреднегодоваяСтоимость = Окр(ДелимоеСреднегодоваяСтоимость / ПараметрыРасчета.Делитель, 2);
			
			КоэффициентыРаспределения.Добавить(СреднегодоваяСтоимость);
			
			Если УчитыватьАвансы Тогда
				КоэффициентыРаспределения1Кв.Добавить(СреднегодоваяСтоимость1Кв);
				КоэффициентыРаспределения2Кв.Добавить(СреднегодоваяСтоимость2Кв);
				КоэффициентыРаспределения3Кв.Добавить(СреднегодоваяСтоимость3Кв);
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого Колонка Из ДопКолонки Цикл
			
			Если РасчетНалогаПоОбъектамОС.Колонки.Найти(Колонка.Ключ) = Неопределено Тогда
				РасчетНалогаПоОбъектамОС.Колонки.Добавить(Колонка.Ключ,
					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2, ДопустимыйЗнак.Неотрицательный));
			КонецЕсли;
			
			Если Колонка.Ключ = "СуммаАвансовыхПлатежей1Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения1Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей2Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения2Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей3Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения3Кв;
			Иначе
				Коэффициенты = КоэффициентыРаспределения;
			КонецЕсли;
			
			Если СтрокаТаблицы[Колонка.Ключ] > 0 Тогда
				РаспределенныеСуммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
										СтрокаТаблицы[Колонка.Ключ], Коэффициенты);
				Если РаспределенныеСуммы <> Неопределено Тогда
					РасчетНалогаПоОбъектамОС.ЗагрузитьКолонку(РаспределенныеСуммы, Колонка.Ключ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			// Уменьшение налога на сумму устанавливается по каждому отдельному объекту,
			// поэтому применяем этот вид льготы уже после распределения по объектам
			Если СтрокаПоОбъектуОС.УменьшениеСуммыНалогаНаСумму Тогда
				
				// Так как это сумма уменьшения за весь год, то в расчете за квартальные периоды нужно взять 1/4. 
				Если ПараметрыРасчета.ГодовойРасчет Тогда
					СуммаЛьготы = СтрокаПоОбъектуОС.СуммаУменьшения;
				Иначе	
					СуммаЛьготы = Окр(СтрокаПоОбъектуОС.СуммаУменьшения / 4);
				КонецЕсли;
				
				СтрокаПоОбъектуОС.СуммаНалога = СтрокаПоОбъектуОС.СуммаНалога - СуммаЛьготы;
				СтрокаПоОбъектуОС.СуммаНалогаКУплате = СтрокаПоОбъектуОС.СуммаНалога;
				СтрокаПоОбъектуОС.СуммаУменьшенияСуммыНалога = СтрокаПоОбъектуОС.СуммаУменьшенияСуммыНалога + СуммаЛьготы;
				
				Если УчитыватьАвансы Тогда
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
						- СуммаЛьготы / 4;
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
						- СуммаЛьготы / 4;
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв
						- СуммаЛьготы / 4;
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			НоваяСтрока.КодВидаИмущества = КодВидаИмущества;
			
			СвойстваПоСтрокеТаблицы = ""
			+ "ИФНС,"
			+ "КодПоОКТМО,"
			+ "НалоговаяСтавка,"
			+ "ОснованиеЛьготы,"
			+ "КБК";
			
			Если КодВидаИмущества = "02" Тогда
				СвойстваПоСтрокеТаблицы = СвойстваПоСтрокеТаблицы + ","
				+ "ДоляСтоимостиЧислитель,"
				+ "ДоляСтоимостиЗнаменатель";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, СвойстваПоСтрокеТаблицы);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПоОбъектуОС, ,СвойстваПоСтрокеТаблицы);
			
			Если УчитыватьАвансы Тогда
				
				НоваяСтрока.СуммаАвансовыхПлатежей = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
				
			КонецЕсли;
			
			Если Не ПараметрыРасчета.ГодовойРасчет Тогда
				НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимости(ПараметрыРасчета, ДополнительныеПараметры)

	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	ДанныеДляРасчета = Неопределено;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	УстановитьПараметрыЗапросаДляРасчетаНалогаНаИмущество(Запрос, ПараметрыРасчета);
		
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(
		ПараметрыРасчета,
		'00010101',
		'00010101',
		ДополнительныеПараметры);
			
	ГодРасчета = Год(ПараметрыРасчета.ПериодРасчета);
	
	Если ПараметрыРасчета.ГодовойРасчет Тогда
		ПервыйМесяцРасчета = 1;
	Иначе
		ПервыйМесяцРасчета = Месяц(НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
	КонецЕсли;
	
	ВременныеТаблицыНаВремяРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	ТекстОбъединяющегоЗапроса = "";
	
	// Каждый отдельный месяц периода соотносится только с каким-то одним набором параметров расчета налога по объекту недвижимости.
	// Это обусловлено тем, что коэффициенты владения (Квл) и использования (Ки), которые характеризуют период действия 
	// каждого такого отдельного набора параметров, также исчисляются до целых месяцев.
	// В качестве момента фиксации берем 15 число месяца, т.к. именно оно определено как граница при определении Квл и Ки.
	// (см. пп. 5, 5.1 ст. 382 НК)
	// В отношении периода действия льгот подход немного другой: месяц начала и месяц окончания применения льготы считаются месяцами,
	// когда льгота отсутствует (абц. 5 п. 8 ст. 382 НК). При этом льготу, у которой начало периода применения установлено на 1 число месяца
	// (окончание периода установлено на последнее число месяца) считаем действующей в данном месяце. 
	// Настройки, на основании которых вычисляется применение льготы в конкретном месяце, также считываем на 15 число этого месяца. 
	Для Сч = ПервыйМесяцРасчета По Месяц(ПараметрыРасчета.ПериодРасчета) Цикл
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Сч, "ЧЦ=2; ЧВН=");
		ВременныеТаблицыНаВремяРасчета.Добавить(ИмяВременнойТаблицы);
		ПредельнаяДатаВозникновенияПрава = Дата(ГодРасчета, Сч, 15, 23, 59, 59);
		
		СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
			ПараметрыРасчета,
			ПредельнаяДатаВозникновенияПрава,
			ДополнительныеПараметры);
						
		СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(
			ПараметрыРасчета,
			ПредельнаяДатаВозникновенияПрава,
			ДополнительныеПараметры);
		
		// При получении действующих настроек расчета налога предполагаем, что по конкретному основному средству в конкретный
		// период времени может применяться только один набор параметров. То есть, если в РС СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		// по одному ОС введены несколько записей с разным ОКТМО, то действующей считается последняя по времени.
		// Исключение - объекты, находящиеся одновременно в нескольких регионах (несколько записей с разными ОКТМО и разными
		// долями в каждом регионе РФ). Но в этом случае преполагаем, что записи вводятся с одинаковым периодом. Поэтому
		// в данной ситуации в расчет попадут несколько записей с разным ОКТМО за наиболее актуальную дату (см. ДатаСведений).
		
		// ПРИМЕЧАНИЕ
		// Льготы по "кадастровым" объектам применяются в следующей последовательности (см. 4-й запрос пакета)
		// 1. Освобождение, установленное для конкретного объекта
		// 2. Освобождение, установленное для всех объектов (в общих настройках по организации) - обычно это федеральная льгота по НК (действует во всех регионах).
		// 3. Любая другая льгота, установленная для конкретного объекта
		// 4. Общая льгота по организации в виде снижения суммы налога на процент, если объект зарегистрирован по месту нахождения организации (региональная льгота).
		
		// Такая иерархия обусловлена тем, что при наличии освобождения другие льготы не имеют смысла. В то же время код льготы в виде освобождения,
		// установленный для конкретного объекта (например, находящегося в другом регионе), считается более надежным, чем общая льгота.
		// Кроме освобождения для каждого конкретного объекта может быть установлена своя льгота, например, в виде снижения ставки или суммы налога.
		// В этом случае учитывается именно такая льгота.
		// Наконец, в целом по организации может применяться снижение суммы налога на процент. Но только для объектов, зарегистрированных в головной организации.
		// Здесь речь идет исключительно о региональной льготе, поэтому некорректно ее применять к объектам, находящимся вне места расположения организации.
		
		// Для определения фактически применяющегося в конкретном месяце набора параметров могут использоваться как настройки по конкретному ОС
		// (РС СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам) - место регистрации, льгота, ставка и т.п., - так и общие настройки по организации
		// (РС СтавкиНалогаНаИМущество) в части применяемой льготы. Поэтому при расчете ДатаСведенийЛьготы учитываем, какие записи этих регистров
		// фактически повлияли на значение льготы.
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ДатаСведений,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|	
		|ПОМЕСТИТЬ ВТ_ПериодПримененияНастроекПоКадастровойСтоимости
		|	
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаРегистрацииПраваСобственности <= &Период
		|	И (СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности > &Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений КАК ДатаСведений,
		|
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы > ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений
		|				И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы > 
		|					НАЧАЛОПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы > ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы
		|		КОГДА ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений > 
		|				НАЧАЛОПЕРИОДА(ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений, МЕСЯЦ, 1), МЕСЯЦ)
		|		ИНАЧЕ ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений
		|	КОНЕЦ КАК НачалоДействияЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы
		|	КОНЕЦ КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы <= &НачалоМесяца
		|		И (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СуммаУменьшения КАК СуммаУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности КАК ДатаРегистрацииПраваСобственности,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель
		|
		|ПОМЕСТИТЬ ВТ_ДействующиеНастройкиПоОтдельнымОсновнымСредствам
		|
		|ИЗ
		|	ВТ_ПериодПримененияНастроекПоКадастровойСтоимости КАК ВТ_ПериодПримененияНастроекПоКадастровойСтоимости
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|		ПО ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ДатаСведений = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период
		|			И ВТ_ПериодПримененияНастроекПоКадастровойСтоимости.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
		|
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоСрезПоследних.Период КАК Период,
		|	СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы > СтавкиНалогаНаИмуществоСрезПоследних.Период
		|				И СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы > 
		|					НАЧАЛОПЕРИОДА(СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы > СтавкиНалогаНаИмуществоСрезПоследних.Период
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы
		|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.Период
		|	КОНЕЦ КАК НачалоДействияЛьготы,
		|
		|	ВЫБОР 
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы
		|	КОНЕЦ КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы <= &НачалоМесяца
		|		И (СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
		|
		|ИЗ
		|	СтавкиНалогаНаИмущество_СрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДатаСведений КАК ДатаСведений,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ИФНС КАК ИФНС,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КодПоОКТМО КАК КодПоОКТМО,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
		|
		|	1 КАК КоличествоМесяцевИспользования,
		|	ВЫБОР
		|		КОГДА &НомерМесяца <= 3
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв1,
		|	ВЫБОР
		|		КОГДА &НомерМесяца > 3 И &НомерМесяца <= 6
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв2,
		|	ВЫБОР
		|		КОГДА &НомерМесяца > 6 И &НомерМесяца <= 9
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв3,
		|
		|	ВЫБОР
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОснованиеЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОснованиеЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|				И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ОснованиеЛьготы,
		|
		|	ВЫБОР
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|				И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы > СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы
		|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы <= &КонецМесяца
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы <= &КонецМесяца
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы
		|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаСведенийЛьготы,
		|
		|	ВЫБОР
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|				ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|				ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевЛьготы,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|			И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|		ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение) КАК ОсвобождениеОтНалогообложения,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|		И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной)) КАК ПонижениеНалоговойСтавки,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|
		|	ВЫБОР
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной))
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.СниженнаяНалоговаяСтавка
		|		ИНАЧЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НалоговаяСтавка
		|	КОНЕЦ КАК НалоговаяСтавка,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|			И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|		ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане) КАК УменьшениеСуммыНалогаНаПроцент,
		|
		|	ВЫБОР
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПроцентУменьшения
		|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА 0
		|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
		|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
		|		И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму) КАК УменьшениеСуммыНалогаНаСумму,
		|
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.СуммаУменьшения КАК СуммаУменьшения,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КодВидаИмущества КАК КодВидаИмущества,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
		|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель
		|ПОМЕСТИТЬ ИмяВременнойТаблицы
		|ИЗ
		|	ВТ_ДействующиеНастройкиПоОтдельнымОсновнымСредствам КАК ДействующиеНастройкиПоОтдельнымОсновнымСредствам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних
		|		ПО (ИСТИНА)";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		Запрос.Текст = ТекстЗапроса;
		
		ПараметрыРегистрацииВФНС = Новый Структура;
		ПараметрыРегистрацииВФНС.Вставить("ОсновнойНалоговыйОрган", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
		ПараметрыРегистрацииВФНС.Вставить("КодПоОКТМО", "");
	
		РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыРегистрацииОрганизацииВФНС(
			ПараметрыРегистрацииВФНС,
			ПараметрыРасчета.Организация,
			ПредельнаяДатаВозникновенияПрава);
			
		Запрос.УстановитьПараметр("Период", ПредельнаяДатаВозникновенияПрава);
		Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРегистрацииВФНС.ОсновнойНалоговыйОрган);
		Запрос.УстановитьПараметр("КодПоОКТМО", ПараметрыРегистрацииВФНС.КодПоОКТМО);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПредельнаяДатаВозникновенияПрава));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПредельнаяДатаВозникновенияПрава));
		Запрос.УстановитьПараметр("НомерМесяца", Сч);
				
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ВременныеТаблицыНаВремяРасчета);
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ РасчетПоКадастровойСтоимости
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
								  |";
				
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	// Получаем итоговые данные за отчетный период.
	//
	// 1. ВТ_ДанныеРасчета
	// Группируем данные по всем полям, которые составляют набор сведений для каждого отдельного расчета (по сути, речь идет
	// об отдельном листе расчета налога в декларации, даже если сама декларация по кадастровым объектам не подается).
	// При этом нужно понимать, что каждый такой набор отличается как минимум полем ДатаСведений или ДатаСведенийЛьготы.
	// Поэтому в дальнейшем при записи данных в РС РасчетНалогаНаИмуществоПоКадастровойСтоимости дублей не возникнет.
	// Теоретически могут быть несколько наборов, которые отличаются только полем ДатаСведений (пользователь ввел несколько
	// одинаковых записей по одному ОС в РС СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам). В этом случае берем самый
	// ранний период в качестве ДатаСведений.
	// 
	// 2. ВТ_ПериодВладения 
	// Период владения - это количество месяцев, когда в течение периода конкретное ОС было учтено по конкретному коду ОКТМО.
	// Это период получаем суммированием периодов действия отдельных наборов параметров расчета.
	//
	// 3. Итоговый запрос
	// Определяем, в каких ИФНС уплачиваются авансы
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР
	|		КОГДА РасчетПоКадастровойСтоимости.ДатаСведенийЛьготы > РасчетПоКадастровойСтоимости.ДатаСведений
	|			ТОГДА РасчетПоКадастровойСтоимости.ДатаСведенийЛьготы
	|		ИНАЧЕ РасчетПоКадастровойСтоимости.ДатаСведений
	|	КОНЕЦ) КАК ДатаСведений,
	|	РасчетПоКадастровойСтоимости.ИФНС КАК ИФНС,
	|	РасчетПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	РасчетПоКадастровойСтоимости.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РасчетПоКадастровойСтоимости.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользования) КАК КоличествоМесяцевИспользования,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв1) КАК КоличествоМесяцевИспользованияКв1,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв2) КАК КоличествоМесяцевИспользованияКв2,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв3) КАК КоличествоМесяцевИспользованияКв3,
	|	РасчетПоКадастровойСтоимости.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевЛьготы) КАК КоличествоМесяцевЛьготы,
	|	РасчетПоКадастровойСтоимости.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	РасчетПоКадастровойСтоимости.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	РасчетПоКадастровойСтоимости.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	РасчетПоКадастровойСтоимости.СуммаУменьшения КАК СуммаУменьшения,
	|	РасчетПоКадастровойСтоимости.КодВидаИмущества КАК КодВидаИмущества,
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель
	|ПОМЕСТИТЬ ВТ_ДанныеРасчета
	|ИЗ
	|	РасчетПоКадастровойСтоимости КАК РасчетПоКадастровойСтоимости
	|СГРУППИРОВАТЬ ПО
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЧислитель,
	|	РасчетПоКадастровойСтоимости.ИФНС,
	|	РасчетПоКадастровойСтоимости.КадастроваяСтоимость,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомер,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомерПомещения,
	|	РасчетПоКадастровойСтоимости.КодВидаИмущества,
	|	РасчетПоКадастровойСтоимости.КодПоОКТМО,
	|	РасчетПоКадастровойСтоимости.НалоговаяСтавка,
	|	РасчетПоКадастровойСтоимости.НеоблагаемаяКадастроваяСтоимость,
	|	РасчетПоКадастровойСтоимости.ОсвобождениеОтНалогообложения,
	|	РасчетПоКадастровойСтоимости.ОснованиеЛьготы,
	|	РасчетПоКадастровойСтоимости.ОсновноеСредство,
	|	РасчетПоКадастровойСтоимости.ПонижениеНалоговойСтавки,
	|	РасчетПоКадастровойСтоимости.ПроцентУменьшенияСуммыНалога,
	|	РасчетПоКадастровойСтоимости.СуммаУменьшения,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаПроцент,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаСумму
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетПоКадастровойСтоимости.ИФНС КАК ИФНС,
	|	РасчетПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользования) КАК КоличествоМесяцевВладения,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв1) КАК КоличествоМесяцевВладенияКв1,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв2) КАК КоличествоМесяцевВладенияКв2,
	|	СУММА(РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв3) КАК КоличествоМесяцевВладенияКв3
	|ПОМЕСТИТЬ ВТ_ПериодВладения
	|ИЗ
	|	РасчетПоКадастровойСтоимости КАК РасчетПоКадастровойСтоимости
	|СГРУППИРОВАТЬ ПО
	|	РасчетПоКадастровойСтоимости.ИФНС,
	|	РасчетПоКадастровойСтоимости.КодПоОКТМО,
	|	РасчетПоКадастровойСтоимости.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ИФНС,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетПоКадастровойСтоимости.ДатаСведений КАК ДатаСведений,
	|	РасчетПоКадастровойСтоимости.ИФНС КАК ИФНС,
	|	РасчетПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетПоКадастровойСтоимости.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	РасчетПоКадастровойСтоимости.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РасчетПоКадастровойСтоимости.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
	|	ВТ_ПериодВладения.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	ВТ_ПериодВладения.КоличествоМесяцевВладенияКв1 КАК КоличествоМесяцевВладенияКв1,
	|	ВТ_ПериодВладения.КоличествоМесяцевВладенияКв2 КАК КоличествоМесяцевВладенияКв2,
	|	ВТ_ПериодВладения.КоличествоМесяцевВладенияКв3 КАК КоличествоМесяцевВладенияКв3,
	|	РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв1 КАК КоличествоМесяцевИспользованияКв1,
	|	РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв2 КАК КоличествоМесяцевИспользованияКв2,
	|	РасчетПоКадастровойСтоимости.КоличествоМесяцевИспользованияКв3 КАК КоличествоМесяцевИспользованияКв3,
	|	РасчетПоКадастровойСтоимости.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	РасчетПоКадастровойСтоимости.КоличествоМесяцевЛьготы КАК КоличествоМесяцевЛьготы,
	|	РасчетПоКадастровойСтоимости.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	РасчетПоКадастровойСтоимости.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	РасчетПоКадастровойСтоимости.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	РасчетПоКадастровойСтоимости.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	РасчетПоКадастровойСтоимости.СуммаУменьшения КАК СуммаУменьшения,
	|	РасчетПоКадастровойСтоимости.КодВидаИмущества КАК КодВидаИмущества,
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
	|	РасчетПоКадастровойСтоимости.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель,
	|	НЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_ДанныеРасчета КАК РасчетПоКадастровойСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодВладения КАК ВТ_ПериодВладения
	|		ПО РасчетПоКадастровойСтоимости.ОсновноеСредство = ВТ_ПериодВладения.ОсновноеСредство
	|		И РасчетПоКадастровойСтоимости.ИФНС = ВТ_ПериодВладения.ИФНС
	|		И РасчетПоКадастровойСтоимости.КодПоОКТМО = ВТ_ПериодВладения.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО РасчетПоКадастровойСтоимости.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";	
	
	ДанныеДляРасчета = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
		СтрокаРасчета.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
	КонецЦикла;
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Процедура ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимости(ТаблицаРасчетНалога, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры)

	Если ДанныеДляРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГодРасчета = Год(ПараметрыРасчета.ПериодРасчета);
	КоличествоМесяцевВПериоде = ?(ПараметрыРасчета.ГодовойРасчет, Месяц(ПараметрыРасчета.ПериодРасчета), 3);
	
	Для Каждого СтрокаТаблицы Из ДанныеДляРасчета Цикл
		
		Если Не ПараметрыРасчета.ГодовойРасчет И Не СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
		
		НоваяСтрока = ТаблицаРасчетНалога.Добавить(); 
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
			// Расчет используется для заполнения декларации. А в декларации суммы округляются до целых.
			НоваяСтрока.КадастроваяСтоимость = Окр(НоваяСтрока.КадастроваяСтоимость);
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = Окр(НоваяСтрока.НеоблагаемаяКадастроваяСтоимость);
		КонецЕсли;
		
		Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость;
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляСтоимостиЧислитель > 0 И СтрокаТаблицы.ДоляСтоимостиЗнаменатель > 0 Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;	
		Иначе
			ДоляСтоимости = 1;
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель > 0 И СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель > 0 Тогда
			ДоляВПравеОбщейСобственности = 
				СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель / СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель;
		Иначе
			ДоляВПравеОбщейСобственности = 1;
		КонецЕсли;
		
		// Рассчитаем налоговую базу с учетом необлагаемой стоимости и доли в праве собственности - требуется для декларации.
		// Неокругленная налоговая база используется в расчете авансов ниже.
		НалоговаяБаза = Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
			* ДоляСтоимости * ДоляВПравеОбщейСобственности;
		НоваяСтрока.НалоговаяБаза = Окр(НалоговаяБаза);
		
		// Рассчитаем сумму налога без учета льгот (в т.ч. без учета необлагаемой кадастровой стоимости) - требуется для сверки
		// с ФНС по сообщению об исчисленных суммах налогов
		НоваяСтрока.СуммаНалогаБезЛьгот = СуммаНалогаПоКадастровойСтоимости(
			НоваяСтрока.КадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
			НоваяСтрока.НалоговаяСтавка,
			КоличествоМесяцевВПериоде,
			НоваяСтрока.КоличествоМесяцевВладения,
			НоваяСтрока.КоличествоМесяцевИспользования,
			ПараметрыРасчета.ГодовойРасчет,
			ГодРасчета);
		
		// В декларации отдельно указывается сумма налога за налоговый период, которая рассчитана с учетом уменьшения
		// налоговой базы (в т.ч. освобождения от налога), но которая не учитывает льгот, применяемых к рассчитанной сумме налога.
		Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
			НоваяСтрока.СуммаНалогаСУчетомНеоблагаемойБазы = СуммаНалогаПоКадастровойСтоимости(
				НоваяСтрока.НалоговаяБаза,
				НоваяСтрока.НалоговаяСтавка,
				КоличествоМесяцевВПериоде,
				НоваяСтрока.КоличествоМесяцевВладения,
				НоваяСтрока.КоличествоМесяцевИспользования,
				ПараметрыРасчета.ГодовойРасчет,
				ГодРасчета);
		КонецЕсли;
		
		// Посчитаем сумму льгот и сумму налога с учетом льгот. Освобождение не сочетается с другими льготами. Остальные 
		// льготы могут комбинироваться (хотя на практике такие случаи маловероятны).
		Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
			НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаНалогаБезЛьгот;
			НоваяСтрока.СуммаНалога = 0;
		Иначе
			// Снижение налоговой базы
			Если СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость > 0 Тогда
				// Из-за промежуточного округления кадастровой стоимости, необлагаемой кадастровой стоимости и налоговой базы
				// сумма льготы может отличаться. При заполнении декларации СуммаЛьгот не используется, тем не менее рассчитаем ее как разницу
				// полной суммы налога без учета уменьшения кадастровой стоимости и суммы налога с учетом уменьшения.
				Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
					НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаНалогаБезЛьгот - НоваяСтрока.СуммаНалогаСУчетомНеоблагаемойБазы;
				Иначе
					НоваяСтрока.СуммаЛьгот = СуммаНалогаПоКадастровойСтоимости(
						СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
						НоваяСтрока.НалоговаяСтавка,
						КоличествоМесяцевВПериоде,
						НоваяСтрока.КоличествоМесяцевВладения,
						НоваяСтрока.КоличествоМесяцевИспользования,
						ПараметрыРасчета.ГодовойРасчет,
						ГодРасчета);
				КонецЕсли;
			КонецЕсли;
			
			// Уменьшение суммы налога на процент
			Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
				СуммаЛьготы = 
					Окр(Макс(НоваяСтрока.СуммаНалогаБезЛьгот - НоваяСтрока.СуммаЛьгот, 0) // сумма налога за вычетом всех других льгот
					* СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаЛьгот + СуммаЛьготы;
				// К сумме налога, рассчитанной с учетом освобождения от налогообложения или уменьшения налоговой базы,
				// могут дополнительно применяться льготы - уменьшение суммы налога на фиксированную сумму или на процент.
				// Поэтому нужно отдельно для декларации рассчитать сумму льгот, которая уменьшает сумму налога.
				Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
					НоваяСтрока.СуммаУменьшенияСуммыНалога = НоваяСтрока.СуммаУменьшенияСуммыНалога + СуммаЛьготы;
				КонецЕсли;
			КонецЕсли;
			
			// Уменьшение налога на фиксированную сумму
			Если СтрокаТаблицы.УменьшениеСуммыНалогаНаСумму Тогда
				СуммаЛьготы = Окр(СтрокаТаблицы.СуммаУменьшения / ?(ПараметрыРасчета.ГодовойРасчет, 1, 4));
				НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаЛьгот + СуммаЛьготы;
				Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
					НоваяСтрока.СуммаУменьшенияСуммыНалога = НоваяСтрока.СуммаУменьшенияСуммыНалога + СуммаЛьготы;
				КонецЕсли;
			КонецЕсли;
			
			// Сумма льгот не может превышать сумму налога
			НоваяСтрока.СуммаЛьгот = Мин(НоваяСтрока.СуммаНалогаБезЛьгот, НоваяСтрока.СуммаЛьгот);
			
			Если ПараметрыРасчета.ИностраннаяОрганизация Тогда
				НоваяСтрока.СуммаНалога = НоваяСтрока.СуммаНалогаСУчетомНеоблагаемойБазы - НоваяСтрока.СуммаУменьшенияСуммыНалога;
			Иначе
				НоваяСтрока.СуммаНалога = НоваяСтрока.СуммаНалогаБезЛьгот - НоваяСтрока.СуммаЛьгот;
			КонецЕсли;
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
				
				СуммаАвансовыхПлатежейКв1 = 0;
				СуммаАвансовыхПлатежейКв2 = 0;
				СуммаАвансовыхПлатежейКв3 = 0;
				
			ИначеЕсли ПараметрыРасчета.ИностраннаяОрганизация Тогда
				
				СуммаАвансовыхПлатежейКв1 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.НалоговаяБаза,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв1,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв1,
					Ложь,
					ГодРасчета);
					
				СуммаАвансовыхПлатежейКв2 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.НалоговаяБаза,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв2,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв2,
					Ложь,
					ГодРасчета);
					
				СуммаАвансовыхПлатежейКв3 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.НалоговаяБаза,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв3,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв3,
					Ложь,
					ГодРасчета);
					
				Если СтрокаТаблицы.УменьшениеСуммыНалогаНаСумму Тогда
					СуммаАвансовыхПлатежейКв1 = Макс(СуммаАвансовыхПлатежейКв1 - Окр(СтрокаТаблицы.СуммаУменьшения / 4), 0);
					СуммаАвансовыхПлатежейКв2 = Макс(СуммаАвансовыхПлатежейКв2 - Окр(СтрокаТаблицы.СуммаУменьшения / 4), 0);
					СуммаАвансовыхПлатежейКв3 = Макс(СуммаАвансовыхПлатежейКв3 - Окр(СтрокаТаблицы.СуммаУменьшения / 4), 0);
				КонецЕсли;
				
				Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
					СуммаАвансовыхПлатежейКв1 = Макс(СуммаАвансовыхПлатежейКв1
						- Окр(СуммаАвансовыхПлатежейКв1 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
					СуммаАвансовыхПлатежейКв2 = Макс(СуммаАвансовыхПлатежейКв2
						- Окр(СуммаАвансовыхПлатежейКв2 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
					СуммаАвансовыхПлатежейКв3 = Макс(СуммаАвансовыхПлатежейКв3
						- Окр(СуммаАвансовыхПлатежейКв3 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
				КонецЕсли;
				
			Иначе
					
				СуммаНалогаБезЛьготКв1 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.КадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв1,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв1,
					Ложь,
					ГодРасчета);
					
				СуммаНалогаБезЛьготКв2 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.КадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв2,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв2,
					Ложь,
					ГодРасчета);
					
				СуммаНалогаБезЛьготКв3 = СуммаНалогаПоКадастровойСтоимости(
					НоваяСтрока.КадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					3,
					СтрокаТаблицы.КоличествоМесяцевВладенияКв3,
					СтрокаТаблицы.КоличествоМесяцевИспользованияКв3,
					Ложь,
					ГодРасчета);
					
				СуммаЛьготКв1 = 0;
				СуммаЛьготКв2 = 0;
				СуммаЛьготКв3 = 0;
				
				Если СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость > 0 Тогда
					
					СуммаЛьготКв1 = СуммаНалогаПоКадастровойСтоимости(
						СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
						НоваяСтрока.НалоговаяСтавка,
						3,
						СтрокаТаблицы.КоличествоМесяцевВладенияКв1,
						СтрокаТаблицы.КоличествоМесяцевИспользованияКв1,
						Ложь,
						ГодРасчета);
						
					СуммаЛьготКв2 = СуммаНалогаПоКадастровойСтоимости(
						СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
						НоваяСтрока.НалоговаяСтавка,
						3,
						СтрокаТаблицы.КоличествоМесяцевВладенияКв2,
						СтрокаТаблицы.КоличествоМесяцевИспользованияКв2,
						Ложь,
						ГодРасчета);
						
					СуммаЛьготКв3 = СуммаНалогаПоКадастровойСтоимости(
						СтрокаТаблицы.НеоблагаемаяКадастроваяСтоимость * ДоляСтоимости * ДоляВПравеОбщейСобственности,
						НоваяСтрока.НалоговаяСтавка,
						3,
						СтрокаТаблицы.КоличествоМесяцевВладенияКв3,
						СтрокаТаблицы.КоличествоМесяцевИспользованияКв3,
						Ложь,
						ГодРасчета);
						
				КонецЕсли;
					
				Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
					СуммаЛьготКв1 = СуммаЛьготКв1 
						+ Окр(Макс(СуммаНалогаБезЛьготКв1 - СуммаЛьготКв1, 0) * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
					СуммаЛьготКв2 = СуммаЛьготКв2
						+ Окр(Макс(СуммаНалогаБезЛьготКв2 - СуммаЛьготКв2, 0) * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
					СуммаЛьготКв3 = СуммаЛьготКв3
						+ Окр(Макс(СуммаНалогаБезЛьготКв3 - СуммаЛьготКв3, 0) * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				КонецЕсли;
				
				Если СтрокаТаблицы.УменьшениеСуммыНалогаНаСумму Тогда
					СуммаЛьготы = Окр(СтрокаТаблицы.СуммаУменьшения / 4);
					СуммаЛьготКв1 = СуммаЛьготКв1 + СуммаЛьготы;
					СуммаЛьготКв2 = СуммаЛьготКв2 + СуммаЛьготы;
					СуммаЛьготКв3 = СуммаЛьготКв3 + СуммаЛьготы;
				КонецЕсли;
				
				СуммаАвансовыхПлатежейКв1 = Макс(СуммаНалогаБезЛьготКв1 - СуммаЛьготКв1, 0);
				СуммаАвансовыхПлатежейКв2 = Макс(СуммаНалогаБезЛьготКв2 - СуммаЛьготКв2, 0);
				СуммаАвансовыхПлатежейКв3 = Макс(СуммаНалогаБезЛьготКв3 - СуммаЛьготКв3, 0);
				
			КонецЕсли;
			
			НоваяСтрока.СуммаАвансовыхПлатежей = СуммаАвансовыхПлатежейКв1 + СуммаАвансовыхПлатежейКв2 + СуммаАвансовыхПлатежейКв3;
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаАвансовыхПлатежей;
				
		Иначе
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
		
		КонецЕсли;
		
		Если Не ПараметрыРасчета.ГодовойРасчет Тогда
			НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СуммаНалогаПоКадастровойСтоимости(НалоговаяБаза, НалоговаяСтавка, КоличествоМесяцевВПериоде, КоличествоМесяцевВладения, КоличествоМесяцевИспользования, ГодовойРасчет, ГодРасчета)
	
	СуммаНалога = 0;
	
	// Расчет авансов отличается только тем, что сумму нужно разделить на 4:
	// "сумма авансового платежа по налогу исчисляется по истечении отчетного периода как одна четвертая кадастровой стоимости объекта недвижимого имущества,
	// умноженная на соответствующую налоговую ставку" (пп. 1, п.12 ст. 378.2 НК)
	КоэффициентАвансов = ?(ГодовойРасчет, 1, 4); 
	
	Если ГодРасчета < 2019 Тогда
		КоэффициентКв = КоличествоМесяцевВладения / КоличествоМесяцевВПериоде;
		СуммаНалога = Окр(НалоговаяБаза * КоэффициентКв
			* НалоговаяСтавка / 100 / КоэффициентАвансов);
	ИначеЕсли ГодРасчета < 2022 Тогда
		КоэффициентКв = Окр(КоличествоМесяцевВладения / КоличествоМесяцевВПериоде, 4);
		КоэффициентКи = ?(КоличествоМесяцевВладения = 0, 1, Окр(КоличествоМесяцевИспользования / КоличествоМесяцевВладения, 4));
		СуммаНалога = Окр(НалоговаяБаза * КоэффициентКв * КоэффициентКи
			* НалоговаяСтавка / 100 / КоэффициентАвансов);
	Иначе
		// Коэффициенты определяются в виде простой дроби (т.е. без округления) - см. Письмо Минфина от 22.12.2021 N 03-05-04-01/104846.
		// Учитывая то, что данные указания связаны с изменениями в НК, применяемым к периодам с 2022 года,
		// то изменения в расчете учитываем начиная с 2022 года. 
		СуммаНалога = Окр(НалоговаяБаза 
			* КоличествоМесяцевВладения / КоличествоМесяцевВПериоде // коэффициент Кв
			* ?(КоличествоМесяцевВладения = 0, 1, КоличествоМесяцевИспользования / КоличествоМесяцевВладения) // коэффициент Ки
			* НалоговаяСтавка / 100 / КоэффициентАвансов);
	КонецЕсли;

	Возврат СуммаНалога;
	
КонецФункции 

#Область РасчетНалогаДо2022Года

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимостиДо2022(ПараметрыРасчета, ДополнительныеПараметры)
	
	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	УстановитьПараметрыЗапросаДляРасчетаНалогаНаИмущество(Запрос, ПараметрыРасчета);
	
	// Признак применения ЕНВД.
	Если ПараметрыРасчета.ПлательщикЕНВД_НаКонецКвартала Тогда 
		РасчетИмущественныхНалоговПереопределяемый.СоздатьВТКоэффициентыЕНВД(ПараметрыРасчета); 
	КонецЕсли;

	// с 2019 года движимое имущество не облагается налогом на имущество (см. N 302-ФЗ от 03.08.2018)
	Если ПараметрыРасчета.ПериодРасчета >= '20190101' Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	NULL КАК ОС
		|ПОМЕСТИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013
		|ГДЕ
		|	ЛОЖЬ";
		Запрос.Выполнить();
	Иначе
		РасчетИмущественныхНалоговПереопределяемый.СоздатьВТДвижимоеИмуществоПринятоеКУчетуПосле2013(ПараметрыРасчета, ДополнительныеПараметры);
	КонецЕсли;
	
	// Сначала сформируем временную таблицу с записями регистра, с учетом исправлений.
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(
		ПараметрыРасчета,
		'00010101',
		КонецМесяца(КонецМесяца(ПараметрыРасчета.мДатаКонцаПериодаОтчета)+1),
		ДополнительныеПараметры);

	// Затем, по записям с исправлениями сформируем временную таблицу среза последних.
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
		ПараметрыРасчета,
		ПараметрыРасчета.мДатаКонцаПериодаОтчета,
		ДополнительныеПараметры);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
	|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|		ИНАЧЕ &КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО,
	|
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи
	|
	|ИЗ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
	|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК Период
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период
	|
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета
	|
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период КАК ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|		ПО ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
	|			И ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.Период = СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|			И ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период.КодПоОКТМО = СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_ВсеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета_Период";
	
	Запрос.Выполнить();
	
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Период = ПараметрыРасчета.мДатаНачалаПериодаОтчета;
	Счетчик = 0;
	
	ТекстОбъединяющегоЗапроса = "";
	Пока Период <= НачалоМесяца(ПараметрыРасчета.мДатаКонцаПериодаОтчета) Цикл
		
		Период = ДобавитьМесяц(ПараметрыРасчета.мДатаНачалаПериодаОтчета, Счетчик);
		ПериодДляВыборкиДанных = ?(Год(Период) = Год(ПараметрыРасчета.ПериодРасчета), Период, ПараметрыРасчета.мДатаКонцаПериодаОтчета);
		
		Счетчик = Счетчик + 1;
		
		ПрименениеУСН = УчетнаяПолитика.ПрименяетсяУСН(
							ПараметрыРасчета.Организация,
							?(Период < ПараметрыРасчета.мДатаКонцаПериодаОтчета, Период, ПараметрыРасчета.мДатаКонцаПериодаОтчета));
			
		Если ПрименениеУСН Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН=");
		ИспользуемыеВременныеТаблицы.Добавить(ИмяВременнойТаблицы);
		
		СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
			ПараметрыРасчета,
			ПериодДляВыборкиДанных,
			ДополнительныеПараметры,
			"СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца");
		
		СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(
			ПараметрыРасчета,
			ПериодДляВыборкиДанных,
			ДополнительныеПараметры);
		
		РасчетИмущественныхНалоговПереопределяемый.СформироватьТаблицуОССостоящиеНаУчете(
			ПараметрыРасчета, 
			ПериодДляВыборкиДанных, 
			ДополнительныеПараметры);
		
		СписокЗапросов = Новый Массив;
		
		// Срез последних записей по основному средству в регистре "Ставки налога на имущество по отдельным основным средствам".
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ПериодСреза
		|
		|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	ПериодСреза
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ОСИсключаемыеИзРасчетаПоСреднейСтоимости
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов КАК СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ОсновноеСредство
		|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ПериодСреза
		|
		|ГДЕ
		|	(СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ СписокРазрешенныхОС
		|
		|ИЗ
		|	ВТ_ОССостоящиеНаУчете КАК ОС
		|
		|ГДЕ
		|	НЕ ОС.ГруппаОС В (
		|		ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), 
		|		ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
		|
		|	И НЕ ОС.ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					ОСИсключаемыеИзРасчетаПоСреднейСтоимости.ОсновноеСредство
		|				ИЗ
		|					ОСИсключаемыеИзРасчетаПоСреднейСтоимости)
		|
		|	И (НЕ &ТолькоНедвижимость
		|			ИЛИ ОС.НедвижимоеИмущество)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСписокОС(СписокЗапросов);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ПериодАктуальныхНастроекПоОС
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
		|		(ВЫБРАТЬ
		|			СписокОС.ОсновноеСредство
		|		ИЗ
		|			СписокОС КАК СписокОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
		|
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган КАК НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И &Период >= СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы
		|		И (СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ &Период <= КОНЕЦПЕРИОДА(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СуммаУменьшения КАК СуммаУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаПоОтдельнымОССрезПоследних
		|
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних_НаНачалоМесяца КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган КАК НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота КАК ДействуетЛьгота,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной)) КАК ОблагаетсяПоСниженнойСтавке,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение) КАК ОсвобождениеОтНалогообложения,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент) КАК УменьшениеСуммыНалогаНаПроцент,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму) КАК УменьшениеСуммыНалогаНаСумму,
		|
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.РегиональнаяЛьгота КАК РегиональнаяЛьгота,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ВидЛьготы КАК ВидЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодЛьготы КАК КодЛьготы,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПроцентУменьшения КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СуммаУменьшения КАК СуммаУменьшения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|ИЗ
		|	ВТ_СтавкиНалогаПоОтдельнымОССрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодАктуальныхНастроекПоОС КАК ПериодАктуальныхНастроекПоОС
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ПериодАктуальныхНастроекПоОС.ОсновноеСредство
		|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период = ПериодАктуальныхНастроекПоОС.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмущество.Период КАК Период,
		|	СтавкиНалогаНаИмущество.Организация КАК Организация,
		|	СтавкиНалогаНаИмущество.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмущество.НалоговаяСтавкаДвижимоеИмущество КАК НалоговаяСтавкаДвижимоеИмущество,
		|	СтавкиНалогаНаИмущество.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
		|	СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
		|	СтавкиНалогаНаИмущество.ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2 КАК ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2,
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы КАК ОснованиеЛьготы,
		|	СтавкиНалогаНаИмущество.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
		|	СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
		|
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И &Период >= СтавкиНалогаНаИмущество.НачалоДействияЛьготы
		|		И (СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ &Период <= КОНЕЦПЕРИОДА(СтавкиНалогаНаИмущество.ОкончаниеДействияЛьготы, ДЕНЬ)) КАК ДействуетЛьгота,
		|
		|	СтавкиНалогаНаИмущество.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом КАК РегиональнаяЛьгота,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
		|	ЕСТЬNULL(СтавкиНалогаНаИмущество.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения
		|
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
		|
		|ИЗ
		|	СтавкиНалогаНаИмущество_СрезПоследних КАК СтавкиНалогаНаИмущество";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСчетаБухгалтерскогоУчета(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСтоимостьИАмортизация(Период, ПараметрыРасчета, СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаДанныеБухУчетаОС(СписокЗапросов);
		
		Если ПараметрыРасчета.ПлательщикЕНВД_НаКонецКвартала Тогда
			
			РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаКоэффициентыЕНВД_ПоОС(СписокЗапросов);
			
			// Получение коэффициента доли выручки от деятельности, облагаемой ЕНВД.
			ПериодПолученияКоэффициентаЕНВД = ?(Период = ПараметрыРасчета.мДатаНачалаПериодаОтчета, Период, ДобавитьМесяц(Период, -1));
			
			КоэффициентРаспределенияЕНВД = УправлениеВнеоборотнымиАктивами.КоэффициентРаспределенияВыручкиПоВидамДеятельности(
												ПараметрыРасчета.Организация, 
												НачалоКвартала(ПериодПолученияКоэффициентаЕНВД), 
												КонецКвартала(ПериодПолученияКоэффициентаЕНВД));
			
			Запрос.УстановитьПараметр("КоэффициентРаспределенияЕНВД", КоэффициентРаспределенияЕНВД);
			
			ШаблонЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СписокОС.ОсновноеСредство,
			|
			// ЭтоПерваяВтораяАмортизационныеГруппы
			|	ВЫБОР
			|		КОГДА СписокОС.АмортизационнаяГруппа В (
			|				ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), 
			|				ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
			|
			// РегистрацияВНалоговомОргане
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган
			|		ИНАЧЕ 
			|			&ОсновнойНалоговыйОрган
			|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
			|
			// КодПоОКТМО
			|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО, &КодПоОКТМО) КАК КодПоОКТМО,
			|
			// НалоговаяСтавка
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавкаДвижимоеИмущество
			|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
			|	КОНЕЦ КАК НалоговаяСтавка,
			|
			// ОснованиеЛьготы
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОснованиеЛьготы
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы 
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка) 
			|	КОНЕЦ КАК ОснованиеЛьготы,
			|
			// СнижениеНалоговойСтавки
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы В (
			|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент),
			|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной))
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК СнижениеНалоговойСтавки,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
			|
			// ОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсвобождениеОтНалогообложения
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
			|	СписокОС.НедвижимоеИмущество КАК ЭтоНедвижимость,
			|
			// ДвижимоеИмуществоПринятоеКУчетуПосле2013
			|	НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
			|
			// КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсвобождениеОтНалогообложения
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодЛьготы
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДействуетЛьгота И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.РегиональнаяЛьгота
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодЛьготы
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И (СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
			|			ИЛИ НЕ СтавкиНалогаНаИмуществоСрезПоследних.РегиональнаяЛьгота)
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.КодЛьготы
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
			|
			// УменьшениеСуммыНалогаНаПроцент
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК УменьшениеСуммыНалогаНаПроцент,
			|
			// ПроцентУменьшенияСуммыНалога
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПроцентУменьшения
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА 0
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
			|
			// УменьшениеСуммыНалогаНаСумму
			|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму, ЛОЖЬ) КАК УменьшениеСуммыНалогаНаСумму,
			|
			// СуммаУменьшения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.СуммаУменьшения
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК СуммаУменьшения,
			|
			|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.УсловныйНомер, """") КАК УсловныйНомер,
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомер, """") КАК КадастровыйНомер,
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомерПомещения, """") КАК КадастровыйНомерПомещения,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК,
			|	&Счетчик КАК Период,
			|
			// ОстаточнаяСтоимость
			|	ВЫРАЗИТЬ((ЕСТЬNULL(ДанныеБухУчетаОС.ПервоначальнаяСтоимость, 0) 
			|				- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА ДанныеБухУчетаОС.АмортизацияДт 
			|								ИНАЧЕ ДанныеБухУчетаОС.АмортизацияКт 
			|							КОНЕЦ, 0) - ЕСТЬNULL(ДанныеБухУчетаОС.Обесценение, 0)) * КоэффициентыЕНВД.НеЕНВД
			|				+ (ЕСТЬNULL(ДанныеБухУчетаОС.ПервоначальнаяСтоимость, 0) 
			|					- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА ДанныеБухУчетаОС.АмортизацияДт 
			|								ИНАЧЕ ДанныеБухУчетаОС.АмортизацияКт 
			|							КОНЕЦ, 0) - ЕСТЬNULL(ДанныеБухУчетаОС.Обесценение, 0)) * КоэффициентыЕНВД.Распределение * &КоэффициентРаспределенияЕНВД КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
			|
			|ПОМЕСТИТЬ ИмяВременнойТаблицы
			|
			|ИЗ
			|	СписокОС
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеБухУчетаОС КАК ДанныеБухУчетаОС
			|		ПО СписокОС.ОсновноеСредство = ДанныеБухУчетаОС.ОсновноеСредство
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета КАК СтавкиНалогаПоОтдельнымОС_НаДатуРасчета
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.ОсновноеСредство
			|			И ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО, &КодПоОКТМО) = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КодПоОКТМО
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЕНВД_ПоОС КАК КоэффициентыЕНВД
			|		ПО СписокОС.ОсновноеСредство = КоэффициентыЕНВД.ОсновноеСредство
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013
			|		ПО СписокОС.ОсновноеСредство = ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС,
			|
			|		ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
						
		Иначе
			
			ШаблонЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СписокОС.ОсновноеСредство,
			|
			// ЭтоПерваяВтораяАмортизационныеГруппы
			|	ВЫБОР
			|		КОГДА СписокОС.АмортизационнаяГруппа В (
			|					ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), 
			|					ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
			|
			// РегистрацияВНалоговомОргане
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговыйОрган
			|		ИНАЧЕ 
			|			&ОсновнойНалоговыйОрган
			|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
			|
			// КодПоОКТМО
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодПоОКТМО, &КодПоОКТМО) КАК КодПоОКТМО,
			|
			// НалоговаяСтавка
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.СниженнаяНалоговаяСтавка
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговаяСтавка
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавкаДвижимоеИмущество
			|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
			|	КОНЕЦ КАК НалоговаяСтавка,
			|
			// ОснованиеЛьготы
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.ОснованиеЛьготы
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы 
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка) 
			|	КОНЕЦ КАК ОснованиеЛьготы,
			|
			// СнижениеНалоговойСтавки
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ОблагаетсяПоСниженнойСтавке
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы В (
			|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент),
			|				ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной))
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК СнижениеНалоговойСтавки,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
			|
			// ОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ОсвобождениеОтНалогообложения
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL
			|			 И НЕ ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|			 И &Год >= 2018 
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,			
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
			|	СписокОС.НедвижимоеИмущество КАК ЭтоНедвижимость,
			|
			// ДвижимоеИмуществоПринятоеКУчетуПосле2013
			|	НЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ЕСТЬ NULL КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
			|
			// КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ОсвобождениеОтНалогообложения
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.КодЛьготы
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ДействуетЛьгота И НЕ СтавкиНалогаПоОтдельнымОС.РегиональнаяЛьгота
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.КодЛьготы
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И (СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
			|			ИЛИ НЕ СтавкиНалогаНаИмуществоСрезПоследних.РегиональнаяЛьгота)
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.КодЛьготы
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,			
			|
			// УменьшениеСуммыНалогаНаПроцент
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК УменьшениеСуммыНалогаНаПроцент,
			|
			// ПроцентУменьшенияСуммыНалога
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаПроцент
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.ПроцентУменьшения
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА 0
			|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
			|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
			|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
			|
			// УменьшениеСуммыНалогаНаСумму
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму, ЛОЖЬ) КАК УменьшениеСуммыНалогаНаСумму,
			|
			// СуммаУменьшения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.УменьшениеСуммыНалогаНаСумму
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.СуммаУменьшения
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК СуммаУменьшения,
			|
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
			|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
			|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
			|	СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.УсловныйНомер КАК УсловныйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомер КАК КадастровыйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
			|	СтавкиНалогаПоОтдельнымОС.КБК КАК КБК,
			|	&Счетчик КАК Период,
			|
			|	ВЫРАЗИТЬ(ЕСТЬNULL(ДанныеБухУчетаОС.ПервоначальнаяСтоимость, 0) 
			|				- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА ДанныеБухУчетаОС.АмортизацияДт 
			|								ИНАЧЕ ДанныеБухУчетаОС.АмортизацияКт 
			|							КОНЕЦ, 0) - ЕСТЬNULL(ДанныеБухУчетаОС.Обесценение, 0) КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
			|
			|ПОМЕСТИТЬ ИмяВременнойТаблицы
			|
			|ИЗ
			|	СписокОС
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеБухУчетаОС КАК ДанныеБухУчетаОС
			|		ПО СписокОС.ОсновноеСредство = ДанныеБухУчетаОС.ОсновноеСредство
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС КАК СтавкиНалогаПоОтдельнымОС
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС.ОсновноеСредство
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОС_НаДатуРасчета КАК СтавкиНалогаПоОтдельнымОС_НаДатуРасчета
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.ОсновноеСредство
			|			И ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодПоОКТМО, &КодПоОКТМО) = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КодПоОКТМО
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013
			|		ПО СписокОС.ОсновноеСредство = ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС,
			|
			|		ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
						
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&Счетчик", Счетчик); // значение Счетчик от 1 до 13
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		
		// При расчете налога за год на последнем цикле Период равен 1 января следующего года.
		// В этом случае подменяем период: выбираем данные на конец налогового периода.
		Запрос.УстановитьПараметр("Период", ПериодДляВыборкиДанных);
		Запрос.УстановитьПараметр("ПериодГраница", ?(Год(Период) = Год(ПараметрыРасчета.ПериодРасчета),
			Новый Граница(Период, ВидГраницы.Исключая), // выборка на момент начала месяца
			Новый Граница(ПараметрыРасчета.мДатаКонцаПериодаОтчета, ВидГраницы.Включая))); // выборка на конец периода включая проводки регламентных операций на 23:59:59 31.12
		
		РасчетИмущественныхНалоговПереопределяемый.УстановитьПараметрыЗапросаПриРасчетеНалогаЗаПериод(
			Запрос,
			ПараметрыРасчета.Организация,
			Период);
		
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ НалоговаяБазаПоНалогуНаИмущество
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
		                            |ГДЕ
		                            |	ОстаточнаяСтоимость > 0 ИЛИ ЭтоНедвижимость
									|";
		
	КонецЦикла;
	
	Если ТекстОбъединяющегоЗапроса = "" Тогда
		// Удалим все таблицы, которые были созданы во время выполнения процедуры.
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
		Возврат Неопределено;
	КонецЕсли;

	Запрос.Текст = ТекстОбъединяющегоЗапроса;	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("КонечныйПериод", ПараметрыРасчета.Делитель);
	Если ПараметрыРасчета.ПериодРасчета >= '20180101' Тогда
		Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2012000");
	Иначе
		Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2010257");
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговаяБазаПоНалогуНаИмущество.Период КАК Период,
	|	НалоговаяБазаПоНалогуНаИмущество.КодВидаИмущества КАК КодВидаИмущества,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЧислитель, 0) КАК ДоляСтоимостиЧислитель,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЗнаменатель, 0) КАК ДоляСтоимостиЗнаменатель,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.УсловныйНомер, """") КАК УсловныйНомер,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомер, """") КАК КадастровыйНомер,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомерПомещения, """") КАК КадастровыйНомерПомещения,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КБК, """") КАК КБК,
	|	НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане КАК ИФНС,
	|	НалоговаяБазаПоНалогуНаИмущество.КодПоОКТМО КАК КодПоОКТМО,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
	|	НалоговаяБазаПоНалогуНаИмущество.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|
	|	НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавки
	|		ИЛИ &Год >= 2018
	|			И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|			И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
	|			И НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавкиДвижимоеИмущество КАК ПонижениеНалоговойСтавки,
	|
	|	НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения
	|		ИЛИ &Год >= 2015
	|			И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|			И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
	|			И (&Год < 2018
	|				ИЛИ НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества)
	|			И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы КАК ОсвобождениеОтНалогообложения,
	|
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения <> """"
	|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения
	|		КОГДА &Год >= 2015
	|			И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|			И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
	|			И (&Год < 2018
	|				ИЛИ НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества)
	|			И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
	|			ТОГДА &КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|
	|	НалоговаяБазаПоНалогуНаИмущество.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	НалоговаяБазаПоНалогуНаИмущество.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	НалоговаяБазаПоНалогуНаИмущество.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	НалоговаяБазаПоНалогуНаИмущество.СуммаУменьшения КАК СуммаУменьшения,
	|	НалоговаяБазаПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
	|	НЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL КАК УплачиваютсяАвансы,
	|
	|	ВЫБОР
	|		КОГДА &Год < 2015
	|			И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|			ИЛИ &Год >= 2015
	|			И &Год < 2019
	|			И НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
	|			ТОГДА 0
	|		ИНАЧЕ НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ЭтоНедвижимость
	|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОстаточнаяСтоимостьНедвижимости,
	|
	|	НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость КАК ОстаточнаяСтоимостьОсновныхСредств
	|
	|ПОМЕСТИТЬ ВТ_НалоговаяБаза
	|
	|ИЗ
	|	НалоговаяБазаПоНалогуНаИмущество КАК НалоговаяБазаПоНалогуНаИмущество
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НалоговаяБаза.КодВидаИмущества КАК КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС КАК ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	ВТ_НалоговаяБаза.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	ВТ_НалоговаяБаза.СуммаУменьшения КАК СуммаУменьшения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер КАК УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК КАК КБК,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 1 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0101,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 2 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0102,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 3 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0103,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 4 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0104,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 5 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0105,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 6 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0106,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 7 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0107,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 8 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0108,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 9 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0109,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 10 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0110,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 11 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0111,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 12 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0112,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 13 И ВТ_НалоговаяБаза.ОстаточнаяСтоимость > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа3112,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод И ВТ_НалоговаяБаза.ОстаточнаяСтоимостьНедвижимости > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьНедвижимости
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНедвижимости,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод И ВТ_НалоговаяБаза.ОстаточнаяСтоимостьОсновныхСредств > 0
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьОсновныхСредств
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьОсновныхСредств
	|ИЗ
	|	ВТ_НалоговаяБаза КАК ВТ_НалоговаяБаза
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НалоговаяБаза.КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ОснованиеЛьготы,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаПроцент,
	|	ВТ_НалоговаяБаза.ПроцентУменьшенияСуммыНалога,
	|	ВТ_НалоговаяБаза.УменьшениеСуммыНалогаНаСумму,
	|	ВТ_НалоговаяБаза.СуммаУменьшения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК";
	
	Результат = Запрос.Выполнить();
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Процедура ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимостиДо2022(ТаблицаРасчетНалога, РасчетНалога, ПараметрыРасчета, ДополнительныеПараметры)

	Если РасчетНалога = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	КолонкиГруппировок = ""
	+ "УплачиваютсяАвансы,"
	+ "ИФНС,"
	+ "КодПоОКТМО,"
	+ "КодВидаИмущества,"
	+ "КБК,"
	+ "ДоляСтоимостиЧислитель,"
	+ "ДоляСтоимостиЗнаменатель,"
	+ "НалоговаяСтавка,"
	+ "ОснованиеЛьготы,"
	+ "ПонижениеНалоговойСтавки,"
	+ "ОсвобождениеОтНалогообложения,"
	+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"
	+ "УменьшениеСуммыНалогаНаПроцент,"
	+ "ПроцентУменьшенияСуммыНалога,"
	+ "УменьшениеСуммыНалогаНаСумму,"
	+ "СуммаУменьшения";
	
	КолонкиСуммирования = ""
	+ "ОстаточнаяСтоимостьНа0101,"
	+ "ОстаточнаяСтоимостьНа0102,"
	+ "ОстаточнаяСтоимостьНа0103,"
	+ "ОстаточнаяСтоимостьНа0104,"
	+ "ОстаточнаяСтоимостьНа0105,"
	+ "ОстаточнаяСтоимостьНа0106,"
	+ "ОстаточнаяСтоимостьНа0107,"
	+ "ОстаточнаяСтоимостьНа0108,"
	+ "ОстаточнаяСтоимостьНа0109,"
	+ "ОстаточнаяСтоимостьНа0110,"
	+ "ОстаточнаяСтоимостьНа0111,"
	+ "ОстаточнаяСтоимостьНа0112,"
	+ "ОстаточнаяСтоимостьНа3112";
	
	КолонкиРасчета = "" 
	+ "СредняяСтоимость,"
	+ "НеоблагаемаяСредняяСтоимость,"
	+ "НалоговаяБаза,"
	+ "СуммаНалога,"
	+ "СуммаУменьшенияСуммыНалога,"
	+ "СуммаНалогаКУплате,"
	+ "СуммаАвансовыхПлатежей1Кв,"
	+ "СуммаАвансовыхПлатежей2Кв,"
	+ "СуммаАвансовыхПлатежей3Кв,"
	+ "СуммаОсвобожденияОтУплатыНалога";
	
	РасчетНалогаПоЛистам = РасчетНалога.Скопировать();
	РасчетНалогаПоЛистам.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	ДопКолонки = Новый Структура(КолонкиРасчета);
	Для каждого Колонка Из ДопКолонки Цикл
		РасчетНалогаПоЛистам.Колонки.Добавить(Колонка.Ключ,
			ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный));
	КонецЦикла;
		
	// С целью оптимизации производительности список имён полей-периодов формируется заранее, а не динамически в циклах.
	КолонкиПериоды = Новый ТаблицаЗначений;
	КолонкиПериоды.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
	КолонкиПериоды.Колонки.Добавить("ОстаточнаяСтоимостьИмя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(25, ДопустимаяДлина.Фиксированная)));
	Для Счетчик = 1 По ПараметрыРасчета.Делитель Цикл
		
		КолонкаПериод = КолонкиПериоды.Добавить();
		КолонкаПериод.Счетчик = Счетчик;
		КолонкаПериод.ОстаточнаяСтоимостьИмя = "ОстаточнаяСтоимостьНа" + ?(Счетчик = 13, "3112", "01" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	КонецЦикла;
		
	Отбор = Новый Структура(КолонкиГруппировок);
	РасчетНалогаПоЛистам.Индексы.Добавить(КолонкиГруппировок);
	
	КэшУчетнойПолитики = Новый Соответствие;
	
	// В 2020 году некоторые организации освобождены от уплаты имущественных налогов (за 2-й квартал). В этом случае сумму освобождения записываем
	// в отдельную колонку расчета СуммаОсвобожденияОтУплатыНалога.
	РассчитыватьОсвобождениеОтНалога = Ложь;
	ЭтоПериодОсвобождения = Ложь;
	Если ДополнительныеПараметры <> Неопределено Тогда
		РассчитыватьОсвобождениеОтНалога = ПараметрыРасчета.ГодовойРасчет
			И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде;
		ЭтоПериодОсвобождения = ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде;	
	КонецЕсли;		
	
	Для Каждого СтрокаТаблицы Из РасчетНалогаПоЛистам Цикл
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И (СтрокаТаблицы.КодВидаИмущества = "04" ИЛИ СтрокаТаблицы.КодВидаИмущества = "07") Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
				
		ДоляСтоимости = 1;
		
		КодВидаИмущества = ?(ПустаяСтрока(СтрокаТаблицы.КодВидаИмущества), "03", СтрокаТаблицы.КодВидаИмущества);
		Если КодВидаИмущества = "04" Тогда
			ДоляСтоимости = 0;
		ИначеЕсли КодВидаИмущества = "02" Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;
		КонецЕсли;
		
		ДелимоеСредняяСтоимость = 0;
		ДелимоеНеоблагаемаяСредняяСтоимость = 0;
		Для каждого КолонкаПериод Из КолонкиПериоды Цикл
			Счетчик = КолонкаПериод.Счетчик;
			
			СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя]);
			ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			
			Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
				ДелимоеНеоблагаемаяСредняяСтоимость = ДелимоеНеоблагаемаяСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			КонецЕсли;
			
			Месяц = Счетчик - 1;
			
			// В годовом расчете учитываются суммы авансовых платежей, если организация платит авансы в текущей ИФНС.
			// Если организация освобождена от уплаты налога за 2-й квартал 2020 года и при этом не платит авансы в текущей ИФНС, 
			// то при расчете налога за год нужно отдельно рассчитать величину такого освобождения. 
			// Эта сумма равна потенциальному авансовому платежу за 2-й квартал (если бы авансы уплачивались).	
			Если (УчитыватьАвансы Или РассчитыватьОсвобождениеОтНалога) И Месяц <> 0 И Месяц % 3 = 0 Тогда
				
				// Если уплачиваются авансы, то в конце года требуются данные всех кварталов.
				// При расчете суммы освобождения нужны только данные 2-го квартала.
				Если Не УчитыватьАвансы И Месяц <> 6 Тогда
					Продолжить;
				КонецЕсли;	
				
				ПериодУчетнойПолитики = Дата(Год(ПараметрыРасчета.ПериодРасчета), Счетчик - 3, 1); // начало отчетного квартала
				НастройкиУчетнойПолитики = КэшУчетнойПолитики[ПериодУчетнойПолитики];
				
				Если НастройкиУчетнойПолитики = Неопределено Тогда
					ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(ПараметрыРасчета.Организация, ПериодУчетнойПолитики);
					КэшУчетнойПолитики.Вставить(ПериодУчетнойПолитики, Новый Структура("ПрименяетсяУСН", ПрименяетсяУСН));
				Иначе
					ПрименяетсяУСН = НастройкиУчетнойПолитики.ПрименяетсяУСН;
				КонецЕсли;	
				
				СредняяСтоимость = ?(ПрименяетсяУСН, 0, ДелимоеСредняяСтоимость / Счетчик);
				НеоблагаемаяСредняяСтоимость = ?(ПрименяетсяУСН, 0, ДелимоеНеоблагаемаяСредняяСтоимость / Счетчик);
				
				Если Месяц = 3 Тогда
					СредняяСтоимость1Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость1Кв = НеоблагаемаяСредняяСтоимость;
				ИначеЕсли Месяц = 6 Тогда
					СредняяСтоимость2Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость2Кв = НеоблагаемаяСредняяСтоимость;
				ИначеЕсли Месяц = 9 Тогда
					СредняяСтоимость3Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость3Кв = НеоблагаемаяСредняяСтоимость;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаТаблицы.СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / ПараметрыРасчета.Делитель);
		СтрокаТаблицы.НеоблагаемаяСредняяСтоимость = Окр(ДелимоеНеоблагаемаяСредняяСтоимость / ПараметрыРасчета.Делитель);
		
		СтоимостьОблагаемогоНалогомИмущества = Макс(СтрокаТаблицы.СредняяСтоимость - СтрокаТаблицы.НеоблагаемаяСредняяСтоимость, 0);
		
		Если УчитыватьАвансы Тогда
			СтоимостьОблагаемогоНалогомИмущества1Кв = Макс(СредняяСтоимость1Кв - НеоблагаемаяСредняяСтоимость1Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества2Кв = Макс(СредняяСтоимость2Кв - НеоблагаемаяСредняяСтоимость2Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества3Кв = Макс(СредняяСтоимость3Кв - НеоблагаемаяСредняяСтоимость3Кв, 0);
		ИначеЕсли РассчитыватьОсвобождениеОтНалога Тогда
			СтоимостьОблагаемогоНалогомИмущества2Кв = Макс(СредняяСтоимость2Кв - НеоблагаемаяСредняяСтоимость2Кв, 0);
		КонецЕсли;
		
		СтрокаТаблицы.НалоговаяБаза = Окр(СтоимостьОблагаемогоНалогомИмущества * ДоляСтоимости);	
		
		Если ПараметрыРасчета.ГодовойРасчет Тогда
			СтрокаТаблицы.СуммаНалога = Окр(СтрокаТаблицы.НалоговаяБаза * СтрокаТаблицы.НалоговаяСтавка / 100);
		Иначе	
			СтрокаТаблицы.СуммаНалога = Окр(СтоимостьОблагаемогоНалогомИмущества
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = Окр(СтоимостьОблагаемогоНалогомИмущества1Кв 
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
			СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = Окр(СтоимостьОблагаемогоНалогомИмущества2Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
			СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = Окр(СтоимостьОблагаемогоНалогомИмущества3Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
				
		КонецЕсли;		
				
		Если РассчитыватьОсвобождениеОтНалога Тогда
			// Аванс за 2-й квартал не уплачивается
			СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = 0;
			// При уплате авансов по месту регистрации имущества сумма освобождения - это аванс за 2-й квартал 2020 года.
			// Если авансы не уплачиваются, то сумма освобождения рассчитывается аналогично авансу за 2-й квартал, если бы такая обязанность была.
			СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога = Окр(СтоимостьОблагаемогоНалогомИмущества2Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
		КонецЕсли;
		
		Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
			
			СтрокаТаблицы.СуммаУменьшенияСуммыНалога = Окр(СтрокаТаблицы.СуммаНалога * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			СтрокаТаблицы.СуммаНалогаКУплате = Макс(СтрокаТаблицы.СуммаНалога - СтрокаТаблицы.СуммаУменьшенияСуммыНалога, 0);
			
			Если УчитыватьАвансы Тогда
				СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			КонецЕсли;		
					
			Если РассчитыватьОсвобождениеОтНалога Тогда
				СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога = СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога
					- Окр(СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			КонецЕсли;
						
		Иначе
			
			СтрокаТаблицы.СуммаНалогаКУплате = СтрокаТаблицы.СуммаНалога;
			
		КонецЕсли;
		
		Если ЭтоПериодОсвобождения Тогда
			// Во 2-м квартале 2020 года организация освобождается от налога в размере полной суммы аванса к уплате 
			СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога = СтрокаТаблицы.СуммаНалогаКУплате;
		КонецЕсли;
		
		// Распределим полученные суммы по объектам ОС
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		РасчетНалогаПоОбъектамОС = РасчетНалога.Скопировать(Отбор);
		РасчетНалогаПоОбъектамОС.Сортировать("ОсновноеСредство", Новый СравнениеЗначений);
			
		КоэффициентыРаспределения = Новый Массив;
		КоэффициентыРаспределения1Кв = Новый Массив;
		КоэффициентыРаспределения2Кв = Новый Массив;
		КоэффициентыРаспределения3Кв = Новый Массив;
		
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			ДелимоеСредняяСтоимость = 0;
			Для каждого КолонкаПериод Из КолонкиПериоды Цикл
				Счетчик = КолонкаПериод.Счетчик;
				
				СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя], 2);
				ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя];
				
				Если УчитыватьАвансы Тогда
					Если Счетчик = 4 Тогда
						СредняяСтоимость1Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 7 Тогда
						СредняяСтоимость2Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 10 Тогда
						СредняяСтоимость3Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					КонецЕсли;			
				ИначеЕсли РассчитыватьОсвобождениеОтНалога И Счетчик = 7 Тогда
					СредняяСтоимость2Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
				КонецЕсли;
				
			КонецЦикла;
			СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / ПараметрыРасчета.Делитель, 2);
			
			КоэффициентыРаспределения.Добавить(СредняяСтоимость);
			
			Если УчитыватьАвансы Тогда
				КоэффициентыРаспределения1Кв.Добавить(СредняяСтоимость1Кв);
				КоэффициентыРаспределения2Кв.Добавить(СредняяСтоимость2Кв);
				КоэффициентыРаспределения3Кв.Добавить(СредняяСтоимость3Кв);
			ИначеЕсли РассчитыватьОсвобождениеОтНалога Тогда
				КоэффициентыРаспределения2Кв.Добавить(СредняяСтоимость2Кв);
			КонецЕсли;
			
		КонецЦикла;	
		
		Для каждого Колонка Из ДопКолонки Цикл
			
			Если Колонка.Ключ = "СуммаОсвобожденияОтУплатыНалога" И Не РассчитыватьОсвобождениеОтНалога И Не ЭтоПериодОсвобождения Тогда
				Продолжить;
			КонецЕсли;	
			
			Если РасчетНалогаПоОбъектамОС.Колонки.Найти(Колонка.Ключ) = Неопределено Тогда
				РасчетНалогаПоОбъектамОС.Колонки.Добавить(Колонка.Ключ,
					ОбщегоНазначения.ОписаниеТипаЧисло(15, 2, ДопустимыйЗнак.Неотрицательный));
			КонецЕсли;	
				
			Если Колонка.Ключ = "СуммаАвансовыхПлатежей1Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения1Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей2Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения2Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей3Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения3Кв;
			ИначеЕсли Колонка.Ключ = "СуммаОсвобожденияОтУплатыНалога" И Не ЭтоПериодОсвобождения Тогда
				Коэффициенты = КоэффициентыРаспределения2Кв;
			Иначе
				Коэффициенты = КоэффициентыРаспределения;
			КонецЕсли;
				
			Если СтрокаТаблицы[Колонка.Ключ] > 0 Тогда
				РаспределенныеСуммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
										СтрокаТаблицы[Колонка.Ключ], Коэффициенты);
				Если РаспределенныеСуммы <> Неопределено Тогда
					РасчетНалогаПоОбъектамОС.ЗагрузитьКолонку(РаспределенныеСуммы, Колонка.Ключ);
				КонецЕсли;
			КонецЕсли;	
			
		КонецЦикла;
				
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			// Уменьшение налога на сумму устанавливается по каждому отдельному объекту,
			// поэтому применяем этот вид льготы уже после распределения по объектам
			Если СтрокаПоОбъектуОС.УменьшениеСуммыНалогаНаСумму Тогда
				
				// Так как это сумма уменьшения за весь год, то в расчете за квартальные периоды нужно взять 1/4. 
				Если ПараметрыРасчета.ГодовойРасчет Тогда
					СуммаУменьшения = СтрокаПоОбъектуОС.СуммаУменьшения;
				Иначе	
					СуммаУменьшения = Окр(СтрокаПоОбъектуОС.СуммаУменьшения / 4);
				КонецЕсли;
				
				СтрокаПоОбъектуОС.СуммаУменьшенияСуммыНалога = СтрокаПоОбъектуОС.СуммаУменьшенияСуммыНалога + СуммаУменьшения;
				СтрокаПоОбъектуОС.СуммаНалогаКУплате = Макс(СтрокаПоОбъектуОС.СуммаНалога - СтрокаПоОбъектуОС.СуммаУменьшенияСуммыНалога, 0);
				
				Если УчитыватьАвансы Тогда
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
						- СуммаУменьшения;
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
						- СуммаУменьшения;
					СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв
						- СуммаУменьшения;
				КонецЕсли;		
						
				Если РассчитыватьОсвобождениеОтНалога Тогда
					СтрокаПоОбъектуОС.СуммаОсвобожденияОтУплатыНалога = СтрокаТаблицы.СуммаОсвобожденияОтУплатыНалога - СуммаУменьшения;
				КонецЕсли;
				
			КонецЕсли;	
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость;
			НоваяСтрока.КодВидаИмущества = КодВидаИмущества;
			
			СвойстваПоСтрокеТаблицы = ""
			+ "ИФНС,"
			+ "КодПоОКТМО,"
			+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"
			+ "НалоговаяСтавка,"
			+ "ОснованиеЛьготы,"
			+ "КБК,"
			+ "ПонижениеНалоговойСтавки";
			
			Если КодВидаИмущества = "02" Тогда
				СвойстваПоСтрокеТаблицы = СвойстваПоСтрокеТаблицы + ","
				+ "ДоляСтоимостиЧислитель,"
				+ "ДоляСтоимостиЗнаменатель";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, СвойстваПоСтрокеТаблицы);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПоОбъектуОС, ,СвойстваПоСтрокеТаблицы);
			
			Если УчитыватьАвансы Тогда
				
				НоваяСтрока.СуммаАвансовыхПлатежей = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв;
													
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
				
			КонецЕсли;
			
			// В 2020 году для некоторых организаций применяется разовая льгота в виде освобождения от налога 
			// за период владения объктом налогообложения в течение 2-го квартала 2020 года.
			// Для таких "освобожденных" организаций СуммаОсвобожденияОтУплатыНалога будет ненулевой 
			// в расчете за 2-й квартал 2020 года (если уплачиваются авансы) и в годовом расчете за 2020 год.
			// В всех остальных случаях СуммаОсвобожденияОтУплатыНалога = 0 и не влияет на расчет.
			// Уменьшим СуммаНалогаКУплате на сумму такого освобождения.
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаОсвобожденияОтУплатыНалога;
			
			Если НЕ ПараметрыРасчета.ГодовойРасчет Тогда
				НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
				
КонецПроцедуры 

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимостиДо2022(ПараметрыРасчета, ДополнительныеПараметры)

	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	ДанныеДляРасчета = Неопределено;
	
	Если Не (ПараметрыРасчета.ПериодРасчета >= '20150101' 
				Или ПараметрыРасчета.ПрименениеОСНО 
				Или (ПараметрыРасчета.ПериодРасчета >= '20140701' 
						И ПараметрыРасчета.ПериодРасчета < '20150101' 
						И ПараметрыРасчета.ПлательщикЕНВД)) Тогда
	
		Возврат ДанныеДляРасчета;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	УстановитьПараметрыЗапросаДляРасчетаНалогаНаИмущество(Запрос, ПараметрыРасчета);
	
	// Сначала сформируем временную таблицу с записями регистра, с учетом исправлений.
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(
		ПараметрыРасчета,
		'00010101',
		'00010101',
		ДополнительныеПараметры);
		
	// Затем, по записям с исправлениями сформируем временную таблицу среза последних.	
	СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(
		ПараметрыРасчета,
		Запрос.Параметры.НачалоПериода,
		ДополнительныеПараметры);
		
	СформироватьТаблицуСтавкиНалогаНаИмуществоСрезПоследних(
		ПараметрыРасчета,
		Запрос.Параметры.НачалоПериода,
		ДополнительныеПараметры);
			
	Если ПараметрыРасчета.ПериодРасчета < '20190101' Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ВТ_ЗаписиЗаПериод.ДатаСведений) КАК ДатаСведений,
		|	МИНИМУМ(ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеДействия,
		|	ВТ_ЗаписиЗаПериод.Организация КАК Организация,
		|	ВТ_ЗаписиЗаПериод.ОсновноеСредство КАК ОсновноеСредство,
		|	ВТ_ЗаписиЗаПериод.КодПоОКТМО КАК КодПоОКТМО
		|	
		|ПОМЕСТИТЬ ВТ_ДатыДействияЗаписей
		|	
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК ДатаСведений,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО
		|	ИЗ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|	ГДЕ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|		И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению
		|		И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаРегистрацииПраваСобственности > &КонецПериода
		|		И (СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности >= &НачалоПериода)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
		|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
		|	ИЗ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|	ГДЕ
		|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|		И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НеПодлежитНалогообложению
		|		И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности > &КонецПериода
		|		И (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности >= &НачалоПериода)) КАК ВТ_ЗаписиЗаПериод
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ЗаписиЗаПериод.Организация,
		|	ВТ_ЗаписиЗаПериод.КодПоОКТМО,
		|	ВТ_ЗаписиЗаПериод.ОсновноеСредство";
		
	Иначе
		
		Запрос.Текст =
		
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО
		|	
		|ПОМЕСТИТЬ ВТ_ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС
		|	
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаРегистрацииПраваСобственности > &КонецПериода
		|	И (СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности >= НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НеПодлежитНалогообложению
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности > &КонецПериода
		|	И (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности >= НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.Период КАК ДатаСведений,
		|	МИНИМУМ(ЕСТЬNULL(СледующиеСтавки.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ОкончаниеДействия,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО
		|
		|ПОМЕСТИТЬ ВТ_ДатыДействияЗаписей
		|
		|ИЗ
		|	ВТ_ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС КАК ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СледующиеСтавки
		|		ПО ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = СледующиеСтавки.ОсновноеСредство
		|			И ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО = СледующиеСтавки.КодПоОКТМО
		|			И ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.Период < СледующиеСтавки.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.Период,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.Организация,
		|	ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ИсторияСтавокНалогаНаИмуществоПоОтдельнымОС";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
	// ПРИМЕЧАНИЕ
	// Льготы по "кадастровым" объектам применяются в следующей последовательности (см. 3-й запрос пакета)
	// 1. Освобождение, установленное для конкретного объекта
	// 2. Освобождение, установленное для всех объектов (в общих настройках по организации) - обычно это федеральная льгота по НК (действует во всех регионах).
	// 3. Любая другая льгота, установленная для конкретного объекта
	// 4. Общая льгота по организации в виде снижения суммы налога на процент, если объект зарегистрирован по месту нахождения организации (региональная льгота).
	
	// Такая иерархия обусловлена тем, что при наличии освобождения другие льготы не имеют смысла. В то же время код льготы в виде освобождения,
	// установленный для конкретного объекта (например, находящегося в другом регионе), считается более надежным, чем общая льгота.
	// Кроме освобождения для каждого конкретного объекта может быть установлена своя льгота, например, в виде снижения ставки или суммы налога.
	// В этом случае учитывается именно такая льгота.
	// Наконец, в целом по организации может применяться снижение суммы налога на процент. Но только для объектов, зарегистрированных в головной организации.
	// Здесь речь идет исключительно о региональной льготе, поэтому некорректно ее применять к объектам, находящимся вне места расположения организации.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_ДатыДействияЗаписей.ДатаСведений КАК ДатаСведений,
	|	ВТ_ДатыДействияЗаписей.ОкончаниеДействия КАК ОкончаниеДействия,
	|
	|	ВЫБОР
	|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|		ИНАЧЕ &ОсновнойНалоговыйОрган
	|	КОНЕЦ КАК ИФНС,
	|
	|	ВЫБОР
	|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
	|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
	|		ИНАЧЕ &КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО,
	|
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы <= &КонецПериода
	|		И (ВТ_ДатыДействияЗаписей.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НачалоДействияЛьготы <= ВТ_ДатыДействияЗаписей.ОкончаниеДействия)
	|		И (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы >= ВТ_ДатыДействияЗаписей.ДатаСведений
	|				И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД)) КАК ДействуетЛьгота,
	|
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяСтавка КАК НалоговаяСтавка,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СуммаУменьшения КАК СуммаУменьшения,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности КАК ДатаРегистрацииПраваСобственности,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
	|
	|	&ПрименениеУСН
	|		ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
	|
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодВидаИмущества КАК КодВидаИмущества,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель
	|
	|ПОМЕСТИТЬ ВТ_ДействующиеНастройкиПоОтдельнымОсновнымСредствам
	|
	|ИЗ
	|	ВТ_ДатыДействияЗаписей КАК ВТ_ДатыДействияЗаписей
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|		ПО ВТ_ДатыДействияЗаписей.ДатаСведений = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период
	|			И ВТ_ДатыДействияЗаписей.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
	|			И ВТ_ДатыДействияЗаписей.КодПоОКТМО = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоСрезПоследних.Период КАК Период,
	|	СтавкиНалогаНаИмуществоСрезПоследних.Организация КАК Организация,
	|	СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы КАК ОснованиеЛьготы,
	|	СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы КАК НачалоДействияЛьготы,
	|	СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы КАК ОкончаниеДействияЛьготы,
	|
	|	СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|		И СтавкиНалогаНаИмуществоСрезПоследних.НачалоДействияЛьготы <= &КонецПериода
	|		И (СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ОкончаниеДействияЛьготы >= НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД)) КАК ДействуетЛьгота,
	|
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.УстановленаМестнымНормативнымАктом, ЛОЖЬ) КАК РегиональнаяЛьгота,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготы,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.КодЛьготы, """") КАК КодЛьготы,
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы.ПроцентУменьшения, 0) КАК ПроцентУменьшения
	|
	|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
	|
	|ИЗ
	|	СтавкиНалогаНаИмущество_СрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДатаСведений КАК ДатаСведений,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ИФНС КАК ИФНС,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КодПоОКТМО КАК КодПоОКТМО,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
	|
	|	ВЫБОР
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
	|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОснованиеЛьготы
	|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
	|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ОснованиеЛьготы
	|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
	|				И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ОснованиеЛьготы
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|	КОНЕЦ КАК ОснованиеЛьготы,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|			И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
	|		ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение) КАК ОсвобождениеОтНалогообложения,
	|	ВЫБОР
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|				И (ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеНалоговойБазыНаСумму))
	|					ИЛИ НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.РегиональнаяЛьгота)
	|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КодЛьготы
	|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|				И (СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
	|					ИЛИ НЕ СтавкиНалогаНаИмуществоСрезПоследних.РегиональнаяЛьгота)
	|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.КодЛьготы
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|		И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной)) КАК ПонижениеНалоговойСтавки,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|
	|	ВЫБОР
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы В (ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент), ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной))
	|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.СниженнаяНалоговаяСтавка
	|		ИНАЧЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.НалоговаяСтавка
	|	КОНЕЦ КАК НалоговаяСтавка,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|			И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
	|		ИЛИ СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|			И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
	|			И НЕ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане) КАК УменьшениеСуммыНалогаНаПроцент,
	|
	|	ВЫБОР
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|				И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
	|			ТОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПроцентУменьшения
	|		КОГДА ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА 0
	|		КОГДА СтавкиНалогаНаИмуществоСрезПоследних.ДействуетЛьгота
	|				И СтавкиНалогаНаИмуществоСрезПоследних.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
	|			ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДействуетЛьгота
	|		И ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ВидЛьготы = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму) КАК УменьшениеСуммыНалогаНаСумму,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.СуммаУменьшения КАК СуммаУменьшения,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности КАК ДатаРегистрацииПраваСобственности,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
	|
	|	&ПрименениеУСН
	|		ИЛИ ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
	|
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.КодВидаИмущества КАК КодВидаИмущества,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
	|	ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель,
	|	НЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL КАК УплачиваютсяАвансы
	|
	|ИЗ
	|	ВТ_ДействующиеНастройкиПоОтдельнымОсновнымСредствам КАК ДействующиеНастройкиПоОтдельнымОсновнымСредствам
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО ДействующиеНастройкиПоОтдельнымОсновнымСредствам.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних
	|		ПО (ИСТИНА)";
	
	ДанныеДляРасчета = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
		СтрокаРасчета.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
	КонецЦикла;
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Процедура ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимостиДо2022(ТаблицаРасчетНалога, ДанныеДляРасчета, ПараметрыРасчета, ДополнительныеПараметры)

	Если ДанныеДляРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	// В 2020 году некоторые организации освобождены от уплаты имущественных налогов (за 2-й квартал). В этом случае сумму освобождения записываем
	// в отдельную колонку расчета СуммаОсвобожденияОтУплатыНалога.
	РассчитыватьОсвобождениеОтНалога = Ложь;
	ЭтоПериодОсвобождения = Ложь;
	Если ДополнительныеПараметры <> Неопределено Тогда
		РассчитыватьОсвобождениеОтНалога = ПараметрыРасчета.ГодовойРасчет
			И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде;
		ЭтоПериодОсвобождения = ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде;	
	КонецЕсли;		
	
	ГодРасчета = Год(ПараметрыРасчета.ПериодРасчета);
	
	Если ГодРасчета < 2016 Или ПараметрыРасчета.ГодовойРасчет Тогда
		ПервыйМесяцРасчета = 1;
	Иначе
		ПервыйМесяцРасчета = Месяц(НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
	КонецЕсли;
	
	КоличествоМесяцевВПериоде = ?(ГодРасчета < 2016 Или ПараметрыРасчета.ГодовойРасчет, Месяц(ПараметрыРасчета.ПериодРасчета), 3);
	
	Для Каждого СтрокаТаблицы Из ДанныеДляРасчета Цикл
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
				
		Если ПараметрыРасчета.ПериодРасчета < '20140701' И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаРегистрацииПраваСобственности) Тогда
			Продолжить;
		КонецЕсли;
				
		КоличествоМесяцевВладения = 0;
		КоличествоМесяцевВладенияЗаКвартал = 0;
		КоличествоМесяцевИспользования = 0;
		КоличествоМесяцевИспользованияЗаКвартал = 0;
		МесяцыВладения = Новый Соответствие;
		МесяцыИспользования = Новый Соответствие;
		Для Сч = ПервыйМесяцРасчета По Месяц(ПараметрыРасчета.ПериодРасчета) Цикл
			
			Если ГодРасчета < 2016 Тогда
				ПредельнаяДатаВозникновенияПрава = КонецМесяца(Дата(ГодРасчета, Сч, 1));
				ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 1);
			Иначе
				ПредельнаяДатаВозникновенияПрава = Дата(ГодРасчета, Сч, 15, 23, 59, 59);
				ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 16);
			КонецЕсли;
			
			Если СтрокаТаблицы.ДатаРегистрацииПраваСобственности <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПрекращенияПраваСобственности)
					ИЛИ СтрокаТаблицы.ДатаПрекращенияПраваСобственности >= ПредельнаяДатаПрекращенияПрава) Тогда
					
				КоличествоМесяцевВладения = КоличествоМесяцевВладения + 1;
				КоличествоМесяцевВладенияЗаКвартал = КоличествоМесяцевВладенияЗаКвартал + 1;
			КонецЕсли;
			
			Если ГодРасчета >= 2019
				И СтрокаТаблицы.ДатаСведений <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОкончаниеДействия)
					ИЛИ СтрокаТаблицы.ОкончаниеДействия >= ПредельнаяДатаПрекращенияПрава)
				И СтрокаТаблицы.ДатаРегистрацииПраваСобственности <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПрекращенияПраваСобственности)
					ИЛИ СтрокаТаблицы.ДатаПрекращенияПраваСобственности >= ПредельнаяДатаПрекращенияПрава) Тогда
					
				КоличествоМесяцевИспользования = КоличествоМесяцевИспользования + 1;
				КоличествоМесяцевИспользованияЗаКвартал = КоличествоМесяцевИспользованияЗаКвартал + 1;
			КонецЕсли;
			
			Если ПараметрыРасчета.ГодовойРасчет И Сч%3 = 0 Тогда
				
				МесяцыВладения.Вставить(Сч, КоличествоМесяцевВладенияЗаКвартал);
				
				Если ГодРасчета >= 2016 Тогда
					КоличествоМесяцевВладенияЗаКвартал = 0;
				КонецЕсли;
				
				Если ГодРасчета >= 2019 Тогда
					МесяцыИспользования.Вставить(Сч, КоличествоМесяцевИспользованияЗаКвартал);
					КоличествоМесяцевИспользованияЗаКвартал = 0;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоМесяцевВладения = 0 ИЛИ (ГодРасчета >= 2019 И КоличествоМесяцевИспользования = 0) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРасчетНалога.Добавить();
		 
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость;
		
		Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость;
		КонецЕсли;
		
		Если ПараметрыРасчета.ГодовойРасчет
		   И ГодРасчета = 2014
		   И ПараметрыРасчета.ПлательщикЕНВД
		   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
		   
			НоваяСтрока.КадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость / 2;
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.НеоблагаемаяКадастроваяСтоимость / 2;
		
		ИначеЕсли ПараметрыРасчета.ГодовойРасчет Тогда 
			// В декларации округленные значения
			НоваяСтрока.КадастроваяСтоимость = Окр(НоваяСтрока.КадастроваяСтоимость);
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = Окр(НоваяСтрока.НеоблагаемаяКадастроваяСтоимость);
		
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляСтоимостиЧислитель > 0 И СтрокаТаблицы.ДоляСтоимостиЗнаменатель > 0 Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;	
		Иначе
			ДоляСтоимости = 1;
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель > 0 И СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель > 0 Тогда
			ДоляВПравеОбщейСобственности = 
				СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель / СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель;
		Иначе
			ДоляВПравеОбщейСобственности = 1;
		КонецЕсли;
		
		НалоговаяБаза = Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
			* ДоляСтоимости * ДоляВПравеОбщейСобственности; // в расчете авансов используется неокругленное значение
		
		НоваяСтрока.НалоговаяБаза = Окр(НалоговаяБаза); // в декларации налоговая база указывается в целых рублях 
		
		НоваяСтрока.КоличествоМесяцевВладения = КоличествоМесяцевВладения;
		НоваяСтрока.КоличествоМесяцевИспользования = КоличествоМесяцевИспользования;
		
		НоваяСтрока.СуммаНалога = СуммаНалогаПоКадастровойСтоимости(
			НалоговаяБаза, // за квартальные периоды берем неокругленную налоговую базу
			НоваяСтрока.НалоговаяСтавка,
			КоличествоМесяцевВПериоде,
			НоваяСтрока.КоличествоМесяцевВладения,
			НоваяСтрока.КоличествоМесяцевИспользования,
			ПараметрыРасчета.ГодовойРасчет,
			ГодРасчета);
				
		Если УчитыватьАвансы Тогда
							
			СуммаАвансовыхПлатежей3 = СуммаНалогаПоКадастровойСтоимости(
				НалоговаяБаза,
				НоваяСтрока.НалоговаяСтавка,
				3,
				МесяцыВладения[3],
				МесяцыИспользования[3],
				Ложь,
				ГодРасчета);	
				
			СуммаАвансовыхПлатежей6 = СуммаНалогаПоКадастровойСтоимости(
				НалоговаяБаза,
				НоваяСтрока.НалоговаяСтавка,
				?(ГодРасчета < 2016, 6, 3),
				МесяцыВладения[6],
				МесяцыИспользования[6],
				Ложь,
				ГодРасчета);	
				
			СуммаАвансовыхПлатежей9 = СуммаНалогаПоКадастровойСтоимости(
				НалоговаяБаза,
				НоваяСтрока.НалоговаяСтавка,
				?(ГодРасчета < 2016, 9, 3),
				МесяцыВладения[9],
				МесяцыИспользования[9],
				Ложь,
				ГодРасчета);					
								
		КонецЕсли;		
		
		// Если организация освобождена от уплаты налога за 2-й квартал 2020 года, 
		// то величина освобождения равна авансовому платежу за 2-й квартал 2020 года. 
		// Если такая организация не платит авансы в текущей ИФНС, 
		// то при расчете налога за год нужно отдельно рассчитать величину такого освобождения. 
		// Эта сумма равна потенциальному авансовому платежу за 2-й квартал (если бы авансы уплачивались).
		Если РассчитыватьОсвобождениеОтНалога Тогда
			// Аванс за 2-й квартал не уплачивается
			СуммаАвансовыхПлатежей6 = 0;
			
			НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = СуммаНалогаПоКадастровойСтоимости(
				НалоговаяБаза,
				НоваяСтрока.НалоговаяСтавка,
				3,
				МесяцыВладения[6],
				МесяцыИспользования[6],
				Ложь,
				ГодРасчета);	
				
		КонецЕсли;
		
		// Сумма налога к уплате может быть уменьшена далее, если применяются льготы
		НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
		
		Если СтрокаТаблицы.УменьшениеСуммыНалогаНаПроцент Тогда
		
			НоваяСтрока.СуммаУменьшенияСуммыНалога = Окр(НоваяСтрока.СуммаНалога * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100);
			НоваяСтрока.СуммаНалогаКУплате = Макс(НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаУменьшенияСуммыНалога, 0);
			
			Если УчитыватьАвансы Тогда
				СуммаАвансовыхПлатежей3 = Макс(СуммаАвансовыхПлатежей3
					- Окр(СуммаАвансовыхПлатежей3 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
				СуммаАвансовыхПлатежей6 = Макс(СуммаАвансовыхПлатежей6
					- Окр(СуммаАвансовыхПлатежей6 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
				СуммаАвансовыхПлатежей9 = Макс(СуммаАвансовыхПлатежей9
					- Окр(СуммаАвансовыхПлатежей9 * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
			КонецЕсли;		
					
			Если РассчитыватьОсвобождениеОтНалога Тогда
				НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = Макс(НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
					- Окр(НоваяСтрока.СуммаОсвобожденияОтУплатыНалога * СтрокаТаблицы.ПроцентУменьшенияСуммыНалога / 100), 0);
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.УменьшениеСуммыНалогаНаСумму Тогда
			
			// Так как сумма снижения указана за весь год, то в расчете за квартальные периоды нужно взять 1/4. 
			Если ПараметрыРасчета.ГодовойРасчет Тогда
				СуммаУменьшения = СтрокаТаблицы.СуммаУменьшения;
			Иначе	
				СуммаУменьшения = Окр(СтрокаТаблицы.СуммаУменьшения / 4);
			КонецЕсли;
			
			НоваяСтрока.СуммаУменьшенияСуммыНалога = НоваяСтрока.СуммаУменьшенияСуммыНалога + СуммаУменьшения;
			НоваяСтрока.СуммаНалогаКУплате = Макс(НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаУменьшенияСуммыНалога, 0);
			
			Если УчитыватьАвансы Тогда
				СуммаАвансовыхПлатежей3 = Макс(СуммаАвансовыхПлатежей3 - СуммаУменьшения, 0);
				СуммаАвансовыхПлатежей6 = Макс(СуммаАвансовыхПлатежей6 - СуммаУменьшения, 0);
				СуммаАвансовыхПлатежей9 = Макс(СуммаАвансовыхПлатежей9 - СуммаУменьшения, 0);
			КонецЕсли;		
					
			Если РассчитыватьОсвобождениеОтНалога Тогда
				НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога - СуммаУменьшения;
			КонецЕсли;
											
		КонецЕсли;
		
		Если ЭтоПериодОсвобождения Тогда
			// Во 2-м квартале 2020 года организация освобождается от налога в размере полной суммы аванса к уплате 
			НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			Если ГодРасчета = 2014
			   И ПараметрыРасчета.ПлательщикЕНВД
			   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
				НоваяСтрока.СуммаАвансовыхПлатежей = 2 * СуммаАвансовыхПлатежей9;
			Иначе
				НоваяСтрока.СуммаАвансовыхПлатежей = СуммаАвансовыхПлатежей3 + СуммаАвансовыхПлатежей6 + СуммаАвансовыхПлатежей9;
			КонецЕсли;
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
			
		КонецЕсли;
		
		// В 2020 году для некоторых организаций применяется разовая льгота в виде освобождения от налога 
		// за период владения объктом налогообложения в течение 2-го квартала 2020 года.
		// Для таких "освобожденных" организаций СуммаОсвобожденияОтУплатыНалога будет ненулевой 
		// в расчете за 2-й квартал 2020 года (если уплачиваются авансы) и в годовом расчете за 2020 год.
		// В всех остальных случаях СуммаОсвобожденияОтУплатыНалога = 0 и не влияет на расчет.
		// Уменьшим СуммаНалогаКУплате на сумму такого освобождения.
		НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаОсвобожденияОтУплатыНалога;
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет Тогда
			НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ТранспортныйНалог

Функция ПолучитьДанныеДляРасчетаТранспортногоНалога(ПараметрыРасчета, ДополнительныеПараметры)

	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	ГодовойРасчет = КонецКвартала(ПараметрыРасчета.ПериодРасчета) = КонецГода(ПараметрыРасчета.ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет, НачалоГода(ПараметрыРасчета.ПериодРасчета), НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПараметрыРасчета.ПериодРасчета);
	
	ПараметрыРегистрацииВФНС = Новый Структура;
	ПараметрыРегистрацииВФНС.Вставить("ОсновнойНалоговыйОрган", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ПараметрыРегистрацииВФНС.Вставить("КодПоОКТМО", "");
	
	РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыРегистрацииОрганизацииВФНС(
		ПараметрыРегистрацииВФНС,
		ПараметрыРасчета.Организация,
		ДатаКонцаПериодаОтчета);
		
	СформироватьТаблицуРегистрацияТранспортныхСредствЗаписи(
		ПараметрыРасчета,
		ДатаКонцаПериодаОтчета,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПараметрыРасчета.ПериодРасчета);
	
	// В соответствии с письмом ФНС от 20.10.2021 № БС-4-21/14849@:
	// "... в случае изменения места нахождения налогоплательщика, на которого зарегистрировано облагаемое налогом транспортное средство,
	// а также перерегистрации такого транспортного средства с одного обособленного подразделения организации на другое,
	// находящееся в другом субъекте РФ, налог уплачивается в бюджет по месту нахождения транспортного средства,
	// определенному по состоянию на 1-е число месяца, в котором произошли указанные изменения.
	// Исходя из нового места нахождения налогоплательщика, его обособленного подразделения налог в отношении рассматриваемого транспортного средства
	// уплачивается начиная со следующего месяца".
	//
	// Поэтому ИФНС и ОКТМО для ТС, зарегистрированных по месту нахождения организации, определяем из истории регистрации организации
	// в налоговых органах на 1-е число каждого месяца (см. ВТ_РегистрацияОрганизацииВНалоговомОргане). В случае изменения регистрационных данных
	// организации в течение налогового периода сумму налога по каждому отдельному месту нахождения определяем в порядке, определенном в п.3 ст. 362 НК:
	// "... исчисление суммы налога (суммы авансового платежа по налогу) производится с учетом коэффициента,
	// определяемого как отношение числа полных месяцев, в течение которых данное транспортное средство было зарегистрировано на налогоплательщика,
	// к числу календарных месяцев в налоговом (отчетном) периоде". 
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРегистрацииВФНС.ОсновнойНалоговыйОрган);
	Запрос.УстановитьПараметр("КодПоОКТМО", ПараметрыРегистрацииВФНС.КодПоОКТМО);
	
	ТекстОбъединяющегоЗапроса = "";
	
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	Период = ДатаНачалаПериодаОтчета;
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// С 2016 года, если регистрация ТС произошла до 15-го числа соответствующего месяца включительно
		// или снятие транспортного средства с регистрации (снятие с учета, исключение из государственного
		// судового реестра и так далее) произошло после 15-го числа соответствующего месяца, за полный 
		// месяц принимается месяц регистрации (снятия с регистрации) транспортного средства.
		
		Если Год(Период) < 2016 Тогда
			КонецПериода = КонецМесяца(Период);
			КонецПрошлогоПериода = НачалоМесяца(Период) - 1;
		Иначе
			КонецПериода = Дата(Год(Период), Месяц(Период), 15, 23, 59, 59);
			КонецПрошлогоПериода = КонецПериода;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("КонецПрошлогоПериода", КонецПрошлогоПериода);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		
		СформироватьТаблицуРегистрацияТранспортныхСредствСрезПоследних(
			ПараметрыРасчета,
			КонецПрошлогоПериода,
			ДополнительныеПараметры);
			
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|
		|ИЗ
		|	РегистрацияТранспортныхСредств_СрезПоследних КАК РегистрацияТранспортныхСредств
		|
		|ГДЕ
		|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|
		|ИЗ
		|	РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
		|
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период = &НачалоМесяца
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВТ_РегистрацииВПервыйДеньМесяца.Период, ВТ_РегистрацииНаКонецПрошлогоПериода.Период) КАК Период,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.Организация КАК Организация,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ВТ_СписокОсновныхСредств
		|
		|ИЗ
		|	ВТ_РегистрацииНаКонецПрошлогоПериода КАК ВТ_РегистрацииНаКонецПрошлогоПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацииВПервыйДеньМесяца КАК ВТ_РегистрацииВПервыйДеньМесяца
		|		ПО ВТ_РегистрацииНаКонецПрошлогоПериода.Организация = ВТ_РегистрацииВПервыйДеньМесяца.Организация
		|			И ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство = ВТ_РегистрацииВПервыйДеньМесяца.ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрацияТранспортныхСредств.Период,
		|	РегистрацияТранспортныхСредств.Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство
		|ИЗ
		|	РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
		|
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоМесяца И &КонецПериода
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СписокОсновныхСредств.Период) КАК Период,
		|	СписокОсновныхСредств.Организация КАК Организация,
		|	СписокОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|
		|ПОМЕСТИТЬ ВТ_ПериодРегистрацииТранспортныхСредств
		|
		|ИЗ
		|	ВТ_СписокОсновныхСредств КАК СписокОсновныхСредств
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокОсновныхСредств.ОсновноеСредство,
		|	СписокОсновныхСредств.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО
		|
		|ПОМЕСТИТЬ ВТ_РегистрацияОрганизацииВНалоговомОргане
		|
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&НачалоМесяца, СтруктурнаяЕдиница = &Организация) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПериодРегистрацииТранспортныхСредств.Период КАК ДатаРегистрационныхДанных,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
		|	РегистрацияТранспортныхСредств.Марка КАК Марка,
		|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияТранспортныхСредств.НалоговыйОрган
		|		ИНАЧЕ ЕСТЬNULL(РегистрацияОрганизацииВНалоговомОргане.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган)
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|				ИЛИ РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|			ТОГДА РегистрацияТранспортныхСредств.КодПоОКТМО
		|		ИНАЧЕ ЕСТЬNULL(РегистрацияОрганизацииВНалоговомОргане.КодПоОКТМО, &КодПоОКТМО)
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически КАК СтавкаОпределяетсяАвтоматически,
		|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
		|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
		|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
		|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
		|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
		|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
		|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
		|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
		|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЧислитель,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|
		|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
		|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
		|	1 КАК КоличествоМесяцев
		|
		|ПОМЕСТИТЬ ИмяВременнойТаблицы
		|
		|ИЗ
		|	ВТ_ПериодРегистрацииТранспортныхСредств КАК ВТ_ПериодРегистрацииТранспортныхСредств
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
		|		ПО ВТ_ПериодРегистрацииТранспортныхСредств.Период = РегистрацияТранспортныхСредств.Период
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.Организация = РегистрацияТранспортныхСредств.Организация
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредств.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацияОрганизацииВНалоговомОргане КАК РегистрацияОрганизацииВНалоговомОргане
		|		ПО (ИСТИНА)";
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Период, "ДФ=MM");
		ИспользуемыеВременныеТаблицы.Добавить(ИмяВременнойТаблицы);

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ РегистрацияТранспортныхСредств
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
									|";
								  
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		ПараметрыРасчета.МенеджерВременныхТаблиц, 
		ПараметрыРасчета.Организация, 
		ПараметрыРасчета.ПериодРасчета, 
		"ТранспортныйНалог");
	
	Запрос.Текст =		
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ВТ_Регистрации
	|
	|ИЗ
	|	РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
	|
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.Регистрация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ВТ_Снятия
	|
	|ИЗ
	|	РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
	|
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПредыдущегоСнятия
	|
	|ПОМЕСТИТЬ ВТ_ПредыдущиеСнятия
	|
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период > ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период КАК Период,
	|	МИНИМУМ(ВТ_Регистрации.Период) КАК ПериодПервоначальнойРегистрации
	|
	|ПОМЕСТИТЬ ВТ_ПервоначальныеРегистрации
	|
	|ИЗ
	|	ВТ_ПредыдущиеСнятия КАК ВТ_ПредыдущиеСнятия
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_ПредыдущиеСнятия.ОсновноеСредство = ВТ_Регистрации.ОсновноеСредство
	|			И ВТ_ПредыдущиеСнятия.ПериодПредыдущегоСнятия < ВТ_Регистрации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодСнятияСУчета
	|
	|ПОМЕСТИТЬ ВТ_СнятияСУчета
	|
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период < ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ВТ_ПервоначальныеРегистрации.ПериодПервоначальнойРегистрации) КАК ПериодПервоначальнойРегистрации,
	|	МАКСИМУМ(ВТ_СнятияСУчета.ПериодСнятияСУчета) КАК ПериодСнятияСУчета
	|
	|ПОМЕСТИТЬ ВТ_ДатыПостановкиИСнятия
	|
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеРегистрации КАК ВТ_ПервоначальныеРегистрации
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_ПервоначальныеРегистрации.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_ПервоначальныеРегистрации.Период
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СнятияСУчета КАК ВТ_СнятияСУчета
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_СнятияСУчета.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_СнятияСУчета.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон
	|
	|ПОМЕСТИТЬ ВТ_РазличныеЗаписи
	|
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_РазличныеЗаписи.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	ВТ_РазличныеЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка КАК Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс КАК ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РазличныеЗаписи.НалоговаяЛьгота <> ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоЗаписейЛьгот
	|
	|ПОМЕСТИТЬ ВТ_ЗаписиБезУчетаЛьгот
	|
	|ИЗ
	|	ВТ_РазличныеЗаписи КАК ВТ_РазличныеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РазличныеЗаписи.ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных) КАК ДатаРегистрационныхДанных,
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцевЛьготы,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев, 0) = 0
	|			ТОГДА СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев)
	|		ИНАЧЕ ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев
	|	КОНЕЦ КАК КоличествоМесяцевВладения,
	|
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК ИФНС,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмерения,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0) КАК КоличествоЗаписейЛьгот,
	|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически КАК СтавкаОпределяетсяАвтоматически,
	|
	|	ВЫБОР
	|		КОГДА РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически
	|			ТОГДА 0
	|		ИНАЧЕ РегистрацияТранспортныхСредств.НалоговаяСтавка
	|	КОНЕЦ КАК НалоговаяСтавка,
	|
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы,
	|
	|	РегистрацияТранспортныхСредств.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации КАК ДатаРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета КАК ДатаПрекращенияРегистрации
	|
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаписиБезУчетаЛьгот КАК ВТ_ЗаписиБезУчетаЛьгот
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ЗаписиБезУчетаЛьгот.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.РегистрационныйЗнак = ВТ_ЗаписиБезУчетаЛьгот.РегистрационныйЗнак
	|			И РегистрацияТранспортныхСредств.Марка = ВТ_ЗаписиБезУчетаЛьгот.Марка
	|			И РегистрацияТранспортныхСредств.ИдентификационныйНомер = ВТ_ЗаписиБезУчетаЛьгот.ИдентификационныйНомер
	|			И РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_ЗаписиБезУчетаЛьгот.РегистрацияВНалоговомОргане
	|			И РегистрацияТранспортныхСредств.КодПоОКТМО = ВТ_ЗаписиБезУчетаЛьгот.КодПоОКТМО
	|			И РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства = ВТ_ЗаписиБезУчетаЛьгот.КодВидаТранспортногоСредства
	|			И РегистрацияТранспортныхСредств.НалоговаяБаза = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяБаза
	|			И РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы = ВТ_ЗаписиБезУчетаЛьгот.ЕдиницаИзмеренияНалоговойБазы
	|			И РегистрацияТранспортныхСредств.ЭкологическийКласс = ВТ_ЗаписиБезУчетаЛьгот.ЭкологическийКласс
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЧислитель
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЗнаменатель
	|			И РегистрацияТранспортныхСредств.ПовышающийКоэффициент = ВТ_ЗаписиБезУчетаЛьгот.ПовышающийКоэффициент
	|			И (ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот = 1)
	|			И РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон = ВТ_ЗаписиБезУчетаЛьгот.ЗарегистрированоВРеестреСистемыПлатон
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыПостановкиИСнятия КАК ВТ_ДатыПостановкиИСнятия
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ДатыПостановкиИСнятия.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных = ВТ_ДатыПостановкиИСнятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически,
	|	ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев,
	|	ВЫБОР
	|		КОГДА РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически
	|			ТОГДА 0
	|		ИНАЧЕ РегистрацияТранспортныхСредств.НалоговаяСтавка
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство.ДатаВыпуска,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0),
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИФНС,
	|	КодПоОКТМО,
	|	ОсновноеСредство";
				
	Результат = Запрос.Выполнить();
	ДанныеДляРасчета = Результат.Выгрузить();
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Функция ВыполнитьРасчетТранспортногоНалога(ТаблицаРасчетНалога, ПараметрыРасчета, ДанныеДляРасчета, ДополнительныеПараметры)
	
	НетТСЗарегистрированныхВРеестреСистемыПлатон = Истина;
	
	Если ДанныеДляРасчета.Количество() <> 0 Тогда
		
		ДанныеДляРасчета.Колонки.Добавить("КоличествоЛетПрошедшихСГодаВыпускаТС", 
			ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
			
		ДополнитьРасчетТранспортногоНалога(ДанныеДляРасчета, ПараметрыРасчета.ДатаКонцаПериодаОтчета);
			
		// В 2020 году некоторые организации освобождены от уплаты имущественных налогов (за 2-й квартал). В этом случае сумму освобождения записываем
		// в отдельную колонку расчета СуммаОсвобожденияОтУплатыНалога.
		РассчитыватьОсвобождениеОтНалога = Ложь;
		ЭтоПериодОсвобождения = Ложь;
		РасчетСуммыНалогаЗаПериодОсвобождения = Ложь;
		Если ДополнительныеПараметры <> Неопределено Тогда
			РассчитыватьОсвобождениеОтНалога = ПараметрыРасчета.ГодовойРасчет
				И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде")
				И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде;
			ЭтоПериодОсвобождения = ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде")
				И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде;	
			РасчетСуммыНалогаЗаПериодОсвобождения = ДополнительныеПараметры.Свойство("РасчетСуммыНалогаЗаПериодОсвобождения") 
				И ДополнительныеПараметры.РасчетСуммыНалогаЗаПериодОсвобождения;	// устанавливается при "вложенном" вызове расчета за период освобождения			
		КонецЕсли;		
			
		Если ПараметрыРасчета.ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			
			ЕстьОбъектыСУплатойАвансов = (ДанныеДляРасчета.Найти(Истина, "УплачиваютсяАвансы") <> Неопределено);
			ЕстьОбъектыБезУплатыАвансов = (ДанныеДляРасчета.Найти(Ложь, "УплачиваютсяАвансы") <> Неопределено);
			
			Если ЕстьОбъектыСУплатойАвансов Тогда
				
				РасчетНалога1Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(
						ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 03, 31), ДополнительныеПараметры);
				РасчетНалога1Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
				РасчетНалога2Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(
						ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 06, 30), ДополнительныеПараметры);
				РасчетНалога2Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
				РасчетНалога3Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(
						ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 09, 30), ДополнительныеПараметры);
				РасчетНалога3Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
			КонецЕсли;	
			
			// Если в каких-то регионах (ИФНС) не уплачиваются авансы, то для расчета налога к уплате за год нужны данные 2-го квартала.
			// Для этого из годового расчета вызываем "вложенный" расчет за 2-й квартал.
			// Взять готовые данные из регистра сведений нельзя, т.к. если авансы не уплачиваются, то и расчета за 2-й квартал нет.
			// ТаблицаРасчетТранспортногоНалога и РасчетНалога2Кв имеют одинаковый состав основных колонок, т.к. созданы по структуре регистра сведений. 
			Если ЕстьОбъектыБезУплатыАвансов И РассчитыватьОсвобождениеОтНалога Тогда
				
				ПараметрыРасчетаЗа2Кв = ОбщегоНазначения.СкопироватьРекурсивно(ДополнительныеПараметры);
				ПараметрыРасчетаЗа2Кв.Вставить("РасчетСуммыНалогаЗаПериодОсвобождения", Истина); // во вложенном расчете будут считаться авансы даже по тем объектам, по которым авансы не уплачиваются
				ПараметрыРасчетаЗа2Кв.Вставить("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде", Истина); // расчет за 2-й квартал 2020 года - действует освобождение 
				
				ТаблицыРасчетаНалогаКОсвобождению = ПолучитьРасчетПоТранспортномуНалогу(
					ПараметрыРасчета.Организация,
					Дата(Год(ПараметрыРасчета.ПериодРасчета), 06, 30),
					ПараметрыРасчетаЗа2Кв);
					
				РасчетНалогаКОсвобождению = ТаблицыРасчетаНалогаКОсвобождению.Налог;	
				РасчетНалогаКОсвобождению.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
			КонецЕсли;
			
		КонецЕсли;
				
		Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
			
			// Если расчет вызван для определения суммы освобождения от налога, то рассчитывать нужно только для тех ИФНС, 
			// где не уплачиваются авансы. В ином случае расчет будет взят из регистра без необходимости расчета.
			Если НЕ ПараметрыРасчета.ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы И НЕ РасчетСуммыНалогаЗаПериодОсвобождения Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаРасчета.УплачиваютсяАвансы И РасчетСуммыНалогаЗаПериодОсвобождения Тогда
				Продолжить;
			КонецЕсли;
			
			Если НетТСЗарегистрированныхВРеестреСистемыПлатон И СтрокаРасчета.ЗарегистрированоВРеестреСистемыПлатон Тогда
				НетТСЗарегистрированныхВРеестреСистемыПлатон = Ложь;
			КонецЕсли;
			
			// Для расчета используется та строка, в которой содержится информация о льготе.
			Если СтрокаРасчета.КоличествоЗаписейЛьгот = 1
			   И СтрокаРасчета.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется Тогда
				Продолжить;
			КонецЕсли;
			
			Если Год(ПараметрыРасчета.ДатаКонцаПериодаОтчета) > 2015 
			   И НачалоМесяца(СтрокаРасчета.ДатаРегистрации) = НачалоМесяца(СтрокаРасчета.ДатаПрекращенияРегистрации)
			   И ((День(СтрокаРасчета.ДатаРегистрации) <= 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) <= 15)
			       ИЛИ (День(СтрокаРасчета.ДатаРегистрации) > 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) > 15)) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета, ,
				"КодПоОКТМО, КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,
				|ПроцентУменьшения,ЛьготнаяСтавка,КоличествоЛетПрошедшихСГодаВыпускаТС");
			
			НоваяСтрока.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
			
			Если СтрокаРасчета.НалоговаяСтавкаЗависитОтГодаВыпускаТС Тогда
				НоваяСтрока.КоличествоЛетПрошедшихСГодаВыпускаТС = СтрокаРасчета.КоличествоЛетПрошедшихСГодаВыпускаТС;
			КонецЕсли;
						
			Если Год(ПараметрыРасчета.ДатаКонцаПериодаОтчета) > 2014 ИЛИ (ПараметрыРасчета.ГодовойРасчет И Год(ПараметрыРасчета.ДатаКонцаПериодаОтчета) = 2014) Тогда
				
				Если НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0 
					ИЛИ НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0 Тогда
					
					ДоляВПравеОбщейСобственности = 1;
					
				Иначе
					
					ДоляВПравеОбщейСобственности = НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель 
						/ НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель;
					
				КонецЕсли;
					
			Иначе
				
				НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
				НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0;
				ДоляВПравеОбщейСобственности = 1;
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПовышающийКоэффициент <> 0
				И (Год(ПараметрыРасчета.ДатаКонцаПериодаОтчета) >= 2015 ИЛИ (ПараметрыРасчета.ГодовойРасчет И Год(ПараметрыРасчета.ДатаКонцаПериодаОтчета) = 2014)) Тогда
				
				ПовышающийКоэффициент = СтрокаРасчета.ПовышающийКоэффициент;
				
			Иначе
				
				ПовышающийКоэффициент = 1;
				НоваяСтрока.ПовышающийКоэффициент = 0;
				
			КонецЕсли;
			
			КоличествоМесяцевВПериоде = ?(ПараметрыРасчета.ГодовойРасчет, 12, 3);
			
			НоваяСтрока.СуммаНалога = Окр(НоваяСтрока.НалоговаяБаза 
				* НоваяСтрока.НалоговаяСтавка 
				* ДоляВПравеОбщейСобственности 
				* НоваяСтрока.КоличествоМесяцевВладения / КоличествоМесяцевВПериоде // коэффициент в соответствии с п.3 ст. 362 НК в виде правильной простой дроби 
				* ПовышающийКоэффициент
				* ?(ПараметрыРасчета.ГодовойРасчет, 1, 1 / 4));
			
			НалоговаяЛьгота = СтрокаРасчета.НалоговаяЛьгота;
			
			Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется
				И НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда 
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета,
					"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,ПроцентУменьшения,ЛьготнаяСтавка");
				
				КоэффициентЛьготы = Окр(НоваяСтрока.КоличествоМесяцевЛьготы / 12, 4);
					
				Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы 
							* (НоваяСтрока.ПроцентУменьшения / 100)
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
						* (НоваяСтрока.НалоговаяСтавка - НоваяСтрока.ЛьготнаяСтавка)
						* КоэффициентЛьготы
						* ДоляВПравеОбщейСобственности
						* ПовышающийКоэффициент, 2));

				КонецЕсли;
				
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаНалоговойЛьготы;
						
			Если ПараметрыРасчета.ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				
				СуммаНалоговогоВычета1Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета1Кв = СуммаНалоговогоВычета1Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				СуммаНалоговогоВычета2Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					Если РассчитыватьОсвобождениеОтНалога Тогда
						// В 2020 году для "освобожденных" организаций аванс за 2-й квартал не уплачивается, 
						// а налог за год уменьшается на сумму аванса за 2-й квартал.
						// Т.к. расчет за 2-й квартал 2020 года мог быть выполнен по старому алгоритму (без расчета освобождения),
						// то учтем здесь оба варианта: 
						// - Либо СуммаНалогаКУплате за 2-й квартал уменьшена на сумму освобождения (т.е. равна 0),
						//   и тогда СуммаОсвобожденияОтУплатыНалога уже рассчитана и заполнена (новый алгоритм расчета)
						// - Либо СуммаНалогаКУплате за 2-й квартал не учитывает сумму освобождения, но тогда и СуммаОсвобожденияОтУплатыНалога = 0
						//   (старый алгоритм расчета). В этом случае СуммаНалогаКУплате за 2-й квартал - это и есть сумма освобождения.
						НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
							+ СтрокаРасчетаАвансовыхПлатежей.СуммаОсвобожденияОтУплатыНалога + СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					Иначе	
						НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
							+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
						СуммаНалоговогоВычета2Кв = СуммаНалоговогоВычета2Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
					КонецЕсли;
				КонецЦикла;
				
				СуммаНалоговогоВычета3Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета3Кв = СуммаНалоговогоВычета3Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				НоваяСтрока.СуммаНалоговогоВычета = СуммаНалоговогоВычета1Кв + СуммаНалоговогоВычета2Кв + СуммаНалоговогоВычета3Кв;
								
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв;
				
			КонецЕсли;

			Если РассчитыватьОсвобождениеОтНалога И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				СтрокиРасчетаНалогаКОсвобождению = РасчетНалогаКОсвобождению.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаНалогаКОсвобождению Из СтрокиРасчетаНалогаКОсвобождению Цикл
					// Если авансы не уплачиваются, то освобождение за 2-й квартал рассчитывается так же, 
					// как аванс за 2-й квартал, если бы он уплачивался.
					// СуммаОсвобожденияОтУплатыНалога уже получена в расчете налога за 2-й квартал.
					НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
						+ СтрокаРасчетаНалогаКОсвобождению.СуммаОсвобожденияОтУплатыНалога;
				КонецЦикла;
			КонецЕсли;	
			
			Если ЭтоПериодОсвобождения Тогда
				// Во 2-м квартале 2020 года организация освобождается от налога в размере полной суммы аванса к уплате. 
				НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
			// В 2020 году для некоторых организаций применяется разовая льгота в виде освобождения от налога 
			// за период владения объктом налогообложения в течение 2-го квартала 2020 года.
			// Для таких "освобожденных" организаций СуммаОсвобожденияОтУплатыНалога будет ненулевой 
			// в расчете за 2-й квартал 2020 года (если уплачиваются авансы) и в годовом расчете за 2020 год.
			// В всех остальных случаях СуммаОсвобожденияОтУплатыНалога = 0 и не влияет на расчет.
			// Уменьшим СуммаНалогаКУплате на сумму такого освобождения.
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаОсвобожденияОтУплатыНалога;
			
			Если НЕ ПараметрыРасчета.ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПараметрыРасчета.ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.Организация, "Организация");
	
	
	// Учет платы в систему "Платон".
	// Вычет действует в 2016 - 2018 годах.
	Если НетТСЗарегистрированныхВРеестреСистемыПлатон 
		ИЛИ Год(ПараметрыРасчета.ПериодРасчета) < 2016 
		ИЛИ Год(ПараметрыРасчета.ПериодРасчета) > 2018 Тогда
		
		РасходыПлатон = Неопределено;
	Иначе
		РасходыПлатон = РасчетИмущественныхНалоговПереопределяемый.УчестьРасходыНаПлатон(
			ПараметрыРасчета, ТаблицаРасчетНалога, ДополнительныеПараметры);
	КонецЕсли;
	
	Возврат Новый Структура("Налог, Платон", ТаблицаРасчетНалога, РасходыПлатон);

КонецФункции

Процедура ДополнитьРасчетТранспортногоНалога(ТаблицаРасчетНалога, Знач ДатаКонцаПериодаОтчета)
	
	ТаблицаРасчетНалога.Колонки.Добавить("КодПоОКТМОРегиона", 
		ОбщегоНазначения.ОписаниеТипаСтрока(8));
		
	ТаблицаРасчетНалога.Колонки.Добавить("КатегорияТС", 
		Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииТранспортныхСредств"));
		
	ТаблицаРасчетНалога.Колонки.Добавить("НалоговаяСтавкаЗависитОтГодаВыпускаТС", 
		Новый ОписаниеТипов("Булево"));
		
	ТаблицаРасчетНалога.Колонки.Добавить("ОтсутствуетСтавка", Новый ОписаниеТипов("Булево"));
		
	КодыВидовИКатегорииТС = КодыВидовИКатегорииТранспортныхСредств();
	
	Для Каждого СтрокаРасчета Из ТаблицаРасчетНалога Цикл
		
		СтрокаРасчета.КодПоОКТМОРегиона = РасчетИмущественныхНалоговКлиентСервер.КодПоОКТМОРегиона(СтрокаРасчета.КодПоОКТМО);
		
		НайденнаяСтрока = КодыВидовИКатегорииТС.Найти(СтрокаРасчета.КодВидаТранспортногоСредства, "КодВида");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаРасчета.КатегорияТС = Перечисления.КатегорииТранспортныхСредств[НайденнаяСтрока.Категория];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаРасчета.ДатаВыпуска) Тогда
			СтрокаРасчета.КоличествоЛетПрошедшихСГодаВыпускаТС = 
				Год(ДатаКонцаПериодаОтчета) - Год(СтрокаРасчета.ДатаВыпуска);
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборСтрок = Новый Структура("СтавкаОпределяетсяАвтоматически", Истина);
	КолонкиДляОпределенияСтавок = "КодПоОКТМОРегиона,КатегорияТС,НалоговаяБаза,КоличествоЛетПрошедшихСГодаВыпускаТС";
	ТаблицаДляОпределенияСтавок = ТаблицаРасчетНалога.Скопировать(ОтборСтрок, КолонкиДляОпределенияСтавок);
	ТаблицаДляОпределенияСтавок.Свернуть(КолонкиДляОпределенияСтавок);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДляОпределенияСтавок", ТаблицаДляОпределенияСтавок);
	Запрос.УстановитьПараметр("Период", ДатаКонцаПериодаОтчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДляОпределенияСтавок.КодПоОКТМОРегиона КАК КодПоОКТМОРегиона,
	|	ТаблицаДляОпределенияСтавок.КатегорияТС КАК КатегорияТС,
	|	ТаблицаДляОпределенияСтавок.НалоговаяБаза КАК НалоговаяБаза,
	|	ТаблицаДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|ПОМЕСТИТЬ ВТДляОпределенияСтавок
	|ИЗ
	|	&ТаблицаДляОпределенияСтавок КАК ТаблицаДляОпределенияСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиТранспортногоНалога.Период КАК Период,
	|	СтавкиТранспортногоНалога.ОКТМО КАК ОКТМО,
	|	СтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалога.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТДляОпределенияСтавок.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|
	|ПОМЕСТИТЬ ВТВсеПодходящиеЗаписи
	|
	|ИЗ
	|	ВТДляОпределенияСтавок КАК ВТДляОпределенияСтавок
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиТранспортногоНалога КАК СтавкиТранспортногоНалога
	|		ПО ВТДляОпределенияСтавок.КодПоОКТМОРегиона = СтавкиТранспортногоНалога.ОКТМО
	|			И ВТДляОпределенияСтавок.КатегорияТС = СтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения
	|			И ВТДляОпределенияСтавок.НалоговаяБаза >= СтавкиТранспортногоНалога.МинимальноеЗначениеМощности
	|			И (СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности = 0
	|				ИЛИ ВТДляОпределенияСтавок.НалоговаяБаза <= СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности)
	|			И (СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС = 0
	|				ИЛИ ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС > СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС)
	|			И (СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС = 0
	|				ИЛИ ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС <= СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС)
	|ГДЕ
	|	СтавкиТранспортногоНалога.Период < &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеПодходящиеЗаписи.Период) КАК Период,
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК ОКТМО,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|
	|ПОМЕСТИТЬ ВТПоследниеСтавки
	|
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.ОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК КодПоОКТМОРегиона,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК КатегорияТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС <> 0
	|		ИЛИ ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС <> 0 КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка
	|
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследниеСтавки КАК ВТПоследниеСтавки
	|		ПО ВТВсеПодходящиеЗаписи.Период = ВТПоследниеСтавки.Период 
	|			И ВТВсеПодходящиеЗаписи.ОКТМО = ВТПоследниеСтавки.ОКТМО
	|			И ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения = ВТПоследниеСтавки.НаименованиеОбъектаНалогообложения
	|			И ВТВсеПодходящиеЗаписи.НалоговаяБаза = ВТПоследниеСтавки.НалоговаяБаза
	|			И ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС = ВТПоследниеСтавки.КоличествоЛетПрошедшихСГодаВыпускаТС";
	
	ТаблицаСтавок = Запрос.Выполнить().Выгрузить();
	Отбор = Новый Структура(КолонкиДляОпределенияСтавок);
	Для Каждого СтрокаРасчета Из ТаблицаРасчетНалога Цикл
		
		Если НЕ СтрокаРасчета.СтавкаОпределяетсяАвтоматически Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаРасчета);
		
		НайденныеСтроки = ТаблицаСтавок.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаРасчета, НайденныеСтроки[0], , КолонкиДляОпределенияСтавок);
		Иначе
			СтрокаРасчета.ОтсутствуетСтавка = Истина;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ЗемельныйНалог
Функция ПолучитьДанныеДляРасчетаЗемельногоНалога(ПараметрыРасчета, ДополнительныеПараметры)
	
	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи(
		ПараметрыРасчета, 
		ПараметрыРасчета.ДатаКонцаПериодаОтчета, 
		ДополнительныеПараметры);
	
	ГодРасчета = Год(ПараметрыРасчета.ПериодРасчета);
	
	Если ПараметрыРасчета.ГодовойРасчет Тогда
		ПервыйМесяцРасчета = 1;
	Иначе
		ПервыйМесяцРасчета = Месяц(НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
	КонецЕсли;
		
	ВременныеТаблицыНаВремяРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	ТекстОбъединяющегоЗапроса = "";
	
	// Каждый отдельный месяц периода соотносится только с каким-то одним набором параметров расчета налога по участку.
	// Это обусловлено тем, что в расчете ФНС количество месяцев владения, которые характеризуют период действия 
	// каждого такого отдельного набора параметров, также исчисляются до целых месяцев.
	// В качестве момента фиксации берем 15 число месяца, т.к. именно оно определено как граница при определении 
	// периода владения (см. пп. 7, 7.1 ст. 396 НК)
	// В отношении периода действия льгот подход немного другой: месяц начала и месяц окончания применения льготы 
	// считаются месяцами, когда льгота отсутствует (абц. 5 п. 10 ст. 396 НК). При этом льготу, у которой начало периода
	// применения установлено на 1 число месяца (окончание периода установлено на последнее число месяца) считаем
	// действующей в данном месяце. 
	// Настройки, на основании которых вычисляется применение льготы в конкретном месяце, также считываем на 15 число
	// этого месяца. 
	
	Для Сч = ПервыйМесяцРасчета По Месяц(ПараметрыРасчета.ПериодРасчета) Цикл
		
		ИмяВременнойТаблицы = "Выборка" + Формат(Сч, "ЧЦ=2; ЧВН=");
		ВременныеТаблицыНаВремяРасчета.Добавить(ИмяВременнойТаблицы);
		ПредельнаяДатаВозникновенияПрава = Дата(ГодРасчета, Сч, 15, 23, 59, 59);
		
		СформироватьТаблицуРегистрацияЗемельныхУчастковСрезПоследних(
			ПараметрыРасчета, 
			ПредельнаяДатаВозникновенияПрава, 
			ДополнительныеПараметры);
		
		// По конкретному основному средству в конкретный период времени может применяться только один набор параметров.
		// (см. РС РегистрацияЗемельныхУчастков). Как следствие, конкретное ОС может быть учтено в конкретном месяце
		// только в одной ИФНС.
		// Считаем, что объекты, находящиеся одновременно в нескольких муниципальных образованиях (то есть одновременно
		// зарегистрированные в разных ИФНС с разными ОКТМО и со своей долей стоимости по каждому ОКТМО), являются редким
		// исключением. В этом случае преполагаем, что по участку в программе добавлено несколько ОС (по одному на каждое
		// муниципальное образование), и по каждому внесены свои данные в РС РегистрацияЗемельныхУчастков.
		// 
		// В сумме в течение отчетного периода по одному ОС могут применяться несколько наборов параметров расчета
		// (с кратностью до месяца). Чтобы данные могли быть записаны в регистр расчета, применяется универсальный
		// разделитель (измерение) ДатаСведений - это дата начала применения конкретного набора параметров расчета.
		// При определении ДатаСведений учитываются периоды записей в РС РегистрацияЗемельныхУчастков, а также периоды
		// действия льгот, указанные в записях РС (даты начала и даты окончания применения льгот также являются "граничными").
		// Дата сведений - это самая последняя среди дат, которые влияют на набор параметров расчета.
		// См. врем. табл. ДатыСведений
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.Период КАК Период,
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.КадастровыйНомер КАК КадастровыйНомер,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	РегистрацияЗемельныхУчастков.НалоговыйОрган КАК НалоговыйОрган,
		|	РегистрацияЗемельныхУчастков.КодПоОКТМО КАК КодПоОКТМО,
		|	РегистрацияЗемельныхУчастков.КБК КАК КБК,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.ОснованиеЛьготыПоНалоговойБазе КАК ОснованиеЛьготыПоНалоговойБазе,
		|	РегистрацияЗемельныхУчастков.НачалоДействияЛьготыПоНалоговойБазе КАК НачалоДействияЛьготыПоНалоговойБазе,
		|	РегистрацияЗемельныхУчастков.ОкончаниеДействияЛьготыПоНалоговойБазе КАК ОкончаниеДействияЛьготыПоНалоговойБазе,
		|	РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЧислитель КАК ДоляНеОблагаемойНалогомПлощадиЧислитель,
		|	РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЗнаменатель КАК ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
		|	РегистрацияЗемельныхУчастков.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
		|	РегистрацияЗемельныхУчастков.ОснованиеЛьготыСнижениеСуммыНалога КАК ОснованиеЛьготыСнижениеСуммыНалога,
		|	РегистрацияЗемельныхУчастков.НачалоДействияЛьготыСнижениеСуммыНалога КАК НачалоДействияЛьготыСнижениеСуммыНалога,
		|	РегистрацияЗемельныхУчастков.ОкончаниеДействияЛьготыСнижениеСуммыНалога КАК ОкончаниеДействияЛьготыСнижениеСуммыНалога,
		|	РегистрацияЗемельныхУчастков.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
		|	РегистрацияЗемельныхУчастков.ОснованиеЛьготыСнижениеСтавки КАК ОснованиеЛьготыСнижениеСтавки,
		|	РегистрацияЗемельныхУчастков.НачалоДействияЛьготыСнижениеСтавки КАК НачалоДействияЛьготыСнижениеСтавки,
		|	РегистрацияЗемельныхУчастков.ОкончаниеДействияЛьготыСнижениеСтавки КАК ОкончаниеДействияЛьготыСнижениеСтавки,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
		|
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
		|
		|ИЗ
		|	РегистрацияЗемельныхУчастков_СрезПоследних КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрацияЗемельныхУчастков.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаПоследнегоСнятияСРегистрационногоУчета
		|
		|ПОМЕСТИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ВладениеЗемельнымиУчастками.Период > РегистрацияЗемельныхУчастков.Период
		|			И (НЕ РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаРегистрацииЗемли
		|
		|ПОМЕСТИТЬ ДатыРегистрацииЗемли
		|
		|ИЗ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета КАК ДатыПоследнегоСнятияСРегистрационногоУчета
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|		ПО ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ДатыПоследнегоСнятияСРегистрационногоУчета.ДатаПоследнегоСнятияСРегистрационногоУчета < РегистрацияЗемельныхУчастков.Период
		|			И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВладениеЗемельнымиУчастками.Период КАК Период,
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	ВладениеЗемельнымиУчастками.КадастровыйНомер КАК КадастровыйНомер,
		|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ВладениеЗемельнымиУчастками.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
		|	ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
		|	ВладениеЗемельнымиУчастками.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ВладениеЗемельнымиУчастками.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ ВладениеЗемельнымиУчастками.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА ВладениеЗемельнымиУчастками.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	ВладениеЗемельнымиУчастками.КБК КАК КБК,
		|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыПоНалоговойБазе КАК ОснованиеЛьготыПоНалоговойБазе,
		|
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе > ВладениеЗемельнымиУчастками.Период
		|				И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе > 
		|					НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе > ВладениеЗемельнымиУчастками.Период
		|			ТОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе <=
		|				НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ)
		|			ТОГДА ВладениеЗемельнымиУчастками.Период
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ, 1), МЕСЯЦ)
		|	КОНЕЦ КАК НачалоДействияЛьготыПоНалоговойБазе,
		|
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе
		|	КОНЕЦ КАК ОкончаниеДействияЛьготыПоНалоговойБазе,
		|
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыПоНалоговойБазе <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыПоНалоговойБазе <= &НачалоМесяца
		|		И (ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыПоНалоговойБазе >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьготаПоНалоговойБазе,
		|
		|	ЕСТЬNULL(ВладениеЗемельнымиУчастками.ОснованиеЛьготыПоНалоговойБазе.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготыПоНалоговойБазе,
		|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЧислитель КАК ДоляНеОблагаемойНалогомПлощадиЧислитель,
		|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЗнаменатель КАК ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
		|	ВладениеЗемельнымиУчастками.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога КАК ОснованиеЛьготыСнижениеСуммыНалога,
		|	
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога > ВладениеЗемельнымиУчастками.Период
		|				И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога > 
		|					НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога > ВладениеЗемельнымиУчастками.Период
		|			ТОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога <=
		|				НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ)
		|			ТОГДА ВладениеЗемельнымиУчастками.Период
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ, 1), МЕСЯЦ)
		|	КОНЕЦ КАК НачалоДействияЛьготыСнижениеСуммыНалога,
		|
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога
		|	КОНЕЦ КАК ОкончаниеДействияЛьготыСнижениеСуммыНалога,
		|
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСуммыНалога <= &НачалоМесяца
		|		И (ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСуммыНалога >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьготаСнижениеСуммыНалога,
		|
		|	ЕСТЬNULL(ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготыСнижениеСуммыНалога,
		|	ЕСТЬNULL(ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога.ПроцентУменьшения, 0) КАК ПроцентУменьшенияСуммыНалога,		
		|	ВладениеЗемельнымиУчастками.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСтавки КАК ОснованиеЛьготыСнижениеСтавки,
		|
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки > ВладениеЗемельнымиУчастками.Период
		|				И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки > 
		|					НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки, МЕСЯЦ, 1), МЕСЯЦ)
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки > ВладениеЗемельнымиУчастками.Период
		|			ТОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки
		|		КОГДА ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки <=
		|				НАЧАЛОПЕРИОДА(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ)
		|			ТОГДА ВладениеЗемельнымиУчастками.Период
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.Период, МЕСЯЦ, 1), МЕСЯЦ)
		|	КОНЕЦ КАК НачалоДействияЛьготыСнижениеСтавки,
		|
		|	ВЫБОР 
		|		КОГДА ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки, ДЕНЬ) < 
		|				КОНЕЦПЕРИОДА(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки, МЕСЯЦ)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки
		|	КОНЕЦ КАК ОкончаниеДействияЛьготыСнижениеСтавки,
		|
		|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСтавки <> ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|		И ВладениеЗемельнымиУчастками.НачалоДействияЛьготыСнижениеСтавки <= &НачалоМесяца
		|		И (ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВладениеЗемельнымиУчастками.ОкончаниеДействияЛьготыСнижениеСтавки >= НАЧАЛОПЕРИОДА(&КонецМесяца, ДЕНЬ)) КАК ДействуетЛьготаСнижениеСтавки,
		|
		|	ЕСТЬNULL(ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСтавки.ВидЛьготы, ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)) КАК ВидЛьготыСнижениеСтавки,
		|	ВладениеЗемельнымиУчастками.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	ДатыРегистрацииЗемли.ДатаРегистрацииЗемли КАК ДатаРегистрацииЗемли,
		|
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ЖилищноеСтроительство
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|						ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|						ТОГДА 
		|							ВЫБОР
		|								КОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3) <= &Период
		|									ТОГДА ""2""
		|								ИНАЧЕ ""1""
		|							КОНЕЦ
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПериодСтроительства,
		|
		|	ВЫБОР
		|		КОГДА НЕ ВладениеЗемельнымиУчастками.ЖилищноеСтроительство
		|			ИЛИ (ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости <> ДАТАВРЕМЯ(1, 1, 1)
		|			И ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости < ВладениеЗемельнымиУчастками.Период)
		|			ТОГДА ВладениеЗемельнымиУчастками.Период
		|		КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости
		|			МЕЖДУ ВладениеЗемельнымиУчастками.Период И &НачалоМесяца 
		|			ТОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости
		|		КОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3)
		|			МЕЖДУ ВладениеЗемельнымиУчастками.Период И &Период
		|			ТОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3)
		|		ИНАЧЕ ВладениеЗемельнымиУчастками.Период
		|	КОНЕЦ КАК ДатаИзмененияПериодаСтроительства
		|
		|ПОМЕСТИТЬ ВТ_ПромежуточныеДанные
		|
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыРегистрацииЗемли КАК ДатыРегистрацииЗемли
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = ДатыРегистрацииЗемли.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(ДатыИзменений.ДатаИзменений) КАК ДатаСведений
		|ПОМЕСТИТЬ ДатыСведений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство КАК ОсновноеСредство,
		|		ВТ_ПромежуточныеДанные.Период КАК ДатаИзменений
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство,
		|		ВЫБОР
		|			КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|				ТОГДА ВТ_ПромежуточныеДанные.НачалоДействияЛьготыПоНалоговойБазе
		|			КОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыПоНалоговойБазе <> ДАТАВРЕМЯ(1,1,1)
		|				И ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыПоНалоговойБазе <= &КонецМесяца
		|				ТОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыПоНалоговойБазе
		|			ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|		КОНЕЦ
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство,
		|		ВЫБОР
		|			КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|				И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|				ТОГДА ДАТАВРЕМЯ(1,1,1)
		|			КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|				ТОГДА ВТ_ПромежуточныеДанные.НачалоДействияЛьготыСнижениеСуммыНалога
		|			КОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСуммыНалога <> ДАТАВРЕМЯ(1,1,1)
		|				И ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСуммыНалога <= &КонецМесяца
		|				ТОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСуммыНалога
		|			ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|		КОНЕЦ
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство,
		|		ВЫБОР
		|			КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|				И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение)
		|				ТОГДА ДАТАВРЕМЯ(1,1,1)
		|			КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСтавки
		|				ТОГДА ВТ_ПромежуточныеДанные.НачалоДействияЛьготыСнижениеСтавки
		|			КОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСтавки <> ДАТАВРЕМЯ(1,1,1)
		|				И ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСтавки <= &КонецМесяца
		|				ТОГДА ВТ_ПромежуточныеДанные.ОкончаниеДействияЛьготыСнижениеСтавки
		|			ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|		КОНЕЦ
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство,
		|		ВТ_ПромежуточныеДанные.ДатаИзмененияПериодаСтроительства
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ВТ_ПромежуточныеДанные.ОсновноеСредство,
		|		&ДатаПоследнейРегистрации
		|	ИЗ
		|		ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|	ГДЕ 
		|		ВТ_ПромежуточныеДанные.ПостановкаНаУчетВНалоговомОргане В (ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации),
		|																	ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО))		
		|	) КАК ДатыИзменений
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыИзменений.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыСведений.ДатаСведений КАК ДатаСведений,
		|	ВТ_ПромежуточныеДанные.ОсновноеСредство КАК ОсновноеСредство,
		|	ВТ_ПромежуточныеДанные.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	ВТ_ПромежуточныеДанные.КадастровыйНомер КАК КадастровыйНомер,
		|	ВТ_ПромежуточныеДанные.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ВТ_ПромежуточныеДанные.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВТ_ПромежуточныеДанные.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ВТ_ПромежуточныеДанные.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
		|	ВТ_ПромежуточныеДанные.ИФНС КАК ИФНС,
		|	ВТ_ПромежуточныеДанные.КодПоОКТМО КАК КодПоОКТМО,
		|	ВТ_ПромежуточныеДанные.КБК КАК КБК,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			ТОГДА ВТ_ПромежуточныеДанные.ОснованиеЛьготыПоНалоговойБазе
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ОснованиеЛьготыПоНалоговойБазе,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			ТОГДА ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ВидЛьготыПоНалоговойБазе,
		|
		|	ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение) КАК ОсвобождениеОтНалогообложения,
		|
		|	ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ДоляНеоблагаемойПлощади) КАК УменьшениеОблагаемойПлощадиНаДолю,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ДоляНеоблагаемойПлощади)
		|			ТОГДА ВТ_ПромежуточныеДанные.ДоляНеОблагаемойНалогомПлощадиЧислитель
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДоляНеОблагаемойНалогомПлощадиЧислитель,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ДоляНеоблагаемойПлощади)
		|			ТОГДА ВТ_ПромежуточныеДанные.ДоляНеОблагаемойНалогомПлощадиЗнаменатель
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
		|
		|	ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеНалоговойБазыНаСумму) КАК УменьшениеНалоговойБазыНаСумму,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаПоНалоговойБазе
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыПоНалоговойБазе = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеНалоговойБазыНаСумму)
		|			ТОГДА ВТ_ПромежуточныеДанные.НеОблагаемаяНалогомСумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НеОблагаемаяНалогомСумма,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			ТОГДА ВТ_ПромежуточныеДанные.ОснованиеЛьготыСнижениеСуммыНалога
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ОснованиеЛьготыСнижениеСуммыНалога,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			ТОГДА ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСуммыНалога
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ВидЛьготыСнижениеСуммыНалога,
		|
		|	ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСуммыНалога = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент) КАК УменьшениеСуммыНалогаНаПроцент,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСуммыНалога = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент)
		|			ТОГДА ВТ_ПромежуточныеДанные.ПроцентУменьшенияСуммыНалога
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПроцентУменьшенияСуммыНалога,
		|
		|	ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСуммыНалога = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму) КАК УменьшениеСуммыНалогаНаСумму,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСуммыНалога
		|			И ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСуммыНалога = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму)
		|			ТОГДА ВТ_ПромежуточныеДанные.СуммаУменьшенияСуммыНалога
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаУменьшенияСуммыНалога,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСтавки
		|			ТОГДА ВТ_ПромежуточныеДанные.ОснованиеЛьготыСнижениеСтавки
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ОснованиеЛьготыСнижениеСтавки,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСтавки
		|			ТОГДА ВТ_ПромежуточныеДанные.ВидЛьготыСнижениеСтавки
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка)
		|	КОНЕЦ КАК ВидЛьготыСнижениеСтавки,
		|
		|	ВЫБОР
		|		КОГДА ВТ_ПромежуточныеДанные.ДействуетЛьготаСнижениеСтавки
		|			ТОГДА ВТ_ПромежуточныеДанные.СниженнаяНалоговаяСтавка
		|		ИНАЧЕ ВТ_ПромежуточныеДанные.НалоговаяСтавка
		|	КОНЕЦ КАК НалоговаяСтавка,
		|
		|	ВТ_ПромежуточныеДанные.ДатаРегистрацииЗемли КАК ДатаРегистрацииЗемли,
		|	ВТ_ПромежуточныеДанные.ПериодСтроительства КАК ПериодСтроительства,
		|	1 КАК КоличествоМесяцевИспользования,
		|	ВЫБОР
		|		КОГДА &НомерМесяца <= 3
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв1,
		|	ВЫБОР
		|		КОГДА &НомерМесяца > 3 И &НомерМесяца <= 6
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв2,
		|	ВЫБОР
		|		КОГДА &НомерМесяца > 6 И &НомерМесяца <= 9
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевИспользованияКв3
		|
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчасткамиПериод
		|
		|ИЗ
		|	ВТ_ПромежуточныеДанные КАК ВТ_ПромежуточныеДанные
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыСведений КАК ДатыСведений
		|		ПО ВТ_ПромежуточныеДанные.ОсновноеСредство = ДатыСведений.ОсновноеСредство";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ВладениеЗемельнымиУчасткамиПериод", "ПОМЕСТИТЬ " + ИмяВременнойТаблицы);
		
		ПараметрыРегистрацииВФНС = Новый Структура;
		ПараметрыРегистрацииВФНС.Вставить("ОсновнойНалоговыйОрган", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
		ПараметрыРегистрацииВФНС.Вставить("КодПоОКТМО", "");
		ПараметрыРегистрацииВФНС.Вставить("ДатаПоследнейРегистрации", '00010101');
	
		РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыРегистрацииОрганизацииВФНС(
			ПараметрыРегистрацииВФНС,
			ПараметрыРасчета.Организация,
			ПредельнаяДатаВозникновенияПрава);
			
		Запрос.УстановитьПараметр("Период", ПредельнаяДатаВозникновенияПрава);
		Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРегистрацииВФНС.ОсновнойНалоговыйОрган);
		Запрос.УстановитьПараметр("КодПоОКТМО", ПараметрыРегистрацииВФНС.КодПоОКТМО);
		Запрос.УстановитьПараметр("ДатаПоследнейРегистрации", ПараметрыРегистрацииВФНС.ДатаПоследнейРегистрации);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПредельнаяДатаВозникновенияПрава));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПредельнаяДатаВозникновенияПрава));
		Запрос.УстановитьПараметр("НомерМесяца", Сч);
		
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ВременныеТаблицыНаВремяРасчета);

		Если ПустаяСтрока(ТекстОбъединяющегоЗапроса) Тогда
			ТекстОбъединяющегоЗапроса = 
			"ВЫБРАТЬ *
			|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
			|ИЗ
			|	" + ИмяВременнойТаблицы;
		Иначе
			ТекстОбъединяющегоЗапроса = 
				ТекстОбъединяющегоЗапроса
				+ "
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ *
				|ИЗ
				|	" + ИмяВременнойТаблицы;
		КонецЕсли;
				
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		ПараметрыРасчета.МенеджерВременныхТаблиц,
		ПараметрыРасчета.Организация,
		ПараметрыРасчета.ПериодРасчета,
		"ЗемельныйНалог");
	
	// Получаем итоговые данные за отчетный период.
	//
	// 1. ВТ_ДанныеРасчета
	// Группируем данные по всем полям, которые составляют набор сведений для каждого отдельного расчета (по конкретному
	// участку может быть несколько расчетов; каждый из них действует только часть периода (см. КоличествоМесяцевИспользования);
	// общая сумма налога по участку будет равна сумме итогов по этим отдельным расчетам).
	// При этом нужно понимать, что каждый такой набор будет отличаться как минимум полем ДатаСведений.
	// Поэтому в дальнейшем при записи данных в РС РасчетЗемельногоНалога дублей не возникнет.
	// Теоретически могут быть несколько наборов, которые отличаются только полем ДатаСведений (пользователь ввел несколько
	// одинаковых записей по одному ОС в РС РегистрацияЗемельныхУчастков). В этом случае берем самую раннюю ДатаСведений.
	// 
	// 2. БлижайшиеСнятияСУчета 
	// Уже в сводных данных для каждой даты регистрации участка определяем следующую после нее дату снятия с учета (если есть).
	// Таком образом определяем границы периода владения (требуются для заполнения отчетных форм).
	//
	// 3. Итоговый запрос
	// Определяем, в каких ИФНС уплачиваются авансы
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВладениеЗемельнымиУчастками.ДатаСведений) КАК ДатаСведений,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВладениеЗемельнымиУчастками.КадастровыйНомер КАК КадастровыйНомер,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
	|	ВладениеЗемельнымиУчастками.ИФНС КАК ИФНС,
	|	ВладениеЗемельнымиУчастками.КодПоОКТМО КАК КодПоОКТМО,
	|	ВладениеЗемельнымиУчастками.КБК КАК КБК,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыПоНалоговойБазе КАК ОснованиеЛьготыПоНалоговойБазе,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыПоНалоговойБазе КАК ВидЛьготыПоНалоговойБазе,
	|	ВладениеЗемельнымиУчастками.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	ВладениеЗемельнымиУчастками.УменьшениеОблагаемойПлощадиНаДолю КАК УменьшениеОблагаемойПлощадиНаДолю,
	|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЧислитель КАК ДоляНеОблагаемойНалогомПлощадиЧислитель,
	|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЗнаменатель КАК ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
	|	ВладениеЗемельнымиУчастками.УменьшениеНалоговойБазыНаСумму КАК УменьшениеНалоговойБазыНаСумму,
	|	ВладениеЗемельнымиУчастками.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога КАК ОснованиеЛьготыСнижениеСуммыНалога,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыСнижениеСуммыНалога КАК ВидЛьготыСнижениеСуммыНалога,
	|	ВладениеЗемельнымиУчастками.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	ВладениеЗемельнымиУчастками.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВладениеЗемельнымиУчастками.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	ВладениеЗемельнымиУчастками.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСтавки КАК ОснованиеЛьготыСнижениеСтавки,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыСнижениеСтавки КАК ВидЛьготыСнижениеСтавки,
	|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВладениеЗемельнымиУчастками.ДатаРегистрацииЗемли КАК ДатаРегистрацииЗемли,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевИспользования) КАК КоличествоМесяцевИспользования,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевИспользованияКв1) КАК КоличествоМесяцевИспользованияКв1,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевИспользованияКв2) КАК КоличествоМесяцевИспользованияКв2,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевИспользованияКв3) КАК КоличествоМесяцевИспользованияКв3
	|ПОМЕСТИТЬ ВТ_ДанныеРасчета
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель,
	|	ВладениеЗемельнымиУчастками.КадастровыйНомер,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость,
	|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВладениеЗемельнымиУчастками.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости,
	|	ВладениеЗемельнымиУчастками.ИФНС,
	|	ВладениеЗемельнымиУчастками.КодПоОКТМО,
	|	ВладениеЗемельнымиУчастками.КБК,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыПоНалоговойБазе,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыПоНалоговойБазе,
	|	ВладениеЗемельнымиУчастками.ОсвобождениеОтНалогообложения,
	|	ВладениеЗемельнымиУчастками.УменьшениеОблагаемойПлощадиНаДолю,
	|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЧислитель,
	|	ВладениеЗемельнымиУчастками.ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
	|	ВладениеЗемельнымиУчастками.УменьшениеНалоговойБазыНаСумму,
	|	ВладениеЗемельнымиУчастками.НеОблагаемаяНалогомСумма,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСуммыНалога,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыСнижениеСуммыНалога,
	|	ВладениеЗемельнымиУчастками.УменьшениеСуммыНалогаНаПроцент,
	|	ВладениеЗемельнымиУчастками.ПроцентУменьшенияСуммыНалога,
	|	ВладениеЗемельнымиУчастками.УменьшениеСуммыНалогаНаСумму,
	|	ВладениеЗемельнымиУчастками.СуммаУменьшенияСуммыНалога,
	|	ВладениеЗемельнымиУчастками.ОснованиеЛьготыСнижениеСтавки,
	|	ВладениеЗемельнымиУчастками.ВидЛьготыСнижениеСтавки,
	|	ВладениеЗемельнымиУчастками.НалоговаяСтавка,
	|	ВладениеЗемельнымиУчастками.ДатаРегистрацииЗемли,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеРасчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ДанныеРасчета.ДатаРегистрацииЗемли КАК ДатаРегистрацииЗемли,
	|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаСнятияСУчета
	|ПОМЕСТИТЬ БлижайшиеСнятияСУчета
	|ИЗ
	|	ВТ_ДанныеРасчета КАК ВТ_ДанныеРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
	|		ПО ВТ_ДанныеРасчета.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
	|		И ВТ_ДанныеРасчета.ДатаРегистрацииЗемли < РегистрацияЗемельныхУчастков.Период
	|ГДЕ
	|	НЕ РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеРасчета.ОсновноеСредство,
	|	ВТ_ДанныеРасчета.ДатаРегистрацииЗемли
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаРегистрацииЗемли
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеРасчета.ДатаСведений КАК ДатаСведений,
	|	ВТ_ДанныеРасчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ДанныеРасчета.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВТ_ДанныеРасчета.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_ДанныеРасчета.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВТ_ДанныеРасчета.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_ДанныеРасчета.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_ДанныеРасчета.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
	|	ВТ_ДанныеРасчета.ИФНС КАК ИФНС,
	|	ВТ_ДанныеРасчета.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_ДанныеРасчета.КБК КАК КБК,
	|	ВТ_ДанныеРасчета.ОснованиеЛьготыПоНалоговойБазе КАК ОснованиеЛьготыПоНалоговойБазе,
	|	ВТ_ДанныеРасчета.ВидЛьготыПоНалоговойБазе КАК ВидЛьготыПоНалоговойБазе,
	|	ВТ_ДанныеРасчета.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	ВТ_ДанныеРасчета.УменьшениеОблагаемойПлощадиНаДолю КАК УменьшениеОблагаемойПлощадиНаДолю,
	|	ВТ_ДанныеРасчета.ДоляНеОблагаемойНалогомПлощадиЧислитель КАК ДоляНеОблагаемойПлощадиЧислитель,
	|	ВТ_ДанныеРасчета.ДоляНеОблагаемойНалогомПлощадиЗнаменатель КАК ДоляНеОблагаемойПлощадиЗнаменатель,
	|	ВТ_ДанныеРасчета.УменьшениеНалоговойБазыНаСумму КАК УменьшениеНалоговойБазыНаСумму,
	|	ВТ_ДанныеРасчета.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
	|	ВТ_ДанныеРасчета.ОснованиеЛьготыСнижениеСуммыНалога КАК ОснованиеЛьготыСнижениеСуммыНалога,
	|	ВТ_ДанныеРасчета.ВидЛьготыСнижениеСуммыНалога КАК ВидЛьготыСнижениеСуммыНалога,
	|	ВТ_ДанныеРасчета.УменьшениеСуммыНалогаНаПроцент КАК УменьшениеСуммыНалогаНаПроцент,
	|	ВТ_ДанныеРасчета.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВТ_ДанныеРасчета.УменьшениеСуммыНалогаНаСумму КАК УменьшениеСуммыНалогаНаСумму,
	|	ВТ_ДанныеРасчета.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
	|	ВТ_ДанныеРасчета.ОснованиеЛьготыСнижениеСтавки КАК ОснованиеЛьготыСнижениеСтавки,
	|	ВТ_ДанныеРасчета.ВидЛьготыСнижениеСтавки КАК ВидЛьготыСнижениеСтавки,
	|	ВТ_ДанныеРасчета.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_ДанныеРасчета.ДатаРегистрацииЗемли КАК ДатаРегистрации,
	|	ЕСТЬNULL(БлижайшиеСнятияСУчета.ДатаСнятияСУчета, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСнятияСУчета,
	|	ВТ_ДанныеРасчета.ПериодСтроительства КАК ПериодСтроительства,
	|	ВТ_ДанныеРасчета.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	ВТ_ДанныеРасчета.КоличествоМесяцевИспользованияКв1 КАК КоличествоМесяцевИспользованияКв1,
	|	ВТ_ДанныеРасчета.КоличествоМесяцевИспользованияКв2 КАК КоличествоМесяцевИспользованияКв2,
	|	ВТ_ДанныеРасчета.КоличествоМесяцевИспользованияКв3 КАК КоличествоМесяцевИспользованияКв3,
	|	НЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_ДанныеРасчета КАК ВТ_ДанныеРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ БлижайшиеСнятияСУчета КАК БлижайшиеСнятияСУчета
	|		ПО ВТ_ДанныеРасчета.ОсновноеСредство = БлижайшиеСнятияСУчета.ОсновноеСредство
	|		И ВТ_ДанныеРасчета.ДатаРегистрацииЗемли = БлижайшиеСнятияСУчета.ДатаРегистрацииЗемли
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО ВТ_ДанныеРасчета.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";	
	
	ДанныеДляРасчета = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
		СтрокаРасчета.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
	КонецЦикла;
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Процедура ВыполнитьРасчетЗемельногоНалога(ТаблицаРасчетНалога, ПараметрыРасчета, ДанныеДляРасчета, ДополнительныеПараметры)

	Если ДанныеДляРасчета = Неопределено
		 Или ДанныеДляРасчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоМесяцевВПериоде = ?(ПараметрыРасчета.ГодовойРасчет, Месяц(ПараметрыРасчета.ПериодРасчета), 3);
	
	// Структура для формирования представления льгот
	ПараметрыЛьготы = ЛьготыПоИмущественнымНалогамКлиентСервер.НовыеПараметрыОписанияЛьготыПоЗемельномуНалогу();
	
	Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
		
		Если Не ПараметрыРасчета.ГодовойРасчет И Не СтрокаРасчета.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы;
		
		НоваяСтрока = ТаблицаРасчетНалога.Добавить(); 
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета);
		
		Если НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель <> 0
			 И НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
			ДоляВПравеОбщейСобственности = 
				Мин(1, НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель / НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель);
		Иначе
			ДоляВПравеОбщейСобственности = 1;
		КонецЕсли;
		
		// Определение повышающего коэффициента
		КоэффициентПериодаСтроительства = 1;
		Если СтрокаРасчета.ПериодСтроительства = "2" Тогда
			КоэффициентПериодаСтроительства = 4;
		ИначеЕсли СтрокаРасчета.ПериодСтроительства = "1" Тогда
			КоэффициентПериодаСтроительства = 2;
		КонецЕсли;
		
		НоваяСтрока.ПовышающийКоэффициент = КоэффициентПериодаСтроительства;
		
		// Рассчитаем сумму налога без учета льгот (в т.ч. без учета необлагаемой кадастровой стоимости) - требуется для сверки
		// с ФНС по сообщению об исчисленных суммах налогов
		НоваяСтрока.СуммаНалогаБезЛьгот = СуммаЗемельногоНалога(
			НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности,
			НоваяСтрока.НалоговаяСтавка,
			КоэффициентПериодаСтроительства,
			НоваяСтрока.КоличествоМесяцевИспользования,
			КоличествоМесяцевВПериоде,
			ПараметрыРасчета.ГодовойРасчет);
		
		// Посчитаем сумму льгот и сумму налога с учетом льгот. Освобождение не сочетается с другими льготами. Остальные 
		// льготы могут комбинироваться (хотя на практике такие случаи маловероятны).
		Если СтрокаРасчета.ОсвобождениеОтНалогообложения Тогда
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость;
			НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаНалогаБезЛьгот;
			НоваяСтрока.СуммаНалога = 0;
		Иначе
			// Снижение налоговой базы
			Если СтрокаРасчета.УменьшениеНалоговойБазыНаСумму И СтрокаРасчета.НеОблагаемаяНалогомСумма > 0 Тогда
				НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = СтрокаРасчета.НеОблагаемаяНалогомСумма;
				// В расчете льготы не учитываем долю в праве общей собственности, т.к. сумма налогового вычета
				// (СтрокаРасчета.НеОблагаемаяНалогомСумма) уже относится исключительно к доле налогоплательщика
				НоваяСтрока.СуммаЛьгот = СуммаЗемельногоНалога(
					НоваяСтрока.НеоблагаемаяКадастроваяСтоимость,
					НоваяСтрока.НалоговаяСтавка,
					КоэффициентПериодаСтроительства,
					НоваяСтрока.КоличествоМесяцевИспользования,
					КоличествоМесяцевВПериоде,
					ПараметрыРасчета.ГодовойРасчет);
			ИначеЕсли СтрокаРасчета.УменьшениеОблагаемойПлощадиНаДолю
				 И СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель <> 0
				 И СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель <> 0 Тогда
				ДоляНеоблагаемойПлощади = 
					Мин(1, СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель / СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель);
				НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость 
					* ДоляВПравеОбщейСобственности * ДоляНеоблагаемойПлощади;
				НоваяСтрока.СуммаЛьгот = СуммаЗемельногоНалога(
					НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности * ДоляНеоблагаемойПлощади,
					НоваяСтрока.НалоговаяСтавка,
					КоэффициентПериодаСтроительства,
					НоваяСтрока.КоличествоМесяцевИспользования,
					КоличествоМесяцевВПериоде,
					ПараметрыРасчета.ГодовойРасчет);
			КонецЕсли;	
			
			// Уменьшение суммы налога на процент
			Если СтрокаРасчета.УменьшениеСуммыНалогаНаПроцент Тогда
				СуммаЛьготы = 
					Окр(Макс(НоваяСтрока.СуммаНалогаБезЛьгот - НоваяСтрока.СуммаЛьгот, 0) // сумма налога за вычетом всех других льгот
					* СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
				НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаЛьгот + СуммаЛьготы;
			КонецЕсли;
			
			// Уменьшение налога на фиксированную сумму
			Если СтрокаРасчета.УменьшениеСуммыНалогаНаСумму Тогда
				СуммаЛьготы = Окр(СтрокаРасчета.СуммаУменьшенияСуммыНалога / ?(ПараметрыРасчета.ГодовойРасчет, 1, 4));
				НоваяСтрока.СуммаЛьгот = НоваяСтрока.СуммаЛьгот + СуммаЛьготы;
			КонецЕсли;
			
			// Сумма льгот не может превышать сумму налога
			НоваяСтрока.СуммаЛьгот = Мин(НоваяСтрока.СуммаНалогаБезЛьгот, НоваяСтрока.СуммаЛьгот);
			НоваяСтрока.СуммаНалога = НоваяСтрока.СуммаНалогаБезЛьгот - НоваяСтрока.СуммаЛьгот;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, СтрокаРасчета);
		ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЧислитель = СтрокаРасчета.ДоляНеОблагаемойПлощадиЧислитель;
		ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель = СтрокаРасчета.ДоляНеОблагаемойПлощадиЗнаменатель;
		ПараметрыЛьготы.СниженнаяНалоговаяСтавка = СтрокаРасчета.НалоговаяСтавка;
	
		НоваяСтрока.ПредставлениеНалоговойЛьготы =
			ЛьготыПоИмущественнымНалогамКлиентСервер.ПредставлениеНалоговойЛьготыПоЗемельномуНалогу(ПараметрыЛьготы);
		
		Если УчитыватьАвансы Тогда
			
			Если СтрокаРасчета.ОсвобождениеОтНалогообложения Тогда
				
				СуммаАвансовыхПлатежейКв1 = 0;
				СуммаАвансовыхПлатежейКв2 = 0;
				СуммаАвансовыхПлатежейКв3 = 0;
				
			Иначе
				
				СуммаНалогаБезЛьготКв1 = СуммаЗемельногоНалога(
					НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					КоэффициентПериодаСтроительства,
					СтрокаРасчета.КоличествоМесяцевИспользованияКв1,
					3,
					Ложь);
				
				СуммаНалогаБезЛьготКв2 = СуммаЗемельногоНалога(
					НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					КоэффициентПериодаСтроительства,
					СтрокаРасчета.КоличествоМесяцевИспользованияКв2,
					3,
					Ложь);
				
				СуммаНалогаБезЛьготКв3 = СуммаЗемельногоНалога(
					НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности,
					НоваяСтрока.НалоговаяСтавка,
					КоэффициентПериодаСтроительства,
					СтрокаРасчета.КоличествоМесяцевИспользованияКв3,
					3,
					Ложь);
					
				СуммаЛьготКв1 = 0;
				СуммаЛьготКв2 = 0;
				СуммаЛьготКв3 = 0;
				
				Если СтрокаРасчета.УменьшениеНалоговойБазыНаСумму И СтрокаРасчета.НеОблагаемаяНалогомСумма > 0 Тогда

					СуммаЛьготКв1 = СуммаЗемельногоНалога(
						НоваяСтрока.НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв1,
						3,
						Ложь);
						
					СуммаЛьготКв2 = СуммаЗемельногоНалога(
						НоваяСтрока.НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв2,
						3,
						Ложь);
						
					СуммаЛьготКв3 = СуммаЗемельногоНалога(
						НоваяСтрока.НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв3,
						3,
						Ложь);
				
				ИначеЕсли СтрокаРасчета.УменьшениеОблагаемойПлощадиНаДолю
					 И СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель <> 0
					 И СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель <> 0 Тогда

					// Не используем рассчитанное выше значение НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, чтобы
					// не было искажения из-за округления
					НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость
						* ДоляВПравеОбщейСобственности * ДоляНеоблагаемойПлощади;
					СуммаЛьготКв1 = СуммаЗемельногоНалога(
						НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв1,
						3,
						Ложь);
						
					СуммаЛьготКв2 = СуммаЗемельногоНалога(
						НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв2,
						3,
						Ложь);
					
					СуммаЛьготКв3 = СуммаЗемельногоНалога(
						НеоблагаемаяКадастроваяСтоимость,
						НоваяСтрока.НалоговаяСтавка,
						КоэффициентПериодаСтроительства,
						СтрокаРасчета.КоличествоМесяцевИспользованияКв3,
						3,
						Ложь);
						
				КонецЕсли;	
				
				Если СтрокаРасчета.УменьшениеСуммыНалогаНаПроцент Тогда
					СуммаЛьготКв1 = СуммаЛьготКв1 
						+ Окр(Макс(СуммаНалогаБезЛьготКв1 - СуммаЛьготКв1, 0) * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
					СуммаЛьготКв2 = СуммаЛьготКв2
						+ Окр(Макс(СуммаНалогаБезЛьготКв2 - СуммаЛьготКв2, 0) * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
					СуммаЛьготКв3 = СуммаЛьготКв3
						+ Окр(Макс(СуммаНалогаБезЛьготКв3 - СуммаЛьготКв3, 0) * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
				КонецЕсли;
				
				Если СтрокаРасчета.УменьшениеСуммыНалогаНаСумму Тогда
					СуммаЛьготы = Окр(СтрокаРасчета.СуммаУменьшенияСуммыНалога / 4);
					СуммаЛьготКв1 = СуммаЛьготКв1 + СуммаЛьготы;
					СуммаЛьготКв2 = СуммаЛьготКв2 + СуммаЛьготы;
					СуммаЛьготКв3 = СуммаЛьготКв3 + СуммаЛьготы;
				КонецЕсли;
				
				СуммаАвансовыхПлатежейКв1 = Макс(СуммаНалогаБезЛьготКв1 - СуммаЛьготКв1, 0);
				СуммаАвансовыхПлатежейКв2 = Макс(СуммаНалогаБезЛьготКв2 - СуммаЛьготКв2, 0);
				СуммаАвансовыхПлатежейКв3 = Макс(СуммаНалогаБезЛьготКв3 - СуммаЛьготКв3, 0);
				
			КонецЕсли;
			
			НоваяСтрока.СуммаАвансовыхПлатежей = СуммаАвансовыхПлатежейКв1 + СуммаАвансовыхПлатежейКв2 + СуммаАвансовыхПлатежейКв3;
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаАвансовыхПлатежей;
				
		Иначе
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
		
		КонецЕсли;
		
		Если Не ПараметрыРасчета.ГодовойРасчет Тогда
			НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Функция СуммаЗемельногоНалога(НалоговаяБаза, НалоговаяСтавка, КоэффициентПериодаСтроительства,
			КоличествоМесяцевИспользования, КоличествоМесяцевВПериоде, ГодовойРасчет)
	
	// Расчет авансов отличается только тем, что сумму нужно разделить на 4:
	// "Налогоплательщики ... исчисляют суммы авансовых платежей по налогу ... как одну четвертую соответствующей
	// налоговой ставки процентной доли кадастровой стоимости земельного участка" (п.6 ст. 396 НК)
	КоэффициентАвансов = ?(ГодовойРасчет, 1, 4); 
	
	СуммаНалога = Окр(НалоговаяБаза 
		* НалоговаяСтавка / 100
		* КоэффициентПериодаСтроительства
		* КоличествоМесяцевИспользования / КоличествоМесяцевВПериоде // коэффициент периода действия
		/ КоэффициентАвансов);

	Возврат СуммаНалога;
	
КонецФункции

#Область РасчетНалогаДо2020Года

Функция ПолучитьДанныеДляРасчетаЗемельногоНалогаДо2020(ПараметрыРасчета, ДополнительныеПараметры)

	ПараметрыРегистрацииВФНС = Новый Структура;
	ПараметрыРегистрацииВФНС.Вставить("ОсновнойНалоговыйОрган", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ПараметрыРегистрацииВФНС.Вставить("КодПоОКТМО", "");
	
	РасчетИмущественныхНалоговПереопределяемый.УточнитьПараметрыРегистрацииОрганизацииВФНС(
		ПараметрыРегистрацииВФНС,
		ПараметрыРасчета.Организация,
		ПараметрыРасчета.ДатаКонцаПериодаОтчета);
		
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ПараметрыРасчета.Организация);
	
	Если Месяц(ПараметрыРасчета.ПериодРасчета)%3 <> 0 ИЛИ (ЭтоНеЮрЛицо И ПараметрыРасчета.ПериодРасчета >= '20150101') Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ДатаКонцаПериодаОтчета", ПараметрыРасчета.ДатаКонцаПериодаОтчета);
	
	СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи(
		ПараметрыРасчета, 
		ПараметрыРасчета.ДатаКонцаПериодаОтчета, 
		ДополнительныеПараметры);
		
	ИспользуемыеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период = ПараметрыРасчета.ДатаНачалаПериодаОтчета;
	Пока Период <= ПараметрыРасчета.ДатаКонцаПериодаОтчета Цикл
		
		// Выборка земельных участков и числа полных месяцев владения ими.
		// При этом если возникновение (прекращение) у налогоплательщика в течение налогового периода 
		// права собственности (постоянного (бессрочного) пользования, пожизненного наследуемого владения) 
		// на земельный участок (его долю) произошло до 15-го числа соответствующего месяца включительно, 
		// за полный месяц принимается месяц возникновения указанных прав. Если возникновение (прекращение)
		// указанных прав произошло после 15-го числа соответствующего месяца, за полный месяц принимается 
		// месяц прекращения указанных прав.
		
		СерединаМесяца = Дата(Год(Период), Месяц(Период), 15);
		
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СерединаМесяца", СерединаМесяца);
		
		СформироватьТаблицуРегистрацияЗемельныхУчастковСрезПоследних(
			ПараметрыРасчета, 
			СерединаМесяца, 
			ДополнительныеПараметры,
			"РегистрацияЗемельныхУчастков_СерединаМесяца");
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация КАК Организация,
		|	РегистрацияЗемельныхУчастков.Период КАК Период,
		|	NULL КАК НалоговаяЛьготаПоНалоговойБазе,
		|	1 КАК КоличествоМесяцев,
		|	0 КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования КАК ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
		|
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
		|
		|ИЗ
		|	РегистрацияЗемельныхУчастков_СерединаМесяца КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	NULL,
		|	1,
		|	0,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &СерединаМесяца
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрацияЗемельныхУчастков_СерединаМесяца КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогуДо2020.НеПрименяется)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &СерединаМесяца
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогуДо2020.НеПрименяется)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация КАК Организация,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрацияЗемельныхУчастков.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаПоследнегоСнятияСРегистрационногоУчета
		|
		|ПОМЕСТИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ВладениеЗемельнымиУчастками.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период < ВладениеЗемельнымиУчастками.Период)
		|			И (НЕ РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаРегистрацииЗемли
		|
		|ПОМЕСТИТЬ ДатыРегистрацииЗемли
		|
		|ИЗ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета КАК ДатыПоследнегоСнятияСРегистрационногоУчета
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
		|		ПО ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ДатыПоследнегоСнятияСРегистрационногоУчета.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период > ДатыПоследнегоСнятияСРегистрационногоУчета.ДатаПоследнегоСнятияСРегистрационногоУчета)
		|			И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МЕСЯЦ(&НачалоМесяца) КАК НомерМесяца,
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы,
		|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
		|	ВладениеЗемельнымиУчастками.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ЖилищноеСтроительство
		|			ТОГДА ВЫБОР
		|					КОГДА ДатыРегистрацииЗемли.ДатаРегистрацииЗемли >= ДАТАВРЕМЯ(2008, 1, 1)
		|						ТОГДА ВЫБОР
		|								КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|										ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|									ТОГДА ВЫБОР
		|											КОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3) <= &СерединаМесяца
		|												ТОГДА ""2""
		|											ИНАЧЕ ""1""
		|										КОНЕЦ
		|								ИНАЧЕ """"
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования <> ДАТАВРЕМЯ(1, 1, 1)
		|								ТОГДА ВЫБОР
		|										КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|												ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|											ТОГДА ВЫБОР
		|													КОГДА ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования, ГОД, 3) <= &СерединаМесяца
		|														ТОГДА ""2""
		|													ИНАЧЕ ""1""
		|												КОНЕЦ
		|										ИНАЧЕ """"
		|									КОНЕЦ
		|							ИНАЧЕ """"
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПериодСтроительства
		|
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчасткамиПериод
		|
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыРегистрацииЗемли КАК ДатыРегистрацииЗемли
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = ДатыРегистрацииЗемли.ОсновноеСредство";
		
		ИмяТаблицы = "ВладениеЗемельнымиУчастками" + Формат(Период, "ДФ=MM");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ВладениеЗемельнымиУчасткамиПериод", "ПОМЕСТИТЬ " + ИмяТаблицы);
		
		Запрос.Выполнить();
		
		ИспользуемыеТаблицы.Добавить(ИмяТаблицы);
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеТаблицы);

		Если ПустаяСтрока(ТекстОбъединяющегоЗапроса) Тогда
			ТекстОбъединяющегоЗапроса = 
			"ВЫБРАТЬ *
			|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
			|ИЗ
			|	" + ИмяТаблицы;
		Иначе
			ТекстОбъединяющегоЗапроса = 
				ТекстОбъединяющегоЗапроса
				+ "
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ *
				|ИЗ
				|	" + ИмяТаблицы;
		КонецЕсли;
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		ПараметрыРасчета.МенеджерВременныхТаблиц, ПараметрыРасчета.Организация, ПараметрыРасчета.ПериодРасчета, "ЗемельныйНалог");

	Запрос.УстановитьПараметр("Период", КонецГода(ПараметрыРасчета.ПериодРасчета));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(ПараметрыРасчета.ПериодРасчета));
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРегистрацииВФНС.ОсновнойНалоговыйОрган);
	Запрос.УстановитьПараметр("КодПоОКТМО", ПараметрыРегистрацииВФНС.КодПоОКТМО);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_НомераПоследнихМесяцевЛьготы
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НомерМесяца КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе
	|
	|ПОМЕСТИТЬ ВТ_ВидыЛьгот
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы) КАК КоличествоМесяцевПримененияЛьготы
	|
	|ПОМЕСТИТЬ ВТ_ПрименениеЛьготы
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПрименениеЛьготы.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрименениеЛьготы.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы
	|
	|ПОМЕСТИТЬ ВТ_Льготы
	|ИЗ
	|	ВТ_НомераПоследнихМесяцевЛьготы КАК НомераПоследнихМесяцев
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыЛьгот КАК ВидыЛьгот
	|		ПО НомераПоследнихМесяцев.ОсновноеСредство = ВидыЛьгот.ОсновноеСредство
	|			И НомераПоследнихМесяцев.НомерМесяца = ВидыЛьгот.НомерМесяца
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрименениеЛьготы КАК ПрименениеЛьготы
	|		ПО (ВидыЛьгот.ОсновноеСредство = ПрименениеЛьготы.ОсновноеСредство)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПрименениеЛьготы.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость
	|
	|ПОМЕСТИТЬ ВТ_НомераПоследнихМесяцевКадастроваяСтоимость
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НомерМесяца КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВладениеЗемельнымиУчастками.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
	|
	|ПОМЕСТИТЬ ВТ_КодКатегорииЗемельСтавкиЗаписи
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	ВладениеЗемельнымиУчастками.КоличествоМесяцев > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодКатегорииЗемельСтавкиЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	КодКатегорииЗемельСтавкиЗаписи.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	КодКатегорииЗемельСтавкиЗаписи.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	КодКатегорииЗемельСтавкиЗаписи.НалоговаяСтавка КАК НалоговаяСтавка,
	|	КодКатегорииЗемельСтавкиЗаписи.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
	|
	|ПОМЕСТИТЬ ВТ_КодКатегорииЗемельНалоговыеСтавки
	|
	|ИЗ
	|	ВТ_НомераПоследнихМесяцевКадастроваяСтоимость КАК НомераПоследнихМесяцев
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КодКатегорииЗемельСтавкиЗаписи КАК КодКатегорииЗемельСтавкиЗаписи
	|		ПО НомераПоследнихМесяцев.ОсновноеСредство = КодКатегорииЗемельСтавкиЗаписи.ОсновноеСредство
	|			И НомераПоследнихМесяцев.КадастроваяСтоимость = КодКатегорииЗемельСтавкиЗаписи.КадастроваяСтоимость
	|			И НомераПоследнихМесяцев.НомерМесяца = КодКатегорииЗемельСтавкиЗаписи.НомерМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) КАК КоличествоМесяцевВладения
	|ПОМЕСТИТЬ ВТ_ВладениеУчастками
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПериодСтроительства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) КАК КоличествоМесяцевИспользования
	|
	|ПОМЕСТИТЬ ВТ_ИспользованиеУчастков
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ВТ_НомераПервыхМесяцев
	|
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость
	|
	|ПОМЕСТИТЬ ВТ_ПервоначальнаяКадастроваяСтоимость
	|
	|ИЗ
	|	ВТ_НомераПервыхМесяцев КАК НомераПервыхМесяцев
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|		ПО НомераПервыхМесяцев.ОсновноеСредство = ВладениеЗемельнымиУчастками.ОсновноеСредство
	|			И НомераПервыхМесяцев.НомерМесяца = ВладениеЗемельнымиУчастками.НомерМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	// Выберем актуальные (последние на конец отчетного периода) параметры земельных участков.
	// Для этого в качестве источника данных берем уже готовую ВТ РегистрацияЗемельныхУчастков_Записи 
	// (см. выше СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи()).
	// Простой срез последних не подходит, т.к. признак того, что земельный участок состоит на учете (ВключатьВНалоговуюБазу)
	// является ресурсом, и поэтому срез последних по значениям измерений может вернуть запись снятия с учета.
	// А нам нужна именно последняя "активная" запись.
	// Данные о применяемой в периоде льготе также берем из последней "активной" записи.
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	МАКСИМУМ(РегистрацияЗемельныхУчастков.Период) КАК АктуальныйПериод,
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ АктуальныйПериодНастроек
	|ИЗ
	|	РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
	|ГДЕ
	|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	АктуальныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияЗемельныхУчастков.Период КАК Период,
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	РегистрацияЗемельныхУчастков.КадастровыйНомер КАК КадастровыйНомер,
	|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РегистрацияЗемельныхУчастков.ОбщаяСобственность КАК ОбщаяСобственность,
	|	РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
	|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования КАК ДатаНачалаПроектирования,
	|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
	|	РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
	|	РегистрацияЗемельныхУчастков.НалоговыйОрган КАК НалоговыйОрган,
	|	РегистрацияЗемельныхУчастков.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияЗемельныхУчастков.КодПоОКАТО КАК КодПоОКАТО,
	|	РегистрацияЗемельныхУчастков.КБК КАК КБК,
	|	РегистрацияЗемельныхУчастков.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
	|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|	РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыПоСтатье391 КАК УменьшениеНалоговойБазыПоСтатье391,
	|	РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыНаСумму КАК УменьшениеНалоговойБазыНаСумму,
	|	РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЧислитель КАК ДоляНеОблагаемойНалогомПлощадиЧислитель,
	|	РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЗнаменатель КАК ДоляНеОблагаемойНалогомПлощадиЗнаменатель,
	|	РегистрацияЗемельныхУчастков.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
	|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
	|	РегистрацияЗемельныхУчастков.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	РегистрацияЗемельныхУчастков.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
	|	РегистрацияЗемельныхУчастков.ВидЗаписи КАК ВидЗаписи
	|ПОМЕСТИТЬ АктуальныеПараметрыЗемельныхУчастков
	|ИЗ
	|	РегистрацияЗемельныхУчастков_Записи КАК РегистрацияЗемельныхУчастков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныйПериодНастроек КАК АктуальныйПериодНастроек
	|		ПО РегистрацияЗемельныхУчастков.ОсновноеСредство = АктуальныйПериодНастроек.ОсновноеСредство
	|			И РегистрацияЗемельныхУчастков.Период = АктуальныйПериодНастроек.АктуальныйПериод
	|ГДЕ
	|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если ПараметрыРасчета.ПериодРасчета >= '20180101' Тогда
	
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ИспользованиеУчастков.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(ИспользованиеУчастков.КадастроваяСтоимость, 0) КАК КадастроваяСтоимость,
		|	ЕСТЬNULL(ИспользованиеУчастков.КоличествоМесяцевИспользования, 0) КАК КоличествоМесяцевИспользования,
		|	ЕСТЬNULL(ВладениеУчастками.КоличествоМесяцевВладения, 0) КАК КоличествоМесяцевВладения,
		|	КодКатегорииЗемельНалоговыеСтавки.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	КодКатегорииЗемельНалоговыеСтавки.НалоговаяСтавка КАК НалоговаяСтавка,
		|	КодКатегорииЗемельНалоговыеСтавки.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.КадастровыйНомер КАК КадастровыйНомер,
		|	РегистрацияЗемельныхУчастков.КБК КАК КБК,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияЗемельныхУчастков.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|				ИЛИ РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|			ТОГДА РегистрацияЗемельныхУчастков.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ОбщаяСобственность, ЛОЖЬ) КАК ОбщаяСобственность,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЧислитель, 0) КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЗнаменатель, 0) КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ЖилищноеСтроительство, ЛОЖЬ) КАК ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ЕСТЬNULL(Льготы.КоличествоМесяцевПримененияЛьготы, 0) КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЧислитель, 0) КАК ДоляНеоблагаемойПлощадиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 0) КАК ДоляНеоблагаемойПлощадиЗнаменатель,
		|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыПоСтатье391, ЛОЖЬ) КАК УменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыНаСумму, ЛОЖЬ) КАК УменьшениеНалоговойБазыНаСумму,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.НеОблагаемаяНалогомСумма, 0) КАК НеОблагаемаяНалогомСумма,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ПроцентУменьшенияСуммыНалога, 0) КАК ПроцентУменьшенияСуммыНалога,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.СуммаУменьшенияСуммыНалога, 0) КАК СуммаУменьшенияСуммыНалога,
		|	ЕСТЬNULL(ИспользованиеУчастков.ПериодСтроительства, """") КАК ПериодСтроительства
		|
		|ПОМЕСТИТЬ ВТ_РасчетНалога
		|
		|ИЗ
		|	ВТ_ИспользованиеУчастков КАК ИспользованиеУчастков
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВладениеУчастками КАК ВладениеУчастками
		|		ПО ИспользованиеУчастков.ОсновноеСредство = ВладениеУчастками.ОсновноеСредство
		|			И ИспользованиеУчастков.ПериодСтроительства = ВладениеУчастками.ПериодСтроительства
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КодКатегорииЗемельНалоговыеСтавки КАК КодКатегорииЗемельНалоговыеСтавки
		|		ПО ИспользованиеУчастков.ОсновноеСредство = КодКатегорииЗемельНалоговыеСтавки.ОсновноеСредство
		|			И ИспользованиеУчастков.КадастроваяСтоимость = КодКатегорииЗемельНалоговыеСтавки.КадастроваяСтоимость
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Льготы КАК Льготы
		|		ПО ИспользованиеУчастков.ОсновноеСредство = Льготы.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеПараметрыЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ИспользованиеУчастков.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИФНС";
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	Иначе
		
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ВладениеУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	0 КАК КоличествоМесяцевИспользования,
		|	ЕСТЬNULL(ВладениеУчастками.КоличествоМесяцевВладения, 0) КАК КоличествоМесяцевВладения,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.КадастровыйНомер КАК КадастровыйНомер,
		|	РегистрацияЗемельныхУчастков.КБК КАК КБК,
		|	ЕСТЬNULL(ПервоначальнаяКадастроваяСтоимость.КадастроваяСтоимость, 0) КАК КадастроваяСтоимость,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияЗемельныхУчастков.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|				ИЛИ РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|			ТОГДА РегистрацияЗемельныхУчастков.КодПоОКТМО
		|		ИНАЧЕ &КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ОбщаяСобственность, ЛОЖЬ) КАК ОбщаяСобственность,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЧислитель, 0) КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляВПравеОбщейСобственностиЗнаменатель, 0) КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ЖилищноеСтроительство, ЛОЖЬ) КАК ЖилищноеСтроительство,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ЕСТЬNULL(Льготы.КоличествоМесяцевПримененияЛьготы, 0) КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЧислитель, 0) КАК ДоляНеоблагаемойПлощадиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 0) КАК ДоляНеоблагаемойПлощадиЗнаменатель,
		|	РегистрацияЗемельныхУчастков.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыПоСтатье391, ЛОЖЬ) КАК УменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.УменьшениеНалоговойБазыНаСумму, ЛОЖЬ) КАК УменьшениеНалоговойБазыНаСумму,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.НеОблагаемаяНалогомСумма, 0) КАК НеОблагаемаяНалогомСумма,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка, 0) КАК СниженнаяНалоговаяСтавка,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.ПроцентУменьшенияСуммыНалога, 0) КАК ПроцентУменьшенияСуммыНалога,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастков.СуммаУменьшенияСуммыНалога, 0) КАК СуммаУменьшенияСуммыНалога,
		|	ЕСТЬNULL(ВладениеУчастками.ПериодСтроительства, """") КАК ПериодСтроительства
		|
		|ПОМЕСТИТЬ ВТ_РасчетНалога
		|
		|ИЗ
		|	ВТ_ВладениеУчастками КАК ВладениеУчастками
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Льготы КАК Льготы
		|		ПО ВладениеУчастками.ОсновноеСредство = Льготы.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяКадастроваяСтоимость КАК ПервоначальнаяКадастроваяСтоимость
		|		ПО ВладениеУчастками.ОсновноеСредство = ПервоначальнаяКадастроваяСтоимость.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеПараметрыЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ВладениеУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИФНС";
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	ВТ_РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	ВТ_РасчетНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	ВТ_РасчетНалога.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВТ_РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_РасчетНалога.КБК КАК КБК,
	|	ВТ_РасчетНалога.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВТ_РасчетНалога.ИФНС КАК ИФНС,
	|	ВТ_РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РасчетНалога.ОбщаяСобственность КАК ОбщаяСобственность,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РасчетНалога.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
	|	ВТ_РасчетНалога.ПериодСтроительства КАК ПериодСтроительства,
	|	ВТ_РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_РасчетНалога.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
	|	ВТ_РасчетНалога.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЧислитель КАК ДоляНеоблагаемойПлощадиЧислитель,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЗнаменатель КАК ДоляНеоблагаемойПлощадиЗнаменатель,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыПоСтатье391 КАК УменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыНаСумму КАК УменьшениеНалоговойБазыНаСумму,
	|	ВТ_РасчетНалога.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
	|	ВТ_РасчетНалога.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
	|	ВТ_РасчетНалога.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВТ_РасчетНалога.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
	|
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы
	|
	|ИЗ
	|	ВТ_РасчетНалога КАК ВТ_РасчетНалога
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО ВТ_РасчетНалога.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляРасчета = РезультатЗапроса.Выгрузить();
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Функция ВыполнитьРасчетЗемельногоНалогаДо2020(ТаблицаРасчетНалога, ПараметрыРасчета, ДанныеДляРасчета, ДополнительныеПараметры)
	
	Если ДанныеДляРасчета = Неопределено
		ИЛИ ДанныеДляРасчета.Количество() = 0 Тогда
		
		Возврат ТаблицаРасчетНалога;
	КонецЕсли;
	
	ГодовойРасчет = КонецКвартала(ПараметрыРасчета.ПериодРасчета) = КонецГода(ПараметрыРасчета.ПериодРасчета);
		
	// В 2020 году некоторые организации освобождены от уплаты имущественных налогов (за 2-й квартал). В этом случае сумму освобождения записываем
	// в отдельную колонку расчета СуммаОсвобожденияОтУплатыНалога.
	РассчитыватьОсвобождениеОтНалога = Ложь;
	ЭтоПериодОсвобождения = Ложь;
	РасчетСуммыНалогаЗаПериодОсвобождения = Ложь;
	Если ДополнительныеПараметры <> Неопределено Тогда
		РассчитыватьОсвобождениеОтНалога = ГодовойРасчет
			И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалоговомПериоде;
		ЭтоПериодОсвобождения = ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде")
			И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде;	
		РасчетСуммыНалогаЗаПериодОсвобождения = ДополнительныеПараметры.Свойство("РасчетСуммыНалогаЗаПериодОсвобождения") 
			И ДополнительныеПараметры.РасчетСуммыНалогаЗаПериодОсвобождения;	// устанавливается при "вложенном" вызове расчета за период освобождения			
	КонецЕсли;		
			
	Если ГодовойРасчет Тогда
		
		РасчетНалога1Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
		РасчетНалога2Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
		РасчетНалога3Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
		
		Отбор = Новый Структура("ИФНС, ОсновноеСредство, ПериодСтроительства, КадастроваяСтоимость");
		
		ЕстьОбъектыСУплатойАвансов = (ДанныеДляРасчета.Найти(Истина, "УплачиваютсяАвансы") <> Неопределено);
		
		Если ЕстьОбъектыСУплатойАвансов Тогда
			
			РасчетНалога1Кв = ПолучитьРасчетПоЗемельномуНалогу(
								ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 03, 31), ДополнительныеПараметры).
								РасчетЗемельногоНалогаДо2020;
								
			РасчетНалога3Кв = ПолучитьРасчетПоЗемельномуНалогу(
								ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 09, 30), ДополнительныеПараметры).
								РасчетЗемельногоНалогаДо2020;
			
		КонецЕсли;
		
		// Данные за 2-й квартал могут понадобиться в расчете налога за год в следующих случаях:
		// 1) действует освобождение от налогов за часть налогового периода (за 2-й квартал в 2020 году)
		// 2) уплачиваются авансы, а освобождение за 2-й квартал не применяется  
		// Разница в том, что в первом случае выбираются все данные, а во втором - только данные по ИФНС с уплатой авансов. 
		Если РассчитыватьОсвобождениеОтНалога Тогда
			
			ПараметрыРасчетаЗа2Кв = ОбщегоНазначения.СкопироватьРекурсивно(ДополнительныеПараметры);
			ПараметрыРасчетаЗа2Кв.Вставить("РасчетСуммыНалогаЗаПериодОсвобождения", Истина); // во вложенном расчете будут считаться авансы даже по тем объектам, по которым авансы не уплачиваются
			ПараметрыРасчетаЗа2Кв.Вставить("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде", Истина); // расчет за 2-й квартал 2020 года - действует освобождение 
			
			РасчетНалога2Кв = ПолучитьРасчетПоЗемельномуНалогу(
				ПараметрыРасчета.Организация, 
				Дата(Год(ПараметрыРасчета.ПериодРасчета), 06, 30), 
				ПараметрыРасчетаЗа2Кв).РасчетЗемельногоНалогаДо2020;
				
			ИначеЕсли ЕстьОбъектыСУплатойАвансов Тогда
				
			РасчетНалога2Кв = ПолучитьРасчетПоЗемельномуНалогу(
				ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), 06, 30), ДополнительныеПараметры).
				РасчетЗемельногоНалогаДо2020;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Для Каждого СтрокаРасчета Из ДанныеДляРасчета Цикл
		
		Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы И НЕ РасчетСуммыНалогаЗаПериодОсвобождения Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРасчетНалога.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета);
		
		Если ПараметрыРасчета.ПериодРасчета >= '20170101' Тогда
			НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 = Ложь;
		КонецЕсли;
		
		ЧислоМесяцевВОтчетномПериоде = ?(ГодовойРасчет, 12, 3);
		
		// Коэффициент Кл определяется как отношение числа полных месяцев, 
		// в течение которых отсутствует налоговая льгота, к числу календарных 
		// месяцев в налоговом периоде.
		Если НоваяСтрока.КоличествоМесяцевПримененияЛьготы = 0 Тогда
			КоэффициентКл = 1;
		Иначе
			ЧислоМесяцевИспользованияЛьготы = НоваяСтрока.КоличествоМесяцевВладения - НоваяСтрока.КоличествоМесяцевПримененияЛьготы;
			КоэффициентКл = Макс(ЧислоМесяцевИспользованияЛьготы / ЧислоМесяцевВОтчетномПериоде, 0);
			КоэффициентКл = Окр(КоэффициентКл, 4);
		КонецЕсли;
		
		Если НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель <> 0 И НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
			ДоляВПравеОбщейСобственности = 
				Мин(1, НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель / НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель);
		Иначе
			ДоляВПравеОбщейСобственности = 1;
		КонецЕсли;
		
		Если НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель <> 0 И НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель <> 0 Тогда
			ДоляНеоблагаемойПлощади = 
				Мин(1, НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель / НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель);
		Иначе
			ДоляНеоблагаемойПлощади = 0;
		КонецЕсли;
		
		// Расчет налоговой базы
		ОсноваДляРасчетаНалоговойБазы = НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности;
		Если НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 Тогда 
			СуммаУменьшенияНалоговойБазыПоСтатье391 = 10000;
		Иначе
			СуммаУменьшенияНалоговойБазыПоСтатье391 = 0;
		КонецЕсли;
		
		Если НоваяСтрока.НеОблагаемаяНалогомСумма <> 0 ИЛИ СуммаУменьшенияНалоговойБазыПоСтатье391 <> 0 Тогда
			НалоговаяБаза = 
				Макс(ОсноваДляРасчетаНалоговойБазы - (НоваяСтрока.НеОблагаемаяНалогомСумма + СуммаУменьшенияНалоговойБазыПоСтатье391), 0);
		ИначеЕсли ДоляНеоблагаемойПлощади > 0 Тогда
			НалоговаяБаза =
				Макс((ОсноваДляРасчетаНалоговойБазы - (ОсноваДляРасчетаНалоговойБазы * ДоляНеоблагаемойПлощади * (1 - КоэффициентКл))), 0);
		Иначе
			НалоговаяБаза = Макс(ОсноваДляРасчетаНалоговойБазы, 0);
		КонецЕсли;
		
        // С 2022 года ФНС (то есть, за 2021 год и далее) в своем расчете указывает кадастровую стоимость до копеек.
        // Поэтому для совместимости расчетов также не округляем налоговую базу при записи в регистр расчета налога.
        Если ПараметрыРасчета.ПериодРасчета >= '20210101' Тогда
        	НоваяСтрока.НалоговаяБаза = НалоговаяБаза;
        Иначе
        	НоваяСтрока.НалоговаяБаза = Окр(НалоговаяБаза);
        КонецЕсли;
		
		// Коэффициент периода использования (КоэффициентКи) отражает изменение 
		// характеристик объекта (кадастровый номер, кадастровая стоимость) в рамках периода владения.
		// Определяется в виде правильной простой дроби (без округления) - 
		// см. Письмо ФНС России от 01.06.2021 № БС-4-21/7598					
		КоэффициентКи = 1;
			
		Если ПараметрыРасчета.ПериодРасчета >= '20180101' 
		   И НоваяСтрока.КоличествоМесяцевИспользования <> НоваяСтрока.КоличествоМесяцевВладения
		   И НоваяСтрока.КоличествоМесяцевВладения <> 0 Тогда
			КоэффициентКи = НоваяСтрока.КоличествоМесяцевИспользования / НоваяСтрока.КоличествоМесяцевВладения;
		КонецЕсли;
		
		// Определение периода строительства
		КоэффициентПериодаСтроительства = 1;
		Если СтрокаРасчета.ЖилищноеСтроительство Тогда
			
			Если НоваяСтрока.ПериодСтроительства = "2" Тогда
				КоэффициентПериодаСтроительства = 4;
			ИначеЕсли НоваяСтрока.ПериодСтроительства = "1" Тогда
				КоэффициентПериодаСтроительства = 2;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыРасчета.ПериодРасчета < '20170101' Или СтрокаРасчета.СниженнаяНалоговаяСтавка = 0 Тогда
			НалоговаяСтавка = НоваяСтрока.НалоговаяСтавка;
		Иначе
			НалоговаяСтавка = СтрокаРасчета.СниженнаяНалоговаяСтавка;
		КонецЕсли;
		
		Если ГодовойРасчет Тогда
			НоваяСтрока.СуммаНалога = Окр(
				НоваяСтрока.НалоговаяБаза * НалоговаяСтавка * КоэффициентПериодаСтроительства
				// КоэФфициент периода владения (КоэффициентКв) отражает владение участком 
				// только в течение части отчетного(налогового) периода.
				// Определяется как отношение числа полных месяцев, 
				// в течение которых данный земельный участок находился в собственности
				// (постоянном (бессрочном) пользовании, пожизненном наследуемом владении) 
				// налогоплательщика, к числу календарных месяцев в налоговом периоде.
				// Коэффициент определяется в виде правильной простой дроби (без округления) - 
				// см. Письмо ФНС России от 01.06.2021 № БС-4-21/7598
				* НоваяСтрока.КоличествоМесяцевВладения / ЧислоМесяцевВОтчетномПериоде
				* КоэффициентКи 
				/ 100);
		Иначе
			НоваяСтрока.СуммаНалога = Окр(
				НоваяСтрока.НалоговаяБаза * НалоговаяСтавка * КоэффициентПериодаСтроительства
				// КоэФфициент периода владения (КоэффициентКв) 					
				* НоваяСтрока.КоличествоМесяцевВладения / ЧислоМесяцевВОтчетномПериоде
				* КоэффициентКи
				/ 100 / 4);
		КонецЕсли;
		
		// Расчет сумм налоговых льгот
		СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = 0;
		СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = 0;
		СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = 0;
		СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
		
		Если НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе = 
			Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогуДо2020.ОсвобождениеОтНалогообложенияМестное Тогда
			
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
			
		ИначеЕсли НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе =
			Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогуДо2020.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
			
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
			
		КонецЕсли;
		
		Если СтрокаРасчета.ПроцентУменьшенияСуммыНалога > 0 Тогда
			СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога =
				Окр(НоваяСтрока.СуммаНалога * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
		КонецЕсли;
			
		Если СтрокаРасчета.СуммаУменьшенияСуммыНалога > 0 Тогда
			СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога
				+ Окр(СтрокаРасчета.СуммаУменьшенияСуммыНалога);
		КонецЕсли;
		
		Если ПараметрыРасчета.ПериодРасчета < '20170101' И СтрокаРасчета.СниженнаяНалоговаяСтавка > 0 Тогда
			СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 
				Окр(НоваяСтрока.НалоговаяБаза * (НоваяСтрока.НалоговаяСтавка - СтрокаРасчета.СниженнаяНалоговаяСтавка) / 100);
		Иначе
			СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
		КонецЕсли;
		
		НоваяСтрока.СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
		НоваяСтрока.СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
		
		ПараметрыЛьготы = Новый Структура(
			"ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 
			|ДоляНеОблагаемойНалогомПлощадиЧислитель, 
			|КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395, 
			|КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391, 
			|НалоговаяЛьготаПоНалоговойБазе, 
			|НеОблагаемаяНалогомСумма, 
			|ПроцентУменьшенияСуммыНалога, 
			|СниженнаяНалоговаяСтавка, 
			|СуммаУменьшенияСуммыНалога, 
			|УменьшениеНалоговойБазыНаСумму, 
			|УменьшениеНалоговойБазыПоСтатье391");
			
		ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, СтрокаРасчета);
		
		ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель;
		ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЧислитель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель;
		
		НоваяСтрока.ПредставлениеНалоговойЛьготы =
			УправлениеВнеоборотнымиАктивамиКлиентСервер.ПредставлениеНалоговойЛьготыПоЗемельномуНалогу(ПараметрыЛьготы);
		
		// Расчет суммы налога к уплате
		Если СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога > 0 Тогда
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
		ИначеЕсли СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки > 0 Тогда
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
		ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное > 0 Тогда
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное;
		ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 > 0 Тогда
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395;
		Иначе
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
		КонецЕсли;
		
		НоваяСтрока.СуммаНалогаКУплате = Макс(0, НоваяСтрока.СуммаНалогаКУплате);
					
		Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
			
			ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
			
			НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
			СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
			Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
			КонецЦикла;
			
			НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
			СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
			Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
				Если РассчитыватьОсвобождениеОтНалога Тогда
					// В 2020 году для "освобожденных" организаций аванс за 2-й квартал не уплачивается, 
					// а налог за год уменьшается на сумму аванса за 2-й квартал.
					// СуммаОсвобожденияОтУплатыНалога уже получена в расчете налога за 2-й квартал
					НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаОсвобожденияОтУплатыНалога;
				Иначе
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЕсли;		
			КонецЦикла;
			
			НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
			СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
			Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
				+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
			КонецЦикла;
							
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
				- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв; 
			
		КонецЕсли;
		
		Если РассчитыватьОсвобождениеОтНалога И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
			ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
			СтрокиРасчетаНалогаКОсвобождению = РасчетНалога2Кв.НайтиСтроки(Отбор);
			Для Каждого СтрокаРасчетаНалогаКОсвобождению Из СтрокиРасчетаНалогаКОсвобождению Цикл
				// Если авансы не уплачиваются, то освобождение за 2-й квартал рассчитывается так же, 
				// как аванс за 2-й квартал, если бы он уплачивался.
				// СуммаОсвобожденияОтУплатыНалога уже получена в расчете налога за 2-й квартал.
				НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
					+ СтрокаРасчетаНалогаКОсвобождению.СуммаОсвобожденияОтУплатыНалога;
			КонецЦикла;
		КонецЕсли;	
		
		Если ЭтоПериодОсвобождения Тогда
			// Во 2-м квартале 2020 года организация освобождается от налога в размере полной суммы аванса к уплате.
			НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
		
		// В 2020 году для некоторых организаций применяется разовая льгота в виде освобождения от налога 
		// за период владения объектом налогообложения в течение 2-го квартала 2020 года.
		// Для таких "освобожденных" организаций СуммаОсвобожденияОтУплатыНалога будет ненулевой 
		// в расчете за 2-й квартал 2020 года (если уплачиваются авансы) и в годовом расчете за 2020 год.
		// В всех остальных случаях СуммаОсвобожденияОтУплатыНалога = 0 и не влияет на расчет.
		// Уменьшим СуммаНалогаКУплате на сумму такого освобождения.
		НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаОсвобожденияОтУплатыНалога;
		
		Если НЕ ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
			НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПараметрыРасчета.ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
			
	КонецЦикла;
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.Организация, "Организация");

	Возврат ТаблицаРасчетНалога;

КонецФункции
#КонецОбласти
#КонецОбласти

#Область Прочее

Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, СписокОрганизаций, Период, Налог) Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		МенеджерВременныхТаблиц, 
		СписокОрганизаций, 
		Период, 
		Налог);
	
КонецПроцедуры

Функция ПустаяСправкаРасчет(ИмяРегистраСведений) Экспорт

	Возврат РасчетИмущественныхНалоговПереопределяемый.ПустаяСправкаРасчет(ИмяРегистраСведений);

КонецФункции

#КонецОбласти

#КонецОбласти
