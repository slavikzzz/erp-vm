
#Область СлужебныеПроцедурыИФункции

#Область КлючиПриглашений

Функция КлючПриглашения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторКонтрагента", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючПриглашенияНаИдентификатор() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.НаИдентификатор"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторКонтрагента", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючШироковещательногоПриглашения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.Широковещательное"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючЗаявкиНаРоуминг() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.ЗаявкаНаРоуминг"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторОператора", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПодсказкаЭлектроннаяПочтаДляУведомленияОСтатусеЗаявкиНаРоуминг() Экспорт
	
	Возврат НСтр("ru = 'На данный адрес электронной почты будут отправляться оповещения о статусе выполнения заявки по настройке роуминга';
				|en = 'Notifications will be sent to this email address about the status of the application for roaming settings'");
	
КонецФункции

Функция ВидПриглашенияИсходящее() Экспорт
	
	Возврат "Исходящее";
	
КонецФункции

Функция ЭтоВходящееПриглашение(Приглашение) Экспорт
	
	Возврат Приглашение.Вид = ВидПриглашенияВходящее();
	
КонецФункции

Функция ВидПриглашенияВходящее() Экспорт
	
	Возврат "Входящее";
	
КонецФункции

Функция ТипыПриглашений() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("НаИдентификатор", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.НаИдентификатор"));	
	Результат.Вставить("Широковещательное", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.Широковещательное"));	
	Результат.Вставить("ЗаявкаНаРоуминг", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.ЗаявкаНаРоуминг"));	

	Возврат Новый ФиксированнаяСтруктура(Результат);
		
КонецФункции

#Область ОбработкаНеисправностей

// Возвращает вид ошибки, описывающий ситуацию, когда операция не может быть выполнена из-за того, что
// не настроена связь с контрагентом.
// 
// Параметры:
// 	СтатусПриглашения - ПеречислениеСсылка.СтатусыПриглашений
// 	ВидОперации - Строка - вид операции, при выполнении которой произошла ошибка, может 
// 	принимать значения "Распаковка", "Отправка".
// Возвращаемое значение:
//  См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиНеНастроенаСвязьСКонтрагентом(СтатусПриглашения, ВидОперации) Экспорт
	
	Если СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяСогласие") Тогда
		Идентификатор = "НеПринятоПриглашение" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Не принято приглашение к обмену электронными документами';
								|en = 'Invitation to exchange electronic documents is not accepted'");
		Если ВидОперации = "Распаковка" Тогда
			ОписаниеРешения = НСтр("ru = 'Необходимо <a href = ""принять"">принять</a> приглашение.
				|После того как оно будет принято, транспортный контейнер можно будет распаковать.';
				|en = 'You must <a href = ""принять"">accept</a>the invitation.
				|Then you can unpack the transport container.'");
		Иначе
			ОписаниеРешения = НСтр("ru = 'Необходимо <a href = ""принять"">принять</a> приглашение.
				|После того как оно будет принято, документ можно будет отправить.';
				|en = 'Please <a href = ""принять"">accept</a> the invitation.
				|After the invitation is accepted the document can be sent.'");
		КонецЕсли;
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ОжидаемСогласия") Тогда 
		Идентификатор = "ОжидаетсяСогласиеНаПриглашение" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами находится в статусе ""Ожидаем согласие""';
								|en = 'Invitation to exchange electronic documents is in the ""Approval pending"" status'");
		ОписаниеРешения = НСтр("ru = 'Уточните у контрагента, принял ли он <a href = ""приглашение"">приглашение</a>.
					   |Если приглашение принято - <a href = ""обратитесь"">обратитесь</a> в тех. поддержку';
					   |en = 'Ask the counterparty if they accepted <a href = ""приглашение"">the invitation</a>.
					   |If the invitation is accepted, <a href = ""обратитесь"">contact</a> technical support'");
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Отклонено")
		Или СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяОтправить") Тогда 
		Идентификатор = "ТребуетсяОтправкаПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами еще не было отправлено';
								|en = 'Invitation to exchange electronic documents has not yet been sent'");
		Если ВидОперации = "Распаковка" Тогда
			ОписаниеРешения = НСтр("ru = 'Чтобы продолжить обмен электронными документами с контрагентом, отправьте <a href = ""приглашение"">приглашение</a>.
					   |После того как оно будет принято, транспортный контейнер можно будет распаковать.';
					   |en = 'To continue electronic document exchange with the counterparty, send <a href = ""приглашение"">an invitation</a>.
					   |You can unpack the transport container once the invitation is accepted.'");
		Иначе
			ОписаниеРешения = НСтр("ru = 'Чтобы продолжить обмен электронными документами с контрагентом, отправьте <a href = ""приглашение"">приглашение</a>.
					   |После того как оно будет принято, документ можно будет отправить.';
					   |en = 'o continue exchanging electronic documents with the counterparty, please send <a href = ""приглашение"">приглашение</a>.
					   |Once it is accepted, the document can be sent.'");
		КонецЕсли;
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Ошибка") Тогда 
		Идентификатор = "ОшибкаПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами находится в статусе ""Ошибка""';
								|en = 'Invitation to exchange electronic documents is in the ""Error"" status'");
		ОписаниеРешения = НСтр("ru = '<a href = ""Обратитесь"">Обратитесь</a> в тех. поддержку';
								|en = '<a href = ""Обратитесь"">Contact</a> technical support'")
	ИначеЕсли СтатусПриглашения = Неопределено Тогда 
		Идентификатор = "НетПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Не настроена связь с контрагентом';
								|en = 'Counterparty connection is not configured'");
		ОписаниеРешения = НСтр("ru = 'Не настроена связь с контрагентом';
								|en = 'Counterparty connection is not configured'")
	Иначе
		Возврат ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
	КонецЕсли;
	
	Если ВидОперации = "Распаковка" Тогда
		ЗаголовокПроблемы = НСтр("ru = 'Не удалось распаковать транспортный контейнер ЭДО';
								|en = 'Cannot unpack the EDI transport container'");
	Иначе
		ЗаголовокПроблемы = НСтр("ru = 'Не удалось отправить электронный документ';
								|en = 'It was not possible to send an electronic document'");
	КонецЕсли;
	
	ОбработчикОткрытияПриглашения = "ПриглашенияЭДОКлиент.ОткрытьПриглашения";
	ОбработчикОткрытияФормыОбращенияВТехподдержку = 
		ОбработкаНеисправностейБЭДКлиентСервер.ОбработчикОткрытияФормыОбращенияВТехподдержку();
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = Идентификатор;
	ВидОшибки.ЗаголовокПроблемы = ЗаголовокПроблемы;
	ВидОшибки.ОписаниеПроблемы = ОписаниеПроблемы;
	ВидОшибки.ОписаниеРешения = ОписаниеРешения;
	ВидОшибки.ОбработчикиНажатия.Вставить("принять", ОбработчикОткрытияПриглашения);
	ВидОшибки.ОбработчикиНажатия.Вставить("приглашение", ОбработчикОткрытияПриглашения);
	ВидОшибки.ОбработчикиНажатия.Вставить("Обратитесь",	ОбработчикОткрытияФормыОбращенияВТехподдержку);
	ВидОшибки.ОбработчикиНажатия.Вставить("обратитесь", ОбработчикОткрытияФормыОбращенияВТехподдержку);
	ВидОшибки.ПараметрыОбработчиков.Вставить("принять", Новый Структура("Группировать", Истина));
	ВидОшибки.ПараметрыОбработчиков.Вставить("приглашение", Новый Структура("Группировать", Истина));
	
	Возврат ВидОшибки;
	
КонецФункции

Функция ВидОшибкиНеУдалосьНайтиКонтрагентаПоПриглашению() Экспорт
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "НеУдалосьНайтиКонтрагентаПоРеквизитамИзПриглашения";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Не удалось найти контрагента по реквизитам из приглашения';
										|en = 'Cannot find a counterparty by information from the invitation'");
	ВидОшибки.ОписаниеРешения = 
		НСтр("ru = '<a href = ""Укажите"">Укажите</a> контрагентов в приглашениях.
			 |Для того, чтобы контрагенты создавались автоматически, <a href = ""ВключитеОпцию"">включите опцию</a>.';
			 |en = '<a href = ""Укажите"">Specify</a>counterparties in invitations.
			 |To generate counterparties automatically, <a href = ""ВключитеОпцию"">enable the option</a>.'");
	ВидОшибки.ОбработчикиНажатия.Вставить("Укажите", "ПриглашенияЭДОКлиент.ОткрытьПриглашения");
	ВидОшибки.ОбработчикиНажатия.Вставить("ВключитеОпцию", "ПриглашенияЭДОКлиент.ВключитьОпциюАвтоматическогоСозданияКонтрагентов");
	ВидОшибки.Статус = ОбработкаНеисправностейБЭДКлиентСервер.СтатусыОшибок().Обычная;
	ВидОшибки.ВыводитьСсылкуНаСписокОшибок = Ложь;
	
	Возврат ВидОшибки;
	
КонецФункции

#КонецОбласти

#КонецОбласти