
#Область СлужебныйПрограммныйИнтерфейс

// 
// Параметры:
// 	ИмяЭлемента - Строка - 
// 	Форма - ФормаКлиентскогоПриложения -  
// 	ДополнительныеПараметры - Неопределено - 
Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры = Неопределено) Экспорт

	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	//++ Локализация
	
	Элементы = Форма.Элементы;

	Если ИмяЭлемента = Элементы.НачислятьАмортизациюНУФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.СрокИспользованияНУ.Имя 
		ИЛИ ИмяЭлемента = Элементы.СрокИспользованияНУФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.КоэффициентАмортизацииБУФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.ОтражениеАмортизационнойПремииФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.СпециальныйКоэффициентНУФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.АмортизационнаяГруппаФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.НедвижимоеИмуществоФлаг.Имя 
		ИЛИ ИмяЭлемента = Элементы.ГрафикАмортизацииФлаг.Имя Тогда	
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", ИмяЭлемента);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			ИмяЭлемента, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

// 
// Параметры:
// 	Команда - КомандаФормы - 
// 	Форма - ФормаКлиентскогоПриложения - 
Процедура ПриВыполненииКоманды(Команда, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	ПродолжитьВыполнениеКоманды = Истина;
	Объект = Форма.Объект;
	
	//++ Локализация
	
	Если Команда.Имя = Форма.Команды.ОткрытьИсправления.Имя Тогда
		
		ПродолжитьВыполнениеКоманды = Ложь;
		
		СписокОС = Новый Массив;
		Для Каждого ДанныеСтроки Из Объект.ОС Цикл
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
		КонецЦикла;
		
		ВнеоборотныеАктивыКлиентЛокализация.ОткрытьВзаимозависимыеИсправленияИмущественныхНалогов(
			Объект.Организация, 
			Объект.НачалоДействия, 
			Объект.Дата, 
			СписокОС);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьВыполнениеКоманды Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьВыполнениеКоманды(
			Форма, 
			Команда.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
