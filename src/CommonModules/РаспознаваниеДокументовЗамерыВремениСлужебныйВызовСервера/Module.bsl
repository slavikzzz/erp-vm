#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьЗамерыВремени() Экспорт 
	
	Если Не РаспознаваниеДокументовЗамерыВремениСлужебный.ДоступнаОтправкаЗамеровВремени() Тогда 
		Возврат;
	КонецЕсли;
	
	КлючЗадания = "РаспознаваниеДокументовЗамерыВремениСлужебный.ОтправитьЗамерыВремени";
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ", КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Если Задания.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Массив;
	Параметры.Добавить(НомерСеансаИнформационнойБазы());
	
	Попытка
		ФоновыеЗадания.Выполнить(
			КлючЗадания,
			Параметры,
			КлючЗадания,
			НСтр("ru = 'Отправка замеров времени распознавания документов.';
				|en = 'Send document recognition time measurements.'")
		);
	Исключение
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Невозможно выполнить отправку замеров времени распознавания документов
			           |%1
			           |Описание ошибки:
			           |%2';
			           |en = 'Cannot send document recognition time measurements
			           |%1
			           |Error details:
			           |%2'"),
			КлючЗадания,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		
		ЗаписьЖурналаРегистрации(
			РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстОшибки
		);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
