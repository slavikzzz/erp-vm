#Область СлужебныеПроцедурыИФункции

// Пропорционально распределяет сумму, изменяя значения в колонках базы
// так, чтобы итог совпадал с переданным значением.
//
// Параметры:
//  Сумма - Число - сумма, которую надо разнести;
//  База - фиксированный массив структур или фиксированный массив фиксированных структур, по которым разносится сумма;
//  ИмяКлючаСуммы- Строка - Имя ключа структуры с суммой в базе;
//  Точность - Число - точность округления при расчетах. Необязателен.
// Возвращаемое значение
//	Фиксированный массив фиксированных структур.
//
Функция РезультатРаспределенияСуммыПоБазе(Сумма, База, ИмяКлючаСуммы, Точность = 2) Экспорт
	
	Если База.Количество() = 0 Тогда
		Возврат Новый ФиксированныйМассив(База);
	КонецЕсли;	
	
	КоллекцияРаспределения = Новый Массив;
	Для каждого ЭлементКоллекции Из База Цикл
		СтруктураРезультат = Новый Структура;
		Для Каждого КлючИЗначение Из ЭлементКоллекции Цикл
			СтруктураРезультат.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		КоллекцияРаспределения.Добавить(СтруктураРезультат);
	КонецЦикла;
	
	ЗарплатаКадры.РазнестиСуммуПоБазе(Сумма, КоллекцияРаспределения, ИмяКлючаСуммы, 0);
	
	РезультатРаспределения = Новый Массив;
	Для каждого ЭлементКоллекции Из КоллекцияРаспределения Цикл
		РезультатРаспределения.Добавить(Новый ФиксированнаяСтруктура(ЭлементКоллекции));
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(РезультатРаспределения);

КонецФункции

// Получает значение свойства удержания - ОкруглятьРезультатРаспределения  
//
Функция ОкруглятьРезультатРаспределенияУдержания(Удержание) Экспорт
	
	Если ТипЗнч(Удержание) <> Тип("ПланВидовРасчетаСсылка.Удержания") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ОкруглятьРезультатРаспределения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Удержание, "ОкруглятьРезультатРаспределения");
	Если ОкруглятьРезультатРаспределения = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат ОкруглятьРезультатРаспределения;
	КонецЕсли;

КонецФункции

Функция БухучетНачисленийИзменился(Знач Коллекция1, Знач Коллекция2) Экспорт

	ИменаКолонок = "СпособОтраженияЗарплатыВБухучете,ДоляРаспределения";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ИменаКолонок = СтрШаблон("%1,%2", ИменаКолонок, "СтатьяФинансирования");
	КонецЕсли;
	Если  ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ИменаКолонок = СтрШаблон("%1,%2", ИменаКолонок, "СтатьяРасходов");
	КонецЕсли; 
	
	Возврат Не ОтражениеЗарплатыВБухучетеРасширенный.КоллекцииБухучетаИдентичны(Коллекция1, Коллекция2, ИменаКолонок);

КонецФункции 

Функция КатегрияОсновногоВидаРасчета(ВидРасчета) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчета, "КатегорияНачисленияИлиНеоплаченногоВремени");

КонецФункции

#КонецОбласти


