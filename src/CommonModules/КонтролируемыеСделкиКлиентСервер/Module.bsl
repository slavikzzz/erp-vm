#Область ПрограммныйИнтерфейс

// Версия уведомления 2012.
// 
// Возвращаемое значение:
//  Число - Версия уведомления 2012
Функция ВерсияУведомления_2012() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 27 июля 2012 г. N ММВ-7-13/524
	
	Возврат 1;
	
КонецФункции

// Версия уведомления 2017.
// 
// Возвращаемое значение:
//  Число - Версия уведомления 2017
Функция ВерсияУведомления_2017() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 27 июля 2012 г. N ММВ-7-13/524
	// с дополнениями по Письму ФНС от 22 марта 2018 г. N ЕД-4-13/5367@
	
	Возврат 2;
	
КонецФункции

// Версия уведомления 2018.
// 
// Возвращаемое значение:
//  Число - Версия уведомления 2018.
Функция ВерсияУведомления_2018() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 07 мая 2018 №ММВ-7-13/249@
	
	Возврат 3;
	
КонецФункции

// Версия уведомления 2019.
// 
// Возвращаемое значение:
//  Число - Версия уведомления 2019.
Функция ВерсияУведомления_2019() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 26.07.2019 № ММВ-7-13/380@
	
	Возврат 4;
	
КонецФункции

// Версия уведомления 2021.
// 
// Возвращаемое значение:
//  Число - Версия уведомления 2021.
Функция ВерсияУведомления_2021() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 26.07.2019 № ММВ-7-13/380@
	// с дополнениями по границам уведомления, внесенными законом от 17 февраля 2021 года №6-ФЗ
	
	Возврат 5;
	
КонецФункции

// Возвращает код формата уведомления о контроируемых сделках согласно приказу ФНС от 02.12.2024 № ЕД-7-13/1088@
//
// Возвращаемое значение:
//  Число - код версии уведомления о контролируемых сделках.
Функция ВерсияУведомления_2024() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 02.12.2024 № ЕД-7-13/1088@
	
	Возврат 6;
	
КонецФункции

// Функция возвращает структуру описания уведомления о контролируемых сделках.
//
// Параметры:
//  ВерсияУведомления - Число - код версии уведомления контролируемых сделок.
// Возвращаемое значение:
//  Структура - структура описания уведомления о контролируемых сделках взависимости от версии.
Функция ОписаниеОснованийКонтролируемости(ВерсияУведомления) Экспорт
	
	Описание = НовыйОписаниеОснованийКонтролируемости();
	Если ВерсияУведомления <= ВерсияУведомления_2017() Тогда
		ЗаполнитьОснованияКонтролируемости2012(Описание);
	ИначеЕсли ВерсияУведомления <= ВерсияУведомления_2018() Тогда
		ЗаполнитьОснованияКонтролируемости2018(Описание);
	ИначеЕсли ВерсияУведомления <= ВерсияУведомления_2021() Тогда
		ЗаполнитьОснованияКонтролируемости2019(Описание);
	Иначе
		ЗаполнитьОснованияКонтролируемости2024(Описание);
	КонецЕсли;
	Возврат Описание;
	
КонецФункции

// Определяет нужно ли заполнять в уведомлении о контролируемой сделке сведения о цепочках стоимости в разделе 1В
//
// Параметры:
//  ИспользованияПроисхожденияТовара - ПеречислениеСсылка.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок - коды
//                                     использования/происхождения товара контролируемых сделок.
// Возвращаемое значение:
//  Булево - Истина, если нужно заполнять сведения о цепочках стоимости в разделе 1В уведомления о контролируемой сделке.
Функция НужноЗаполнятьСведенияОСделкеЛиста1В(ИспользованияПроисхожденияТовара) Экспорт
	
	Возврат ЗначениеЗаполнено(ИспользованияПроисхожденияТовара)
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Переработка")
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Производство")
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Иное");
	
КонецФункции

// Функция возвращает максимальное количество сделок в цепочке.
// 
// Возвращаемое значение:
//  Число - максимальное количество сделок в цепочке.
Функция МаксимальноеКоличествоСделокВЦепочке() Экспорт
	Возврат 4;
КонецФункции

// Определяет по признаку участника сделки является ли сделка розничной продажей
//
// Параметры:
//  ПризнакУчастникаСделки - ПеречислениеСсылка.ТипыУчастниковЦепочкиКонтролируемыхСделок - признак участника сделки.
// Возвращаемое значение:
//  Булево - Истина, если сделка является розничной продажей.
Функция ЭтоРозничнаяПродажаЦепочкиСделок(ПризнакУчастникаСделки) Экспорт
	
	Возврат ЗначениеЗаполнено(ПризнакУчастникаСделки)
		И ПризнакУчастникаСделки = ПредопределенноеЗначение("Перечисление.ТипыУчастниковЦепочкиКонтролируемыхСделок.КонечныйРозничныйПокупатель");
	
КонецФункции

// Возвращает дату отказа от ЕНВД.
//
// Возвращаемое значение:
//  Дата - дата отказа от ЕНВД.
Функция ДатаОтказаОтЕНВД() Экспорт
	
	// В 2021 году налоговый режим ЕНВД отменен.
	// В контролируемых сделках учет ЕНВД отменен 6-ФЗ от 17.02.2021.
	Возврат '20210101';
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возращает структуру описания основания контролируемости сделок.
// Возвращаемое значение:
//  Структура  - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
Функция НовыйОписаниеОснованийКонтролируемости()
	
	Описание = Новый Структура;
	Описание.Вставить("ОснованиеКонтролируемости131", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости132", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости133", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости134", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости135", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости136", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости137", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости138", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости139", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости140", Неопределено);
	Возврат Описание;
	
КонецФункции

// Возращает структуру описания основания контролируемости сделок.
// Возвращаемое значение:
//  Структура  - параметры описания основания:
//  * Наименование - Строка - наименование описания реквизита.
//  * Подсказка    - Строка - описание подсказки для реквизита.
Функция НовыйОписаниеОснования()
	
	Описание = Новый Структура();
	Описание.Вставить("Наименование", "");
	Описание.Вставить("Подсказка", "");
	Возврат Описание;
	
КонецФункции

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименования и подсказки свойств документе
// согласно формату уведомления, определенного приказом ФНС от 27 июля 2012 г. N ММВ-7-13/524
// с дополнениями по Письму ФНС от 22 марта 2018 г. N ЕД-4-13/5367@.
// Параметры:
//  Описание - Структура - описание уведомления о контролируемых сделках.
Процедура ЗаполнитьОснованияКонтролируемости2012(Описание)
	
	ДобавитьОснованиеОбщийПределРФ(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеРезидентОЭЗ(Описание, "135");
	
КонецПроцедуры

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименования и подсказки свойств документе
// согласно формату уведомления, определенного приказом ФНС от 07 мая 2018 №ММВ-7-13/249@.
// Параметры:
//  Описание - Структура - описание уведомления о контролируемых сделках.
Процедура ЗаполнитьОснованияКонтролируемости2018(Описание)
	
	ДобавитьОснованиеОбщийПределРФ(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеРезидентОЭЗ(Описание, "135");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "136");
	ДобавитьОснованиеУчастникРегиональногоИнвестиционногоПроекта(Описание, "137");
	ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, "138");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "139");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "140");
	
КонецПроцедуры

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименования и подсказки свойств документе
// согласно формату уведомления, определенного приказом ФНС от 26.07.2019 № ММВ-7-13/380@
// с дополнениями по границам уведомления, внесенными законом от 17 февраля 2021 года №6-ФЗ.
// Параметры:
//  Описание - Структура - описание уведомления о контролируемых сделках.
Процедура ЗаполнитьОснованияКонтролируемости2019(Описание)
	
	ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "135");
	ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, "136");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "137");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "138");
	
КонецПроцедуры

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименования и подсказки свойств документе
// согласно формату уведомления, определенного приказом ФНС от 02.12.2024 № ЕД-7-13/1088@.
// Параметры:
//  Описание - Структура - описание уведомления о контролируемых сделках.
Процедура ЗаполнитьОснованияКонтролируемости2024(Описание)
	
	ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "135");
	ДобавитьОснованиеИсследовательскийЦентрСколковоИЭра(Описание, "136");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "137");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "138");
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание общего предела в РФ.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеОбщийПределРФ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - сумма доходов по сделкам превышает предел';
						|en = '%1 - the amount of income from transactions exceeds the limit'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Сумма доходов по сделкам (сумма цен сделок) между взаимозависимыми лицами за соответствующий календарный год превышает 1 млрд. рублей (пп. 1 п. 2 статьи 105.14 НК РФ)';
			|en = 'The amount of income from transactions (the amount of transaction prices) between interdependent parties for the corresponding calendar year exceeds 1 billion rubles (subcl. 1 cl. 2 art. 105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание НДПИ.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеНДПИ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - уплата НДПИ по процентной ставке';
						|en = '%1 - the payment of the MET on the interest rate'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Одна из сторон сделки является налогоплательщиком налога на добычу полезных ископаемых, исчисляемого по налоговой ставке  в процентах, и предметом сделки является добытое полезное ископаемое (пп.2 п.2 статьи 105.14 НК РФ).';
			|en = 'One of the parties to the transaction is a taxpayer of the mineral extraction tax, calculated at the tax rate in percent, and the subject of the transaction is the extracted mineral (subcl.2 cl.2 art. 105.14 TC RF).'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание применения спецрежима.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеСпецрежим(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение спец.режимов налогообложения';
						|en = '%1 - the use of special taxation regime'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки применяет специальный режим налогообложения ЕСХН или ЕНВД, а другая сторона - не применяет. При этом сделка заключена в рамках этого спецрежима (пп.3.п2. ст.105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction applies a special taxation regime of the UAT or UTII, and the other party does not apply. At the same time, the transaction is concluded within this special regime (subcl.3.cl.2. art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание освобождения от уплаты налог на прибыль.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - освобождение от уплаты налога на прибыль';
						|en = '%1 - income tax exemption'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки освобождена от уплаты налога на прибыль или применяет налоговую ставку 0% в соответствии с п. 5.1 статьи 284 НК РФ, а другая сторона - не освобождена и не применяет ставку 0% (пп.4 п.2 ст. 105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction is exempt from income tax or applies the tax rate of 0% in accordance with cl. 5.1 of art. 284 of the TC RF, and the other party is not exempt and does not apply the rate of 0% (subcl.4 cl.2 art. 105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание резиденства в офшоре.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеРезидентОЭЗ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - резидент особой экономической зоны';
						|en = '%1 - resident of the special economic zone'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является резидентом особой экономической зоны, в которой предусматриваются льготы по налогу на прибыль, а другая сторона не является таким резидентом (пп.5 п.2 ст.105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction is a resident of a special economic zone, which provides income tax benefits, and the other party is not such a resident (subcl.5 cl.2 art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание разработки нового морского месторождения.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - оператор нового морского месторождения';
						|en = '%1 - the operator of a new offshore field'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Одна из сторон сделки учитывает доходы (расходы) при расчете базы по налогу на прибыль в соответствии со ст.275.2 НК РФ, а вторая не применяет ст.275.2 НК РФ (пп.6.п.2 ст.105.14 НК РФ)';
			|en = 'One of the parties to the transaction takes into account income (expenses) when calculating the income tax base in accordance with art. 275.2 TC RF, and the second does not apply art. 275.2 TC RF(subcl.6.cl.2 art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание участие в региональном инвестиционном проекте.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеУчастникРегиональногоИнвестиционногоПроекта(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - участник регионального инвестиционного проекта';
						|en = '%1 - participant of the regional investment project'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон является участником регионального инвестиционного проекта, применяющим пониженную налоговую ставку по налогу на прибыль в соответствии со ст. 284.3 и 284.3-1 НК РФ (пп.7.п.2 ст.105.14 НК РФ)';
			|en = 'At least one of the parties is a participant of the regional investment project, applying a reduced tax rate on income tax in accordance with art. 284.3 and 284.3-1 TC RF (subcl.7.cl.2 art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименование и подсказку 
// для кода 136 соответствии с п.4 ст.105.14 НК РФ п.4 ст.105.14 НК РФ 
// согласно формату уведомления, определенного приказами ФНС от 07 мая 2018 №ММВ-7-13/249@, от 26.07.2019 № ММВ-7-13/380@
// и от 17 февраля 2021 года №6-ФЗ.
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - исследовательский корпоративный центр ""Сколково""';
						|en = '%1 - corporate research center ""Scolkovo""'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является исследовательским корпоративным центром в ""Сколково"", применяющим освобождение от НДС в соответствии со ст.145.1 НК РФ (пп.8.п.2 ст.105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction is a research corporate center in Skolkovo, applying VAT exemption in accordance with art. 145.1 TC RF (subcl.8.cl.2 art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Процедура добавляет в структуру описания уведомления о контролируемых сделках наименование и подсказку 
// для кода 136 соответствии с п.4 ст.105.14 НК РФ п.4 ст.105.14 НК РФ 
// согласно формату уведомления, определенного приказом ФНС от 02.12.2024 № ЕД-7-13/1088@.
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеИсследовательскийЦентрСколковоИЭра(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - корпоративный центр ""Сколково"" или технополис ""Эра""';
						|en = '%1 - корпоративный центр ""Сколково"" или технополис ""Эра""'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является либо исследовательским корпоративным центром в ""Сколково"", применяющим освобождение от НДС в соответствии со ст.145.1 НК РФ,
			|либо участником Военного инновационного технополиса ""Эра"" Министерства обороны, применяющим освобождение от НДС в соответствии со ст.145.2 НК РФ (пп.8.п.2 ст.105.14 НК РФ)';
			|en = 'Хотя бы одна из сторон сделки является либо исследовательским корпоративным центром в ""Сколково"", применяющим освобождение от НДС в соответствии со ст.145.1 НК РФ,
			|либо участником Военного инновационного технополиса ""Эра"" Министерства обороны, применяющим освобождение от НДС в соответствии со ст.145.2 НК РФ (пп.8.п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание получения инвестиционного налогового вычета.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение инвестиционного налогового вычета';
						|en = '%1 - application of investment tax deduction'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки применяет в течение налогового периода инвестиционный налоговый вычет по налогу на прибыль, предусмотренный ст. 286.1 НК РФ (пп. 9 п. 2 с. 105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction applies during the fiscal period the investment tax deduction for income tax provided for in art. 286.1 TC RF (subcl. 9 cl. 2 art. 105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки основание дополнительного дохода от реализации углеводородов.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение налога на дополнительный доход от добычи углеводородов';
						|en = '%1 - application of tax on additional income from hydrocarbon production'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является плательщиком налога на дополнительный доход от добычи углеводородного сырья и доходы (расходы) по такой сделке учитываются при определении налоговой базы по этому налогу (пп.10 п.2. ст.105.14 НК РФ)';
			|en = 'At least one of the parties to the transaction is the payer of the tax on additional income from the production of hydrocarbons and income (expenses) on such a transaction are taken into account when determining the tax base for this tax (subcl.10 cl.2. art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

// Добавляет в описание причин контролируемости сделки разные ставки налога на прибыль.
//
// Параметры:
//  Описание - Структура - параметры описания основания:
//  * ОснованиеКонтролируемости131 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости132 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости133 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости134 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости135 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости136 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости137 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости138 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости139 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//  * ОснованиеКонтролируемости140 - Строка, Неопределено - номер пуктка причины контролируемости сделки.
//
//  НомерПункта - Строка - номер пуктка причины контролируемости сделки.
//
Процедура ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применяются разные ставки налога на прибыль';
						|en = '%1 - different corporate profit tax rates are applied'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Стороны сделки применяют разные ставки по налогу на прибыль по деятельности, в рамках которой заключена сделка (пп.1 п.2. ст.105.14 НК РФ)';
			|en = 'Parties to the transaction apply different profit tax rates for the activity within which the transaction is made (subcl.1 cl.2. art.105.14 TC RF)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

#КонецОбласти