
#Область ПрограммныйИнтерфейс

// Описывает правила прорисовки интерфейсных элементов
//
// Параметры:
//	ОрганизацияСсылка - СправочникСсылка.Организации
//
// Возвращаемое значение:
//	Структура
//
Функция ПараметрыПрорисовкиКнопокОтправки(ОрганизацияСсылка) Экспорт
	
	ПараметрыПрорисовкиКнопокОтправки = Новый Структура;
	
	ПараметрыПрорисовкиКнопокОтправки.Вставить("ГруппаОтправкаВКонтролирующийОрган", Истина);
	
	ИспользоватьОбмен = Ложь;
	ОнлайнПроверкаДоступна = Ложь;
	
	Если НЕ ИспользоватьОбмен Тогда
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ФормаИсторияОтправок", Ложь);
		// ожидается доработка в объектах потребителях
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ОбновитьОтправку", Ложь);
	КонецЕсли;
	
	// Обмен производится без учетной записи
	ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
	
	Возврат ПараметрыПрорисовкиКнопокОтправки;
	
КонецФункции

// Вызывается при открытии формы отчетности
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения
//	ПараметрыПрорисовкиПанели - Структура - возвращает параметры 
//
Процедура ПриИнициализацииФормыРегламентированногоОтчета(Форма, ПараметрыПрорисовкиПанели = Неопределено) Экспорт
	
	// прорисовываем панель отправки
	ПараметрыПрорисовкиПанели 			= ПолучитьПараметрыПрорисовкиПанелиОтправки(Форма);
	
	// если кнопка отправки отсутствует, то не будем регулировать
	КнопкаОтправитьВКонтролирующийОрган = Форма.Элементы.Найти("ОтправитьВКонтролирующийОрган");
	Если КнопкаОтправитьВКонтролирующийОрган = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацияСсылка = ПолучитьОрганизациюПоФорме(Форма);
	
	// прорисовываем кнопки отправки
	ПараметрыПрорисовкиКнопокОтправки   = ПараметрыПрорисовкиКнопокОтправки(ОрганизацияСсылка);
	
	// регулируем видимость кнопки в зависимости от результата
	УстановитьВидимостьГруппыКнопокОтправки(Форма, ПараметрыПрорисовкиКнопокОтправки);
	
КонецПроцедуры

// Определяет по источнику данных ссылку на документ, используемый в обмене.
//
// Параметры:
//	ИсточникДанных - ДокументСсылка.УведомлениеОСпецрежимахНалогообложения, ФормаКлиентскогоПриложения
//
// Возвращаемое значение:
//	ДокументСсылка.УведомлениеОСпецрежимахНалогообложения, Неопределено
//
Функция ПолучитьСсылкуДанных(ИсточникДанных) Экспорт
	
	Если ТипЗнч(ИсточникДанных) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		Результат = ИсточникДанных;
	ИначеЕсли РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ИсточникДанных, "ЗаявлениеОрганизации") Тогда
		Возврат ИсточникДанных.ЗаявлениеОрганизации;
	Иначе	
		Результат = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СсылкаНаОтчетПоФорме(ИсточникДанных);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет организацию по источнику данных
//
// Параметры:
//	Форма - ДокументСсылка.УведомлениеОСпецрежимахНалогообложения, ФормаКлиентскогоПриложения
//
// Возвращаемое значение:
//	СправочникСсылка.Организации, Неопределено
//
Функция ПолучитьОрганизациюПоФорме(Форма) Экспорт
	
	Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "СтруктураРеквизитовФормы")
		И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма.СтруктураРеквизитовФормы, "Организация") Тогда
		Возврат Форма.СтруктураРеквизитовФормы.Организация;
	ИначеЕсли РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "Объект")
		И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма.Объект, "Организация") Тогда
		Возврат Форма.Объект.Организация;
	ИначеЕсли РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "Организация") Тогда
		Возврат Форма.Организация.Ссылка;
	ИначеЕсли ТипЗнч(Форма) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		Возврат ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗначениеРеквизитаОбъекта(Форма, "Организация");
	ИначеЕсли СтрНайти(Форма.ИмяФормы, "ДлительнаяОперация") <> 0
		И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(Форма, "ТекущаяОрганизация") Тогда
		Возврат Форма.ТекущаяОрганизация;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Определяет транспорт подключения к сервису приема заявлений
// Если 0 - прямое подключение к сервису ФНС, 1 - через промежуточный жлюз
//
// Возвращаемое значение:
//	Число
//
Функция ВерсияОбменаПоУмолчанию() Экспорт
	
	Возврат 1;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьВидимостьГруппыКнопокОтправки(Форма, ПараметрыПрорисовкиКнопокОтправки)
	
	Для Каждого Эл Из ПараметрыПрорисовкиКнопокОтправки Цикл
		ЭУ = Форма.Элементы.Найти(Эл.Ключ);
		Если ЭУ <> Неопределено Тогда
			ЭУ.Видимость = Эл.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПараметрыПрорисовкиПанелиОтправки(Форма) Экспорт
	
	ОтчетСсылка 		= ПолучитьСсылкуДанных(Форма);
	ОрганизацияСсылка 	= ПолучитьОрганизациюПоФорме(Форма);
	ПараметрыПрорисовкиПанелиОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(ОтчетСсылка, ОрганизацияСсылка, "РегистрацияЮЛФНС");
			
	Возврат ПараметрыПрорисовкиПанелиОтправки;
	
КонецФункции

#КонецОбласти