#Область СлужебныйПрограммныйИнтерфейс

// Дополняет документы, которые не должны учитываться, как проверяемые. См. ПроверкаДокументовСервер.ИсключаемыеТипыДляПроверки
// 
// Параметры:
// 	ИсключаемыТипы - Массив Из Тип - Типы значения, которые не должны проверяться.
//
Процедура ДополнитьИсключаемыеДокументыДляПроверки(ИсключаемыТипы) Экспорт
	
	//++ Локализация
	МДокументы = Метаданные.Документы;
	
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковНДСПредъявленного.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковТМЦВЭксплуатации.Имя);
	ИсключаемыТипы.Добавить(МДокументы.КорректировкаНалогообложенияНДСПартийТоваров.Имя);
	ИсключаемыТипы.Добавить(МДокументы.МаркировкаТоваровГИСМ.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПеремаркировкаТоваровГИСМ.Имя);
	//++ НЕ УТ
	ИсключаемыТипы.Добавить(МДокументы.АмортизацияНМА.Имя);
	ИсключаемыТипы.Добавить(МДокументы.АмортизацияОС.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковВнеоборотныхАктивов.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковРасходовПриУСН.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводДанныхПоНалогуНаПрибыль.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВосстановлениеНДСПоОбъектамНедвижимости.Имя);
	ИсключаемыТипы.Добавить(МДокументы.КорректировкаРасчетовПоНалогам.Имя);
	ИсключаемыТипы.Добавить(МДокументы.НачислениеЕНП.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПогашениеСтоимостиТМЦВЭксплуатации.Имя);
	ИсключаемыТипы.Добавить(МДокументы.РегламентнаяОперация.Имя);
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ИсключаемыТипы.Добавить(МДокументы.АмортизацияНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.АмортизацияОСМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ВводОстатковОСМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ИзменениеПараметровНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ИзменениеПараметровОСМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПереводОсновныхСредствИнвестиционногоИмуществаМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПеремещениеНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПеремещениеОСМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПринятиеКУчетуНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.ПринятиеКУчетуОСМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.СписаниеНМАМеждународныйУчет.Имя);
	ИсключаемыТипы.Добавить(МДокументы.СписаниеОСМеждународныйУчет.Имя);
	//-- НЕ УТКА
	
	//-- Локализация
	
КонецПроцедуры

// Получает из переданного объекта (ссылки) массив реквизитов с типом "Организация" и дополняет переданный параметр "МассивОрганизаций".
//
//	Параметры:
//		МассивОрганизаций - Массив Из СправочникСсылка.Организации - массив Организаций для заполнения
//		ДокументИсточник - ДокументСсылка, ДокументОбъект - объект, реквизиты которого надо проанализировать на соответствие типу "Организация".
// 															Если передан объект - значение реквизиты считывается непосредственно из него, если передана ссылка -
//															- происходит обращение к данным базы запросом (см. ОбщегоНазначения.ЗначенияРеквизитовОбъекта)
//		УчитыватьУправленческуюОрганизацию - Булево - признак, определяющий, должна ли среди возвращаемого массива быть управленческая организация.
// 														Значение по умолчанию - истина;
//
Процедура ДополнитьМассивОрганизацийДокумента(МассивОрганизаций, ДокументИсточник, УчитыватьУправленческуюОрганизацию) Экспорт
	
	//++ Локализация
	ТипДокументаИсточник = ТипЗнч(ДокументИсточник);
	Если (ТипДокументаИсточник = Тип("ДокументОбъект.СчетФактураВыданный") 
			Или ТипДокументаИсточник = Тип("ДокументСсылка.СчетФактураВыданный"))
		И ЗначениеЗаполнено(ДокументИсточник.ДокументОснование) Тогда
		
		Для Каждого Организация Из ПроверкаДокументовСервер.МассивОрганизацийДокумента(ДокументИсточник.ДокументОснование) Цикл
			Если МассивОрганизаций.Найти(Организация) = Неопределено Тогда
				МассивОрганизаций.Добавить(Организация);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	//-- Локализация

КонецПроцедуры

#КонецОбласти