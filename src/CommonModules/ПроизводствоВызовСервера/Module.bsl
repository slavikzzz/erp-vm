////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы "Производство"
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Переопределяемый обработчик получения данных выбора справочника СтруктураПредприятия.
//
// Параметры:
//  ДанныеВыбора - СписокЗначений - значения выбора.
//  Параметры - Структура - параметры выбора.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура СтруктураПредприятияОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
		|	СтруктураПредприятия.Ссылка КАК Ссылка,
		|	СтруктураПредприятия.Представление КАК Представление
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Наименование ПОДОБНО &Текст
		|	И НЕ СтруктураПредприятия.ПометкаУдаления
		|	И (СтруктураПредприятия.ПодразделениеДиспетчер
		|			ИЛИ СтруктураПредприятия.ПроизводственноеПодразделение)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
		Запрос.УстановитьПараметр("Текст", "%" + СокрЛП(Параметры.СтрокаПоиска) + "%");
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЦикла; 
								
	КонецЕсли;
	
КонецПроцедуры

// Возвращает параметры производственного подразделения
//
// Параметры:
//  Подразделение	- СправочникСсылка.СтруктураПредприятия - Подразделение для которого требуется получить параметры.
//
// Возвращаемое значение:
//   Структура   - содержит параметры производственного подразделения.
//
Функция ПараметрыПроизводственногоПодразделения(Подразделение) Экспорт
	
	Параметры = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение);
	Возврат Параметры;
	
КонецФункции

// Определяет используется ли производство версии 2.1
// 
// Возвращаемое значение:
//   Булево - Истина, если используется производство версии 2.1
//
Функция ИспользуетсяПроизводство21() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством");
	
КонецФункции

// Определяет используется ли производство версии 2.2
// 
// Возвращаемое значение:
//   Булево - Истина, если используется производство версии 2.2
//
Функция ИспользуетсяПроизводство22() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2");
	
КонецФункции

#Область ПроизводствоБезЗаказов

// Оформить производство без заказа на основании.
// 
// Параметры:
//  МассивСсылок - Массив
//  ПараметрыДокументов - см. ПроизводствоКлиентСервер.ПараметрыФормированияДокументовПроизводстваБезЗаказов
// 
// Возвращаемое значение:
//  Структура - Оформить производство без заказа на основании:
//  * ОбъектФормы - ДокументОбъект
//  * ТипДокумента - Строка
//  * ЕстьОшибки - Булево -
//  * СписокДокументов - СписокЗначений -
//  * МассивОшибок - ФиксированныйМассив -
//
Функция ОформитьПроизводствоБезЗаказаНаОсновании(МассивСсылок, ПараметрыДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка                                    КАК Ссылка,
	|	ЗаказКлиента.Статус                                    КАК Статус,
	|	НЕ ЗаказКлиента.Проведен                               КАК ЕстьОшибкиПроведен,
	|	ЗаказКлиента.Статус =
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
	|		И &ИспользоватьРасширенныеВозможностиЗаказаКлиента КАК ЕстьОшибкиСтатус
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка В (&МассивСсылок)
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказДавальца.Ссылка                                           КАК Ссылка,
	|	ЗаказДавальца.Статус                                           КАК Статус,
	|	НЕ ЗаказДавальца.Проведен                                      КАК ЕстьОшибкиПроведен,
	|	ЗаказДавальца.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.НеСогласован),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.Согласован)) КАК ЕстьОшибкиСтатус
	|ИЗ
	|	Документ.ЗаказДавальца2_5 КАК ЗаказДавальца
	|ГДЕ
	|	ЗаказДавальца.Ссылка В (&МассивСсылок)
	//-- НЕ УТКА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0                                                              КАК НомерГруппыЗатрат,
	|	ЗаказКлиентаТовары.Ссылка.Организация                          КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукции) КАК ГруппировкаЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)         КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	КОНЕЦ                                                          КАК НаправлениеВыпуска,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗаказКлиентаТовары.Подразделение
	|		ИНАЧЕ ЗаказКлиентаТовары.Склад
	|	КОНЕЦ                                                          КАК Получатель,
	|	ЗаказКлиентаТовары.Номенклатура                                КАК Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика                              КАК Характеристика,
	|	ЗаказКлиентаТовары.Серия                                       КАК Серия,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Обособленно
	|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ                                                          КАК Назначение,
	|	ЗаказКлиентаТовары.Ссылка.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)        КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка) КАК ПравилоРаспределения,
	|	ЗаказКлиентаТовары.Количество                                  КАК Количество,
	|	ИСТИНА                                                         КАК ОтражатьЗатратыДокументом,
	|	ЛОЖЬ                                                           КАК ОшибкаВНастройкахМодели
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка В (&МассивСсылок)
	|	И ЗаказКлиентаТовары.Ссылка.Проведен
	|	И (НЕ ЗаказКлиентаТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
	|		ИЛИ НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента)
	|	И НЕ ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|	И НЕ ЗаказКлиентаТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0                                                              КАК НомерГруппыЗатрат,
	|	ЗаказДавальцаПродукция.Ссылка.Организация                      КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукцииИНазначениям) КАК ГруппировкаЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)         КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ЗаказДавальцаПродукция.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	КОНЕЦ                                                          КАК НаправлениеВыпуска,
	|	ВЫБОР
	|		КОГДА ЗаказДавальцаПродукция.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗаказДавальцаПродукция.Подразделение
	|		ИНАЧЕ ЗаказДавальцаПродукция.Склад
	|	КОНЕЦ                                                          КАК Получатель,
	|	ЗаказДавальцаПродукция.Номенклатура                            КАК Номенклатура,
	|	ЗаказДавальцаПродукция.Характеристика                          КАК Характеристика,
	|	ЗаказДавальцаПродукция.Серия                                   КАК Серия,
	|	ВЫБОР
	|		КОГДА ЗаказДавальцаПродукция.Обособленно
	|			И Константы.ВариантОбособленияПродукцииПриПриемеВПереработку =
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленияПриПриемеВПереработку.ЗаказДавальца)
	|			ТОГДА ЗаказДавальцаПродукция.Ссылка.Назначение
	|		КОГДА ЗаказДавальцаПродукция.Обособленно
	|			ТОГДА ЗаказДавальцаПродукция.Ссылка.Договор.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ                                                          КАК Назначение,
	|	ЗаказДавальцаПродукция.Ссылка.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ЗаказДавальцаПродукция.Спецификация                            КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка) КАК ПравилоРаспределения,
	|	ЗаказДавальцаПродукция.Количество                              КАК Количество,
	|	ИСТИНА                                                         КАК ОтражатьЗатратыДокументом,
	|	ЛОЖЬ                                                           КАК ОшибкаВНастройкахМодели
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Продукция КАК ЗаказДавальцаПродукция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|	ПО ИСТИНА
	|
	|ГДЕ
	|	ЗаказДавальцаПродукция.Ссылка В (&МассивСсылок)
	|	И ЗаказДавальцаПродукция.Ссылка.Проведен
	|	И НЕ ЗаказДавальцаПродукция.Ссылка.Статус В (
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.НеСогласован),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.Согласован))
	|	И НЕ ЗаказДавальцаПродукция.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И НЕ ЗаказДавальцаПродукция.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И НЕ ЗаказДавальцаПродукция.Отменено
	//-- НЕ УТКА
	|";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента", ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	Результаты = Запрос.ВыполнитьПакет();
	
	ДопустимыеСтатусыЗаказКлиента = Новый Массив;
	ДопустимыеСтатусыЗаказКлиента.Добавить(Перечисления.СтатусыЗаказовКлиентов.КОбеспечению);
	ДопустимыеСтатусыЗаказКлиента.Добавить(Перечисления.СтатусыЗаказовКлиентов.КОтгрузке);
	ДопустимыеСтатусыЗаказКлиента.Добавить(Перечисления.СтатусыЗаказовКлиентов.Закрыт);
	
	//++ НЕ УТКА
	ДопустимыеСтатусыЗаказДавальца = Новый Массив;
	ДопустимыеСтатусыЗаказДавальца.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	ДопустимыеСтатусыЗаказДавальца.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	ДопустимыеСтатусыЗаказДавальца.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	//-- НЕ УТКА
	
	ЗаказыСОшибкой = Результаты[0].Выгрузить();
	Для Каждого ТекущиеДанные Из ЗаказыСОшибкой Цикл
		
		Если ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ЗаказКлиента")Тогда
			ДопустимыеСтатусы = ДопустимыеСтатусыЗаказКлиента;
		//++ НЕ УТКА
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ЗаказДавальца2_5")Тогда
			ДопустимыеСтатусы = ДопустимыеСтатусыЗаказДавальца;
		//-- НЕ УТКА
		Иначе
			ДопустимыеСтатусы = Новый Массив;
		КонецЕсли;
		
		ПараметрыПроверки                          = ОбщегоНазначенияУТ.ПараметрыПроверкиВозможностиВводаНаОсновании();
		ПараметрыПроверки.Статус                   = ТекущиеДанные.Статус;
		ПараметрыПроверки.ЕстьОшибкиПроведен       = ТекущиеДанные.ЕстьОшибкиПроведен;
		ПараметрыПроверки.ЕстьОшибкиСтатус         = ТекущиеДанные.ЕстьОшибкиСтатус;
		ПараметрыПроверки.МассивДопустимыхСтатусов = ДопустимыеСтатусы;
		ПараметрыПроверки.ПрерыватьВыполнение      = Ложь;
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСПараметрами(ТекущиеДанные.Ссылка, ПараметрыПроверки);
		
	КонецЦикла;
	
	Для Каждого ТекущиеДанные Из Результаты[Результаты.ВГраница()].Выгрузить() Цикл
		СтруктураСтрокиИзделия = ПроизводствоКлиентСервер.СтруктураСтрокиИзделия();
		ЗаполнитьЗначенияСвойств(СтруктураСтрокиИзделия, ТекущиеДанные);
		ПараметрыДокументов.ИзделияПоСпецификации.Добавить(СтруктураСтрокиИзделия);
	КонецЦикла;
	
	Возврат Обработки.ОформлениеПроизводстваБезЗаказов.СформироватьДокументыПоПараметрам(ПараметрыДокументов);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетДолейСтоимости

Функция ДоступнаРасшифровкаРасчетаДолиСтоимости(СпособРаспределенияЗатратНаВыходныеИзделия) Экспорт
	
	Если СпособРаспределенияЗатратНаВыходныеИзделия = Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоПлановойСтоимости Тогда
		Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25() Тогда
			ЕстьДоступ = ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатуры25);
		Иначе
			ЕстьДоступ = ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатуры);
		КонецЕсли;
	Иначе
		ЕстьДоступ = Истина;
	КонецЕсли;
	
	Возврат ЕстьДоступ;
	
КонецФункции

#КонецОбласти

Функция ДоступноРабочееМестоЗаказыПереработчикам() Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство")
		И Пользователи.РолиДоступны("ИспользованиеРабочегоМестаЗаказыПереработчикам") Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

//++ НЕ УТ

Функция ПараметрыСозданияДвиженияПродукцииИМатериаловНаОсновании(МассивЗаказов, ХозяйственнаяОперация) Экспорт
	
	ОбъектыОснований = МассивЗаказов;
	
	РеквизитыШапки = Документы.ДвижениеПродукцииИМатериалов.ДанныеЗаполненияНакладной(ОбъектыОснований, Новый Структура("ХозяйственнаяОперация", ХозяйственнаяОперация));
	РезультатыПроверки = Документы.ДвижениеПродукцииИМатериалов.ПроверитьДанныеЗаполненияНакладной(РеквизитыШапки);
	
	ПараметрыОснования = Новый Структура;
	Если РеквизитыШапки.ПоРаспоряжениям Тогда
		ПараметрыОснования.Вставить("РеквизитыШапки", РеквизитыШапки);
		ПараметрыОснования.Вставить("МассивЗаказов",  ОбъектыОснований);
	Иначе
		ПараметрыОснования.Вставить("ОбъектыОснований", ОбъектыОснований);
		ПараметрыОснования.Вставить("ХозяйственнаяОперация", РеквизитыШапки.ХозяйственнаяОперация);
	КонецЕсли;
	
	Возврат Новый Структура("Основание, РезультатыПроверки", ПараметрыОснования, РезультатыПроверки);
	
КонецФункции

Функция ДокументыДвиженияПродукцииИМатериаловПоПараметрам(ПараметрыДокументов) Экспорт
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ПроизводствоВызовСервера.ДокументыДвиженияПродукцииИМатериаловПоПараметрам");
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	МассивОбъектов = Документы.ДвижениеПродукцииИМатериалов.ДокументыПоПараметрам(ПараметрыДокументов);
	Результат = Новый Структура;
	
	Если МассивОбъектов.Количество() = 1 Тогда
		ЗначениеВДанныеФормы(МассивОбъектов[0], ПараметрыДокументов.ОбъектФормы);
		Результат.Вставить("ОткрытьФормуНового");
	Иначе
		
		СозданныеДокументы = Новый Массив;
		Для Каждого ТекДокумент Из МассивОбъектов Цикл
			ТекДокумент.Записать(РежимЗаписиДокумента.Запись);
			СозданныеДокументы.Добавить(ТекДокумент.Ссылка);
		КонецЦикла;
		
		Если СозданныеДокументы.Количество() > 1 Тогда
			
			ПараметрыФормы = Новый Структура("КлючДанных, ВидимыеКолонки, СобытияОбновления",
				"ВыпускПродукцииБезЗаказаНаОсновании", Новый Массив, Новый Массив);
			
			ПараметрыФормы.ВидимыеКолонки.Добавить("Номер");
			ПараметрыФормы.ВидимыеКолонки.Добавить("Дата");
			ПараметрыФормы.ВидимыеКолонки.Добавить("ТипЗначения");
			ПараметрыФормы.ВидимыеКолонки.Добавить("ХозяйственнаяОперация");
			ПараметрыФормы.ВидимыеКолонки.Добавить("Организация");
			ПараметрыФормы.ВидимыеКолонки.Добавить("Подразделение");
			ПараметрыФормы.ВидимыеКолонки.Добавить("НаправлениеДеятельности");
			Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыДвиженийПродукцииИМатериалов") Тогда
				ПараметрыФормы.ВидимыеКолонки.Добавить("Статус");
			КонецЕсли;
			ПараметрыФормы.ВидимыеКолонки.Добавить("Дополнительно");
			
			ПараметрыФормы.СобытияОбновления.Добавить("Запись_ДвижениеПродукцииИМатериалов");
			
			Результат.Вставить("ИмяФормы", "ОбщаяФорма.ФормаСозданныхДокументов");
			Результат.Вставить("Параметры", ПараметрыФормы);
			
			Владелец = Пользователи.АвторизованныйПользователь();
			УстановитьПривилегированныйРежим(Истина);
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, СозданныеДокументы, ПараметрыФормы.КлючДанных);
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
		Результат.Вставить("КоличествоСозданныхДокументов", СозданныеДокументы.Количество());
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	КоличествоОпераций = ПараметрыДокументов.ОбъектыОснований.Количество();
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОпераций);
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	Возврат Результат;
	
	
КонецФункции

Функция СоздатьАктВыполненныхВнутреннихРаботПроверкаОснований(ОбъектыОснований) Экспорт
	
	ТекстыЗапросов = Новый Массив;
	ТекстЗапроса =
	"
	//++ НЕ УТКА
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка                          КАК Ссылка,
	|	ТаблицаТовары.НомерГруппыЗатрат               КАК НомерГруппыЗатрат,
	|	МАКСИМУМ(ТаблицаТовары.ВнутренняяПереработка) КАК ВнутренняяПереработка,
	|	МАКСИМУМ(ТаблицаТовары.ОрганизацияДавалец)    КАК ОрганизацияДавалец
	|ПОМЕСТИТЬ ГруппыЗатрат
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&ОбъектыОснований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерГруппыЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерГруппыЗатрат
	|;
	|
	//-- НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	// Получатели выходных изделий (производство без заказа)
	|ВЫБРАТЬ
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА ТаблицаТоваров.ВнутренняяПереработка
	|			ТОГДА ТаблицаТоваров.ОрганизацияДавалец
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА Реквизиты.Организация
	|	КОНЕЦ								КАК Организация,
	|	Реквизиты.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ТаблицаТоваров.Получатель			КАК Подразделение
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК Реквизиты
	|		ПО ТаблицаТоваров.Ссылка = Реквизиты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаТоваров.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТоваров.Ссылка В(&ОбъектыОснований)
	|	И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаТоваров.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.НаправлениеДеятельности,
	|	ТаблицаТоваров.Получатель,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА ТаблицаТоваров.ВнутренняяПереработка
	|			ТОГДА ТаблицаТоваров.ОрганизацияДавалец
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА Реквизиты.Организация
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Получатели побочных изделий (производство без заказа)
	|ВЫБРАТЬ
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ТаблицаТоваров.ВладелецИзделия = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.ВладелецИзделия
	|		КОГДА СпрПартииПроизводства.ВнутренняяПереработка
	|			ТОГДА СпрПартииПроизводства.Организация
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА Реквизиты.Организация
	|	КОНЕЦ								КАК Организация,
	|	Реквизиты.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	ТаблицаТоваров.Получатель			КАК Подразделение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТоваров
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК Реквизиты
	|	ПО ТаблицаТоваров.Ссылка = Реквизиты.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО ТаблицаТоваров.Номенклатура = СпрНоменклатура.Ссылка
	//++ НЕ УТКА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|	ПО СпрПартииПроизводства.Документ = ТаблицаТоваров.Ссылка
	|	И СпрПартииПроизводства.Код = ТаблицаТоваров.НомерГруппыЗатрат
	|	И НЕ СпрПартииПроизводства.ПометкаУдаления
	//-- НЕ УТКА
	|	
	|ГДЕ
	|	ТаблицаТоваров.Ссылка В(&ОбъектыОснований)
	|	И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаТоваров.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.НаправлениеДеятельности,
	|	ТаблицаТоваров.Получатель,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ТаблицаТоваров.ВладелецИзделия = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.ВладелецИзделия
	|		КОГДА СпрПартииПроизводства.ВнутренняяПереработка
	|			ТОГДА СпрПартииПроизводства.Организация
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА Реквизиты.Организация
	|	КОНЕЦ
	|";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	//++ НЕ УТКА
	ТекстыЗапросов.Добавить(Документы.ЭтапПроизводства2_2.ТекстЗапросаПроверкиОснованийАктовРабот());
	//-- НЕ УТКА
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	Запрос.Текст = Запрос.Текст + "
	|;
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаТоваров.Организация)							КАК Организация,
	|	МАКСИМУМ(ТаблицаТоваров.НаправлениеДеятельности)				КАК НаправлениеДеятельности,
	|	МАКСИМУМ(ТаблицаТоваров.Подразделение)							КАК Подразделение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаТоваров.Организация)				КАК РазличныеОрганизации,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаТоваров.НаправлениеДеятельности)	КАК РазличныеНаправленияДеятельности,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаТоваров.Подразделение)				КАК РазличныеПолучатели
	|ИЗ
	|	ВтТовары КАК ТаблицаТоваров
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТоваров.Организация) ЕСТЬ НЕ NULL";
	
	Запрос.УстановитьПараметр("ОбъектыОснований", ОбъектыОснований);
	
	Ошибки = Запрос.Выполнить().Выбрать();
	РеквизитыШапки = Новый Структура("Организация, НаправлениеДеятельности, Подразделение");
	Результат = Новый Структура;
	
	Если Ошибки.Следующий() Тогда
		
		ЕстьОшибки = Ложь;
		ТекстОшибки = НСтр("ru = 'В документах-основаниях различаются значения:';
							|en = 'Values are different in base documents:'");
		Если Не Ошибки.РазличныеОрганизации = 1 Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = '- Организаций';
															|en = '- Companies'");
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если Не Ошибки.РазличныеПолучатели = 1 Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = '- Получателей работ';
															|en = '- Work recipients'");
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если Не Ошибки.РазличныеНаправленияДеятельности = 1 Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = '- Направлений деятельности';
															|en = '- Lines of business'");
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если ЕстьОшибки Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Оформление документа невозможно!';
															|en = 'Cannot register the document.'");
			Результат.Вставить("ЕстьОшибки");
			Результат.Вставить("ТекстОшибки", ТекстОшибки);
		Иначе
			ЗаполнитьЗначенияСвойств(РеквизитыШапки, Ошибки);
		КонецЕсли;
	Иначе
		ТекстОшибки = НСтр("ru = 'Отсутствуют данные для заполнения!';
							|en = 'No data to populate.'");
		Результат.Вставить("ЕстьОшибки");
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
	КонецЕсли;
	Результат.Вставить("РеквизитыШапки", РеквизитыШапки);
	
	Возврат Результат;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти
