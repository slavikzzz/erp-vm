#Область СлужебныйПрограммныйИнтерфейс

// Определяет включено ли ведение учета продукции, подлежащей маркированию.
//
// Параметры:
// 	РасширеннаяВерсияГосИС - Булево
// Возвращаемое значение:
//  Булево - Истина, если учет ведется.
Функция ВедетсяУчетМаркируемойПродукции(РасширеннаяВерсияГосИС) Экспорт
	
	Если РасширеннаяВерсияГосИС Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("ШтрихкодированиеИС");
		УчитываемыеВидыМаркируемойПродукции = Модуль.УчитываемыеВидыМаркируемойПродукции();
		
		Возврат УчитываемыеВидыМаркируемойПродукции.Количество() > 0;
		
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к виду продукции ИС МП.
// 
// Параметры:
// 	ВидПродукции              - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// 	ВключаяТабачнуюПродукцию  - Булево                             - Признак включения табачной продукции.
// 	ВключатьМолочнуюПродукцию - Булево                             - Признак включения молочной продукции.
// 	ВключатьПиво              - Булево                             - Признак включения пивной продукции.
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ЭтоПродукцияИСМП(ВидПродукции, ВключаяТабачнуюПродукцию = Ложь, ВключатьМолочнуюПродукцию = Истина, ВключатьПиво = Истина) Экспорт
	
	ВидыПродукцииИСМП = ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииИСМП(ВключаяТабачнуюПродукцию, ВключатьМолочнуюПродукцию, ВключатьПиво);
	
	Возврат ВидыПродукцииИСМП.Найти(ВидПродукции) <> Неопределено;
	
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к табачной продукции.
// 
// Параметры:
// 	ВидПродукции              - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ЭтоПродукцияМОТП(ВидПродукции) Экспорт
	
	Возврат ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции);
	
КонецФункции

// Классифицирует текущий сеанс, как сеанс, запущенный в фоновом задании в клиент-серверном варианте, в остальных
// случаях, сеанс имеет ту же файловую систему на стороне сервера, что и основной сеанс.
//	
// Возвращаемое значение:
// 	Булево - Описание
Функция ЭтоФоновоеЗаданиеНаСервере() Экспорт
	Возврат ОбщегоНазначенияИСВызовСервера.ЭтоФоновоеЗаданиеНаСервере();
КонецФункции

Функция ПредставленияВскрытыхПотребительскихУпаковок(ИмяПодсистемы) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ЗаголовокСписка", НСтр("ru = 'Вскрытые потребительские упаковки ИС МП';
															|en = 'Вскрытые потребительские упаковки ИС МП'"));
	ВозвращаемоеЗначение.Вставить("ЗаголовокЗаписи", НСтр("ru = 'Вскрытие потребительской упаковки ИС МП';
															|en = 'Вскрытие потребительской упаковки ИС МП'"));
	
	ВидыПродукции = ОбщегоНазначенияИСВызовСервера.ВидыПродукцииИспользующиеВскрытыеПотребительскихУпаковок(
		ИмяПодсистемы = "ИСМП", ИмяПодсистемы = "ЕГАИС");
	
	Если ИмяПодсистемы = "ИСМП" Тогда
		
		Если ВидыПродукции.Количество() = 1
			И (ВидыПродукции[0] = Перечисления.ВидыПродукцииИС.Пиво
				Или ВидыПродукции[0] = Перечисления.ВидыПродукцииИС.ПивоВПотребительскихУпаковках) Тогда
			
			ВозвращаемоеЗначение.ЗаголовокСписка = НСтр("ru = 'Кеги на оборудовании розлива ИС МП';
														|en = 'Кеги на оборудовании розлива ИС МП'");
			ВозвращаемоеЗначение.ЗаголовокЗаписи = НСтр("ru = 'Подключение кега к оборудованию розлива';
														|en = 'Подключение кега к оборудованию розлива'");
			
		КонецЕсли;
		
	ИначеЕсли ИмяПодсистемы = "ЕГАИС" Тогда
		
		ВозвращаемоеЗначение.ЗаголовокСписка = НСтр("ru = 'Вскрытые бутылки ЕГАИС';
													|en = 'Вскрытые бутылки ЕГАИС'");
		ВозвращаемоеЗначение.ЗаголовокЗаписи = НСтр("ru = 'Вскрытие бутылки ЕГАИС';
													|en = 'Вскрытие бутылки ЕГАИС'");
		
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(ВозвращаемоеЗначение);
	
КонецФункции

// Возвращает объект ЗащищенноеСоединениеOpenSSL
// 
// Возвращаемое значение:
// 	ЗащищенноеСоединениеOpenSSL - Описание
Функция ЗащищенноеСоединение() Экспорт
	
	Возврат ОбщегоНазначенияИСКлиентСерверПовтИсп.ЗащищенноеСоединение();
	
КонецФункции

#КонецОбласти