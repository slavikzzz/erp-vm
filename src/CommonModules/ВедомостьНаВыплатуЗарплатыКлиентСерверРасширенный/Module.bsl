
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОтображениеВХО(Форма, ВыплатаЗарплаты = Истина,  ПеречислениеНДФЛ = Истина) Экспорт
	Если ВыплатаЗарплаты Тогда
		УстановитьОтображениеВХОВыплатыЗарплаты(Форма);
		УстановитьОтображениеВХОПВыплатыДоходов(Форма);
	КонецЕсли;
	Если ПеречислениеНДФЛ Тогда 
		УстановитьОтображениеВХОПеречисленияНДФЛ(Форма);
	КонецЕсли	
КонецПроцедуры

Процедура УстановитьОтображениеВХОВыплатыЗарплаты(Форма)
	
	Ведомость = Форма.Объект.Ссылка;
	
	ЕстьНевыплаты = Ложь;
	
	ФормаОплатыВедомости = ФормаОплатыВедомости(Ведомость);
	
	Если ЗначениеЗаполнено(ФормаОплатыВедомости) 
		И ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Безналичная") Тогда
		
		ЕстьНевыплаты = ЗарплатаКадрыПриложенияВызовСервера.ЕстьНезачисленияПоВедомости(Ведомость, Истина);
		
	ИначеЕсли ЗначениеЗаполнено(ФормаОплатыВедомости)
		И ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Наличная") Тогда
		
		ЕстьНевыплаты = ЗарплатаКадрыПриложенияВызовСервера.ЕстьДепонированияПоВедомости(Ведомость, Истина);
		
	КонецЕсли;
	
	Если ЕстьНевыплаты Тогда
		Форма.Элементы.ВыплатаЗарплатыСтраницы.ТекущаяСтраница = Форма.Элементы.ВыплатаЗарплатыСтраницыЧастично
	Иначе
		Форма.Элементы.ВыплатаЗарплатыСтраницы.ТекущаяСтраница = Форма.Элементы.ВыплатаЗарплатыСтраницыПолностью
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтображениеВХОПеречисленияНДФЛ(Форма)
	
	Если Форма.Объект.ПеречислениеНДФЛВыполнено Тогда
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаПолностью
	Иначе
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаЧастично
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтображениеВХОПВыплатыДоходов(Форма)
	
	Ведомость = Форма.Объект.Ссылка;
	
	Форма.ДатаВыплаты = ВедомостьНаВыплатуЗарплатыКлиентСервер.ДатаВыплаты(Форма.Объект);
	
	ЕстьПодтверждениеВыплатыДоходов = ЗарплатаКадрыПриложенияВызовСервера.ЕстьПодтверждениеВыплатыДоходовПоВедомости(Ведомость);
	Если ЕстьПодтверждениеВыплатыДоходов Тогда
		Форма.Элементы.УточнениеДатыВыплаты.ТекущаяСтраница = Форма.Элементы.ПодтверждениеВыплатыГруппа
	Иначе
		Форма.Элементы.УточнениеДатыВыплаты.ТекущаяСтраница = Форма.Элементы.ДатаВыплатыГруппа
	КонецЕсли;
	
КонецПроцедуры

Функция ФормаОплатыВедомости(Ведомость)
	
	Если ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк") Или ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыПеречислением") Тогда
		ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Безналичная")
	ИначеЕсли ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу") Или ТипЗнч(Ведомость) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыРаздатчиком") Тогда
		ФормаОплатыВедомости = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Наличная")
	Иначе
		ФормаОплатыВедомости = Неопределено
	КонецЕсли;	
	
	Возврат ФормаОплатыВедомости
	
КонецФункции	

#КонецОбласти
