
#Область ПрограммныйИнтерфейс

#Область Форма

// Формирует гиперссылку заказа переработчика
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма документа
Процедура СформироватьГиперссылкуСозданияЗаказа(Форма) Экспорт
	
	Объект = Форма.Объект;
	Если Объект.ПроизводствоНаСтороне Тогда
		
		Если Форма.СозданЗаказПереработчику Тогда
			
			МассивСтрок = Новый Массив;
			
			//++ Устарело_Переработка24
			Если ТипЗнч(Объект.ЗаказПереработчику) = Тип("ДокументСсылка.ЗаказПереработчику") Тогда
				ПредставлениеЗаказа =
					СтрШаблон(
						НСтр("ru = 'заказ переработчику (2.4) № %1 от %2';
							|en = 'purchase order — Subcontracting (2.4) %1, %2'"),
						ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
							Форма.РеквизитыЗаказаПереработчика.Номер, Ложь, Истина),
						Формат(Форма.РеквизитыЗаказаПереработчика.Дата, "ДЛФ=D"));
			Иначе
			//-- Устарело_Переработка24
				ПредставлениеЗаказа =
					СтрШаблон(
						НСтр("ru = 'заказ переработчику № %1 от %2';
							|en = 'Subcontracting purchase order No. %1, %2'"),
						ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
							Форма.РеквизитыЗаказаПереработчика.Номер, Ложь, Истина),
						Формат(Форма.РеквизитыЗаказаПереработчика.Дата, "ДЛФ=D"));
			//++ Устарело_Переработка24
			КонецЕсли;
			//-- Устарело_Переработка24
			
			Если Форма.ПравоПросмотраЗаказаПереработчику
				Или Форма.ПравоПросмотраЗаказаПереработчику2_5 Тогда
				СсылкаНаЗаказ = ПолучитьНавигационнуюСсылку(Форма.Объект.ЗаказПереработчику);
			Иначе
				СсылкаНаЗаказ = Неопределено;
			КонецЕсли;
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЗаказа,,,, СсылкаНаЗаказ));
			
			Если НЕ Форма.РеквизитыЗаказаПереработчика.Проведен Тогда
				МассивСтрок.Добавить(" ");
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
											НСтр("ru = 'не проведен';
												|en = 'not posted'"),, Форма.ЦветОсобогоТекста));
			КонецЕсли;
			
			ТекстЗаказ = Новый ФорматированнаяСтрока(МассивСтрок);
			
		Иначе
			
			ТекстСтроки = НСтр("ru = 'создать заказ переработчику';
								|en = 'create subcontracting purchase order'");
			
			Если Форма.ИспользоватьЗаказыПереработчикам2_5
				И Форма.ПравоДобавленияЗаказаПереработчику2_5 Тогда
				СсылкаНаЗаказ = "#СоздатьЗаказ2_5";
			//++ Устарело_Переработка24
			ИначеЕсли Форма.ИспользуетсяПроизводствоНаСтороне
				И Не Форма.ИспользуетсяПроизводствоНаСтороне2_5
				И Форма.ПравоДобавленияЗаказаПереработчику Тогда
				СсылкаНаЗаказ = "#СоздатьЗаказ";
			//-- Устарело_Переработка24
			Иначе
				ТекстСтроки = НСтр("ru = 'нет заказа переработчику';
									|en = 'no subcontracting purchase order'");
				СсылкаНаЗаказ = "";
			КонецЕсли;
			
			ТекстЗаказ = Новый ФорматированнаяСтрока(ТекстСтроки,,,, СсылкаНаЗаказ);
			
		КонецЕсли;
		
		Форма.НадписьЗаказПереработчику = Новый ФорматированнаяСтрока("(", ТекстЗаказ, ")");
		
	Иначе
		Форма.НадписьЗаказПереработчику = Новый ФорматированнаяСтрока("");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
