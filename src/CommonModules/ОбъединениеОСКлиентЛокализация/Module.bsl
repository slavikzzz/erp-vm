////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Объединение ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаДокумента

// Обработчик события "ПриИзменении".
// 
// Параметры:
// 	Элемент - ПолеФормы - Элемент формы.
// 	Форма - ФормаКлиентскогоПриложения - Форма.
Процедура ПриИзмененииРеквизита(Элемент, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	ВнеоборотныеАктивыКлиентЛокализация.ПриИзмененииРеквизитаОбъединениеОС(
		Элемент, Форма, ДополнительныеПараметры, ТребуетсяВызовСервера);
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			Элемент.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
	
	КонецЕсли;
	
КонецПроцедуры

Функция ТребуетсяВызовСервераДляНастройкиЭлементовФормы(Объект, СтруктураИзмененныхРеквизитов) Экспорт

	//++ Локализация
	Если СтруктураИзмененныхРеквизитов.Количество() = 0
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете") 
		
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")  
			И Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету")
			И ЗначениеЗаполнено(Объект.СтатьяРасходовБУ) Тогда
			
		Возврат Истина;
	КонецЕсли; 
	//-- Локализация
	
	Возврат Ложь;

КонецФункции

Процедура ОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	
	Объект = Форма.Объект;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "УстановкаПорядкаУчетаНУ" Тогда
		СтандартнаяОбработка = Ложь;
		
		Форма.Модифицированность = Истина;
		Объект.ПорядокУчетаНУ = Форма.РекомендуемыйПорядокУчетаНУ;
		
		ДополнительныеПараметрыДействия = Новый Структура;
		ДополнительныеПараметрыДействия.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы");
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			"ПорядокУчетаНУ", 
			ДополнительныеПараметрыДействия,
			Ложь);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Обработчик события "НачалоВыбора" поля формы.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент формы.
//  ДанныеВыбора - СписокЗначений - 
//  СтандартнаяОбработка - Булево - 
//  Форма - ФормаКлиентскогоПриложения - Форма.
Процедура НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент = Элементы.СтатьяРасходовБУ
		ИЛИ Элемент = Элементы.СтатьяРасходовНалог Тогда
		
		ДоходыИРасходыКлиент.НачалоВыбораСтатьи(Форма, Элемент, СтандартнаяОбработка);
		
	ИначеЕсли Элемент = Элементы.АналитикаРасходовБУ
		ИЛИ Элемент = Элементы.АналитикаРасходовНалог Тогда
		
		ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(Форма, Элемент, СтандартнаяОбработка);
		
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

// Обработчик события "АвтоПодбор" поля формы.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент формы.
//  Текст - Строка -
//  ДанныеВыбора - СписокЗначений -
//  ПараметрыПолученияДанных - Структура -
//  Ожидание - Число -
//  СтандартнаяОбработка - Булево -
//  Форма - ФормаКлиентскогоПриложения - Форма.
Процедура АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент = Элементы.АналитикаРасходовБУ
		ИЛИ Элемент = Элементы.АналитикаРасходовНалог Тогда
		
		ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

// Обработчик события "ОкончаниеВводаТекста".
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент формы.
//  Текст - Строка - 
//  ДанныеВыбора - СписокЗначений -
//  ПараметрыПолученияДанных - Структура -
//  СтандартнаяОбработка - Булево - 
//  Форма - ФормаКлиентскогоПриложения -
Процедура ОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент = Элементы.АналитикаРасходовБУ
		ИЛИ Элемент = Элементы.АналитикаРасходовНалог Тогда
		
		ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
