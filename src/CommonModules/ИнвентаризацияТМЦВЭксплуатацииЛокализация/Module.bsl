////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Инвентаризация ТМЦ в эксплуатации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// ИНВ-22 (Приказ о проведении инвентаризации)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Порядок = 1;
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о проведении инвентаризации (ИНВ-22)';
										|en = 'Stocktaking order (INV-22)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Инвентаризационная опись (ИНВ-3)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Порядок = 2;
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ3";
	КомандаПечати.Идентификатор = "ИНВ3_НаОснованииИнвентаризации";
	КомандаПечати.Представление = НСтр("ru = 'Инвентаризационная опись (ИНВ-3)';
										|en = 'Physical inventory register (INV-3)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Сличительная ведомость (ИНВ-19)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Порядок = 3;
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ19";
	КомандаПечати.Идентификатор = "ИНВ19_НаОснованииИнвентаризации";
	КомандаПечати.Представление = НСтр("ru = 'Сличительная ведомость (ИНВ-19)';
										|en = 'Inventory reconciliation report (INV-19)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Получает данные для формирования печатной формы ИНВ19
//
// Параметры:
//	ПараметрыПечати - Структура
//	ДокументОснование - ДокументСсылка.ИнвентаризацияТМЦВЭксплуатации - Документ, по которому необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* Шапка - РезультатЗапроса
// 		* ТабличнаяЧасть - РезультатЗапроса
//
Функция ДанныеДляПечатнойФормыИНВ19(ПараметрыПечати, ДокументОснование) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаОкончания,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);	
 	Выборка = Запрос.Выполнить().Выбрать();
			
	Выборка.Следующий();
	
	ДатаОкончания = КонецДня(Выборка.ДатаОкончания);
	Организация = Выборка.Организация;
				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Руководитель", СтруктураОтветственных.Руководитель);
	Запрос.УстановитьПараметр("ДолжностьРуководителя", СтруктураОтветственных.РуководительДолжность);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер", СтруктураОтветственных.ГлавныйБухгалтер);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка.Организация КАК Организация,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.КоличествоФактическое - ТаблицаТовары.КоличествоПоДаннымУчета > 0
	|				ТОГДА ТаблицаТовары.КоличествоФактическое - ТаблицаТовары.КоличествоПоДаннымУчета 
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК КоличествоИзлишков,
	|
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТовары.КоличествоФактическое - ТаблицаТовары.КоличествоПоДаннымУчета < 0
	|				ТОГДА -(ТаблицаТовары.КоличествоФактическое - ТаблицаТовары.КоличествоПоДаннымУчета) 
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК КоличествоНедостачи
	|
	|ПОМЕСТИТЬ втТовары
	|
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации.Товары КАК ТаблицаТовары
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.ИнвентарныйНомер
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.КоличествоФактическое - ТаблицаТовары.КоличествоПоДаннымУчета) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтоимостьТМЦ.Организация,
	|	СтоимостьТМЦ.Номенклатура,
	|	СтоимостьТМЦ.Характеристика,
	|	СтоимостьТМЦ.Серия,
	|	СтоимостьТМЦ.ИнвентарныйНомер,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьТМЦ.Количество) <> 0
	|			ТОГДА СУММА(СтоимостьТМЦ.СтоимостьРегл) / СУММА(СтоимостьТМЦ.Количество)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаРегл,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьТМЦ.Количество) <> 0
	|			ТОГДА СУММА(СтоимостьТМЦ.СтоимостьУпр) / СУММА(СтоимостьТМЦ.Количество)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаУпр
	|		
	|ПОМЕСТИТЬ СтоимостьТМЦ
	|	
	|ИЗ
	|	РегистрСведений.СтоимостьТМЦВЭксплуатации КАК СтоимостьТМЦ
	|	
	|ГДЕ
	|	СтоимостьТМЦ.Активность
	|	
	|	И (СтоимостьТМЦ.Организация, СтоимостьТМЦ.Номенклатура, СтоимостьТМЦ.Характеристика, СтоимостьТМЦ.Серия, СтоимостьТМЦ.ИнвентарныйНомер) В 
	|		(ВЫБРАТЬ
	|			втТовары.Организация, 
	|			втТовары.Номенклатура, 
	|			втТовары.Характеристика, 
	|			втТовары.Серия, 
	|			втТовары.ИнвентарныйНомер 
	|		ИЗ
	|			втТовары КАК втТовары)
	|	
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьТМЦ.Организация,
	|	СтоимостьТМЦ.Номенклатура,
	|	СтоимостьТМЦ.Характеристика,
	|	СтоимостьТМЦ.Серия,
	|	СтоимостьТМЦ.ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Руководитель КАК Руководитель,
	|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК ОрганизацияПредставление,
	|	ДанныеДокумента.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.ФизическоеЛицо КАК Кладовщик,
	|	"""" КАК ДолжностьКладовщика,
	|	ДанныеДокумента.Подразделение.Наименование КАК ПодразделениеПредставление,
	|	ДанныеДокумента.ДатаНачалаИнвентаризации КАК ДатаНачала,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаОкончания,
	|	ДанныеДокумента.ДокументОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ДокументОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.Номер КАК НомерДокумента,
	|	ДанныеДокумента.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаСнятияОстатков
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
	|	ТаблицаТовары.Серия.Наименование КАК СерияПредставление,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаТовары.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаТовары.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|
	|	ТаблицаТовары.КоличествоИзлишков КАК КоличествоИзлишков,
	|
	|	ТаблицаТовары.КоличествоИзлишков * ЕСТЬNULL(СтоимостьТМЦ.ЦенаРегл, 0) КАК СуммаИзлишков,
	|
	|	ТаблицаТовары.КоличествоНедостачи КАК КоличествоНедостачи,
	|
	|	ТаблицаТовары.КоличествоНедостачи * ЕСТЬNULL(СтоимостьТМЦ.ЦенаРегл, 0) КАК СуммаНедостачи
	|
	|ИЗ
	|	втТовары КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьТМЦ КАК СтоимостьТМЦ
	|		ПО СтоимостьТМЦ.Организация = ТаблицаТовары.Организация
	|			И СтоимостьТМЦ.Номенклатура = ТаблицаТовары.Номенклатура
	|			И СтоимостьТМЦ.Характеристика = ТаблицаТовары.Характеристика
	|			И СтоимостьТМЦ.Серия = ТаблицаТовары.Серия
	|			И СтоимостьТМЦ.ИнвентарныйНомер = ТаблицаТовары.ИнвентарныйНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварНаименование,
	|	ХарактеристикаПредставление,
	|	СерияПредставление,
	|	ИнвентарныйНомер
	|";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
	
	Запрос.Текст = ТекстЗапроса;
	
	Результаты = Запрос.ВыполнитьПакет();
		
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Шапка", Результаты[Результаты.ВГраница()-1]);
	СтруктураВозврата.Вставить("ТабличнаяЧасть", Результаты[Результаты.ВГраница()]);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Получает данные для формирования печатной формы ИНВ3
//
// Параметры:
//	ПараметрыПечати - Структура
//	ДокументОснование - ДокументСсылка.ИнвентаризацияТМЦВЭксплуатации - Документ, по которому необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* Шапка - РезультатЗапроса -
// 		* ТабличнаяЧасть - РезультатЗапроса -
// 		* ИнвентаризационнаяКомиссия - РезультатЗапроса -
//
Функция ДанныеДляПечатнойФормыИНВ3(ПараметрыПечати, ДокументОснование) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаОкончания,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТМЦВЭксплуатации.ИнвентаризационнаяКомиссия КАК ТабИнвентаризационнаяКомиссия
	|		ПО ТабИнвентаризационнаяКомиссия.Ссылка = ДанныеДокумента.Ссылка
	|			И ТабИнвентаризационнаяКомиссия.Председатель
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументОснование";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);	
 	Выборка = Запрос.Выполнить().Выбрать();
		
	Если НЕ Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Выборка.Организация, КонецДня(Выборка.ДатаОкончания));
	Запрос.УстановитьПараметр("Руководитель", СтруктураОтветственных.Руководитель);
	Запрос.УстановитьПараметр("ДолжностьРуководителя", СтруктураОтветственных.РуководительДолжность);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер", СтруктураОтветственных.ГлавныйБухгалтер);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИнвентаризации.Ссылка.Организация КАК Организация,
	|	ТаблицаИнвентаризации.Номенклатура КАК Номенклатура,
	|	ТаблицаИнвентаризации.Характеристика КАК Характеристика,
	|	ТаблицаИнвентаризации.Серия КАК Серия,
	|	ТаблицаИнвентаризации.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТаблицаИнвентаризации.КоличествоПоДаннымУчета КАК КоличествоПоУчету,
	|	ТаблицаИнвентаризации.КоличествоФактическое КАК КоличествоФактическое
	|
	|ПОМЕСТИТЬ втТовары
	|
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации.Товары КАК ТаблицаИнвентаризации
	|
	|ГДЕ
	|	ТаблицаИнвентаризации.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтоимостьТМЦ.Организация,
	|	СтоимостьТМЦ.Номенклатура,
	|	СтоимостьТМЦ.Характеристика,
	|	СтоимостьТМЦ.Серия,
	|	СтоимостьТМЦ.ИнвентарныйНомер,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьТМЦ.Количество) <> 0
	|			ТОГДА СУММА(СтоимостьТМЦ.СтоимостьРегл) / СУММА(СтоимостьТМЦ.Количество)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаРегл,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьТМЦ.Количество) <> 0
	|			ТОГДА СУММА(СтоимостьТМЦ.СтоимостьУпр) / СУММА(СтоимостьТМЦ.Количество)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаУпр
	|		
	|ПОМЕСТИТЬ СтоимостьТМЦ
	|	
	|ИЗ
	|	РегистрСведений.СтоимостьТМЦВЭксплуатации КАК СтоимостьТМЦ
	|	
	|ГДЕ
	|	СтоимостьТМЦ.Активность
	|	
	|	И (СтоимостьТМЦ.Организация, СтоимостьТМЦ.Номенклатура, СтоимостьТМЦ.Характеристика, СтоимостьТМЦ.Серия, СтоимостьТМЦ.ИнвентарныйНомер) В 
	|		(ВЫБРАТЬ
	|			втТовары.Организация, 
	|			втТовары.Номенклатура, 
	|			втТовары.Характеристика, 
	|			втТовары.Серия, 
	|			втТовары.ИнвентарныйНомер 
	|		ИЗ
	|			втТовары КАК втТовары)
	|	
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьТМЦ.Организация,
	|	СтоимостьТМЦ.Номенклатура,
	|	СтоимостьТМЦ.Характеристика,
	|	СтоимостьТМЦ.Серия,
	|	СтоимостьТМЦ.ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Руководитель КАК Руководитель,
	|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК ОрганизацияПредставление,
	|	ДанныеДокумента.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.ФизическоеЛицо КАК Кладовщик,
	|	"""" КАК ДолжностьКладовщика,
	|	ДанныеДокумента.Подразделение.Наименование КАК ПодразделениеПредставление,
	|	ДанныеДокумента.ДатаНачалаИнвентаризации КАК ДатаНачала,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаОкончания,
	|	ДанныеДокумента.Подразделение КАК Склад,
	|	ДанныеДокумента.ДокументОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ДокументОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.Номер КАК НомерДокумента,
	|	ДанныеДокумента.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаСнятияОстатков
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИнвентаризации.Номенклатура КАК Номенклатура,
	|	ТаблицаИнвентаризации.Характеристика КАК Характеристика,
	|	ТаблицаИнвентаризации.Серия КАК Серия,
	|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаИнвентаризации.Серия.Наименование КАК СерияНаименование,
	|	ТаблицаИнвентаризации.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаИнвентаризации.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|
	|	ТаблицаИнвентаризации.КоличествоПоУчету КАК КоличествоПоУчету,
	|	ТаблицаИнвентаризации.КоличествоФактическое КАК КоличествоФактическое,
	|
	|	ЕСТЬNULL(СтоимостьТМЦ.ЦенаРегл, 0) КАК Цена,
	|	ТаблицаИнвентаризации.КоличествоПоУчету * ЕСТЬNULL(СтоимостьТМЦ.ЦенаРегл, 0) КАК СуммаПоУчету,
	|	ТаблицаИнвентаризации.КоличествоФактическое * ЕСТЬNULL(СтоимостьТМЦ.ЦенаРегл, 0) КАК СуммаФактическая,
	|
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|
	|ИЗ
	|	втТовары КАК ТаблицаИнвентаризации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьТМЦ КАК СтоимостьТМЦ
	|		ПО СтоимостьТМЦ.Организация = ТаблицаИнвентаризации.Организация
	|			И СтоимостьТМЦ.Номенклатура = ТаблицаИнвентаризации.Номенклатура
	|			И СтоимостьТМЦ.Характеристика = ТаблицаИнвентаризации.Характеристика
	|			И СтоимостьТМЦ.Серия = ТаблицаИнвентаризации.Серия
	|			И СтоимостьТМЦ.ИнвентарныйНомер = ТаблицаИнвентаризации.ИнвентарныйНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварНаименование,
	|	ХарактеристикаНаименование,
	|	СерияНаименование,
	|	ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ФизЛицо КАК ФизЛицо,
	|	Таблица.Председатель КАК Председатель
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации.ИнвентаризационнаяКомиссия КАК Таблица
	|
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);

	Запрос.Текст = ТекстЗапроса;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечатнойФормы = Новый Структура();
	ДанныеДляПечатнойФормы.Вставить("Шапка", Результаты[Результаты.ВГраница()-2]);
	ДанныеДляПечатнойФормы.Вставить("ТабличнаяЧасть", Результаты[Результаты.ВГраница()-1]);
	ДанныеДляПечатнойФормы.Вставить("ИнвентаризационнаяКомиссия", Результаты[Результаты.ВГраница()]);

	Возврат ДанныеДляПечатнойФормы;
	
КонецФункции

// Получает данные для формирования печатной формы ИНВ22
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ИнвентаризацияТМЦВЭксплуатации - Документ, по которому необходимо получить данные.
//
// Возвращаемое значение:
// 	РезультатЗапроса -
//
Функция ДанныеДляПечатнойФормыИНВ22(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	Запрос.УстановитьПараметр(
		"НаименованиеИмуществаОбязательствШапка", 
		НСтр("ru = 'товарно-материальных ценностей, находящихся в эксплуатации';
			|en = 'inventory in operation'"));
		
	Запрос.УстановитьПараметр(
		"НаименованиеИмуществаОбязательствПодвал",
		НСтр("ru = 'товарно-материальные ценности, находящиеся в эксплуатации';
			|en = 'inventory in operation'"));
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК ПодразделениеОрганизации,
	|	ДанныеДокумента.Подразделение.Наименование КАК Подразделение,
	|	ДанныеДокумента.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ДанныеДокумента.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ДанныеДокумента.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	ДанныеДокумента.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ДанныеДокумента.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ДанныеДокумента.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|
	|	&НаименованиеИмуществаОбязательствШапка КАК НаименованиеИмуществаОбязательствШапка,
	|	&НаименованиеИмуществаОбязательствПодвал КАК НаименованиеИмуществаОбязательствПодвал,
	|
	|	ДанныеДокумента.ИнвентаризационнаяКомиссия.(
	|		ФизЛицо.Наименование КАК ФИОПолные,
	|		Председатель КАК Председатель,
	|		"""" КАК Должность
	|	) КАК ИнвентаризационнаяКомиссия
	|
	|ИЗ
	|	Документ.ИнвентаризацияТМЦВЭксплуатации КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	Председатель УБЫВ";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

//-- Локализация

#КонецОбласти

#КонецОбласти
