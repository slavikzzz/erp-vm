
#Область ПрограммныйИнтерфейс

// Функция возвращает используемые аналитики статьи бюджетов, разделенные " / "
//
// Параметры:
// 	 РеквизитыСтатьиБюджетов - Структура, ВыборкаИзРезультатаЗапроса, СтрокаТаблицыЗначений  - реквизиты статьи бюджетов.
//
// Возвращаемое значение:
// 	 Строка - Представление.
//
Функция ПредставлениеАналитикСтатьиБюджетов(РеквизитыСтатьиБюджетов) Экспорт
	
	Представление = "";
	
	Для НомерАналитики = 1 По РеквизитыСтатьиБюджетов.КоличествоИспользуемыхАналитик Цикл
		Если ЗначениеЗаполнено(РеквизитыСтатьиБюджетов["ВидАналитики" + НомерАналитики]) Тогда
			Представление = Представление 
				+ ?(Представление = "", "", " / ") 
				+ РеквизитыСтатьиБюджетов["ВидАналитики" + НомерАналитики];
		КонецЕсли;
	КонецЦикла;
	
	Возврат Представление;
	
КонецФункции

// Заполняет представление типа значения объекта
//
// Параметры:
// 	 Объект - ПланВидовХарактеристикОбъект.АналитикиСтатейБюджетов  - объект, реквизит которого будет заполнен.
//
// Возвращаемое значение:
// 	 Строка - представление типа
//
Функция ПредставлениеТипаЗначения(Объект) Экспорт
	
	ПредставлениеТипаЗначения = "";
	
	Если ЗначениеЗаполнено(Объект.ДополнительноеСвойство) Тогда
		ПредставлениеТипаЗначения = Строка(Объект.ДополнительноеСвойство);
	Иначе
		ПредставлениеТипаЗначения = Строка(Объект.ТипЗначения);
	КонецЕсли;
	
	Возврат ПредставлениеТипаЗначения;
	
КонецФункции

#Область ПроцедурыИФункцииРаботыСПериодами

// Функция добавляет интервал к дате
//
// Параметры:
//	Дата - Дата - произвольная дата
//	Периодичность - ПеречислениеСсылка.Периодичность - периодичность планирования по сценарию.
//	Смещение - Число - определяет направление и количество периодов, в котором сдвигается дата
//	Кэш - Соответствие - кэш добавления периодов. См. функцию БюджетированиеКлиентСервер.ПреобразоватьПараметрыДанныхВДанныеПериодов.
//
// Возвращаемое значение:
//	Дата - дата, отстоящая от исходной на заданное количество периодов.
//
Функция ДобавитьИнтервал(Дата, Периодичность, Смещение, Кэш = Неопределено) Экспорт
	
	Если Кэш <> Неопределено Тогда
		ДатаВКэш = Кэш[Дата];
		Если ДатаВКэш <> Неопределено Тогда
			ПериодичностьВКэш = ДатаВКэш[Периодичность];
			Если ПериодичностьВКэш <> Неопределено Тогда
				Результат = ПериодичностьВКэш[Смещение];
				Если Результат <> Неопределено Тогда
					Возврат Результат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Смещение = 0 Тогда
		Результат = Дата;
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		Результат = НачалоНеделиПоМесяцу(Дата + Смещение * 7 * 24 * 3600);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		Результат = НачалоДекады(Дата + Смещение * 11 * 24 * 3600);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		Результат = ДобавитьМесяц(Дата, Смещение);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		Результат = ДобавитьМесяц(Дата, Смещение * 3);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		Результат = ДобавитьМесяц(Дата, Смещение * 6);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		Результат = ДобавитьМесяц(Дата, Смещение * 12);
		
	Иначе
		Результат = НачалоДня(Дата) + Смещение * 24 * 3600;
	КонецЕсли;
	
	Если Кэш <> Неопределено Тогда
		
		Если Кэш[Дата] = Неопределено Тогда
			Кэш.Вставить(Дата, Новый Соответствие);
		КонецЕсли;
		
		Если Кэш[Дата][Периодичность] = Неопределено Тогда
			Кэш[Дата].Вставить(Периодичность, Новый Соответствие);
		КонецЕсли;
		
		Кэш[Дата][Периодичность].Вставить(Смещение, Результат);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция добавляет заданное количество дней к дате.
//
// Параметры:
// 	Дата - Дата - Произвольная дата
// 	ЧислоДней - Число - Число дней, которые следует добавить к дате.
// 
// Возвращаемое значение:
// 	Дата - Итоговая дата.
//
Функция ДобавитьДень(Дата, ЧислоДней) Экспорт
	
	Возврат Дата + ЧислоДней * 86400; // 86400 = 24*60*60 - число секунд в дне;
	
КонецФункции 

// Функция возвращает ближайшую дату начала периода планирования.
//
// Параметры:
//	ДатаВПериоде - Дата - Произвольная дата.
//	Периодичность - ПеречислениеСсылка.Периодичность - Периодичность планирования по сценарию.
//
// Возвращаемое значение:
//	Дата - ближайшая дата начала периода планирования.
//
Функция ДатаНачалаПериода(ДатаВПериоде, Периодичность) Экспорт
	
	Если НЕ ТипЗнч(ДатаВПериоде) = Тип("Дата") Тогда
		Возврат '00010101';
	КонецЕсли;
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		Возврат НачалоНеделиПоМесяцу(ДатаВПериоде);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		Возврат НачалоДекады(ДатаВПериоде);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		Возврат НачалоМесяца(ДатаВПериоде);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		Возврат НачалоКвартала(ДатаВПериоде);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		Возврат НачалоПолугодия(ДатаВПериоде);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		Возврат НачалоГода(ДатаВПериоде);
	Иначе
		Возврат НачалоДня(ДатаВПериоде);
	КонецЕсли;
	
КонецФункции

// Функция возвращает начало декады по дате.
//
// Параметры:
//	Дата - Дата - Произвольная дата.
//
// Возвращаемое значение:
//	Дата - начало декады для переданной даты.
//
Функция НачалоДекады(Дата) Экспорт
	
	Если День(Дата) < 11 Тогда
		Возврат НачалоМесяца(Дата);
	ИначеЕсли День(Дата) < 21 Тогда
		Возврат ДобавитьДень(НачалоМесяца(Дата), 10);
	Иначе 
		Возврат ДобавитьДень(НачалоМесяца(Дата), 20);
	КонецЕсли;
	
КонецФункции

// Функция возвращает конец декады по дате.
//
// Параметры:
//	Дата - Дата - Произвольная дата.
//
// Возвращаемое значение:
//	Дата - конец декады для переданной даты.
//
Функция КонецДекады(Дата) Экспорт
	
	Если День(Дата) < 11 Тогда
		Возврат ДобавитьДень(НачалоМесяца(Дата), 10) - 1;
	ИначеЕсли День(Дата) < 21 Тогда
		Возврат ДобавитьДень(НачалоМесяца(Дата), 20) - 1;
	Иначе 
		Возврат КонецМесяца(Дата);
	КонецЕсли;
	
КонецФункции // КонецДекады() 

// Функция возвращает начало полугодия по дате.
//
// Параметры:
//	Дата - Дата - Произвольная дата.
//
// Возвращаемое значение:
//	Дата - начало полугодия для переданной даты.
//
Функция НачалоПолугодия(Дата) Экспорт
	
	Если Месяц(Дата) < 7 Тогда
		Возврат НачалоГода(Дата);
	Иначе
		Возврат ДобавитьМесяц(НачалоГода(Дата), 6);
	КонецЕсли;
	
КонецФункции

// Функция возвращает конец полугодия по дате.
//
// Параметры:
//	Дата - Дата - Произвольная дата.
//
// Возвращаемое значение:
//	Дата - начало полугодия для переданной даты.
//
Функция КонецПолугодия(Дата) Экспорт
	
	Если Месяц(Дата) < 7 Тогда
		Возврат КонецДня(Дата(Год(Дата), 6, 30));
	Иначе
		Возврат КонецГода(Дата);
	КонецЕсли;
	
КонецФункции

// Функция возвращает дату начала недели с учетом начала месяца
//
// Параметры:
//	Дата - Дата - Произвольная дата.
//
// Возвращаемое значение:
//	Дата - начало недели.
//
Функция НачалоНеделиПоМесяцу(Дата) Экспорт
	
	Если День(Дата) < 7 Тогда
		Возврат ?(День(НачалоНедели(Дата)) > День(Дата), НачалоМесяца(Дата), НачалоНедели(Дата));
	Иначе
		Возврат НачалоНедели(Дата);
	КонецЕсли;
	
КонецФункции

// Функция возвращает дату окончания периода
//
// Параметры:
//	Дата - Дата - произвольная дата.
//	Периодичность - ПеречислениеСсылка.Периодичность - периодичность планирования по сценарию.
//
// Возвращаемое значение:
//	Дата - дата окончания периода.
//
Функция ДатаКонцаПериода(Дата, Периодичность) Экспорт
	
	Если НЕ ТипЗнч(Дата) = Тип("Дата") Тогда
		Возврат '00010101';
	КонецЕсли;
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		Возврат КонецНедели(Дата);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		Возврат КонецДекады(Дата);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		Возврат КонецМесяца(Дата);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		Возврат КонецКвартала(Дата);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		Возврат КонецПолугодия(Дата);
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		Возврат КонецГода(Дата);
	Иначе
		Возврат КонецДня(Дата);
	КонецЕсли;
	
КонецФункции

// Функция возвращает представление периода с учетом периодичности
//
// Параметры:
//	ДатаНачала - Дата - произвольная дата.
//	Периодичность - ПеречислениеСсылка.Периодичность - периодичность.
//
// Возвращаемое значение:
//	Строка - представление периода.
//
Функция ПредставлениеПериодаПоДате(ДатаНачала, Периодичность) Экспорт
	
	Представление = "";
	ФорматнаяСтрокаПредставленияПериода = "ФП = Истина";
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		
		Представление = НСтр("ru = 'Неделя с %1';
							|en = 'Week from %1'");
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Представление, Формат(ДатаНачала, "ДЛФ=D"));
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		
		Если День(ДатаНачала) < 10 Тогда
			Представление = НСтр("ru = '1 декада. %1';
								|en = '1 ten-day period. %1'");
		ИначеЕсли День(ДатаНачала) < 20 Тогда
			Представление = НСтр("ru = '2 декада. %1';
								|en = '2 ten-day period. %1'");
		Иначе 
			Представление = НСтр("ru = '3 декада. %1';
								|en = '3 ten-day period. %1'");
		КонецЕсли;
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Представление, Формат(ДатаНачала, "ДЛФ=D"));
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		
		Представление = ПредставлениеПериода(ДатаНачала, КонецМесяца(ДатаНачала), ФорматнаяСтрокаПредставленияПериода);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		
		Представление = ПредставлениеПериода(ДатаНачала, КонецКвартала(ДатаНачала), ФорматнаяСтрокаПредставленияПериода);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		
		Представление = ПредставлениеПериода(ДатаНачала, КонецПолугодия(ДатаНачала), ФорматнаяСтрокаПредставленияПериода);
		
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		
		Представление = ПредставлениеПериода(ДатаНачала, КонецГода(ДатаНачала), ФорматнаяСтрокаПредставленияПериода);
		
	Иначе
		
		Представление = Формат(ДатаНачала, "ДЛФ=D");
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Функция возвращает число цифрами и период в нужном падеже
//
// Параметры:
// 	 ЧислоПериодов - Число - количество периодов
// 	 Периодичность - ПеречислениеСсылка.Периодичность - периодичность периодов.
//
// Возвращаемое значение:
// 	 Строка - Представление.
//
Функция ПериодЦифрамиПериодичностьПрописью(ЧислоПериодов, Периодичность) Экспорт
	
	ОписаниеДлительности = "";
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 день;;%1 дня;%1 дней;';
									|en = ';%1 day;;%1 days;%1 days;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 неделя;;%1 недели;%1 недель;';
									|en = ';%1 week;;%1 weeks;%1 weeks;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 декада;;%1 декады;%1 декад;';
									|en = ';%1 ten-day period;;%1 ten-day periods;%1 ten-day periods;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 месяц;;%1 месяца;%1 месяцев;';
									|en = ';%1 month;;%1 month;%1 months;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 квартал;;%1 квартала;%1 кварталов;';
									|en = ';%1 quarter;;%1 quarter;%1 quarters;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 полугодие;;%1 полугодия;%1 полугодий;';
									|en = ';%1 half-year;;%1 half-year;%1 half-years;'");
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ОписаниеДлительности = НСтр("ru = ';%1 год;;%1 года;%1 лет;';
									|en = ';%1 year;;%1 years;%1 years;'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ОписаниеДлительности, ЧислоПериодов);
	
КонецФункции

#КонецОбласти

#Область ПреобразованиеТиповАналитики

// Возвращает значение аналитики, приведенное к типу. Тип аналитики может включать или не включать тип Неопределено.
// Значение будет приведено к исходному типу, без неопределено. Но если приведенное значение пустое, то оно приводится к
// неопределено.
//
// Параметры:
// 	ЗначениеАналитики - Произвольный - Значение, которое необходимо привести к типу.
// 	ОписаниеТиповАналитики - ОписаниеТипов - Описание типов, к которому необходимо привести значение
// 						Но если приведенное значение - пустое, оно будет приведено к Неопределено.
//
// Возвращаемое значение:
// 	Произвольный - Значение аналитики, приведенное к типу. Если значение аналитики не заполнено, то вернется Неопределено,
// 						вне зависимости от исходного описания типов.
//
Функция ПриведенноеЗначениеАналитики(Знач ЗначениеАналитики, Знач ОписаниеТиповАналитики) Экспорт
	
	ЗначениеАналитики = ОписаниеТиповАналитики.ПривестиЗначение(ЗначениеАналитики);
	
	Если НЕ ЗначениеЗаполнено(ЗначениеАналитики) Тогда
		ЗначениеАналитики = ПустоеЗначениеАналитики();
	КонецЕсли;
	
	Возврат ЗначениеАналитики;
КонецФункции

Функция ПустоеЗначениеАналитики() Экспорт
	Возврат Неопределено;
КонецФункции

// Возвращает количество аналитик в бюджетировании
//
// Возвращаемое значение:
// 	Число - Количество поддерживаемых в бюджетировании аналитик.
//
Функция МаксимальноеКоличествоАналитик() Экспорт
	Возврат 6;
КонецФункции

#КонецОбласти

#КонецОбласти
