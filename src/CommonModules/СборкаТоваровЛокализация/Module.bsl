#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	ПараметрыПроверки = Документы.СборкаТоваров.ПараметрыПроверкиКорректностиКомплекта(Объект);
	
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьКорректностьКомплекта(Объект, Отказ, ПараметрыПроверки);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  ТекстЗаполнения - Строка - 
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС") Тогда
		
		ИнтеграцияВЕТИСУТ.ЗаполнитьСборкуТоваровНаОснованииПроизводственнойОперацииВЕТИС(
			Объект,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.МаркировкаТоваровИСМП") Тогда
		
		ИнтеграцияИСМПУТ.ЗаполнитьСборкуТоваровНаОснованииМаркировкиТоваровИСМП(
			Объект,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВыводИзОборотаИСМП") Тогда
		
		ИнтеграцияИСМПУТ.ЗаполнитьСборкуТоваровНаОснованииВыводаИзОборотаИСМП(
			Объект,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ФормированиеПартийПриПроизводствеЗЕРНО") Тогда
		
		ИнтеграцияЗЕРНОУТ.ЗаполнитьСборкуТоваровНаОснованииФормированияПартийПриПроизводствеЗЕРНО(
			Объект,
			ДанныеЗаполнения);
			
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.СборкаТоваров - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-1 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ1";
		КомандаПечати.Представление = НСтр("ru = 'Акт о приеме-передаче ТМЦ на хранение (МХ-1)';
											|en = 'Certificate of inventory handover for storage (MH-1)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-3 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ3";
		КомандаПечати.Представление = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)';
											|en = 'Certificate of stored inventory return (MH-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

// Возвращает данные для формирования печатной формы МХ - 1.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.СборкаТоваров - коллекция значений ссылок на документы,
//																по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасовОприходование.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасовОприходование.ВидЗапасов
	|ПОМЕСТИТЬ ВтВидыЗапасовОприходование
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасовОприходование КАК ВидыЗапасовОприходование
	|ГДЕ
	|	ВидыЗапасовОприходование.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Номер, ДанныеДокумента.Номер)	КАК Номер,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата)	КАК Дата,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата)	КАК ДатаДокумента,
	|	ДанныеДокумента.ХозяйственнаяОперация					КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ДанныеДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.ХозяйственнаяОперация				КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Шапка.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	ДокументШапка.Склад КАК Склад,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ДокументШапка.Серия КАК Серия,	
	|	1 КАК НомерСтроки,
	|	ДокументШапка.Номенклатура КАК Номенклатура,
	|	ДокументШапка.Характеристика КАК Характеристика,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Представление КАК ВидУпаковки,
	|	ДокументШапка.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ДокументШапка.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ДокументШапка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ИЗ
	|	Документ.СборкаТоваров КАК ДокументШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ДокументШапка.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	ВидыЗапасовТовары.Ссылка КАК Ссылка,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасовОприходование.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	Шапка.Склад КАК Склад,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВидыЗапасовТовары.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление ВидУпаковки,
	|	ВидыЗапасовТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасовТовары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасовТовары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ВидыЗапасовТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасовТовары.Ссылка = Шапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыЗапасовОприходование КАК ВидыЗапасовОприходование
	|		ПО ВидыЗапасовТовары.АналитикаУчетаНоменклатуры = ВидыЗапасовОприходование.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасовТовары.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументШапка.Ссылка КАК Ссылка,
	|	ДокументШапка.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВтВидыЗапасовОприходование.ВидЗапасов КАК ВидЗапасов,
	|	ДокументШапка.Организация КАК Организация,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	1 КАК НомерСтроки,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление ВидУпаковки,
	|	ДокументШапка.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ДокументШапка.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ДокументШапка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ДокументШапка.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ИЗ
	|	Документ.СборкаТоваров КАК ДокументШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ДокументШапка.Ссылка = Шапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыЗапасовОприходование КАК ВтВидыЗапасовОприходование
	|		ПО ДокументШапка.АналитикаУчетаНоменклатуры = ВтВидыЗапасовОприходование.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	ДокументШапка.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ДокументШапка.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 3.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.СборкаТоваров - коллекция значений ссылок на документы,
//																по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Номер, ДанныеДокумента.Номер)	КАК Номер,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата)	КАК Дата,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата)	КАК ДатаДокумента,
	|	ДанныеДокумента.ХозяйственнаяОперация					КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.СборкаТоваров КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ДанныеДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка	КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.ХозяйственнаяОперация				КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Шапка.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	ДокументШапка.Склад КАК Склад,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ДокументШапка.Серия КАК Серия,
	|	1 КАК НомерСтроки,
	|	ДокументШапка.Номенклатура КАК Номенклатура,
	|	ДокументШапка.Характеристика КАК Характеристика,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ДокументШапка.Номенклатура.ЕдиницаИзмерения.Представление КАК ВидУпаковки,
	|	ДокументШапка.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ДокументШапка.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ДокументШапка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ИЗ
	|	Документ.СборкаТоваров КАК ДокументШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ДокументШапка.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Шапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление ВидУпаковки,
	|	ВидыЗапасов.Количество КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.СборкаТоваров.ВидыЗапасовСписание КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация

	ТекстыОтражения = Новый Массив;
	
#Область ТекстСписаниеКомплектующихНаСборку                // (Дт 20 :: Кт 41.01)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Списание собственных комплектующих на сборку (Дт 20 :: Кт 41.01)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ)) КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	Строки.КорНоменклатура КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)) КАК СуммаНУДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)) КАК СуммаПРДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)) КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		Строки.ГруппаФинансовогоУчетаРасчетов
	|	ИНАЧЕ
	|		Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	Строки.Склад КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		Строки.Контрагент
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	СУММА(Строки.Количество) КАК КоличествоКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)) КАК СуммаНУКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)) КАК СуммаПРКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)) КАК СуммаВРКт,
	|	""Списание собственных комплектующих на сборку"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций ПО	
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|			И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СборкаТоваров КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.КорНоменклатура = Стоимости.КорНоменклатура
	|		И Строки.ПодразделениеАналитики = Стоимости.ПодразделениеАналитики
	|		И Строки.КорПодразделениеАналитики = Стоимости.КорПодразделениеАналитики
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Стоимости.ВидДвижения = Строки.ВидДвижения
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|	И (Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	ИЛИ Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И (Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции),
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		Строки.ГруппаФинансовогоУчетаРасчетов
	|	ИНАЧЕ
	|		Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ,
	|	Строки.Склад,
	|	Строки.ПодразделениеАналитики,
	|	Строки.НаправлениеДеятельности,
	|	Строки.Номенклатура,
	|	Строки.КорПодразделениеАналитики,
	|	Строки.КорНаправлениеДеятельности,
	|	Строки.КорНоменклатура,
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		Строки.Контрагент
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстСписаниеКомплектующихНаСборкуСкладПроизводства // (Дт 20 :: Кт 20)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Списание собственных комплектующих на сборку с производственного склада (Дт 20 :: Кт 20)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ)) КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЕСТЬNULL(Стоимости.КорГруппаПродукции, Строки.КорГруппаПродукции)
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			Строки.КорПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство)
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	Строки.КорНоменклатура КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЕСТЬNULL(Стоимости.КорГруппаПродукции, Строки.КорГруппаПродукции)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)) КАК СуммаНУДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)) КАК СуммаПРДт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеАналитики КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)) КАК СуммаНУКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)) КАК СуммаПРКт,
	|	СУММА(ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)) КАК СуммаВРКт,
	|	""Списание собственных комплектующих на сборку"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СборкаТоваров КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.КорНоменклатура = Стоимости.КорНоменклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Строки.ПодразделениеАналитики = Стоимости.ПодразделениеАналитики
	|		И Строки.КорПодразделениеАналитики = Стоимости.КорПодразделениеАналитики
	|		И Стоимости.ВидДвижения = Строки.ВидДвижения
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ГруппаПродукции = Стоимости.ГруппаПродукции
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|ПО
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|	
	|ГДЕ
	|	Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры
	|	И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Строки.Номенклатура,
	|	Строки.ГруппаФинансовогоУчета,
	|	Строки.Склад,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции),
	|	Строки.ПодразделениеАналитики,
	|	Строки.КорПодразделениеАналитики,
	|	Строки.НаправлениеДеятельности,
	|	Строки.КорНоменклатура,
	|	Строки.КорНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Стоимости.КорГруппаПродукции, Строки.КорГруппаПродукции)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			Строки.КорПодразделениеАналитики
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство)
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЕСТЬNULL(Стоимости.КорГруппаПродукции, Строки.КорГруппаПродукции)
	|	КОНЕЦ";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстОприходованиеКомплекта                       // (Дт 41.01 :: Кт 20)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Оприходование собранного комплекта (Дт 41.01 :: Кт 20) 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	Строки.Склад КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	ВЫБОР
	|		КОГДА Строки.НастройкаХозяйственнойОперации = ЕСТЬNULL(Стоимости.НастройкаХозяйственнойОперации, Строки.НастройкаХозяйственнойОперации)
	|		ТОГДА Строки.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство) КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|	""Оприходование собранного комплекта"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций ПО	
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|			И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СборкаТоваров КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.КорНоменклатура = Стоимости.КорНоменклатура
	|		И Строки.ПодразделениеАналитики = Стоимости.ПодразделениеАналитики
	|		И Строки.КорПодразделениеАналитики = Стоимости.КорПодразделениеАналитики
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Стоимости.ВидДвижения = Строки.ВидДвижения
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|	И (Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	ИЛИ Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И (Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации))";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстОприходованиеКомплектаСкладПроизводства // (Дт 20 :: Кт 20)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Оприходование собранного комплекта на склад производства (Дт 20 :: Кт 20) 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК АналитикаУчетаДт,
	|	Строки.ПодразделениеАналитики КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт2,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,
	|
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ 
	|			ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции)
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			Строки.ПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство)
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции)
	|	КОНЕЦ КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|	""Оприходование собранного комплекта"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СборкаТоваров КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.ПодразделениеАналитики = Стоимости.ПодразделениеАналитики
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Стоимости.ВидДвижения = Строки.ВидДвижения
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО	
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|
	|ГДЕ
	|	Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры
	|	И НЕ Строки.Склад ССЫЛКА Справочник.Организации";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстВключениеИсключениеНДСВСтоимость             // (Дт 20 :: Кт 19.03)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Включение/исключение НДС В стоимость (Дт 20 :: Кт 19.03) @НДСПредъявленный
	|
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Партии.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР КОГДА Партии.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|			ТОГДА Партии.КорГруппаПродукции
	|	ИНАЧЕ Партии.КорГруппаФинансовогоУчета КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР КОГДА Партии.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|								ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|			ТОГДА Партии.КорПодразделение
	|		ИНАЧЕ Партии.Склад КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.КорПодразделение КАК ПодразделениеДт,
	|	Партии.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|
	|	ВЫБОР КОГДА Партии.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) ИНАЧЕ Партии.Склад КОНЕЦ КАК СубконтоДт1,
	|	Партии.КорНоменклатура КАК СубконтоДт2,
	|	Партии.КорГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности)
	|		ИНАЧЕ ЕСТЬNULL(КорВводСобственныхСредств.НаправлениеДеятельности, Партии.КорНаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Включение/исключение НДС в стоимость"" КАК Содержание
	|	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СборкаТоваров КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций ПО	
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|			И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО	
	|		Партии.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|		И Партии.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК КорВводСобственныхСредств
	|	ПО Партии.Ссылка = КорВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = КорВводСобственныхСредств.СчетФактура
	|		И Партии.КорНаправлениеДеятельности = КорВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстСборкаРазборкаТоваровНаСкладах // (Дт 41.01 :: Кт 41.01)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Комплектация товаров через 41 счет (Дт 41.01 :: Кт 41.01)
	|	СтрокиПриход.Ссылка КАК Ссылка,
	|	СтрокиПриход.Период КАК Период,
	|	СтрокиПриход.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.Сумма, СтрокиРасход.Сумма)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.Сумма, СтрокиПриход.Сумма)
	|	КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаУУ, СтрокиРасход.СуммаУУ)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаУУ, СтрокиПриход.СуммаУУ)
	|	КОНЕЦ) КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	СтрокиПриход.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	СтрокиПриход.Склад КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиПриход.ПодразделениеАналитики КАК ПодразделениеДт,
	|	СтрокиПриход.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	СтрокиПриход.Номенклатура КАК СубконтоДт1,
	|	СтрокиПриход.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	СУММА(ЕстьNULL(СтрокиПриход.Количество, 0)) КАК КоличествоДт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаНУ, СтрокиРасход.СуммаНУ)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаНУ, СтрокиПриход.СуммаНУ)
	|	КОНЕЦ) КАК СуммаНУДт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаПР, СтрокиРасход.СуммаПР)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаПР, СтрокиПриход.СуммаПР)
	|	КОНЕЦ) КАК СуммаПРДт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаВР, СтрокиРасход.СуммаВР)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаВР, СтрокиПриход.СуммаВР)
	|	КОНЕЦ) КАК СуммаВРДт,
	|	ВЫБОР
	|		КОГДА СтрокиРасход.Склад ССЫЛКА Справочник.Партнеры ИЛИ СтрокиРасход.Склад ССЫЛКА Справочник.Организации
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР КОГДА СтрокиРасход.Склад ССЫЛКА Справочник.Партнеры ИЛИ СтрокиРасход.Склад ССЫЛКА Справочник.Организации ТОГДА
	|		СтрокиРасход.ГруппаФинансовогоУчетаРасчетов
	|	ИНАЧЕ
	|		СтрокиРасход.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	СтрокиРасход.Склад КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	СтрокиРасход.ПодразделениеАналитики КАК ПодразделениеКт,
	|	СтрокиРасход.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	СтрокиРасход.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА СтрокиРасход.Склад ССЫЛКА Справочник.Партнеры ИЛИ СтрокиРасход.Склад ССЫЛКА Справочник.Организации
	|			ТОГДА СтрокиРасход.Контрагент
	|		ИНАЧЕ СтрокиРасход.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	СтрокиРасход.ГруппаПродукции КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	СУММА(СтрокиРасход.Количество) КАК КоличествоКт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаНУ, СтрокиРасход.СуммаНУ)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаНУ, СтрокиПриход.СуммаНУ)
	|	КОНЕЦ) КАК СуммаНУКт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаПР, СтрокиРасход.СуммаПР)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаПР, СтрокиПриход.СуммаПР)
	|	КОНЕЦ) КАК СуммаПРКт,
	|	СУММА(ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ЕСТЬNULL(СтоимостиРасход.СуммаВР, СтрокиРасход.СуммаВР)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостиПриход.СуммаВР, СтрокиПриход.СуммаВР)
	|	КОНЕЦ) КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА СтрокиПриход.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|		ТОГДА ""Сборка товаров""
	|		ИНАЧЕ ""Разборка товаров""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций ПО	
	|		ДокументыКОтражению.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|			И ДокументыКОтражению.Период = УчетнаяПолитикаОрганизаций.Период
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтрокиСборка КАК СтрокиПриход ПО
	|		СтрокиПриход.Ссылка = ДокументыКОтражению.Ссылка
	|		И СтрокиПриход.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтрокиСборка КАК СтрокиРасход ПО
	|		СтрокиРасход.Ссылка = СтрокиПриход.Ссылка
	|		И СтрокиРасход.ВидДвижения <> СтрокиПриход.ВидДвижения
	|		И СтрокиРасход.КорНоменклатура = СтрокиПриход.Номенклатура
	|		И СтрокиРасход.ИдентификаторФинЗаписи = СтрокиПриход.ИдентификаторФинЗаписи
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостиСборка КАК СтоимостиПриход ПО
	|		СтрокиПриход.Ссылка = СтоимостиПриход.Ссылка
	|		И СтрокиПриход.ВидДвижения = СтоимостиПриход.ВидДвижения
	|		И СтрокиПриход.Номенклатура = СтоимостиПриход.Номенклатура
	|		И СтрокиПриход.ИдентификаторФинЗаписи = СтоимостиПриход.ИдентификаторФинЗаписи
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостиСборка КАК СтоимостиРасход ПО
	|		СтрокиРасход.Ссылка = СтоимостиРасход.Ссылка
	|		И СтрокиРасход.ВидДвижения = СтоимостиРасход.ВидДвижения
	|		И СтрокиРасход.Номенклатура = СтоимостиРасход.Номенклатура
	|		И СтрокиРасход.ИдентификаторФинЗаписи = СтоимостиРасход.ИдентификаторФинЗаписи
	|ГДЕ
	|	НЕ УчетнаяПолитикаОрганизаций.СборкаТоваровЧерезСчет20
	|	И (СтрокиРасход.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			ИЛИ СтрокиРасход.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|				И (СтрокиРасход.Склад ССЫЛКА Справочник.Партнеры
	|					ИЛИ СтрокиРасход.Склад ССЫЛКА Справочник.Организации))
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиПриход.Ссылка,
	|	СтрокиПриход.Период,
	|	СтрокиПриход.Организация,
	|	СтрокиПриход.ГруппаФинансовогоУчета,
	|	СтрокиПриход.Склад,
	|	СтрокиПриход.ПодразделениеАналитики,
	|	СтрокиПриход.НаправлениеДеятельности,
	|	СтрокиПриход.Номенклатура,
	|	СтрокиПриход.Склад,
	|	СтрокиРасход.Склад,
	|	СтрокиРасход.ГруппаФинансовогоУчетаРасчетов,
	|	СтрокиРасход.ГруппаФинансовогоУчета,
	|	СтрокиРасход.ПодразделениеАналитики,
	|	СтрокиРасход.НаправлениеДеятельности,
	|	СтрокиРасход.Номенклатура,
	|	СтрокиРасход.Контрагент,
	|	СтрокиРасход.ГруппаПродукции,
	|	СтрокиПриход.ХозяйственнаяОперация";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстСписаниеТоваровЗаБалансом());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	
	//++ Локализация
	
	#Область ВтДвиженияСборкиПредварительная
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.ДокументДвижения,
	|	Движения.Организация,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.КорАналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.КорВидЗапасов,
	|	Движения.РазделУчета,
	|	Движения.КорРазделУчета,
	|	Движения.Партия,
	|	Движения.АналитикаУчетаПоПартнерам,
	|	Движения.АналитикаФинансовогоУчета,
	|	Движения.ГруппаПродукции,
	|	Движения.ХозяйственнаяОперация,
	|	Движения.ИдентификаторФинЗаписи,
	|	Движения.РасчетСебестоимости,
	|	Движения.Сторно,
	|
	|	Движения.Количество,
	|	Движения.СтоимостьРегл,
	|	Движения.ДопРасходыРегл,
	|	Движения.ТрудозатратыРегл,
	|	Движения.ПостатейныеПостоянныеРегл,
	|	Движения.ПостатейныеПеременныеРегл,
	|	Движения.СтоимостьЗабалансоваяРегл,
	|	Движения.СтоимостьУпр,
	|	Движения.ДопРасходыУпр,
	|	Движения.ТрудозатратыУпр,
	|	Движения.ПостатейныеПостоянныеУпр,
	|	Движения.ПостатейныеПеременныеУпр,
	|	Движения.ПостояннаяРазница,
	|	Движения.ВременнаяРазница
	|ПОМЕСТИТЬ ВтДвиженияСборкиПредварительная
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Регистратор = ДокументыКОтражению.Ссылка
	|		И Движения.Активность
	|ГДЕ
	|	Движения.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И Движения.НастройкаХозяйственнойОперации В (ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РазборкаТоваров),
	|												ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СборкаТоваров))
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВТОтсутствующийРасход
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Движения.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ Движения.КорАналитикаУчетаНоменклатуры
	|	КОНЕЦ КАК КорАналитикаУчетаНоменклатуры,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.ВидЗапасов
	|		ИНАЧЕ Движения.КорВидЗапасов
	|	КОНЕЦ КАК КорВидЗапасов,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.РазделУчета
	|		ИНАЧЕ Движения.КорРазделУчета
	|	КОНЕЦ КАК КорРазделУчета,
	|	Движения.ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ ВТОтсутствующийРасход
	|ИЗ
	|	ВтДвиженияСборкиПредварительная КАК Движения
	|ГДЕ
	|	Движения.Количество <> 0
	|	И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|СГРУППИРОВАТЬ ПО
	|	Движения.Регистратор,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ Движения.КорАналитикаУчетаНоменклатуры
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.ВидЗапасов
	|		ИНАЧЕ Движения.КорВидЗапасов
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА Движения.РазделУчета
	|		ИНАЧЕ Движения.КорРазделУчета
	|	КОНЕЦ,
	|	Движения.ИдентификаторФинЗаписи
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ) = ЛОЖЬ
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВТАналитикиРасход
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.ДокументДвижения,
	|	Движения.Организация,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.РазделУчета,
	|	Движения.Партия,
	|	Движения.АналитикаУчетаПоПартнерам,
	|	Движения.АналитикаФинансовогоУчета,
	|	Движения.ГруппаПродукции,
	|	Движения.ХозяйственнаяОперация,
	|	Движения.РасчетСебестоимости,
	|	Движения.Сторно
	|ПОМЕСТИТЬ ВТАналитикиРасход
	|ИЗ
	|	ВтДвиженияСборкиПредварительная КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтсутствующийРасход КАК Отбор
	|		ПО Движения.Регистратор = Отбор.Регистратор
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НЕ Движения.РасчетСебестоимости
	|		И Движения.Количество <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВтДвиженияСборки
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.ДокументДвижения,
	|	Движения.Организация,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.КорАналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.КорВидЗапасов,
	|	Движения.РазделУчета,
	|	Движения.КорРазделУчета,
	|	Движения.Партия,
	|	Движения.АналитикаУчетаПоПартнерам,
	|	Движения.АналитикаФинансовогоУчета,
	|	Движения.ГруппаПродукции,
	|	Движения.ХозяйственнаяОперация,
	|	Движения.ИдентификаторФинЗаписи,
	|	Движения.РасчетСебестоимости,
	|	Движения.Сторно,
	|
	|	Движения.Количество,
	|	Движения.СтоимостьРегл,
	|	Движения.ДопРасходыРегл,
	|	Движения.ТрудозатратыРегл,
	|	Движения.ПостатейныеПостоянныеРегл,
	|	Движения.ПостатейныеПеременныеРегл,
	|	Движения.СтоимостьЗабалансоваяРегл,
	|	Движения.СтоимостьУпр,
	|	Движения.ДопРасходыУпр,
	|	Движения.ТрудозатратыУпр,
	|	Движения.ПостатейныеПостоянныеУпр,
	|	Движения.ПостатейныеПеременныеУпр,
	|	Движения.ПостояннаяРазница,
	|	Движения.ВременнаяРазница
	|ПОМЕСТИТЬ ВтДвиженияСборки
	|ИЗ
	|	ВтДвиженияСборкиПредварительная КАК Движения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.ДокументДвижения,
	|	Движения.Организация,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Дополнение.КорАналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Дополнение.КорВидЗапасов,
	|	Движения.РазделУчета,
	|	Дополнение.КорРазделУчета,
	|	Движения.Партия,
	|	Движения.АналитикаУчетаПоПартнерам,
	|	Движения.АналитикаФинансовогоУчета,
	|	Движения.ГруппаПродукции,
	|	Движения.ХозяйственнаяОперация,
	|	Дополнение.ИдентификаторФинЗаписи,
	|	Движения.РасчетСебестоимости,
	|	Движения.Сторно,
	|
	|	0 КАК Количество,
	|	0 КАК СтоимостьРегл,
	|	0 КАК ДопРасходыРегл,
	|	0 КАК ТрудозатратыРегл,
	|	0 КАК ПостатейныеПостоянныеРегл,
	|	0 КАК ПостатейныеПеременныеРегл,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	0 КАК СтоимостьУпр,
	|	0 КАК ДопРасходыУпр,
	|	0 КАК ТрудозатратыУпр,
	|	0 КАК ПостатейныеПостоянныеУпр,
	|	0 КАК ПостатейныеПеременныеУпр,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	ВТАналитикиРасход КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтсутствующийРасход КАК Дополнение
	|		ПО Движения.Регистратор = Дополнение.Регистратор
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВтСтроки
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Движения.Регистратор КАК Ссылка,
	|	Движения.Период КАК Период,
	|	Движения.Организация КАК Организация,
	|	Движения.ВидДвижения КАК ВидДвижения,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(КорАналитика.Номенклатура, НЕОПРЕДЕЛЕНО) КАК КорНоменклатура,
	|	Аналитика.МестоХранения КАК Склад,
	|	ВЫБОР
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		ТОГДА Аналитика.СкладскаяТерритория.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|		ТОГДА Аналитика.Подразделение
	|		КОГДА Движения.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|	ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ КАК ПодразделениеАналитики,
	|
	|	ВЫБОР КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|	КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ЕСТЬNULL(ВидыЗапасов.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	ИНАЧЕ
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА КорВидыЗапасов.Контрагент
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ВидыЗапасов.Контрагент
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Контрагент, НЕОПРЕДЕЛЕНО)
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ТОГДА Движения.АналитикаУчетаПоПартнерам.Контрагент
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|			И КорВидыЗапасов.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = КорВидыЗапасов.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(КорВидыЗапасов.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ЕСТЬNULL(КорВидыЗапасов.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|			И ВидыЗапасов.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = ВидыЗапасов.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|			И Движения.АналитикаУчетаПоПартнерам.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = Движения.АналитикаУчетаПоПартнерам.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(Движения.АналитикаУчетаПоПартнерам.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(Движения.АналитикаУчетаПоПартнерам.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаРасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 И Движения.Период >= НАЧАЛОПЕРИОДА(&ДатаПереходаНаПартионныйУчетВерсии22, МЕСЯЦ)
	|			И ТИПЗНАЧЕНИЯ(Движения.АналитикаФинансовогоУчета) = ТИП(Справочник.ГруппыАналитическогоУчетаНоменклатуры)
	|			ТОГДА Движения.АналитикаФинансовогоУчета
	|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|		КОГДА НЕ Движения.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА Движения.ГруппаПродукции
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаПродукции,
	|
	|	Движения.РазделУчета КАК РазделУчета,
	|	Движения.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|
	|	Движения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Движения.Партия) = ТИП(Справочник.ПартииПроизводства)
	|			ТОГДА ВЫРАЗИТЬ(Движения.Партия КАК Справочник.ПартииПроизводства).НаправлениеДеятельности
	|		ИНАЧЕ ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	СУММА(Движения.Количество) КАК Количество,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Движения.СтоимостьРегл <> 0
	|				ТОГДА Движения.СтоимостьРегл
	|			КОГДА РасчетСебестоимостиТоваровОкончательный.Ссылка ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ДопРасходыРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ТрудозатратыРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПостоянныеРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПеременныеРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК Сумма,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Движения.СтоимостьУпр <> 0
	|				ТОГДА Движения.СтоимостьУпр
	|			КОГДА РасчетСебестоимостиТоваровОкончательный.Ссылка ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.СтоимостьУпр, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ДопРасходыУпр, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ТрудозатратыУпр, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПостоянныеУпр, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПеременныеУпр, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК СуммаУУ,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Движения.СтоимостьРегл <> 0
	|				ТОГДА Движения.ПостояннаяРазница
	|			КОГДА РасчетСебестоимостиТоваровОкончательный.Ссылка ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостояннаяРазница, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК СуммаПР,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Движения.СтоимостьРегл <> 0
	|				ТОГДА Движения.ВременнаяРазница
	|			КОГДА РасчетСебестоимостиТоваровОкончательный.Ссылка ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ВременнаяРазница, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК СуммаВР,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Движения.СтоимостьРегл <> 0
	|				ТОГДА Движения.СтоимостьРегл - Движения.ПостояннаяРазница - Движения.ВременнаяРазница
	|			КОГДА РасчетСебестоимостиТоваровОкончательный.Ссылка ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.СтоимостьРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ДопРасходыРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ТрудозатратыРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПостоянныеРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					+ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостатейныеПеременныеРегл, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					- ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ПостояннаяРазница, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|					- ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьТоваров.ВременнаяРазница, 0) КАК ЧИСЛО(31,2)) * ВЫРАЗИТЬ(Движения.Количество КАК ЧИСЛО(31,3))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК СуммаНУ
	|
	|ПОМЕСТИТЬ ВтСтрокиСборка
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтДвиженияСборки КАК Движения
	|	ПО
	|		Движения.Регистратор = ДокументыКОтражению.Ссылка
	|		И НЕ Движения.РасчетСебестоимости
	|		И НЕ Движения.Сторно
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	ПО Движения.ВидЗапасов = ВидыЗапасов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КорВидыЗапасов
	|	ПО Движения.КорВидЗапасов = КорВидыЗапасов.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		Движения.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорАналитика
	|	ПО
	|		Движения.КорАналитикаУчетаНоменклатуры = КорАналитика.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|		ПО Аналитика.Назначение = Назначения.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
	|	ПО
	|		НАЧАЛОПЕРИОДА(Движения.Период, Месяц) = СтоимостьТоваров.Период
	|		И Движения.АналитикаУчетаНоменклатуры = СтоимостьТоваров.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = СтоимостьТоваров.РазделУчета
	|		И Движения.ВидЗапасов = СтоимостьТоваров.ВидЗапасов
	|		И Движения.Организация = СтоимостьТоваров.Организация
	|		И СтоимостьТоваров.Активность
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОкончательный
	|	ПО
	|		НАЧАЛОПЕРИОДА(Движения.Период, Месяц) = НАЧАЛОПЕРИОДА(РасчетСебестоимостиТоваровОкончательный.Ссылка.Дата, МЕСЯЦ)
	|		И Движения.Организация = РасчетСебестоимостиТоваровОкончательный.Организация
	|		И РасчетСебестоимостиТоваровОкончательный.Ссылка.Проведен
	|		И НЕ РасчетСебестоимостиТоваровОкончательный.Ссылка.ПредварительныйРасчет
	|
	|ГДЕ
	|	Движения.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|СГРУППИРОВАТЬ ПО
	|	Движения.Регистратор,
	|	Движения.Период,
	|	Движения.Организация,
	|	Движения.ВидДвижения,
	|	Аналитика.Номенклатура,
	|	КорАналитика.Номенклатура,
	|	Аналитика.МестоХранения,
	|	ВЫБОР
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		ТОГДА Аналитика.СкладскаяТерритория.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|		ТОГДА Аналитика.Подразделение
	|		КОГДА Движения.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.Подразделение, НЕОПРЕДЕЛЕНО)
	|	ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ,
	|
	|	ВЫБОР КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|	КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ЕСТЬNULL(ВидыЗапасов.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	ИНАЧЕ
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА КорВидыЗапасов.Контрагент
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ВидыЗапасов.Контрагент
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Контрагент, НЕОПРЕДЕЛЕНО)
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ТОГДА Движения.АналитикаУчетаПоПартнерам.Контрагент
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|			И КорВидыЗапасов.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = КорВидыЗапасов.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(КорВидыЗапасов.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА КорВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ЕСТЬNULL(КорВидыЗапасов.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|			И ВидыЗапасов.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = ВидыЗапасов.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА ВидыЗапасов.ТипЗапасов В (&ТипыЗапасовВнешнийВладелец)
	|		ТОГДА ЕСТЬNULL(ВидыЗапасов.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам),
	|									ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам))
	|		ТОГДА ЕСТЬNULL(Аналитика.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|			И Движения.АналитикаУчетаПоПартнерам.Договор ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			И Движения.Организация = Движения.АналитикаУчетаПоПартнерам.Договор.ОрганизацияПолучатель
	|		ТОГДА ЕСТЬNULL(Движения.АналитикаУчетаПоПартнерам.Договор.ГруппаФинансовогоУчетаПолучателя, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|		КОГДА Движения.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(Движения.АналитикаУчетаПоПартнерам.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Движения.Партия) = ТИП(Справочник.ПартииПроизводства)
	|			ТОГДА ВЫРАЗИТЬ(Движения.Партия КАК Справочник.ПартииПроизводства).НаправлениеДеятельности
	|		ИНАЧЕ ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ,
	|
	|	Движения.РазделУчета,
	|	Движения.ХозяйственнаяОперация,
	|	Движения.ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 И Движения.Период >= НАЧАЛОПЕРИОДА(&ДатаПереходаНаПартионныйУчетВерсии22, МЕСЯЦ)
	|			И ТИПЗНАЧЕНИЯ(Движения.АналитикаФинансовогоУчета) = ТИП(Справочник.ГруппыАналитическогоУчетаНоменклатуры)
	|			ТОГДА Движения.АналитикаФинансовогоУчета
	|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|		КОГДА НЕ Движения.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА Движения.ГруппаПродукции
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ВидДвижения,
	|	ИдентификаторФинЗаписи";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ВтСтоимости
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Движения.ДокументДвижения КАК Ссылка,
	|	Движения.Период КАК Период,
	|	Движения.ВидДвижения КАК ВидДвижения,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Движения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	СУММА(Движения.СтоимостьРегл + Движения.ДопРасходыРегл + Движения.ТрудозатратыРегл + Движения.ПостатейныеПостоянныеРегл
	|		+ Движения.ПостатейныеПеременныеРегл + Движения.СтоимостьЗабалансоваяРегл) КАК Сумма,
	|	СУММА(Движения.СтоимостьУпр + Движения.ДопРасходыУпр + Движения.ТрудозатратыУпр
	|		+ Движения.ПостатейныеПостоянныеУпр + Движения.ПостатейныеПеременныеУпр) КАК СуммаУУ,
	|	СУММА(Движения.СтоимостьРегл + Движения.ДопРасходыРегл + Движения.ТрудозатратыРегл + Движения.ПостатейныеПостоянныеРегл
	|		+ Движения.ПостатейныеПеременныеРегл - Движения.ПостояннаяРазница - Движения.ВременнаяРазница) КАК СуммаНУ,
	|	СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|	СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|
	|ПОМЕСТИТЬ ВтСтоимостиСборка
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтДвиженияСборки КАК Движения
	|	ПО
	|		Движения.ДокументДвижения = ДокументыКОтражению.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		Движения.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|ГДЕ
	|	Движения.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И НЕ Движения.Сторно
	|
	|СГРУППИРОВАТЬ ПО
	|	Движения.ДокументДвижения,
	|	Движения.Период,
	|	Движения.ВидДвижения,
	|	Аналитика.Номенклатура,
	|	Движения.ИдентификаторФинЗаписи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ВидДвижения,
	|	ИдентификаторФинЗаписи";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
#КонецОбласти
	
	ТекстыЗапроса.Добавить("");
	
	//-- Локализация
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
