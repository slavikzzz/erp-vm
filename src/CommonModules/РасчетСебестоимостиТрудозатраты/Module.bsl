// Вызываются процедуры и функции общих модулей:
//	- РасчетСебестоимостиПрикладныеАлгоритмы.
//	- РасчетСебестоимостиПроизводство

#Область ПрограммныйИнтерфейс

// Этап 1б (Контекст: "РаспределениеТрудозатратНаВыпуск")
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеТрудозатратНаВыпуск(ПараметрыРасчета) Экспорт
	
	Если ПараметрыРасчета.РежимЗакрытияМесяца = Перечисления.РежимыЗакрытияМесяца.ПредварительноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеТрудозатратНаВыпуск");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияТрудозатрат(ПараметрыРасчета),
		ОписаниеЦепочекРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийТрудозатратНаВыпуск(ПараметрыРасчета));

	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру ТрудозатратыНезавершенногоПроизводства.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

// Этап 9а (Контекст: "ПереносСтоимостиТрудозатрат")
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПереносСтоимостиТрудозатрат(ПараметрыРасчета) Экспорт
	
	Если ПараметрыРасчета.РежимЗакрытияМесяца = Перечисления.РежимыЗакрытияМесяца.ПредварительноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ПереносСтоимостиТрудозатрат");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияСтоимостиТрудозатрат(ПараметрыРасчета),
		ОписаниеЦепочекСтоимостиТрудозатрат(ПараметрыРасчета),
		ОписаниеДвиженийСтоимостиТрудозатрат(ПараметрыРасчета),
		ОписаниеНезаписываемыхТиповСтоимостьТрудозатрат(ПараметрыРасчета));
	
	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляСтоимостиТрудозатрат(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру СебестоимостьТоваров.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапа1б_РаспределениеТрудозатратНаВыпуск

// Инициализация данных

Функция ПоляПотребленийРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета)
	
	Возврат "Организация, ПартияПроизводства";
	
КонецФункции

Функция ОписаниеЦепочекРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений	= ПоляПотребленийРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета);
	
	// Трудозатраты <- Расчетное
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Трудозатраты, ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Трудозатраты,
		Перечисления.ТипыЗаписейПартий.Расчетное, ПоляПотреблений);
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияТрудозатрат(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеТрудозатратНаВыпуск");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.ТрудозатратыНезавершенногоПроизводства.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели",	"Количество, НормативнаяСтоимость, Стоимость, СтоимостьРегл, ВременнаяРазница, ПостояннаяРазница, СтоимостьНДД");
	ОписаниеДвижений.Вставить("ИсключенияПоказатели", "Знаменатель, КоличествоПродукции");
	ОписаниеДвижений.Вставить("БазисПрихода", "Знаменатель");
	ОписаниеДвижений.Вставить("БазисРасхода", "Знаменатель");
	ОписаниеДвижений.Вставить("КлючРасхода", "ДокументИсточник");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период");
	ОписаниеДвижений.Вставить("ПоляСортировки",	"ГруппаПродукции");
		
	ОписаниеДвижений.Вставить("ПоляСуммирования", "Количество, НормативнаяСтоимость, Стоимость, СтоимостьРегл, ВременнаяРазница, ПостояннаяРазница, СтоимостьНДД");
	ОписаниеДвижений.Вставить("ИсключенияПоляСуммирования", "Знаменатель, КоличествоПродукции");
	ОписаниеДвижений.Вставить("ПоляГруппировки", 
		РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения, ОписаниеДвижений.ПоляСуммирования));
	
	ОписаниеДвижений.Вставить(
		"ВременныеТаблицыДляСледующихЭтапов",
		"ОстаткиМатериалов, ДолиВыпуска, ПартииМатериаловВНЗП, Продукция, ЗамкнутыеПартии");
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийТрудозатратНаВыпуск(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Расчетное, Истина);
	
	НезаписываемыеРазделы = Новый Соответствие;
	НезаписываемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей, НезаписываемыеРазделы);
	
КонецФункции

Функция ТаблицаДляРаспределенияТрудозатрат(ПараметрыРасчета) Экспорт
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляТрудозатратНЗП());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета)
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"Организация", // разделитель
		"Знаменатель, КоличествоПродукции, Количество, НормативнаяСтоимость, Стоимость, СтоимостьРегл, СтоимостьНДД", // ресурсы
		"Организация, Период, Регистратор, Приоритет"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ РасчетСебестоимостиПроизводство.ТекстОстаткиПартийМатериаловВНЗП() // ОстаткиМатериалов, ПартииМатериаловВНЗП
		+ РасчетСебестоимостиПроизводство.ТекстДолиСтоимостиПоПродукции() // Продукция
		+ РасчетСебестоимостиЛокализация.ТекстЗапросаНастроекНДД(
				"", 
				"РасчетСебестоимостиТрудозатраты_ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск", 
				ПараметрыРасчета)
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ТекстТрудозатратыНЗПИнициализация()
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		// выборка данных
		+ ТекстОписаниеДанныхДляТрудозатратНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстИсходныеТрудозатратыКРаспределению2_2()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПрочиеТрудозатратыКРаспределению2_2()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРаспределениеТрудозатратПоПродукции2_2()
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляТрудозатратНЗП() Экспорт
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80))			  				 			 КАК ЗапросИсточник,
		|	0 													  				 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка) 				 КАК ТипЗаписи,
		|	ЛОЖЬ 												  				 КАК РасчетЗавершен,
		|
		|	ДД.ВидДвижения 				  				 						 КАК ВидДвижения,
		|	ДД.Период 										 					 КАК Период,
		|	ДД.Регистратор 														 КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация 														 КАК Организация,
		|	ДД.Подразделение 													 КАК Подразделение,
		|	ДД.ПартияПроизводства												 КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство 												 КАК ЗаказНаПроизводство,
		|	0 																	 КАК КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов 													 КАК ВидФондаВзносов,
		|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)				 КАК Спецификация,
		|	ДД.Этап 															 КАК Этап,
		|	ДД.СтатьяКалькуляции					 							 КАК СтатьяКалькуляции,
		|	ДД.ВидРабот 														 КАК ВидРабот,
		|	ДД.ГруппаПродукции 													 КАК ГруппаПродукции,
		|	0 													 				 КАК Знаменатель,
		|	ДД.КоличествоПродукции								 				 КАК КоличествоПродукции,
		|	ДД.Количество										 				 КАК Количество,
		|	ДД.НормативнаяСтоимость								 				 КАК НормативнаяСтоимость,
		|	ДД.Стоимость										 				 КАК Стоимость,
		|	ДД.СтоимостьРегл									 				 КАК СтоимостьРегл,
		|	ДД.ВременнаяРазница									 				 КАК ВременнаяРазница,
		|	ДД.ПостояннаяРазница									 			 КАК ПостояннаяРазница,
		|	ДД.СтоимостьНДД									 					 КАК СтоимостьНДД,
		|	ЛОЖЬ 													 			 КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)			 КАК ХозяйственнаяОперация,
		|	ДД.Сотрудник 												 		 КАК Сотрудник,
		|	ДД.КорАналитикаУчетаПродукции 										 КАК КорАналитикаУчетаПродукции,
		|	ДД.КорРазделУчета 												 	 КАК КорРазделУчета,
		|	ДД.КорВидЗапасовПродукции 											 КАК КорВидЗапасовПродукции,
		|	ДД.КорАналитикаУчетаПартий											 КАК КорАналитикаУчетаПартий,
		|	ДД.СтатьяКалькуляцииБезЗаказа 										 КАК СтатьяКалькуляцииБезЗаказа,
		|	ДД.ДокументВыпуска 												 	 КАК ДокументВыпуска,
		|	0 												 					 КАК КодСтроки,
		|	ДД.Регистратор 												 		 КАК ДокументИсточник,
		|	ДД.Продукция 												 		 КАК Продукция,
		|	ДД.ХарактеристикаПродукции 											 КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) 						 КАК Назначение,
		|	ДД.НастройкаХозяйственнойОперации									 КАК НастройкаХозяйственнойОперации,
		|	ДД.ИдентификаторФинЗаписи											 КАК ИдентификаторФинЗаписи
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ДД
		|";

	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

// ... выборки данных

Функция ТекстТрудозатратыНЗПИнициализация()
	Возврат
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.Подразделение,
		|	Т.ПартияПроизводства,
		|	Т.СтатьяКалькуляции,
		|	Т.ВидРабот,
		|	Т.ГруппаПродукции,
		|	Т.ВидФондаВзносов,
		|	Т.КоличествоОстаток,
		|	Т.НормативнаяСтоимостьОстаток,
		|	Т.СтоимостьОстаток,
		|	Т.СтоимостьРеглОстаток,
		|	Т.ВременнаяРазницаОстаток,
		|	Т.ПостояннаяРазницаОстаток,
		|	Т.СтоимостьНДДОстаток
		|ПОМЕСТИТЬ НачальныеОстаткиТрудозатратНЗП
		|ИЗ
		|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства.Остатки(
		|			&ГраницаНачалоПериода,
		|			Организация В (&МассивОрганизаций)
		|				И НЕ ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)) КАК Т
		|;
		|
		//++ НЕ УТКА
		
		//++ Устарело_Переработка24
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.РазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.Период КАК ДатаПроизводства
		|ПОМЕСТИТЬ РаботыДляДавальца
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
		|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
		|	И ДД.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		|;
		//-- Устарело_Переработка24
		
		//-- НЕ УТКА
		|
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТрудозатратыПоЭтапам.Организация,
		|	ТрудозатратыПоЭтапам.Подразделение,
		|	ТрудозатратыПоЭтапам.ПартияПроизводства,
		|	ТрудозатратыПоЭтапам.СтатьяКалькуляции,
		|	ТрудозатратыПоЭтапам.ВидРабот,
		|	ТрудозатратыПоЭтапам.ГруппаПродукции,
		|	ТрудозатратыПоЭтапам.ВидФондаВзносов,
		|	СУММА(ТрудозатратыПоЭтапам.Количество) КАК Количество,
		|	СУММА(ТрудозатратыПоЭтапам.НормативнаяСтоимость) КАК НормативнаяСтоимость,
		|	СУММА(ТрудозатратыПоЭтапам.Стоимость) КАК Стоимость,
		|	СУММА(ТрудозатратыПоЭтапам.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ТрудозатратыПоЭтапам.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ТрудозатратыПоЭтапам.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВЫБОР
		|			КОГДА ЛОЖЬ ТОГДА 0 // для удобства расстановки тегов
		|			КОГДА ЕСТЬNULL(НастройкиПризнанияРасходовНДДПоПодразделениям.РасходыВключаютсяВстоимость, ЛОЖЬ)
		|				ТОГДА ТрудозатратыПоЭтапам.СтоимостьНДД
		|			КОГДА ЕСТЬNULL(НастройкиПризнанияРасходовНДДПоНаправлениямДеятельности.РасходыВключаютсяВстоимость, ЛОЖЬ)
		|				ТОГДА ТрудозатратыПоЭтапам.СтоимостьНДД
		|			ИНАЧЕ 0 
		|		КОНЕЦ) КАК СтоимостьНДД
		|
		|ПОМЕСТИТЬ ТрудозатратыПоЭтапам
		|ИЗ
		|	(ВЫБРАТЬ
		|		Остатки.Организация КАК Организация,
		|		Остатки.Подразделение КАК Подразделение,
		|		Остатки.ПартияПроизводства КАК ПартияПроизводства,
		|		Остатки.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		Остатки.ВидРабот КАК ВидРабот,
		|		Остатки.ГруппаПродукции КАК ГруппаПродукции,
		|		Остатки.ВидФондаВзносов КАК ВидФондаВзносов,
		|		Остатки.КоличествоОстаток КАК Количество,
		|		Остатки.НормативнаяСтоимостьОстаток КАК НормативнаяСтоимость,
		|		Остатки.СтоимостьОстаток КАК Стоимость,
		|		Остатки.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|		Остатки.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|		Остатки.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|		Остатки.СтоимостьНДДОстаток КАК СтоимостьНДД
		|	ИЗ
		|		НачальныеОстаткиТрудозатратНЗП КАК Остатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК Партии
		|			ПО Остатки.ПартияПроизводства = Партии.ПартияПроизводства
		|			И Остатки.Организация = Партии.Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.Подразделение,
		|		ДД.ПартияПроизводства,
		|		ДД.СтатьяКалькуляции,
		|		ДД.ВидРабот,
		|		ДД.ГруппаПродукции,
		|		ДД.ВидФондаВзносов,
		|		ДД.Количество,
		|		ДД.НормативнаяСтоимость,
		|		ДД.Стоимость,
		|		ДД.СтоимостьРегл,
		|		ДД.ВременнаяРазница,
		|		ДД.ПостояннаяРазница,
		|		ДД.СтоимостьНДД
		|	ИЗ
		|		РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ДД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваСводно КАК Партии
		|			ПО ДД.ПартияПроизводства = Партии.ПартияПроизводства
		|			И ДД.Организация = Партии.Организация
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ДД.Организация В(&МассивОрганизаций)) КАК ТрудозатратыПоЭтапам
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоПодразделениям
		|	ПО НастройкиПризнанияРасходовНДДПоПодразделениям.Организация = ТрудозатратыПоЭтапам.Организация
		|		И НастройкиПризнанияРасходовНДДПоПодразделениям.ОбъектРаздельногоУчетаНДД = ТрудозатратыПоЭтапам.Подразделение
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоНаправлениямДеятельности
		|	ПО НастройкиПризнанияРасходовНДДПоНаправлениямДеятельности.Организация = ТрудозатратыПоЭтапам.Организация
		|		И НастройкиПризнанияРасходовНДДПоНаправлениямДеятельности.ОбъектРаздельногоУчетаНДД 
		|			= ТрудозатратыПоЭтапам.ПартияПроизводства.НаправлениеДеятельности
		|
		|СГРУППИРОВАТЬ ПО
		|	ТрудозатратыПоЭтапам.ПартияПроизводства,
		|	ТрудозатратыПоЭтапам.Организация,
		|	ТрудозатратыПоЭтапам.СтатьяКалькуляции,
		|	ТрудозатратыПоЭтапам.ВидФондаВзносов,
		|	ТрудозатратыПоЭтапам.ГруппаПродукции,
		|	ТрудозатратыПоЭтапам.ВидРабот,
		|	ТрудозатратыПоЭтапам.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.ВидРабот,
		|	ДД.СтатьяКалькуляции,
		|	СУММА(ДД.Количество) КАК Количество
		|ПОМЕСТИТЬ ОстаткиВидовРаботПоЭтапам
		|ИЗ
		|	ТрудозатратыПоЭтапам КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ВидРабот,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Организация КАК Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.ПартияПроизводства КАК ПартияПроизводства,
		|	ДД.ГруппаПродукции КАК ГруппаПродукции,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	СУММА(ДД.План) КАК План,
		|	СУММА(ДД.Факт) КАК Факт
		|ПОМЕСТИТЬ ПланФактТрудозатратПоЭтапам
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПроизводствоБезЗаказа.Организация КАК Организация,
		|		ВидыРабот.Подразделение КАК Подразделение,
		|		ДД.ПартияПроизводства КАК ПартияПроизводства,
		|		ВЫБОР
		|			КОГДА ДД.АналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО
		|				ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			ИНАЧЕ
		|				ДД.АналитикаФинансовогоУчета
		|		КОНЕЦ КАК ГруппаПродукции,
		|		ВидыРабот.ВидРабот КАК ВидРабот,
		|		ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ВидыРабот.Количество КАК План,
		|		0 КАК Факт
		|	ИЗ
		|		ПартииПроизводстваПоЭтапам КАК ДД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.Трудозатраты КАК ВидыРабот
		|			ПО ДД.Этап = ВидыРабот.Ссылка
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
		|			ПО ДД.Этап = ПроизводствоБезЗаказа.Ссылка
		|				И ДД.Организация = ПроизводствоБезЗаказа.Организация
		|
		|	ГДЕ
		|		ПроизводствоБезЗаказа.РаспоряжениеДляТрудозатрат
		|
		//++ НЕ УТКА
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ДокументЭтап.Организация КАК Организация,
		|		ВидыРабот.Подразделение КАК Подразделение,
		|		ДД.ПартияПроизводства КАК ПартияПроизводства,
		|		ВЫБОР
		|			КОГДА ДД.АналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО
		|				ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			ИНАЧЕ
		|				ДД.АналитикаФинансовогоУчета
		|		КОНЕЦ КАК ГруппаПродукции,
		|		ВидыРабот.ВидРабот КАК ВидРабот,
		|		ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ВидыРабот.Количество КАК План,
		|		0 КАК Факт
		|	ИЗ
		|		ПартииПроизводстваСводно КАК ДД
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДокументЭтап
		|			ПО ДД.ПартияПроизводства = ДокументЭтап.ПартияПроизводства
		|				И ДД.Организация = ДокументЭтап.Организация
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Трудозатраты КАК ВидыРабот
		|			ПО ДокументЭтап.Ссылка = ВидыРабот.Ссылка
		|	ГДЕ
		|		НЕ ВидыРабот.Отменено
		//-- НЕ УТКА
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Движения.Организация КАК Организация,
		|		Движения.Подразделение КАК Подразделение,
		|		Движения.ПартияПроизводства КАК ПартияПроизводства,
		|		Движения.ГруппаПродукции КАК ГруппаПродукции,
		|		Движения.ВидРабот КАК ВидРабот,
		|		Движения.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		0 КАК План,
		|		Движения.Количество КАК Факт
		|	ИЗ
		|		ПартииПроизводстваСводно КАК ДД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК Движения
		|			ПО ДД.ПартияПроизводства = Движения.ПартияПроизводства
		|				И ДД.Организация = Движения.Организация
		|
		|	ГДЕ
		|		Движения.Период <= &КонецПериода
		|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Движения.Количество > 0
		|	) КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.ВидРабот,
		|	ДД.СтатьяКалькуляции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расходы.ПартияПроизводства,
		|	Расходы.ГруппаПродукции,
		|	Расходы.Подразделение,
		|	Расходы.ВидРабот,
		|	Расходы.СтатьяКалькуляции,
		|	Расходы.КоличествоРасход
		|ПОМЕСТИТЬ втСписаноВПрошлыхМесяцахПредварительная
		|ИЗ
		|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства.Обороты(, ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1), , ) КАК Расходы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Расходы.ПартияПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расходы.ПартияПроизводства,
		|	Расходы.ГруппаПродукции,
		|	Расходы.Подразделение,
		|	Расходы.ВидРабот,
		|	Расходы.СтатьяКалькуляции,
		|	Расходы.КоличествоРасход КАК Количество
		|ПОМЕСТИТЬ втСписаноВПрошлыхМесяцах
		|ИЗ
		|	ПартииПроизводстваСводно КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСписаноВПрошлыхМесяцахПредварительная КАК Расходы
		|		ПО ДД.ПартияПроизводства = Расходы.ПартияПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втСписаноВПрошлыхМесяцахПредварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.Подразделение,
		|	ДД.ВидРабот,
		|	ДД.СтатьяКалькуляции,
		|	ВЫБОР
		|		КОГДА ДД.План > ДД.Факт
		|			ТОГДА ДД.План
		|		ИНАЧЕ ДД.Факт
		|	КОНЕЦ * ДолиВыпуска.ДоляВыпускаВсего - ЕСТЬNULL(Списано.Количество, 0) КАК РасчетноеКоличество,
		|	ДолиВыпуска.ПроизводствоЗавершено
		|ПОМЕСТИТЬ втРасчетноеКоличество
		|ИЗ
		|	ПланФактТрудозатратПоЭтапам КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиВыпуска КАК ДолиВыпуска
		|		ПО ДД.ПартияПроизводства = ДолиВыпуска.ПартияПроизводства
		|		И ДД.Организация = ДолиВыпуска.Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСписаноВПрошлыхМесяцах КАК Списано
		|		ПО ДД.ПартияПроизводства = Списано.ПартияПроизводства
		|			И ДД.Подразделение = Списано.Подразделение
		|			И ДД.ВидРабот = Списано.ВидРабот
		|			И ДД.СтатьяКалькуляции = Списано.СтатьяКалькуляции
		|			И ДД.ГруппаПродукции = Списано.ГруппаПродукции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.Подразделение,
		|	ДД.ВидРабот,
		|	ДД.СтатьяКалькуляции,
		|	Остатки.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ДД.ПроизводствоЗавершено
		|			ТОГДА ЕСТЬNULL(Остатки.Количество, 0)
		|		КОГДА ДД.РасчетноеКоличество > ЕСТЬNULL(Остатки.Количество, 0)
		|			ТОГДА ЕСТЬNULL(Остатки.Количество, 0)
		|		ИНАЧЕ ДД.РасчетноеКоличество
		|	КОНЕЦ КАК РасчетноеКоличество
		|ПОМЕСТИТЬ Расчетное
		|ИЗ
		|	втРасчетноеКоличество КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиВидовРаботПоЭтапам КАК Остатки
		|		ПО ДД.Организация = Остатки.Организация
		|			И ДД.ПартияПроизводства = Остатки.ПартияПроизводства
		|			И ДД.Подразделение = Остатки.Подразделение
		|			И ДД.ВидРабот = Остатки.ВидРабот
		|			И ДД.СтатьяКалькуляции = Остатки.СтатьяКалькуляции
		|			И ДД.ГруппаПродукции = Остатки.ГруппаПродукции
		|ГДЕ
		|	ВЫБОР
		|		КОГДА ДД.ПроизводствоЗавершено
		|			ТОГДА ЕСТЬNULL(Остатки.Количество, 0)
		|		КОГДА ДД.РасчетноеКоличество > ЕСТЬNULL(Остатки.Количество, 0)
		|			ТОГДА ЕСТЬNULL(Остатки.Количество, 0)
		|		ИНАЧЕ ДД.РасчетноеКоличество
		|	КОНЕЦ > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Расчетное количество без статей калькуляции. Применяется для распределения взносов и сумм зарплаты,
		// начисленных по статьям калькуляции, отличной от статьи калькуляции количества. То есть, расчетное количество для расчета сумм начисленных
		// по статьям калькуляции количества распределяются в разрезе статей калькуляции, а расчетное количество для начислений по другим статьям калькуляции в взносам (с нулевым количеством)
		// рассчитывается исходя из общего количества видов работ по всем статьям калькуляции.
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.Подразделение,
		|	ДД.ВидРабот,
		|	СУММА(ДД.Количество),
		|	СУММА(ДД.РасчетноеКоличество)
		|ПОМЕСТИТЬ РасчетноеСводно
		|ИЗ
		|	Расчетное КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.ПартияПроизводства,
		|	ДД.ГруппаПродукции,
		|	ДД.Подразделение,
		|	ДД.ВидРабот
		|";
КонецФункции

Функция ТекстИсходныеТрудозатратыКРаспределению2_2() // использует ТрудозатратыПоЭтапам, ДолиВыпуска, Расчетное
	Возврат
		"ВЫБРАТЬ
		|	""ТекстИсходныеТрудозатратыКРаспределению2_2"" КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Расчетное) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&КонецПериода КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.ПартияПроизводства КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.ГруппаПродукции КАК ГруппаПродукции,
		|	Итог.ДоляВыпускаМесяца КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	Расчетное.РасчетноеКоличество КАК Количество,
		|	ДД.НормативнаяСтоимость * Расчетное.РасчетноеКоличество / ДД.Количество КАК НормативнаяСтоимость,
		|	ДД.Стоимость * Расчетное.РасчетноеКоличество / ДД.Количество КАК Стоимость,
		|	ДД.СтоимостьРегл * Расчетное.РасчетноеКоличество / ДД.Количество КАК СтоимостьРегл,
		|	ДД.ВременнаяРазница * Расчетное.РасчетноеКоличество / ДД.Количество КАК ВременнаяРазница,
		|	ДД.ПостояннаяРазница * Расчетное.РасчетноеКоличество / ДД.Количество КАК ПостояннаяРазница,
		|	ДД.СтоимостьНДД * Расчетное.РасчетноеКоличество / ДД.Количество КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	ТрудозатратыПоЭтапам КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиВыпуска КАК Итог
		|		ПО ДД.ПартияПроизводства = Итог.ПартияПроизводства
		|		И ДД.Организация = Итог.Организация
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Расчетное КАК Расчетное
		|		ПО ДД.ПартияПроизводства = Расчетное.ПартияПроизводства
		|			И ДД.Подразделение = Расчетное.Подразделение
		|			И ДД.ВидРабот = Расчетное.ВидРабот
		|			И ДД.СтатьяКалькуляции = Расчетное.СтатьяКалькуляции
		|			И ДД.ГруппаПродукции = Расчетное.ГруппаПродукции
		|ГДЕ
		|	ДД.Количество > 0";

КонецФункции

Функция ТекстПрочиеТрудозатратыКРаспределению2_2() // использует ТрудозатратыПоЭтапам, ДолиВыпуска, РасчетноеСводно  
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПрочиеТрудозатратыКРаспределению2_2"" КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Расчетное) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&КонецПериода КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.ПартияПроизводства КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.ГруппаПродукции КАК ГруппаПродукции,
		|	Итог.ДоляВыпускаМесяца КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	ВЫБОР
		|		КОГДА РасчетноеСводно.Количество = 0
		|			ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.Стоимость * РасчетноеСводно.РасчетноеКоличество / РасчетноеСводно.Количество
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА РасчетноеСводно.Количество = 0
		|			ТОГДА ДД.СтоимостьРегл
		|		ИНАЧЕ ДД.СтоимостьРегл * РасчетноеСводно.РасчетноеКоличество / РасчетноеСводно.Количество
		|	КОНЕЦ КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА РасчетноеСводно.Количество = 0
		|			ТОГДА ДД.ВременнаяРазница
		|		ИНАЧЕ ДД.ВременнаяРазница * РасчетноеСводно.РасчетноеКоличество / РасчетноеСводно.Количество
		|	КОНЕЦ КАК ВременнаяРазница,
		|	ВЫБОР
		|		КОГДА РасчетноеСводно.Количество = 0
		|			ТОГДА ДД.ПостояннаяРазница
		|		ИНАЧЕ ДД.ПостояннаяРазница * РасчетноеСводно.РасчетноеКоличество / РасчетноеСводно.Количество
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА РасчетноеСводно.Количество = 0
		|			ТОГДА ДД.СтоимостьНДД
		|		ИНАЧЕ ДД.СтоимостьНДД * РасчетноеСводно.РасчетноеКоличество / РасчетноеСводно.Количество
		|	КОНЕЦ КАК СтоимостьНДД,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации,
		|	"""" КАК ИдентификаторФинЗаписи
		|ИЗ
		|	ТрудозатратыПоЭтапам КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиВыпуска КАК Итог
		|		ПО ДД.ПартияПроизводства = Итог.ПартияПроизводства
		|		И ДД.Организация = Итог.Организация
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетноеСводно КАК РасчетноеСводно
		|		ПО ДД.ПартияПроизводства = РасчетноеСводно.ПартияПроизводства
		|			И ДД.Подразделение = РасчетноеСводно.Подразделение
		|			И ДД.ВидРабот = РасчетноеСводно.ВидРабот
		|			И ДД.ГруппаПродукции = РасчетноеСводно.ГруппаПродукции
		|ГДЕ
		|	ДД.Количество = 0";

КонецФункции
	
Функция ТекстРаспределениеТрудозатратПоПродукции2_2() // использует Продукция
	
	Возврат "
	|ВЫБРАТЬ
	|	""ТекстРаспределениеТрудозатратПоПродукции2_2"" КАК ЗапросИсточник,
	|	0 КАК Приоритет,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Трудозатраты) КАК ТипЗаписи,
	|	ЛОЖЬ КАК РасчетЗавершен,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДД.ДатаПроизводства КАК Период,
	|	ДД.ПартияВыпуска КАК Регистратор,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
	|	ДД.Организация,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.ПартияПроизводства КАК ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
	|	0 КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
	|	НЕОПРЕДЕЛЕНО КАК Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК Этап,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
	|	ДД.ДоляНаименованияМесяца КАК Знаменатель,
	|	ДД.КоличествоПродукции КАК КоличествоПродукции,
	|	0 КАК Количество,
	|	0 КАК НормативнаяСтоимость,
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	0 КАК ВременнаяРазница,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК СтоимостьНДД,
	|	ЛОЖЬ КАК Первичное,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
	|	ВЫБОР
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|		КОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры ЕСТЬ НЕ NULL
	|			ТОГДА РаботыДляДавальца.АналитикаУчетаНоменклатуры
	//-- Устарело_Переработка24
	
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|	КОНЕЦ КАК КорАналитикаУчетаПродукции,
	|	ВЫБОР
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|		КОГДА РаботыДляДавальца.РазделУчета ЕСТЬ НЕ NULL
	|			ТОГДА РаботыДляДавальца.РазделУчета
	//-- Устарело_Переработка24
	
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ДД.РазделУчета
	|	КОНЕЦ КАК КорРазделУчета,
	|	ВЫБОР
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|		КОГДА РаботыДляДавальца.ВидЗапасов ЕСТЬ НЕ NULL
	|			ТОГДА РаботыДляДавальца.ВидЗапасов
	//-- Устарело_Переработка24
	
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ДД.ВидЗапасов
	|	КОНЕЦ КАК КорВидЗапасовПродукции,
	|	ВЫБОР
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая)
	|			И РаботыДляДавальца.АналитикаУчетаПартий ЕСТЬ НЕ NULL
	|			ТОГДА РаботыДляДавальца.АналитикаУчетаПартий
	//-- Устарело_Переработка24
	
	//-- НЕ УТКА
	|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА ДД.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
	|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
	|	0 КАК КодСтроки,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО КАК Продукция,
	|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
	|	НЕОПРЕДЕЛЕНО КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииТрудозатраты) КАК НастройкаХозяйственнойОперации,
	|	ДД.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Продукция КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ПО ПроизводствоБезЗаказа.Ссылка = ДД.ПартияВыпуска
	|
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
	|		ПО РаботыДляДавальца.Регистратор = ДД.ПартияВыпуска
	|			И РаботыДляДавальца.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
	|			И РаботыДляДавальца.КорВидЗапасов = ДД.ВидЗапасов
	|			И РаботыДляДавальца.ДатаПроизводства = ДД.ДатаПроизводства
	//-- Устарело_Переработка24
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ДД.ПартияПроизводства
	//-- НЕ УТКА
	|	
	|ГДЕ
	|	((ЕСТЬNULL(ПроизводствоБезЗаказа.РаспоряжениеДляТрудозатрат, ЛОЖЬ)
	|		И ДД.Организация = ПроизводствоБезЗаказа.Организация)
	//++ НЕ УТКА
	|		ИЛИ ТИПЗНАЧЕНИЯ(СпрПартииПроизводства.Документ) = ТИП(Документ.ЗаказНаПроизводство2_2)
	|		ИЛИ ТИПЗНАЧЕНИЯ(ДД.ПартияВыпуска) = ТИП(Документ.ОтчетДавальцуМеждуОрганизациями)
	//-- НЕ УТКА
	|	)
	|	И ДД.ДоляНаименованияМесяца > 0
	|";
	
КонецФункции

// Заполнение расчетной партии
Процедура ЗаполнитьРасчетнуюПартиюРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход.Знаменатель = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// расчетная партия заполняется на наибольшее общее вычитаемое
	ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Количество 			 = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
	РасчетнаяПартия.НормативнаяСтоимость = Окр(ЗнаменательСписания * Приход.НормативнаяСтоимость / Приход.Знаменатель, 2);
	РасчетнаяПартия.Стоимость 			 = Окр(ЗнаменательСписания * Приход.Стоимость / Приход.Знаменатель, 2);
	РасчетнаяПартия.СтоимостьРегл 		 = Окр(ЗнаменательСписания * Приход.СтоимостьРегл / Приход.Знаменатель, 2);
	РасчетнаяПартия.ВременнаяРазница 	 = Окр(ЗнаменательСписания * Приход.ВременнаяРазница / Приход.Знаменатель, 2);
	РасчетнаяПартия.ПостояннаяРазница 	 = Окр(ЗнаменательСписания * Приход.ПостояннаяРазница / Приход.Знаменатель, 2);
	РасчетнаяПартия.СтоимостьНДД 		 = Окр(ЗнаменательСписания * Приход.СтоимостьНДД / Приход.Знаменатель, 2);
	
	// корректируем базу расчета в приходе
	Приход.Количество 			= Приход.Количество - РасчетнаяПартия.Количество;
	Приход.НормативнаяСтоимость = Приход.НормативнаяСтоимость - РасчетнаяПартия.НормативнаяСтоимость;
	Приход.Стоимость 			= Приход.Стоимость - РасчетнаяПартия.Стоимость;
	Приход.СтоимостьРегл 		= Приход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
	Приход.ВременнаяРазница 	= Приход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
	Приход.ПостояннаяРазница 	= Приход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
	Приход.СтоимостьНДД 		= Приход.СтоимостьНДД - РасчетнаяПартия.СтоимостьНДД;
	Приход.Знаменатель 			= Приход.Знаменатель - ЗнаменательСписания;
	
	// Заполняем партионную идентификацию в расчетной партии
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
		РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Регистратор) Тогда
		РасчетнаяПартия.Регистратор = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.СтатьяКалькуляцииБезЗаказа) Тогда
		РасчетнаяПартия.СтатьяКалькуляцииБезЗаказа = Приход.СтатьяКалькуляцииБезЗаказа;
	КонецЕсли;
	
	РасчетнаяПартия.Подразделение 	  = Приход.Подразделение;
	РасчетнаяПартия.ВидФондаВзносов   = Приход.ВидФондаВзносов;
	РасчетнаяПартия.ВидРабот 		  = Приход.ВидРабот;
	РасчетнаяПартия.ГруппаПродукции   = Приход.ГруппаПродукции;
	РасчетнаяПартия.СтатьяКалькуляции = Приход.СтатьяКалькуляции;
	
	РасчетнаяПартия.РасчетЗавершен 	  = Приход.РасчетЗавершен;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа9_ПереносСтоимостиТрудозатрат

// Инициализация данных

Функция ПоляПотребленийСтоимостиТрудозатрат(ПараметрыРасчета)
	
	Возврат "АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета";
	
КонецФункции

Функция ОписаниеЦепочекСтоимостиТрудозатрат(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	
	// Выпуск <- Трудозатраты
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Выпуск, 				"Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, КорАналитикаУчетаПартий");
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Выпуск,
		Перечисления.ТипыЗаписейПартий.Трудозатраты, 		"Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, КорАналитикаУчетаПартий");	
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийСтоимостиТрудозатрат(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияСтоимостиТрудозатрат(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "ПереносСтоимостиТрудозатрат");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийСтоимостиТрудозатрат(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Знаменатель, Количество, Стоимость, СтоимостьБезНДС, ДопРасходы, ДопРасходыБезНДС,
		|СтоимостьРегл, ПостояннаяРазница, ВременнаяРазница, СтоимостьНДД,
		|СтоимостьЗабалансовая, Трудозатраты, ПостатейныеПостоянныеСНДС, ПостатейныеПостоянныеБезНДС,
		|ПостатейныеПеременныеСНДС, ПостатейныеПеременныеБезНДС,
		|СтоимостьУпр, ДопРасходыУпр, ТрудозатратыУпр, ПостатейныеПостоянныеУпр, ПостатейныеПеременныеУпр,
		|СтоимостьЗабалансоваяРегл, ДопРасходыРегл, ТрудозатратыРегл,
		|ПостатейныеПостоянныеРегл, ПостатейныеПеременныеРегл");
	ОписаниеДвижений.Вставить("БазисПрихода", "Количество");
	ОписаниеДвижений.Вставить("БазисРасхода", "Количество");
	ОписаниеДвижений.Вставить("КлючРасхода", "ДокументИсточник");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период");
	ОписаниеДвижений.Вставить("ПоляСортировки", "Партия, ВидДеятельностиНДС, АналитикаФинансовогоУчета"); 
	ОписаниеДвижений.Вставить("СортировкаПоУсловию", Истина);	
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхТиповСтоимостьТрудозатрат(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	Для Каждого ТекЭлемент Из ПараметрыРасчета.НепересчитываемыеТипыЗаписей Цикл
		НезаписываемыеТипыЗаписей.Вставить(ТекЭлемент, Истина);
	КонецЦикла;
	
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Трудозатраты, Истина);
	
	НезаписываемыеРазделы = Новый Соответствие;
	НезаписываемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Истина, НезаписываемыеТипыЗаписей, НезаписываемыеРазделы);
	
КонецФункции

Функция ТаблицаДляРаспределенияСтоимостиТрудозатрат(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляСтоимостиТрудозатрат());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляСтоимостиТрудозатрат(ПараметрыРасчета)
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"АналитикаУчетаНоменклатуры", // разделитель
		"Знаменатель, Количество,
			|Стоимость, СтоимостьБезНДС, СтоимостьЗабалансовая, ДопРасходы, ДопРасходыБезНДС, Трудозатраты,
			|ПостатейныеПостоянныеСНДС, ПостатейныеПостоянныеБезНДС,
			|ПостатейныеПеременныеСНДС, ПостатейныеПеременныеБезНДС,
			|СтоимостьУпр, ДопРасходыУпр, ТрудозатратыУпр, ПостатейныеПостоянныеУпр, ПостатейныеПеременныеУпр,
			|СтоимостьРегл, СтоимостьЗабалансоваяРегл, ПостояннаяРазница, ВременнаяРазница, СтоимостьНДД,
			|ДопРасходыРегл, ТрудозатратыРегл, ПостатейныеПостоянныеРегл, ПостатейныеПеременныеРегл", // ресурсы
		"Номенклатура УБЫВ, Приоритет ВОЗР, Период, Организация, ВидЗапасов,
			|Партия, АналитикаУчетаПартий, Регистратор, ВидДвижения УБЫВ"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляСтоимостиТрудозатрат(ПараметрыРасчета),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляСтоимостиТрудозатрат(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		+ ТекстОписаниеДанныхДляСтоимостиТрудозатрат()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеВыпускиПродукции()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеРасходыТрудозатратыНЗП()
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляСтоимостиТрудозатрат() Экспорт
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 0 // 76 ПОЛЕЙ
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) 					КАК ЗапросИсточник,
		|	Т.ТипЗаписи										КАК ТипЗаписи,
		|	ЛОЖЬ											КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ											КАК Сторно,
		|	0 												КАК Приоритет,
		|	0 												КАК Знаменатель,
		|	ЛОЖЬ 											КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)  КАК Номенклатура,
		|	Т.АналитикаУчетаПартий.НалогообложениеНДС 		КАК НалогообложениеПартии,
		|	Т.Период										КАК Период,
		|	Т.Период										КАК ПериодПоступления,
		|	Т.Регистратор									КАК Регистратор,
		|	Т.ВидДвижения 									КАК ВидДвижения,
		|
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.РазделУчета,
		|	Т.ВидЗапасов,
		|	Т.Организация,
		|	Т.Партия,
		|	Т.Партия КАК ЗаказНаПроизводство,
		|	Т.АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.ВидДеятельностиНДС,
		|
		|	Т.Количество,
		|	Т.Стоимость,
		|	Т.СтоимостьБезНДС,
		|	Т.СтоимостьЗабалансовая,
		|	Т.СтоимостьУпр,
		|	Т.ДопРасходыУпр,
		|	Т.ПостатейныеПеременныеСНДС,
		|	Т.ПостатейныеПеременныеБезНДС,
		|	Т.ТрудозатратыУпр,
		|	Т.ПостатейныеПостоянныеУпр,
		|	Т.ПостатейныеПеременныеУпр,
		|	Т.ДопРасходы,
		|	Т.ДопРасходыБезНДС,
		|	Т.Трудозатраты,
		|	Т.ПостатейныеПостоянныеСНДС,
		|	Т.ПостатейныеПостоянныеБезНДС,
		|	Т.СтоимостьРегл,
		|	Т.СтоимостьЗабалансоваяРегл,
		|	Т.ПостояннаяРазница,
		|	Т.ВременнаяРазница,
		|	Т.СтоимостьНДД,
		|	Т.ДопРасходыРегл,
		|	Т.ТрудозатратыРегл,
		|	Т.ПостатейныеПостоянныеРегл,
		|	Т.ПостатейныеПеременныеРегл,
		|
		|	Т.ХозяйственнаяОперация,
		|	Т.КорАналитикаУчетаНоменклатуры,
		|	Т.КорРазделУчета,
		|	Т.КорВидЗапасов,
		|	Т.КорОрганизация,
		|	Т.КорСтоимость,
		|	Т.АналитикаУчетаПоПартнерам,
		|	Т.ЗаказКлиента,
		|	Т.Подразделение,
		|	Т.АналитикаРасходов,
		|	Т.СтатьяРасходовСписания,
		|	Т.СтатьяДоходов,
		|	Т.АналитикаДоходов,
		|	Т.ПериодПродажи,
		|	Т.СтатьяАктивовПассивов,
		|	Т.АналитикаАктивовПассивов,
		|	Т.ДокументДвижения,
		|	Т.ИдентификаторСтроки,
		|	Т.ГруппаПродукции,
		|	Т.КорПартия,
		|	Т.КорАналитикаУчетаПартий,
		|	Т.КорАналитикаФинансовогоУчета,
		|	Т.КорВидДеятельностиНДС,
		|	Т.КорВидДеятельностиНДС КАК ВидДеятельностиНДСДокумента,
		|	Т.НДСУпр,
		|	Т.НДСРегл,
		|	Т.СтатьяКалькуляции,
		|	Т.ДокументИсточник,
		|	Т.КорНаправлениеДеятельности,
		|	Т.КодСтроки,
		|	Т.НастройкаХозяйственнойОперации,
		|	Т.ИдентификаторФинЗаписи
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|";

	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстТекущиеВыпускиПродукции()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеВыпускиПродукции"" 	      				КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Выпуск) 		КАК ТипЗаписи,
		|	ЛОЖЬ													КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ													КАК Сторно,
		|	99 										  				КАК Приоритет,
		|	0 										  				КАК Знаменатель,
		|	ЛОЖЬ 													КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) 			КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК ВидДвижения,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.РазделУчета КАК КорРазделУчета,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	АналитикаДавальца.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	"""" КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	ДД.Партия КАК КорПартия,
		|	ДД.АналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
		|	ДД.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
		|	ДД.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
		|	ДД.ВидДеятельностиНДС КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО,
		|	ДД.НастройкаХозяйственнойОперации,
		|	ДД.ИдентификаторФинЗаписи
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПоПартнерамИзОтчетовДавальцам КАК АналитикаДавальца
		|		ПО АналитикаДавальца.Регистратор = ДД.Регистратор
		|ГДЕ
		|	ДД.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|	И НЕ ДД.Сторно
		|";
КонецФункции

Функция ТекстТекущиеРасходыТрудозатратыНЗП()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеРасходыТрудозатратыНЗП""					КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Трудозатраты)	КАК ТипЗаписи,
		|	ЛОЖЬ													КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ													КАК Сторно,
		|	45														КАК Приоритет,
		|	0														КАК Знаменатель,
		|	ИСТИНА 													КАК РасчетЗавершен,
		|	НЕОПРЕДЕЛЕНО											КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО											КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	МАКСИМУМ(ДД.КоличествоПродукции) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА &УправленческийУчетОрганизаций ТОГДА ДД.Стоимость
		|		ИНАЧЕ 0 КОНЕЦ) КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	СУММА(ДД.Стоимость) КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ДД.СтоимостьНДД) КАК СтоимостьНДД,
		|	0 КАК ДопРасходыРегл,
		|	СУММА(ДД.СтоимостьРегл) КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаПродукции КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	ДД.КорВидЗапасовПродукции КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	ДД.Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ДД.Регистратор КАК ДокументДвижения,
		|	"""" КАК ИдентификаторСтроки,
		|	ДД.ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	ДД.КорАналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО,
		|	ДД.НастройкаХозяйственнойОперации,
		|	ДД.ИдентификаторФинЗаписи
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.КорАналитикаУчетаПродукции
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И (ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (ТИП(Документ.ПроизводствоБезЗаказа)
		//++ НЕ УТКА
		|		,ТИП(Документ.ЭтапПроизводства2_2)
		|		,ТИП(Документ.ОтчетДавальцуМеждуОрганизациями)
		//-- НЕ УТКА

		//++ Устарело_Производство21
		|		,ТИП(Документ.ВыпускПродукции)
		//-- Устарело_Производство21
		|	
		//++ Устарело_Переработка24
		|		,ТИП(Документ.ОтчетПереработчика)
		//-- Устарело_Переработка24
		|		,ТИП(Документ.ОтчетПереработчика2_5)
		|		))
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорРазделУчета,
		|	ДД.КорВидЗапасовПродукции,
		|	ДД.Подразделение,
		|	ДД.ГруппаПродукции,
		|	ДД.СтатьяКалькуляции,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.НастройкаХозяйственнойОперации,
		|	ДД.ИдентификаторФинЗаписи
		|";
КонецФункции

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюСтоимостьТрудозатрат(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	СуммовыеРесурсы = Новый Структура(
	"Стоимость, СтоимостьБезНДС, ДопРасходы, ДопРасходыБезНДС,
	|СтоимостьУпр, ДопРасходыУпр, ТрудозатратыУпр, ПостатейныеПостоянныеУпр, ПостатейныеПеременныеУпр,
	|СтоимостьРегл, ПостояннаяРазница, ВременнаяРазница, СтоимостьНДД,
	|СтоимостьЗабалансовая, Трудозатраты, ПостатейныеПостоянныеСНДС, ПостатейныеПостоянныеБезНДС,
	|ПостатейныеПеременныеСНДС, ПостатейныеПеременныеБезНДС,
	|НДСРегл, НДСУпр,
	|СтоимостьЗабалансоваяРегл, ДопРасходыРегл, ТрудозатратыРегл, ПостатейныеПостоянныеРегл, ПостатейныеПеременныеРегл");
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	// Заполнение партионной идентификации в расчетной партии
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, "ВидДвижения, ВидДеятельностиНДС, АналитикаУчетаПоПартнерам, Партия, АналитикаУчетаПартий, 
		|НалогообложениеПартии, АналитикаФинансовогоУчета, ДокументИсточник, НастройкаХозяйственнойОперации, ИдентификаторФинЗаписи");
	
	//++ Устарело_Производство21

	//++ НЕ УТКА
	Если ТипЗнч(Расход.Партия) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
		РасчетнаяПартия.Партия = Расход.Партия;
	КонецЕсли;
	//-- НЕ УТКА

	//-- Устарело_Производство21
	
	// Заполнение показателей расчетной партии.
	РасчетнаяПартия.Количество = 0;	
	Для Каждого ИмяРесурса Из СуммовыеРесурсы Цикл
		
		РасчетнаяПартия[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ];
		
		// Корректировка базы расчета в приходе.
		Приход[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ] - РасчетнаяПартия[ИмяРесурса.Ключ];
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(РасчетнаяПартия.РазделУчета) Тогда
		РасчетнаяПартия.РазделУчета = Приход.РазделУчета;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РасчетнаяПартия.ВидЗапасов) И ЗначениеЗаполнено(Приход.ВидЗапасов) Тогда
		РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
	КонецЕсли;
	
	РасчетнаяПартия.РазделУчета = Расход.КорРазделУчета;
	РасчетнаяПартия.ВидЗапасов = Расход.КорВидЗапасов;
	РасчетнаяПартия.АналитикаУчетаНоменклатуры = Расход.КорАналитикаУчетаНоменклатуры;
	РасчетнаяПартия.Партия = Расход.КорПартия;
	РасчетнаяПартия.АналитикаУчетаПартий = Расход.КорАналитикаУчетаПартий;
	РасчетнаяПартия.АналитикаФинансовогоУчета = Расход.КорАналитикаФинансовогоУчета;
	РасчетнаяПартия.ВидДеятельностиНДС = Расход.КорВидДеятельностиНДС;
	РасчетнаяПартия.КорВидЗапасов = Неопределено;
	РасчетнаяПартия.КорАналитикаУчетаНоменклатуры = Неопределено;
	РасчетнаяПартия.КорАналитикаФинансовогоУчета = Неопределено;
	РасчетнаяПартия.КорРазделУчета = Неопределено;
	РасчетнаяПартия.ДокументДвижения = Приход.ДокументДвижения;
	РасчетнаяПартия.ГруппаПродукции = Приход.ГруппаПродукции;
	РасчетнаяПартия.Подразделение = Приход.Подразделение;
	РасчетнаяПартия.СтатьяКалькуляции = Приход.СтатьяКалькуляции;
	РасчетнаяПартия.ИдентификаторСтроки = Приход.ИдентификаторСтроки;
	РасчетнаяПартия.КодСтроки = Приход.КодСтроки;
	
	Если РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах 
	 И РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
	 И ПараметрыРасчета.УчетныеПолитики.ФИФОСкользящая.ИспользуютВТекущемПериоде.Найти(РасчетнаяПартия.Организация) = Неопределено Тогда
		
		РасчетнаяПартия.Партия = Неопределено;
		РасчетнаяПартия.АналитикаУчетаПартий = Неопределено;
		
	КонецЕсли;
	
	РасчетнаяПартия.РасчетЗавершен = Приход.РасчетЗавершен;
		
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапов_Контекстные

// Используется для всех вызовов заполнения расчетной партии.
//
Процедура ЗаполнитьРасчетнуюПартию(ПараметрыРасчета, Контекст, РасчетнаяПартия, Расход, Приход, ПартияЗаполнена) Экспорт
	
	Если Контекст = "РаспределениеТрудозатрат" Тогда
		// Этап 1
		РасчетСебестоимостиЛокализация.ЗаполнитьРасчетнуюПартиюРаспределениеТрудозатрат(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "РаспределениеТрудозатратНаВыпуск" Тогда
		// Этап 3
		ЗаполнитьРасчетнуюПартиюРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);		
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "РаспределениеТрудозатратНаВыпускПроизводство21" Тогда
		// Этап 3
		РасчетСебестоимостиЛокализация.ЗаполнитьРасчетнуюПартиюРаспределениеТрудозатратНаВыпуск(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);		
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "ПереносСтоимостиТрудозатрат" Тогда
		// Этап 9
		ЗаполнитьРасчетнуюПартиюСтоимостьТрудозатрат(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Тестирование


// Дополняет список этапов расчета.
// 
// Параметры:
// 	СписокЭтапов - СписокЗначений - 
//
Процедура ДополнитьЭтапыСРаспределениемПартий(СписокЭтапов) Экспорт
	
	СписокЭтапов.Добавить("РаспределениеТрудозатрат");
	СписокЭтапов.Добавить("РаспределениеТрудозатратНаВыпуск");
	СписокЭтапов.Добавить("РаспределениеТрудозатратНаВыпускПроизводство21");
	СписокЭтапов.Добавить("ПереносСтоимостиТрудозатрат");
	
КонецПроцедуры

Процедура ТекстЗапросаДляРасчетаЭтапа(ИмяЭтапа, ПараметрыРасчета, ТекстЗапроса) Экспорт
	
	Если ИмяЭтапа = "РаспределениеТрудозатратНаВыпуск" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск(ПараметрыРасчета);
		
	ИначеЕсли ИмяЭтапа = "ПереносСтоимостиТрудозатрат" Тогда
		ТекстЗапроса = ТекстЗапросаДляСтоимостиТрудозатрат(ПараметрыРасчета);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
