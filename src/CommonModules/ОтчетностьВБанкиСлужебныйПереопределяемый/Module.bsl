////////////////////////////////////////////////////////////////////////////////
// ОтчетностьВБанкиСлужебныйПереопределяемый: переопределяемые механизмы
// формирования и отправки финансовой отчетности в банки.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Находит счет по коду в плане счетов, в котором ведется бухгалтерский учет,
// и помещает ссылку в переданную переменную.
//
// Параметры:
//   КодСчета - Строка - код счета, который нужно найти в плане счетов;
//   СчетУчета - Произвольный - переменная, в которую будет помещен результат поиска.
//
// Пример реализации:
//   СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодСчета);
//
Процедура УстановитьСчетУчетаПоКоду(КодСчета, СчетУчета) Экспорт
	
	СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодСчета);
	
КонецПроцедуры

// Добавляет ссылки на счета учета и их названия в переданную таблицу.
//
// Параметры:
//   ИмяФормы - Строка - имя формы отчета БухгалтерскаяОтчетностьВБанк, для которой заполняется список счетов.
//   ВидОтчета - Строка - имя отчета, для которого надо заполнить список счетов.
//   ТаблицаСчетов - ТаблицаЗначений - таблица с колонками, в которую будут помещены счета для отчетов:
//     * Включен - Булево - признак включения счета в отчет;
//     * Счет - ПланСчетовСсылка - ссылка на счет плана счетов, в котором ведется бухгалтерский учет;
//     * Наименование - Строка - наименование счета плана счетов, в котором ведется бухгалтерский учет.
//
// Пример реализации:
//   ЗаполнениеОтчетностиВБанки.ДобавитьСчетаЗаполнения(ИмяФормы, ВидОтчета, ТаблицаСчетов);
//
Процедура ЗаполнитьТаблицуСчетовЗаполнения(ИмяФормы, ВидОтчета, ТаблицаСчетов) Экспорт
	
	ЗаполнениеОтчетностиВБанки.ДобавитьСчетаЗаполнения(ИмяФормы, ВидОтчета, ТаблицаСчетов);
	
КонецПроцедуры

// Добавляет банки и их БИК в переданную таблицу.
// 
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой нужно получить таблицу с банками.
//   ТаблицаБанков - ТаблицаЗначений - таблица с колонками:
//     * БИК  - Строка - БИК банка.
//     * Банк - ОпределяемыйТип.СправочникБанкиБРО - банк.
//
// Пример реализации:
//   Запрос = Новый Запрос;
//   Запрос.УстановитьПараметр("Организация", Организация);
//   Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
//                  |	БанковскиеСчетаОрганизаций.Банк.Код КАК БИК,
//                  |	БанковскиеСчетаОрганизаций.Банк КАК Банк
//                  |ИЗ
//                  |	Справочник.БанковскиеСчета КАК БанковскиеСчетаОрганизаций
//                  |ГДЕ
//                  |	БанковскиеСчетаОрганизаций.Владелец = &Организация
//                  |	И НЕ БанковскиеСчетаОрганизаций.ПометкаУдаления
//                  |
//                  |СГРУППИРОВАТЬ ПО
//                  |	БанковскиеСчетаОрганизаций.Банк,
//                  |	БанковскиеСчетаОрганизаций.Банк.Код";
//   
//   ТаблицаБанков = Запрос.Выполнить().Выгрузить();
//
Процедура ЗаполнитьТаблицуБанковРасчетныхСчетовОрганизации(Организация, ТаблицаБанков) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА БанковскиеСчетаОрганизаций.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчетаОрганизаций.БИКБанка
	|	ИНАЧЕ ЕСТЬNULL(БанковскиеСчетаОрганизаций.Банк.Код,"""") КОНЕЦ КАК БИК,
	|	БанковскиеСчетаОрганизаций.Банк КАК Банк
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|ГДЕ
	|	БанковскиеСчетаОрганизаций.Владелец = &Организация
	|	И НЕ БанковскиеСчетаОрганизаций.ПометкаУдаления";
	
	ТаблицаБанков = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Заполняет дерево счетов бухгалтерского учета.
//
// Параметры:
//   ДеревоСчетовБУ - ДеревоЗначений - дерево значений, в которое будут помещены счета.
//	 ВключатьЗабалансовые - Булево - Если установлен в значение Истина, то выгружаются в том числе и забалансовые счета.
//
// Пример реализации:
//   Запрос = Новый Запрос;
//   Запрос.Текст =
//       "ВЫБРАТЬ
//       |	Хозрасчетный.Ссылка КАК Ссылка,
//       |	Хозрасчетный.Код КАК Код,
//       |	Хозрасчетный.Наименование КАК Наименование,
//       |	Хозрасчетный.Забалансовый КАК Забалансовый,
//       |	ВЫБОР
//       |		КОГДА Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
//       |			ТОГДА ""А""
//       |		ИНАЧЕ ВЫБОР
//       |				КОГДА Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
//       |					ТОГДА ""П""
//       |				ИНАЧЕ ""АП""
//       |			КОНЕЦ
//       |	КОНЕЦ КАК Вид
//       |ИЗ
//       |	ПланСчетов.Хозрасчетный КАК Хозрасчетный
//       |ГДЕ
//       |	НЕ Хозрасчетный.Забалансовый
//       |
//       |УПОРЯДОЧИТЬ ПО
//       |	Код
//       |ИТОГИ ПО
//       |	Ссылка ИЕРАРХИЯ";
//   
//   ДеревоСчетовБУ = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
//
Процедура ЗаполнитьДеревоСчетовБУ(ДеревоСчетовБУ, ВключатьЗабалансовые = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка,
	|	Хозрасчетный.Код КАК Код,
	|	Хозрасчетный.Наименование КАК Наименование,
	|	Хозрасчетный.Забалансовый КАК Забалансовый,
	|	ВЫБОР
	|		КОГДА Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|			ТОГДА ""А""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Хозрасчетный.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|					ТОГДА ""П""
	|				ИНАЧЕ ""АП""
	|			КОНЕЦ
	|	КОНЕЦ КАК Вид
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	НЕ Хозрасчетный.Забалансовый
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ";
	
	Если ВключатьЗабалансовые Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ Хозрасчетный.Забалансовый", "ИСТИНА");
	КонецЕсли;
	
	ДеревоСчетовБУ = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
КонецПроцедуры

// Устанавливает банк по умолчанию в переданной переменой.
//
// Параметры:
//   БанкОрганизации - Произвольный - переменная, которой будет присвоена ссылка на банк.
//
// Пример реализации:
//   БанкОрганизации = Справочники.Банки.НайтиПоКоду("044525225");
//
Процедура ЗаполнитьБанкПоУмолчанию(БанкОрганизации) Экспорт
	
	// Найдем банк или создадим из классификатора.
	ЗаписьОБанке = "";	
	РаботаСБанками.ПолучитьДанныеКлассификатора("044525225", , ЗаписьОБанке);
	Если ЗначениеЗаполнено(ЗаписьОБанке) Тогда
		БанкОрганизации = ЗаписьОБанке;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает перечень разделов, заполняемых по сведениям ИБ.
//
// Параметры:
//   ИмяФормыОтчета - Строка - имя формы отчета БухгалтерскаяОтчетностьВБанк, для которой заполняется перечень разделов.
//   ЗаполняемыеРазделы - Массив из Строка - массив, в который будут добавлены идентификаторы заполняемых разделов.
//
// Пример реализации:
//   Если ИмяФормыОтчета = "ФормаОтчета2017Кв3" Тогда
//     ЗаполняемыеРазделы.Добавить("Баланс");
//     ЗаполняемыеРазделы.Добавить("ОФР");
//     ЗаполняемыеРазделы.Добавить("ОИК");
//     ЗаполняемыеРазделы.Добавить("ОДДС");
//     ЗаполняемыеРазделы.Добавить("ДополнительныеПоказатели");
//     ЗаполняемыеРазделы.Добавить("РасшифровкиОбеспеченийПолученные");
//     ЗаполняемыеРазделы.Добавить("РасшифровкиОбеспеченийВыданные");
//     ЗаполняемыеРазделы.Добавить("ЗадолженностьПоКредитамЗаймамКраткосрочная");
//     ЗаполняемыеРазделы.Добавить("ЗадолженностьПоКредитамЗаймамДолгосрочная");
//     ЗаполняемыеРазделы.Добавить("ДенежныеСредства50");
//     ЗаполняемыеРазделы.Добавить("ДенежныеСредства51");
//     ЗаполняемыеРазделы.Добавить("ДенежныеСредства52");
//     ЗаполняемыеРазделы.Добавить("ДенежныеСредства55");
//     ЗаполняемыеРазделы.Добавить("ОСВ");
//     ЗаполняемыеРазделы.Добавить("ОСВПоСчету");
//     ЗаполняемыеРазделы.Добавить("АнализСчета");
//     ЗаполняемыеРазделы.Добавить("Операции51");
//     ЗаполняемыеРазделы.Добавить("Операции52");
//     ЗаполняемыеРазделы.Добавить("Операции55");
//     ЗаполняемыеРазделы.Добавить("Дебиторская");
//     ЗаполняемыеРазделы.Добавить("Кредиторская");
//   КонецЕсли;
//
Процедура ЗаполнитьПереченьЗаполняемыхРазделов(ИмяФормыОтчета, ЗаполняемыеРазделы) Экспорт
	
	Если ИмяФормыОтчета = "ФормаОтчета2017Кв3" Тогда
		ЗаполняемыеРазделы.Добавить("Баланс");
		ЗаполняемыеРазделы.Добавить("ОФР");
		ЗаполняемыеРазделы.Добавить("ОИК");
		ЗаполняемыеРазделы.Добавить("ОДДС");
		ЗаполняемыеРазделы.Добавить("ДополнительныеПоказатели");
		ЗаполняемыеРазделы.Добавить("РасшифровкиОбеспеченийПолученные");
		ЗаполняемыеРазделы.Добавить("РасшифровкиОбеспеченийВыданные");
		ЗаполняемыеРазделы.Добавить("ЗадолженностьПоКредитамЗаймамКраткосрочная");
		ЗаполняемыеРазделы.Добавить("ЗадолженностьПоКредитамЗаймамДолгосрочная");
		ЗаполняемыеРазделы.Добавить("ЛизингОбязательства");
		ЗаполняемыеРазделы.Добавить("ДенежныеСредства50");
		ЗаполняемыеРазделы.Добавить("ДенежныеСредства51");
		ЗаполняемыеРазделы.Добавить("ДенежныеСредства52");
		ЗаполняемыеРазделы.Добавить("ДенежныеСредства55");
		ЗаполняемыеРазделы.Добавить("ОСВ");
		ЗаполняемыеРазделы.Добавить("ОСВПоСчету");
		ЗаполняемыеРазделы.Добавить("АнализСчета");
		ЗаполняемыеРазделы.Добавить("Операции51");
		ЗаполняемыеРазделы.Добавить("Операции52");
		ЗаполняемыеРазделы.Добавить("Операции55");
		ЗаполняемыеРазделы.Добавить("Дебиторская");
		ЗаполняемыеРазделы.Добавить("Кредиторская");
		
		ЗаполняемыеРазделы.Добавить("ДоходыРасходы");
		ЗаполняемыеРазделы.Добавить("ФинансовыеВложенияКраткосрочные");
		ЗаполняемыеРазделы.Добавить("ФинансовыеВложенияДолгосрочные");
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает описание типов сущности с указанным наименованием.
//
// Параметры:
//   НазваниеСущности - Строка - название сущности, для которой нужно заполнить типы.
//   ОписаниеТиповСущности - ОписаниеТипов - описание типов, в который будут добавлены типы сущности.
//
// Пример реализации:
//   Если НазваниеСущности = "БанковскиеСчета" Тогда
//     ОписаниеТиповСущности = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета");
//   КонецЕсли;
//
Процедура ЗаполнитьТипыСущности(НазваниеСущности, ОписаниеТиповСущности) Экспорт
	
	Если НазваниеСущности = "БанковскиеСчета" Тогда
		ОписаниеТиповСущности = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчетаОрганизаций,СправочникСсылка.БанковскиеСчетаКонтрагентов");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет сводные и детальные сведения о банковских операциях по организации за заданный период.
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - организация, по которой заполняются сведения.
//     * НачалоПериода - Дата - начало периода, за который заполняются сведения.
//     * КонецПериода - Дата - конец периода, за который заполняются сведения.
//   СведенияОбОперациях - Структура - контейнер для размещения сведений об операциях по банковским счетам:
//     * СчетУчета - Произвольный - счет бухгалтерского учета, по которому были получены сведения.
//     * СводныеСведения - Произвольный - сводные сведения по банковским операциям.
//     * ДетальныеСведения - Произвольный - детальные сведения по банковским операциям.
//
// Пример реализации:
//   ЗаполнениеОтчетностиВБанки.ЗаполнитьСведенияОбОперацияхПоБанковскимСчетам(Параметры, СведенияОбОперациях);
//
Процедура ЗаполнитьСведенияОбОперацияхПоБанковскимСчетам(Параметры, СведенияОбОперациях) Экспорт
	
	ЗаполнениеОтчетностиВБанки.ЗаполнитьСведенияОбОперацияхПоБанковскимСчетам(Параметры, СведенияОбОперациях);
	
КонецПроцедуры

// Заполняет переданную переменную сведениями для выгрузки ОСВ.
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - организация, по которой заполняются сведения.
//     * НачалоПериода - Дата - начало периода, за который заполняются сведения.
//     * КонецПериода - Дата - конец периода, за который заполняются сведения.
//   СведенияОСВ - ДеревоЗначений - контейнер для размещения выборки из запроса.
//
// Пример реализации:
//   СведенияОСВ = ЗаполнениеОтчетностиВБанки.СведенияОСВЗаПериод_2017Кв3(Параметры);
//
Процедура ЗаполнитьСведенияОСВ(Параметры, СведенияОСВ) Экспорт
	
	СведенияОСВ = ЗаполнениеОтчетностиВБанки.СведенияОСВЗаПериод_2017Кв3(Параметры);
	
КонецПроцедуры

// Заполняет переданную переменную результатом выполнения запроса для выгрузки ОСВ по счету.
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - организация, по которой заполняются сведения.
//     * НачалоПериода - Дата - начало периода, за который заполняются сведения.
//     * КонецПериода - Дата - конец периода, за который заполняются сведения.
//     * Счет - ПланСчетовСсылка - счет, по которому выполняется запрос.
//   СведенияОСВПоСчету - ВыборкаИзРезультатаЗапроса - контейнер для размещения выборки из запроса.
//
// Пример реализации:
//   СведенияОСВПоСчету = ЗаполнениеОтчетностиВБанки.СведенияОСВПоСчету(Параметры);
//
Процедура ЗаполнитьСведенияОСВПоСчету(Параметры, СведенияОСВПоСчету) Экспорт
	
	СведенияОСВПоСчету = ЗаполнениеОтчетностиВБанки.СведенияОСВПоСчету(Параметры);
	
КонецПроцедуры

// Заполняет переданную переменную результатом выполнения запроса для выгрузки анализа счета.
//
// Параметры:
//   Параметры - Структура:
//     * Организация - СправочникСсылка.Организации - организация, по которой заполняются сведения.
//     * НачалоПериода - Дата - начало периода, за который заполняются сведения.
//     * КонецПериода - Дата - конец периода, за который заполняются сведения.
//     * Счет - ПланСчетовСсылка - счет, по которому выполняется запрос.
//   СведенияАнализаСчета - ВыборкаИзРезультатаЗапроса - контейнер для размещения выборки из запроса.
//
// Пример реализации:
//   СведенияАнализаСчета = ЗаполнениеОтчетностиВБанки.СведенияАнализаЗаПериод_2017Кв3(Параметры);
//
Процедура ЗаполнитьСведенияАнализаСчета(Параметры, СведенияАнализаСчета) Экспорт
	
	СведенияАнализаСчета = ЗаполнениеОтчетностиВБанки.СведенияАнализаЗаПериод_2017Кв3(Параметры);

КонецПроцедуры

// Устанавливает счета, по которым производится автоматическая отправка ОСВ по счетам в банки.
//
// Параметры:
//   СчетаОСВПоСчетам - Произвольный - переменная, в которую будут помещен массив ссылок на счета.
//
// Пример реализации:
//   СчетаОСВПоСчетам = ЗаполнениеОтчетностиВБанки.СчетаОСВПоСчетам();
//
Процедура ЗаполнитьСчетаОСВПоСчетам(СчетаОСВПоСчетам) Экспорт
	
	СчетаОСВПоСчетам = ЗаполнениеОтчетностиВБанки.СчетаОСВПоСчетам();
	
КонецПроцедуры

// Устанавливает счета, по которым производится автоматическая отправка анализов счетов в банки.
//
// Параметры:
//   СчетаАнализовСчетов - Произвольный - переменная, в которую будут помещен массив ссылок на счета.
//
// Пример реализации:
//   СчетаАнализовСчетов = ЗаполнениеОтчетностиВБанки.СчетаАнализовСчетов();
//
Процедура ЗаполнитьСчетаАнализовСчетов(СчетаАнализовСчетов) Экспорт
	
	СчетаАнализовСчетов = ЗаполнениеОтчетностиВБанки.СчетаАнализовСчетов();

КонецПроцедуры

// Возвращает все счета, где присутствует субконто "Банковские счета".
//
// Параметры:
//	 СписокСчетов - Массив из ПланСчетовСсылка.Хозрасчетный - массив счетов.
//
// Пример реализации:
//   СписокСчетов = УчетДенежныхСредствПовтИсп.ПолучитьСчетаССубконтоБанковскиеСчета();
//
Процедура ПолучитьСчетаССубконтоБанковскиеСчета(СписокСчетов) Экспорт
	
	ДоступныеСчетаУчетаДенежныхСредств = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаДенежныхСредств();
	СписокСчетов = Новый ФиксированныйМассив(ДоступныеСчетаУчетаДенежныхСредств.СчетаБезналичныхДенежныхСредств);

КонецПроцедуры

// Возвращает префикс для сохраненного отчета "Карточка счета".
//
// Параметры:
//   Префикс - Строка.
//   Счет - ПланСчетовСсылка.Хозрасчетный - счет, по которому формируется отчет.
//   НачалоПериода - Дата - дата начала формирования отчета.
//   КонецПериода - Дата - дата окончания формирования отчета.
//
// Пример реализации:
//   Префикс = ЗаполнениеРасшифровокБухОтчетности.ПрефиксКарточкиСчета(Счет, НачалоПериода, КонецПериода);
//
Процедура ПрефиксКарточкиСчета(Префикс, Счет, НачалоПериода, КонецПериода) Экспорт
	
КонецПроцедуры

// Заполняет параметры счета, используемого при отправке.
//
// Параметры:
//   ОтправляемыйСчет - Строка - код головного счета.
//   ПараметрыСчета - ТаблицаЗначений:
//     * Счета - Массив из ПланСчетовСсылка.Хозрасчетный - счет или его субсчета,
//               использующие комбинацию субконто, указаннюу в колонке "Субконто";
//     * Субконто - Массив из ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные - комбинация субконто
//                  счета или его субсчетах;
//     * Количественный - Булево - признак отражения количества на счете или его субсчетах.
//
// Пример реализации:
//   ЗаполнениеОтчетностиВБанкиПереопределяемый.УстановитьПараметрыСчета(ОтправляемыйСчет, ПараметрыСчета);
//   ЗаполнениеОтчетностиВБанки.ЗаполнитьПараметрыПоСубсчетам(ОтправляемыйСчет, ПараметрыСчета);
//
Процедура УстановитьПараметрыСчета(ОтправляемыйСчет, ПараметрыСчета) Экспорт
	ЗаполнениеОтчетностиВБанкиПереопределяемый.УстановитьПараметрыСчета(ОтправляемыйСчет, ПараметрыСчета);
	ЗаполнениеОтчетностиВБанки.ЗаполнитьПараметрыПоСубсчетам(ОтправляемыйСчет, ПараметрыСчета);
КонецПроцедуры

#КонецОбласти
