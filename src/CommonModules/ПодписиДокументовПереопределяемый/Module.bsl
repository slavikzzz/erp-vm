#Область ПрограммныйИнтерфейс

// В данной процедуре необходимо добавить описания ролей подписантов конфигурации.
//
// Параметры:
//   РолиПодписантов - Массив - Элементы создаются функцией ПодписиДокументов.ИменаРеквизитовРолиПодписанта().
//
Процедура ПриОпределенииРолейПодписантов(РолиПодписантов) Экспорт
	
КонецПроцедуры

// Формирует структуру значений подписей документов по переданному описанию имен реквизитов.
//
// Параметры:
//   ОписаниеПодписей - Соответствие - коллекция, описывающая состав ответственных и их размещение в реквизитах
//       * Ключ - имена ролей подписантов
//       * Значение - Структура - содержит 3 поля - "ФизическоеЛицо", "ОписаниеПолномочий", "Должность",
//                    в которых содержатся имена переменных для возвращаемых значений реквизитов подписантов.
//   Организация - СправочникСсылка.Организации - организация, по которой будут получаться значения.
//   ЗначенияПодписей - Структура - содержит имена (ключи) и значения затребованных реквизитов.
//   СтандартнаяОбработка - Булево - флаг необходимости дальнейшего заполнения значений подписей документов.
//
// Пример:
//
//	Процедура ЗаполнитьСведенияОПодписяхДокументов(ОписаниеПодписей, Организация, ЗначенияПодписей, СтандартнаяОбработка) Экспорт
//
//		ОтветственныеОрганизации = ОтветственныеЛица.ОтветственныеЛицаОрганизации(Организация, ТекущаяДатаСеанса());
//
//		ОписаниеРуководителя = ОписаниеПодписей["Руководитель"];
//		Если ОписаниеРуководителя <> Неопределено Тогда
//			ЗначенияПодписей.Вставить(ОписаниеРуководителя["ФизическоеЛицо"], ОтветственныеОрганизации.Руководитель);
//			ЗначенияПодписей.Вставить(ОписаниеРуководителя["Должность"], ОтветственныеОрганизации.ДолжностьРуководителя);
//		КонецЕсли;
//
//		СтандартнаяОбработка = Ложь;
//
//	КонецПроцедуры
//
Процедура ЗаполнитьСведенияОПодписяхДокументов(ОписаниеПодписей, Организация, ЗначенияПодписей, СтандартнаяОбработка, ДатаСведений = Неопределено) Экспорт
	
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, ДатаСведений);
	
	ПодписьРуководителя = ОписаниеПодписей["Руководитель"];
	Если ПодписьРуководителя <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ПодписьРуководителя["ФизическоеЛицо"], ОтветственныеЛица.Руководитель);
		ЗначенияПодписей.Вставить(ПодписьРуководителя["Должность"],      ОтветственныеЛица.РуководительДолжностьСсылка);
	КонецЕсли;
	
	ПодписьГлавногоБухгалтера = ОписаниеПодписей["ГлавныйБухгалтер"];
	Если ПодписьГлавногоБухгалтера <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ПодписьГлавногоБухгалтера["ФизическоеЛицо"], ОтветственныеЛица.ГлавныйБухгалтер);
		ЗначенияПодписей.Вставить(ПодписьГлавногоБухгалтера["Должность"],      ОтветственныеЛица.ГлавныйБухгалтерДолжностьСсылка);
	КонецЕсли;
	
	ПодписьКадровика = ОписаниеПодписей["РаботникКадровойСлужбы"];
	Если ПодписьКадровика <> Неопределено Тогда
		ЗначенияПодписей.Вставить(ПодписьКадровика["ФизическоеЛицо"], ОтветственныеЛица.РуководительКадровойСлужбы);
		ЗначенияПодписей.Вставить(ПодписьКадровика["Должность"],      ОтветственныеЛица.РуководительКадровойСлужбыДолжностьСсылка);
	КонецЕсли;
	
	ПодписьПредставителяСФР = ОписаниеПодписей["ПредставительСФР"];
	Если ПодписьПредставителяСФР <> Неопределено Тогда
		Если ЗначениеЗаполнено(ОтветственныеЛица.ГлавныйБухгалтер) Тогда
			ЗначенияПодписей.Вставить(ПодписьПредставителяСФР["ФизическоеЛицо"], ОтветственныеЛица.ГлавныйБухгалтер);
			ЗначенияПодписей.Вставить(ПодписьПредставителяСФР["Должность"],      ОтветственныеЛица.ГлавныйБухгалтерДолжностьСсылка);
		Иначе
			ЗначенияПодписей.Вставить(ПодписьПредставителяСФР["ФизическоеЛицо"], ОтветственныеЛица.Руководитель);
			ЗначенияПодписей.Вставить(ПодписьПредставителяСФР["Должность"],      ОтветственныеЛица.РуководительДолжностьСсылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Дополняет коллекцию, описывающую состав ответственных и их размещение в реквизитах.
//
// Параметры:
//   ОбрабатываемыеИдентификаторы - Массив Из Строка - заказанные идентификаторы.
//   ЗаполненныеИдентификаторы - Соответствие - коллекция, описывающая состав ответственных и их размещение в реквизитах
//       * Ключ - Строка - идентификатор ответственного,
//       * Значение - Структура - см. ПодписиДокументов.ОписаниеРеквизитовПодписанта()
//
// Пример:
//
//	Процедура ЗаполнитьСоответствиеИдентификаторамиОтветственныхЛицОрганизаций(ОбрабатываемыеИдентификаторы, ЗаполненныеИдентификаторы) Экспорт
//		
//		ДоступныеИдентификаторы = Новый Массив;
//		ДоступныеИдентификаторы.Добавить("Руководитель");
//		ДоступныеИдентификаторы.Добавить("ГлавныйБухгалтер");
//		ДоступныеИдентификаторы.Добавить("РаботникКадровойСлужбы");
//		ДоступныеИдентификаторы.Добавить("УполномоченныйПоПрямымВыплатамФСС");
//		
//		Для Каждого ДоступныйИдентификатор Из ДоступныеИдентификаторы Цикл
//			Если ОбрабатываемыеИдентификаторы.Найти(ДоступныйИдентификатор) = Неопределено Тогда // идентификатор не заказан.
//				Продолжить;
//			КонецЕсли;
//			
//			Если ЗаполненныеИдентификаторы.Получить(ДоступныйИдентификатор) <> Неопределено Тогда // идентификатор уже заполнен.
//				Продолжить;
//			КонецЕсли;
//			
//			СтруктураИменРеквизитов = ПодписиДокументов.ОписаниеРеквизитовПодписанта();
//			СтруктураИменРеквизитов.ФизическоеЛицо = ДоступныйИдентификатор;
//			СтруктураИменРеквизитов.Должность = ДоступныйИдентификатор + "Должность";
//			
//			ЗаполненныеИдентификаторы.Вставить(ДоступныйИдентификатор, СтруктураИменРеквизитов);
//		КонецЦикла;
//
//	КонецПроцедуры
//
Процедура ЗаполнитьСоответствиеИдентификаторамиОтветственныхЛицОрганизаций(ОбрабатываемыеИдентификаторы, ЗаполненныеИдентификаторы) Экспорт
	
	ДоступныеИдентификаторы = Новый Массив;
	ДоступныеИдентификаторы.Добавить("Руководитель");
	ДоступныеИдентификаторы.Добавить("ГлавныйБухгалтер");
	ДоступныеИдентификаторы.Добавить("РаботникКадровойСлужбы");
	ДоступныеИдентификаторы.Добавить("УполномоченныйПоПрямымВыплатамФСС");
	
	Для Каждого ДоступныйИдентификатор Из ДоступныеИдентификаторы Цикл
		Если ОбрабатываемыеИдентификаторы.Найти(ДоступныйИдентификатор) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗаполненныеИдентификаторы.Получить(ДоступныйИдентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураИменРеквизитов = ПодписиДокументов.ОписаниеРеквизитовПодписанта();
		СтруктураИменРеквизитов.ФизическоеЛицо = ДоступныйИдентификатор;
		СтруктураИменРеквизитов.Должность = ДоступныйИдентификатор + "Должность";
		
		ЗаполненныеИдентификаторы.Вставить(ДоступныйИдентификатор, СтруктураИменРеквизитов);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти