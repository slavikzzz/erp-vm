
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Параметры:
//  ОписаниеФайлов - Массив (Адрес, Имя)
//
Процедура ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи(ВыполняемоеОповещение, ОписаниеФайлов, МенеджерКриптографии = Неопределено) Экспорт
	
	Если МенеджерКриптографии = Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
		ДополнительныеПараметры.Вставить("ОписаниеФайлов", ОписаниеФайлов);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияКонтекста", 
			ЭтотОбъект,
			ДополнительныеПараметры);
			
		ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
		
	Иначе
		
		ОпределитьНаличиеВстроеннойПодписиФайлов(ВыполняемоеОповещение, ОписаниеФайлов, МенеджерКриптографии);
		
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  ОписаниеФайлов - Массив (Адрес, Имя)
//
Процедура ОпределитьНаличиеВстроеннойПодписиФайлов(ВыполняемоеОповещение, ОписаниеФайлов, МенеджерКриптографии) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
	ДополнительныеПараметры.Вставить("ОписаниеФайлов", ОписаниеФайлов);
	ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	ДополнительныеПараметры.Вставить("Номер", 0);
	ДополнительныеПараметры.Вставить("Выполнено", Ложь);
	
	ОпределитьНаличиеВстроеннойПодписиФайлов_ПоДопПараметрам(ДополнительныеПараметры);
	
КонецПроцедуры

// Параметры:
//  ОписаниеФайла - Адрес, Имя
//
Процедура ОпределитьНаличиеВстроеннойПодписиФайла(ВыполняемоеОповещение, ОписаниеФайла, МенеджерКриптографии) Экспорт
	
	ДополнительныеПараметры = ДокументооборотСКОКлиентСервер.НеВыполнено();
	ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
	ДополнительныеПараметры.Вставить("ОписаниеФайла", ОписаниеФайла);
	ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	
	ЭтоPDFФайл = ОбработкаФайловPDFКлиентСервер.ЭтоPDFФайл(ОписаниеФайла.Имя); 
	ДополнительныеПараметры.Вставить("ЭтоPDF", ЭтоPDFФайл);
	Если НЕ ЭтоPDFФайл Тогда
		ДополнительныеПараметры.ОписаниеФайла.Вставить("ЕстьВстроеннаяПодпись", Ложь);
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДвДанные = ПолучитьИзВременногоХранилища(ОписаниеФайла.Адрес);
	
	Base64 = Base64Строка(ДвДанные);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОпределитьНаличиеВстроеннойПодписиФайла_ПослеСохраненияФайла", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	ОперацииСФайламиЭДКОКлиент.Base64ВФайл(
		ОписаниеОповещения, 
		Base64,
		".pdf");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОпределитьНаличиеВстроеннойПодписиФайлов

Процедура ОпределитьНаличиеВстроеннойПодписиФайлов_ПоДопПараметрам(ВходящийКонтекст) Экспорт
	
	ОписаниеФайлов = ВходящийКонтекст.ОписаниеФайлов;
	
	Если ВходящийКонтекст.Номер = ОписаниеФайлов.Количество() Тогда
		
		ВходящийКонтекст.Выполнено = Истина;
		
		ВыполнитьОбработкуОповещения(ВходящийКонтекст.ВыполняемоеОповещение, ВходящийКонтекст);
		
	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОпределитьНаличиеВстроеннойПодписиФайлов_ПослеЗавершенияОдного", 
			ЭтотОбъект, 
			ВходящийКонтекст);
		
		ОписаниеФайла = ОписаниеФайлов[ВходящийКонтекст.Номер];
		ОпределитьНаличиеВстроеннойПодписиФайла(
			ОписаниеОповещения, 
			ОписаниеФайла, 
			ВходящийКонтекст.МенеджерКриптографии);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьНаличиеВстроеннойПодписиФайлов_ПослеЗавершенияОдного(Результат, ВходящийКонтекст) Экспорт
	
	ВходящийКонтекст.Номер = ВходящийКонтекст.Номер + 1;
	ОпределитьНаличиеВстроеннойПодписиФайлов_ПоДопПараметрам(ВходящийКонтекст)
	
КонецПроцедуры

#КонецОбласти

#Область ОпределитьНаличиеВстроеннойПодписиФайла

Процедура ОпределитьНаличиеВстроеннойПодписиФайла_ПослеСохраненияФайла(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат.Выполнено Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОпределитьНаличиеВстроеннойПодписиФайла_ПослеПолученияОписанияПодписей", 
			ЭтотОбъект, 
			ВходящийКонтекст); 
		
		PDF = Новый ЧтениеPDF(Результат.ИмяФайла);
		PDF.НачатьПолучениеОписанияПодписей(
			ОписаниеОповещения,
			ВходящийКонтекст.МенеджерКриптографии);
		
	Иначе
		ВыполнитьОбработкуОповещения(ВходящийКонтекст.ВыполняемоеОповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьНаличиеВстроеннойПодписиФайла_ПослеПолученияОписанияПодписей(ОписанияПодписей, ВходящийКонтекст) Экспорт
	
	ВходящийКонтекст.ОписаниеФайла.Вставить("ЕстьВстроеннаяПодпись", ОписанияПодписей.Количество() > 0);
	ВыполнитьОбработкуОповещения(ВходящийКонтекст.ВыполняемоеОповещение, ВходящийКонтекст.ОписаниеФайла);
	
КонецПроцедуры


#КонецОбласти

#Область ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи

Процедура ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияКонтекста(Результат, ВходящийКонтекст) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	ВходящийКонтекст.Вставить("КонтекстЭДОКлиент", КонтекстЭДОКлиент);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияКриптопровайдера", 
		ЭтотОбъект,
		ВходящийКонтекст);
		
	КонтекстЭДОКлиент.ПроверитьНаличиеКриптопровайдера(ОписаниеОповещения, Истина);
	
КонецПроцедуры

Процедура ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияКриптопровайдера(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат.Выполнено Тогда
		
		КонтекстЭДОКлиент = ВходящийКонтекст.КонтекстЭДОКлиент;
		
		Если Результат.CryptoProCSPУстановлен Тогда
			ТипКриптопровайдера = ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.CryptoPro");
		Иначе
			ТипКриптопровайдера = ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.VipNet");
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияМенеджераКриптографии", 
			ЭтотОбъект, 
			ВходящийКонтекст);
		
		КонтекстЭДОКлиент.ПолучитьМенеджерИСертификатКриптографии(
			Неопределено, 
			ОписаниеОповещения, 
			ТипКриптопровайдера);
			
	Иначе
			
		ВыполнитьОбработкуОповещения(ВходящийКонтекст.ВыполняемоеОповещение, Результат);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьМенеджерИОпределитьНаличиеВстроеннойПодписи_ПослеПолученияМенеджераКриптографии(Результат, ВходящийКонтекст) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		ВыполнитьОбработкуОповещения(ВходящийКонтекст.ВыполняемоеОповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Результат.Вставить("МенеджерКриптографии", Результат.МенеджерКриптографии);
	
	ОпределитьНаличиеВстроеннойПодписиФайлов(
		ВходящийКонтекст.ВыполняемоеОповещение, 
		ВходящийКонтекст.ОписаниеФайлов, 
		Результат.МенеджерКриптографии);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
