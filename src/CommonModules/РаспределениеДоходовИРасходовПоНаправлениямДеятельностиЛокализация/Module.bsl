
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	#Область СписаниеКосвенныхРасходовОСНО
	ТекстСписаниеКосвенныхРасходовОСНО = "
	|ВЫБРАТЬ 
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО) КАК Сумма,
	|	СУММА(КосвенныеРасходы.СуммаУпрОСНО) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьУчетЗатратПоНаправлениямДеятельности ТОГДА
	|			КосвенныеРасходы.КорНаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаНУДт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаОСНО) КАК СуммаПРДт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаНУКт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаОСНО) КАК СуммаПРКт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаВРКт,
	|	""Списание косвенных расходов (ОСНО)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (КосвенныеРасходы.СуммаРеглОСНО <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.СуммаУпрОСНО <> 0)
	|
	|СГРУППИРОВАТЬ ПО 
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	КосвенныеРасходы.Организация,
	|	КосвенныеРасходы.СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов,
	|	КосвенныеРасходы.Подразделение,
	|	КосвенныеРасходы.НаправлениеДеятельности,
	|	КосвенныеРасходы.КорНаправлениеДеятельности
	|";
	#КонецОбласти
	
	#Область СписаниеКосвенныхРасходовЕНВД
	ТекстСписаниеКосвенныхРасходовЕНВД = "
	|ВЫБРАТЬ 
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД) КАК Сумма,
	|	СУММА(КосвенныеРасходы.СуммаУпрЕНВД) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьУчетЗатратПоНаправлениямДеятельности ТОГДА
	|			КосвенныеРасходы.КорНаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаНУДт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаЕНВД) КАК СуммаПРДт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаНУКт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаЕНВД) КАК СуммаПРКт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаВРКт,
	|	""Списание косвенных расходов (ЕНВД)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (КосвенныеРасходы.СуммаРеглЕНВД <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.СуммаУпрЕНВД <> 0)
	|
	|СГРУППИРОВАТЬ ПО 
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	КосвенныеРасходы.Организация,
	|	КосвенныеРасходы.СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов,
	|	КосвенныеРасходы.Подразделение,
	|	КосвенныеРасходы.НаправлениеДеятельности,
	|	КосвенныеРасходы.КорНаправлениеДеятельности
	|";
	#КонецОбласти

	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстСписаниеКосвенныхРасходовОСНО);
	ТекстыОтражения.Добавить(ТекстСписаниеКосвенныхРасходовЕНВД);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	Возврат РеглУчетВыборкиСерверПовтИсп.ТекстКосвенныеРасходы() + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

// Вызывается из Документы.РаспределениеДоходовПоНаправлениямДеятельности.СоздатьДокументыЗаПериод()
// Перед генерацией документов.
// 
// Параметры:
//	Период - Дата - Начало месяца, за который необходимо создать документы.
//	Организация - Массив, Неопределено - Список организаций по которым формируются документы. Если список пустой,
//												то документы формируются по всем организациям.
// 	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц содержит таблицу ВТОрганизации 
// 	                                            с разрезами для создания документов.
//
Процедура ПередСозданиемДокументовЗаПериод(Период, Организация, МенеджерВременныхТаблиц) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Ссылка
	|ИЗ
	|	ВТОрганизации КАК ВТОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|		ПО ВТОрганизации.Организация = РегламентнаяОперация.Организация
	|ГДЕ
	|	РегламентнаяОперация.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ЗакрытиеГода))
	|	И РегламентнаяОперация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РегламентнаяОперация.Проведен
	|СГРУППИРОВАТЬ ПО
	|	РегламентнаяОперация.Ссылка
	|";
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));
	
	ДокументыСписания = Запрос.Выполнить().Выбрать();
	Пока ДокументыСписания.Следующий() Цикл
		ДокументСписания = ДокументыСписания.Ссылка.ПолучитьОбъект();
		ДокументСписания.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

// Изменяет текст формирования втПрочиеРасходы
// 
// Параметры:
// 	ТекстЗапроса - Строка - Текст запроса формирования втПрочиеРасходы
// 	Запрос - Запрос - Запрос проведения документа
//  ТекстыЗапроса - СписокЗначений - Список запросов проведения документа.
//
Процедура ИзменитьТекстЗапросаТаблицыВтПрочиеРасходы(ТекстЗапроса, Запрос, ТекстыЗапроса) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	ИнициализироватьВтДолиСписанияКосвенныхРасходов(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПрочиеРасходы.НомерСтроки КАК НомерСтроки,
	|	ПрочиеРасходы.Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	ПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПрочиеРасходы.СпособРаспределения КАК СпособРаспределения,
	|	ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) КАК Нормируемые,
	|
	|	ПрочиеРасходы.Сумма КАК Сумма,
	|	ПрочиеРасходы.СуммаУпр КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
	|			ТОГДА ПрочиеРасходы.СуммаРегл * ДолиСписания.ДоляСписания
	|		ИНАЧЕ ПрочиеРасходы.СуммаРегл КОНЕЦ) КАК СуммаРегл,
	|	(ВЫБОР
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
	//		Постоянная разница формируется расчетом себестоимости
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
	|			ТОГДА ПрочиеРасходы.ПостояннаяРазница * ДолиСписания.ДоляСписания
	//		Сумма нормируемых расходов по НУ к списанию: БУ - НУ (в части отнесения затрат на 90 счет).
	|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
	|		 И ПрочиеРасходы.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|			ТОГДА ПрочиеРасходы.СуммаРегл - ПрочиеРасходы.ВременнаяРазница                                      // Остаток БУ
	|				- (ПрочиеРасходы.СуммаРегл - ПрочиеРасходы.ПостояннаяРазница - ПрочиеРасходы.ВременнаяРазница)
	|					* ДолиСписания.ДоляСписания  																// Остаток НУ, который мы можем отнести на затраты (90 счет)
	//		Закрытие года или не нормируемые расходы
	|		ИНАЧЕ ПрочиеРасходы.ПостояннаяРазница КОНЕЦ) КАК ПостояннаяРазница,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
	|			ТОГДА ПрочиеРасходы.ВременнаяРазница * ДолиСписания.ДоляСписания
	|		ИНАЧЕ ПрочиеРасходы.ВременнаяРазница КОНЕЦ) КАК ВременнаяРазница
	|
	|ПОМЕСТИТЬ ВтПрочиеРасходы
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности.Расходы КАК ПрочиеРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|		ПО ДолиСписания.Ссылка = ПрочиеРасходы.СтатьяРасходов
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|";
	//-- НЕ УТ

	//-- Локализация
КонецПроцедуры


//++ Локализация

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.РаспределениеДоходовПоНаправлениямДеятельности.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеДоходы", ТекстыЗапроса) Тогда
		Документы.РаспределениеДоходовПоНаправлениямДеятельности.ТекстЗапросаТаблицаВтПрочиеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период                      КАК Период,
	|	ПрочиеДоходы.Организация     КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтПрочиеДоходы КАК ПрочиеДоходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период                      КАК Период,
	|	ПрочиеРасходы.Организация    КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтПрочиеРасходы КАК ПрочиеРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьВтДолиСписанияКосвенныхРасходов(Запрос)

	Если Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ВтДолиСписанияКосвенныхРасходов") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросИнициализации = Новый Запрос;
	ЗапросИнициализации.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	ЗапросИнициализации.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросИнициализации.УстановитьПараметр("НачДата", Запрос.Параметры.НачДата);
	ЗапросИнициализации.УстановитьПараметр("КонДата", Запрос.Параметры.КонДата);
	ЗапросИнициализации.УстановитьПараметр("Организация", Запрос.Параметры.Организация);
	ЗапросИнициализации.УстановитьПараметр("РаспределениеПоВсемОрганизациям", Запрос.Параметры.РаспределениеПоВсемОрганизациям);
	
	РасчетныеДоли = НалоговыйУчет.ДолиСписанияРасходов(Запрос.Параметры.Период, Запрос.Параметры.Организация, Ложь, Истина);
	ЗапросИнициализации.УстановитьПараметр("ДоляСписанияТранспортныхРасходов", РасчетныеДоли.ДоляТранспортныхРасходов);
	ЗапросИнициализации.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	
	ЗапросИнициализации.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходов.Ссылка КАК Ссылка,
	|	(ВЫБОР
	|		КОГДА СтатьиРасходов.ВидРасходов В (&ВидыРасходовНормируемые) ТОГДА ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ)
	|	КАК Нормируемые,
	|	(ВЫБОР
	|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы) ТОГДА ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ)
	|	КАК Транспортные,
	|	(ВЫБОР
	|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
	|			ТОГДА &ДоляСписанияТранспортныхРасходов
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаРекламуНормируемые)
	|		ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаРекламу, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПредставительскиеРасходы)
	|		ТОГДА ЕСТЬNULL(ДолиСписания.ДоляПредставительскихРасходов, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование)
	|		ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеМедицинскоеСтрахование, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников)
	|		ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеЖизни, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности)
	|		ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов)
	|	ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаВозмещениеПроцентовРаботникам, 0)
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ)
	|	КАК ДоляСписания
	|
	|ПОМЕСТИТЬ ВтДолиСписанияКосвенныхРасходов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|		ПО (ДолиСписания.Организация = &Организация ИЛИ &РаспределениеПоВсемОрганизациям)
	|		И ДолиСписания.ПериодРасчета МЕЖДУ &НачДата И &КонДата
	|		И ДолиСписания.Регистратор <> &Ссылка
	|ГДЕ
	|	СтатьиРасходов.ВариантРаспределенияРасходовНУ = 
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	ЗапросИнициализации.Выполнить();
	
КонецПроцедуры

//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти
