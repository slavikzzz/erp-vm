
#Область СлужебныеПроцедурыИФункции

// Возвращает имя события оповещения о выборе документов-оснований ведомости на выплату зарплаты.
//
//	Возвращаемое значение:
//		Строка - имя события
//
Функция ИмяСобытияВыбораДокументовОснованийВедомости(ИдентификаторФормыВладельца) Экспорт
	Возврат СтрШаблон("ВыборДокументовОснованийВедомостейНаВыплатуЗарплаты%1", ИдентификаторФормыВладельца);
КонецФункции

#Область ОбработчикиСобытийФормы

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	ВедомостьНаВыплатуЗарплатыКлиент.ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = ВзаиморасчетыССотрудникамиКлиент.ИмяСобытияИзмененияОплатыВедомости() Тогда
		
		Если Форма.Элементы.ВнешниеОперацииГруппа.Видимость Тогда
			ВедомостьНаВыплатуЗарплатыКлиентСерверРасширенный.УстановитьОтображениеВХО(Форма)		
		КонецЕсли	
		
	ИначеЕсли ИмяСобытия = ИмяСобытияВыбораДокументовОснованийВедомости(Форма.УникальныйИдентификатор) Тогда
		
		Форма.Активизировать();
	
		Форма.Объект.Основания.Очистить();
		Для Каждого Основание Из Параметр Цикл
			Строка = Форма.Объект.Основания.Добавить();
			Строка.Документ = Основание.Значение
		КонецЦикла;	
		
		Если Форма.Объект.Основания.Количество() > 0 Тогда
			ВедомостьНаВыплатуЗарплатыКлиент.Заполнить(Форма);
		Иначе
			ОчиститьСообщения();
			Форма.ОчиститьНаСервере()
		КонецЕсли;	
		
		Форма.Модифицированность = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ДокументыОснованияОбработкаНавигационнойСсылки(Форма, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныеДокументы = Новый СписокЗначений;
	Для Каждого Основание Из Форма.Объект.Основания Цикл
		ВыбранныеДокументы.Добавить(Основание.Документ)
	КонецЦикла;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",          Форма.Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение",        Форма.Объект.Подразделение);
	ПараметрыФормы.Вставить("ПериодВзаиморасчетов", Форма.Объект.ПериодРегистрации);
	ПараметрыФормы.Вставить("ВидДокумента",         Форма.СпособВыплаты.ВидДокументаОснования);
	ПараметрыФормы.Вставить("ПорядокВыплаты",       Форма.СпособВыплаты.ХарактерВыплаты);
	ПараметрыФормы.Вставить("ВыбранныеДокументы",   ВыбранныеДокументы);
	
	ОткрытьФорму("ОбщаяФорма.ВыборДокументовОснованийВедомостейНаВыплатуЗарплаты", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ДатаПриИзменении(Форма) Экспорт
	Форма.ДатаВыплаты = ВедомостьНаВыплатуЗарплатыКлиентСервер.ДатаВыплаты(Форма.Объект);
КонецПроцедуры

Процедура ДатаВыплатыПриИзменении(Форма) Экспорт
	Форма.Объект.ДатаВыплаты = Форма.ДатаВыплаты;
	Форма.ДатаВыплаты = ВедомостьНаВыплатуЗарплатыКлиентСервер.ДатаВыплаты(Форма.Объект);
КонецПроцедуры

// Внешние хозяйственные операции.

Процедура ПеречислениеНДФЛВыполненоПриИзменении(Форма, Элемент) Экспорт
	ВедомостьНаВыплатуЗарплатыКлиентСерверРасширенный.УстановитьОтображениеВХО(Форма, Ложь, Истина);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
