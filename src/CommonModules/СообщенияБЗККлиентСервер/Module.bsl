#Область СлужебныйПрограммныйИнтерфейс

// АПК:299-выкл Использование методов служебного API не контролируется
// АПК:558-выкл Кандидаты в публичный программный интерфейс.
// АПК:559-выкл Кандидаты в публичный программный интерфейс.
// АПК:581-выкл Кандидаты в публичный программный интерфейс.

#Область СозданиеСообщенийПользователю

// Выводит сообщение об ошибке заполнения реквизита формы.
//
// Параметры:
//   Отказ        - Булево - Флажок отказа, который включается в процедуре.
//   Текст        - Строка - Текст сообщения.
//   ИмяРеквизита - Строка - Имя реквизита формы, к которому необходимо привязать сообщение.
//   ПутьКДанным  - Строка - Имя реквизита формы типа "ДанныеФормыСтруктура", в котором расположен указанный реквизит.
//       Например, в форме элемента справочника: ИмяРеквизита = "Наименование", ПутьКДанным = "Объект".
//   Форма - ФормаКлиентскогоПриложения, УникальныйИдентификатор, Структура - Форма,
//       значение свойства "УникальныйИдентификатор" формы, или структура со свойством "УникальныйИдентификатор".
//       Позволяет точно указать форму, к которой должно быть привязано сообщение.
//       Если не указано то сообщение привязывается к форме в активном окне (вкладке).
//
Процедура СообщитьОбОшибкеВФорме(Отказ, Текст, ИмяРеквизита = "", ПутьКДанным = Неопределено, Форма = Неопределено) Экспорт
	Отказ = Истина;
	СообщитьВФорме(Текст, ИмяРеквизита, ПутьКДанным, Форма);
КонецПроцедуры

// Выводит сообщение предупреждающее о проблеме связанной с указанным реквизитом формы.
//
// Параметры:
//   Текст        - Строка - Текст сообщения.
//   ИмяРеквизита - Строка - Полное имя реквизита формы. См. также ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти.
//   ПутьКДанным  - Строка - Имя реквизита формы типа "ДанныеФормыСтруктура", в котором расположен указанный реквизит.
//       Например, в форме элемента справочника: Поле = "Наименование", ПутьКДанным = "Объект".
//   Форма - ФормаКлиентскогоПриложения, УникальныйИдентификатор, Структура - Форма,
//       значение свойства "УникальныйИдентификатор" формы, или структура со свойством "УникальныйИдентификатор".
//       Позволяет точно указать форму, к которой должно быть привязано сообщение.
//       Если не указано то сообщение привязывается к форме в активном окне (вкладке).
//
Процедура СообщитьВФорме(Текст, ИмяРеквизита = "", ПутьКДанным = Неопределено, Форма = Неопределено) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Поле  = ИмяРеквизита;
	Если ПутьКДанным <> Неопределено Тогда
		Сообщение.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
	Если Форма <> Неопределено Тогда
		Сообщение.ИдентификаторНазначения = ?(ТипЗнч(Форма) = Тип("УникальныйИдентификатор"), Форма, Форма.УникальныйИдентификатор);
	КонецЕсли;
	Сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти

// АПК:581-вкл
// АПК:559-вкл
// АПК:558-вкл
// АПК:299-вкл

#КонецОбласти

