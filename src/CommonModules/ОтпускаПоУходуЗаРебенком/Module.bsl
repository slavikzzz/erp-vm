#Область СлужебныйПрограммныйИнтерфейс

// Проверяет значения периодов выплат пособий по уходу за ребенком
//
// Параметры:
//  Объект				 - ДокументОбъект - Проверяемый документ
//  Отказ				 - Булево - устанавливает в истина, если реквизиты заполнены не правильно.
//  ПроверяемыеРеквизиты - Массив - массив проверяемых реквизитов.
//  ДатаНачалаОтпуска	 - Дата - дата начала отпуска по уходу за ребенком.
//  ДатаОкончанияОтпуска - Дата - дата окончания отпуска по уходу за ребенком.
//
Процедура ПроверитьЗаполнениеПериодовВыплатыПособий(Объект, Отказ, ПроверяемыеРеквизиты, Знач ДатаНачалаОтпуска = Неопределено,
	Знач ДатаОкончанияОтпуска = Неопределено) Экспорт
	
	Если ДатаНачалаОтпуска = Неопределено
		И ДатаОкончанияОтпуска = Неопределено Тогда
		
		ДатыОтпуска = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументОснование, "ДатаНачала,ДатаОкончания");
		ДатаНачалаОтпуска = ДатыОтпуска.ДатаНачала;
		ДатаОкончанияОтпуска = ДатыОтпуска.ДатаОкончания;
	КонецЕсли;
	
	Если Объект.ВыплачиватьПособиеДоПолутораЛет Тогда
		Если Не ЗначениеЗаполнено(Объект.ДатаОкончанияПособияДоПолутораЛет) Тогда
			ТекстСообщения = НСтр("ru = 'Не указана дата окончания выплаты пособия за счет ФСС до полутора лет.';
									|en = 'End date of child care allowance for children under one and a half years old paid by SSF is not specified.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоПолутораЛет",, Отказ);
		ИначеЕсли Объект.ДатаОкончанияПособияДоПолутораЛет > ДатаОкончанияОтпуска Тогда
			ТекстСообщения = НСтр("ru = 'Дата окончания выплаты пособия за счет ФСС до полутора лет превышает дату окончания отпуска.';
									|en = 'End date of child care allowance for children under one and a half years old at SSF expense is more than the leave end date.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоПолутораЛет",, Отказ);
		ИначеЕсли Объект.ДатаОкончанияПособияДоПолутораЛет < ДатаНачалаОтпуска Тогда
			ТекстСообщения = НСтр("ru = 'Дата окончания выплаты пособия за счет ФСС до полутора лет не должна быть меньше даты начала отпуска.';
									|en = 'End date of child care allowance for children under one and a half years old at SSF expense cannot be less than the leave start date.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоПолутораЛет",, Отказ);
		ИначеЕсли ДобавитьМесяц(Объект.ДатаОкончанияПособияДоПолутораЛет, 6) < НачалоДня(Объект.Дата) Тогда
			ТекстСообщения = НСтр("ru = 'Обращение за ежемесячным пособием возможно не позднее 6-ти месяцев со дня достижения ребенком возраста полутора лет.';
									|en = 'You can apply for the monthly allowance no later than 6 month after the child turned 18 months.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоПолутораЛет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВыплачиватьПособиеДоТрехЛет Тогда
		Если Не ЗначениеЗаполнено(Объект.ДатаОкончанияПособияДоТрехЛет) Тогда
			ТекстСообщения = НСтр("ru = 'Не указана дата окончания выплаты пособия до трех лет.';
									|en = 'End date of child care allowance for children under three years old is not specified.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоТрехЛет",, Отказ)
		ИначеЕсли Объект.ДатаОкончанияПособияДоТрехЛет > ДатаОкончанияОтпуска Тогда
			ТекстСообщения = НСтр("ru = 'Дата окончания выплаты пособия до трех лет превышает дату окончания отпуска.';
									|en = 'End date of child care allowance for children under three years old is later than the leave end date.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоТрехЛет",, Отказ);
		ИначеЕсли Объект.ДатаОкончанияПособияДоТрехЛет < ДатаНачалаОтпуска Тогда
			ТекстСообщения = НСтр("ru = 'Дата окончания выплаты пособия до трех лет не должна быть меньше даты начала отпуска.';
									|en = 'End date of child care allowance for children under three years old cannot be less than the leave start date.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ДатаОкончанияПособияДоТрехЛет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет на форме вид расчета оплаты пособия отпуска по уходу за ребенком до полутора лет
//
// Параметры:
//  Форма	 - Управляемая форма.
//
Процедура ЗаполнитьВидПособияДоПолутораЛет(Форма) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	ПланыВидовРасчета.Начисления.УстановитьНачислениеПоУмолчаниюВФорме(Форма, "ПособиеДоПолутораЛет");
КонецПроцедуры

// Заполняет на форме вид расчета оплаты пособия отпуска по уходу за ребенком до трех лет
//
// Параметры:
//  Форма	 - Управляемая форма.
//
Процедура ЗаполнитьВидПособияДоТрехЛет(Форма) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПособиеПоУходуЗаРебенкомДоТрехЛет") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ПланыВидовРасчета.Начисления.УстановитьНачислениеПоУмолчаниюВФорме(Форма, "ПособиеДоТрехЛет");
	ОбновитьПоляВводаПоказателейПособияДоТрехЛет(Форма);
	
КонецПроцедуры

// Устанавливает на форме видимость группы пособие до трех лет
//
// Параметры:
//  Форма					 - Управляемая форма.
//  ГруппаПособиеДоТрехЛет	 - Строка - имя группы, хранящей реквизиты оплаты пособий по уходу за ребенком до трех лет
//
Процедура УстановитьВидимостьГруппыПособиеДоТрехЛет(Форма, ГруппаПособиеДоТрехЛет= "ГруппаПособиеДоТрехЛет") Экспорт
	
	ЗарплатаКадрыРасширенный.УстановитьОтображениеГруппыФормы(
		Форма.Элементы, 
		ГруппаПособиеДоТрехЛет,
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьПособиеПоУходуЗаРебенкомДоТрехЛет") Или Форма.Объект.ВыплачиватьПособиеДоТрехЛет);
		
КонецПроцедуры

#Область ОбслуживаниеВводаПоказателейПособияДоТрехЛетВФормеДокументов

Процедура ОбновитьПоляВводаПоказателейПособияДоТрехЛет(Форма, ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	// Если вид пособия не заполнен.
	Если Не ЗначениеЗаполнено(Объект.ПособиеДоТрехЛет) Тогда
		// "Включаем" пустую декорацию, чтобы отображать страницу без элементов.
		Элементы.ГруппаПособиеДоТрехЛетРассчитываетсяДекорация.Видимость = Истина;
		Элементы.ГруппаПоказателиПособияДоТрехЛет.ТекущаяСтраница = Элементы.ГруппаПособиеДоТрехЛетРассчитывается;
		Возврат;
	КонецЕсли;
	
	// Для ввода запрашиваемых показателей пособия до трех лет нужно оформить необходимое количество полей.
	ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Объект.ПособиеДоТрехЛет);
	
	Если ДобавлятьРеквизитыФормы Тогда
		
		ДобавляемыеРеквизиты = Новый Массив;
		ПоказателиПособия = Новый Соответствие;
		
		Для Каждого ОписаниеПоказателя Из ВидРасчетаИнфо.Показатели Цикл
			ПоказателиПособия.Вставить(ОписаниеПоказателя.Показатель, Истина);
			Если Не ОписаниеПоказателя.ЗапрашиватьПриВводе Тогда
				Продолжить;
			КонецЕсли;
			// Показатель запрашивается - создаем для него поле ввода.
			// Проверяем, возможно для такого показателя поле уже существует.
			Если Форма.ПоказателиПособияДоТрехЛет.НайтиСтроки(Новый Структура("Показатель", ОписаниеПоказателя.Показатель)).Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			ИмяРеквизита = "Показатель_" + ЗарплатаКадрыРасширенныйКлиентСервер.УникальноеИмяРеквизита();
			ДобавляемыеРеквизиты.Добавить(
				Новый РеквизитФормы(
					ИмяРеквизита, 
					Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15, ОписаниеПоказателя.Точность)), , 
					ОписаниеПоказателя.КраткоеНаименование, 
					Истина));
			НовыйПоказатель = Форма.ПоказателиПособияДоТрехЛет.Добавить();		
			НовыйПоказатель.Показатель = ОписаниеПоказателя.Показатель;
			НовыйПоказатель.ИмяРеквизита = ИмяРеквизита;
		КонецЦикла;
		
		// Для показателей, которые не встречаются в новой коллекции удаляем поля.
		УдаляемыеРеквизиты = Новый Массив;
		УдаляемыеСтроки = Новый Массив;
		Для Каждого СтрокаПоказателя Из Форма.ПоказателиПособияДоТрехЛет Цикл
			Если ПоказателиПособия[СтрокаПоказателя.Показатель]	= Неопределено Тогда
				УдаляемыеРеквизиты.Добавить(СтрокаПоказателя.ИмяРеквизита);
				УдаляемыеСтроки.Добавить(СтрокаПоказателя);
			КонецЕсли;
		КонецЦикла;
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			Форма.ПоказателиПособияДоТрехЛет.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы, УдаляемыеРеквизиты, ОтложенноеИзменение);
		
	КонецЕсли;
	
	Если ДобавлятьЭлементыФормы Тогда
		
		// Если не рассчитывается, то отображаем заранее подготовленное поле Размер.
		Если Не ВидРасчетаИнфо.Рассчитывается Тогда
			Элементы.ГруппаПоказателиПособияДоТрехЛет.ТекущаяСтраница = Элементы.ГруппаПособиеДоТрехЛетНеРассчитывается;
			Возврат;
		КонецЕсли;
		
		// Если рассчитывается, то для каждого запрашиваемого показателя добавим поле ввода.
		Элементы.ГруппаПоказателиПособияДоТрехЛет.ТекущаяСтраница = Элементы.ГруппаПособиеДоТрехЛетРассчитывается;
		
		// Добавляем элементы формы
		Для Каждого СтрокаПоказателя Из Форма.ПоказателиПособияДоТрехЛет Цикл
			Если Элементы.Найти(СтрокаПоказателя.ИмяРеквизита) <> Неопределено Тогда
				// Поле уже существует
				Продолжить;
			КонецЕсли;
			ПолеВвода = Элементы.Добавить(СтрокаПоказателя.ИмяРеквизита, Тип("ПолеФормы"), Элементы.ГруппаПособиеДоТрехЛетРассчитывается);
			ПолеВвода.ПутьКДанным = СтрокаПоказателя.ИмяРеквизита;
			ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
			ПолеВвода.КнопкаВыбора = Истина;
		КонецЦикла;
		
		// "Включаем" пустую декорацию, чтобы отображать страницу без элементов.
		Элементы.ГруппаПособиеДоТрехЛетРассчитываетсяДекорация.Видимость = Форма.ПоказателиПособияДоТрехЛет.Количество() = 0;
		
		// Обнуляем фиксированный размер.
		Объект.РазмерПособияДоТрехЛетФиксированнойСуммой = 0;
		
		ПрочитатьЗначенияПоказателейПособияДоТрехЛет(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьЗначенияПоказателейПособияДоТрехЛет(Форма, ТекущийОбъект) Экспорт
	
	Для Каждого СтрокаПоказателей Из Форма.ПоказателиПособияДоТрехЛет Цикл
		НоваяСтрока = ТекущийОбъект.Показатели.Добавить();
		НоваяСтрока.Показатель = СтрокаПоказателей.Показатель;
		НоваяСтрока.Значение = Форма[СтрокаПоказателей.ИмяРеквизита];
		НоваяСтрока.ИдентификаторСтрокиВидаРасчета = Документы.ОтпускПоУходуЗаРебенком.ИдентификаторСтрокПоказателейПособияДоТрехЛет();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьЗначенияПоказателейПособияДоТрехЛет(Форма, ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = Форма.Объект;
	КонецЕсли;
	
	// Заполним поля ввода значениями показателей.
	Для Каждого СтрокаТаблицы Из ТекущийОбъект.Показатели Цикл
		НайденныеСтроки = Форма.ПоказателиПособияДоТрехЛет.НайтиСтроки(Новый Структура("Показатель", СтрокаТаблицы.Показатель));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Форма[НайденныеСтроки[0].ИмяРеквизита] = СтрокаТаблицы.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
