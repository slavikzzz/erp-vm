////////////////////////////////////////////////////////////////////////////////
// Управление производством (повт.исп.): содержит процедуры для управления производством.
// Модуль входит в подсистему "УправлениеПредприятием".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область НастройкиПодсистемы

// Определяет используется ли график производства.
// 
// Возвращаемое значение:
//   Булево - Истина, если график производства используется.
//
Функция ИспользуетсяГрафикПроизводства() Экспорт
	
	МетодикаУправления = Константы.МетодикаУправленияПроизводством.Получить();
	Возврат МетодикаУправления <> Перечисления.МетодикаУправленияПроизводством.БезПланирования;
	
КонецФункции

// Определяет используется ли планирование по производственным ресурсам
// 
// Возвращаемое значение:
//   Булево - Истина, если используется планирование по производственным ресурсам.
//
Функция ИспользуетсяПланированиеПоПроизводственнымРесурсам() Экспорт
	
	Возврат Константы.ИспользоватьПланированиеПоПроизводственнымРесурсам.Получить();
	
КонецФункции

// Определяет используется ли планирование по материальным ресурсам
// 
// Возвращаемое значение:
//   Булево - Истина, если используется планирование по материальным ресурсам.
//
Функция ИспользуетсяПланированиеПоМатериальнымРесурсам() Экспорт
	
	Возврат Константы.ИспользоватьПланированиеПоМатериальнымРесурсам.Получить();
	
КонецФункции

#КонецОбласти

#Область Прочее

// Реквизиты заказа на производство
//
// Параметры:
//  Ссылка	 - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство.
// 
// Возвращаемое значение:
//  Структура - значения ключевых реквизитов заказа на производство.
//
Функция ДанныеПроизводственногоЗаказа(Ссылка) Экспорт
	
	Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка,
		"Номер,
		|Дата,
		|ТипПроизводственногоПроцесса,
		|ХозяйственнаяОперация,
		|НаправлениеДеятельности,
		|ЗаказПодДеятельность,
		|Организация,
		|Партнер,
		|Договор,
		|РазмещениеВыпуска,
		|НазначениеПродукция,
		|Ссылка");
	
	Возврат Результат;
	
КонецФункции

// Значение ключевых реквизитов заказа на производства, используемых при формировании этапов производства.
//
// Параметры:
//  НазначениеПродукция	 - СправочникСсылка.Назначения - назначение продукции из шапки заказа.
// 
// Возвращаемое значение:
//  СправочникСсылка.НаправленияДеятельности - направление деятельности назначения продукции.
//
Функция НаправлениеДеятельностиНазначенияПродукции(НазначениеПродукция) Экспорт
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(НазначениеПродукция);
	НаправленияДеятельности = Справочники.Назначения.НаправленияДеятельностиНазначений(МассивСсылок);
	Возврат НаправленияДеятельности[НазначениеПродукция];
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ДанныеЗаполненияНовойПартииПроизводства(Распоряжение, НазначениеПродукция) Экспорт
	
	Возврат УправлениеПроизводством.ДанныеЗаполненияНовойПартииПроизводства(Распоряжение, НазначениеПродукция);
	
КонецФункции

// Определяет дату, с которой операция используется как распоряжение выработки.
// 
// Возвращаемое значение:
//   Дата - момент, с которого операция используется как распоряжение выработки.
//
Функция ДатаНачалаВеденияУчетаВыработкиПоОперациям() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.ДатаНачалаВеденияУчетаВыработкиПоОперациям.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
