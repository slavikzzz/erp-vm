
#Область ПрограммныйИнтерфейс

// Возвращает текст запроса с временной таблицей амортизации
//
// Возвращаемое значение:
// 		Строка - Текст запроса для получения пустой временной таблицы "втТаблицаАмортизации".
//
Функция ТекстЗапросаПустаяВременнаяТаблицаАмортизации() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	0 КАК ОбъектУчета,
	|	0 КАК Подразделение,
	|	0 КАК НаправлениеДеятельности,
	|	0 КАК СуммаБУ,
	|	0 КАК СуммаНУ,
	|	0 КАК СуммаПР,
	|	0 КАК СуммаВР,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	0 КАК АналитикаРасходов,
	|	ЛОЖЬ КАК ЭтоАмортизационнаяПремия,
	|	ЛОЖЬ КАК ЭтоЦелевыеСредства,
	|	0 КАК ДокументАмортизационнойПремии,
	|	ЛОЖЬ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ОрганизацияПолучательРасходов,
	|	0 КАК СчетПередачиРасходов
	|ПОМЕСТИТЬ втТаблицаАмортизации
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Выполняет проверку ошибок в заполнении способов отражения расходов по амортизации.
//
// Параметры:
// 		ЭтотОбъект - ДокументОбъект.АмортизацияНМА - Объект документа амортизации
// 		Отказ - Булево - Возврат, признак ошибки при выполнения проверки.
//
Процедура ПроверитьСпособыОтраженияРасходовНаПрочиеАктивы(ЭтотОбъект, Отказ) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПорядокУчетаНМАБУ.НематериальныйАктив КАК ОбъектУчета,
	|	ЕСТЬNULL(ТаблицаИзменениеПараметровНМА.СтатьяРасходов, ПорядокУчетаНМАБУ.СтатьяРасходовБУ) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиАмортизации
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(&Дата, Организация В (&Организация)) КАК ПорядокУчетаНМАБУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ТаблицаИзменениеПараметровНМА
	|		ПО ТаблицаИзменениеПараметровНМА.Ссылка = ПорядокУчетаНМАБУ.СпособОтраженияРасходовБУ
	|ГДЕ
	|	ПорядокУчетаНМАБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.ПринятКУчету)
	|	И (ПорядокУчетаНМАБУ.НачислятьАмортизациюБУ
	|		ИЛИ ПорядокУчетаНМАБУ.НачислятьАмортизациюНУ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка КАК СтатьяРасходов,
	|	втСтатьиАмортизации.ОбъектУчета КАК ОбъектУчета
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиАмортизации КАК втСтатьиАмортизации
	|		ПО (втСтатьиАмортизации.СтатьяРасходов = СтатьиРасходов.Ссылка)
	|ГДЕ
	|	СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", КонецМесяца(ЭтотОбъект.Дата));
	Запрос.УстановитьПараметр("Организация", ЭтотОбъект.Организация);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СообщениеОбОшибке =
		НСтр("ru = 'В способе отражения расходов по амортизации для ""%1"" использована статья ""%2"" с не поддерживаемым вариантом распределения расходов ""На прочие активы""
		|Измените способ отражения расходов для данного актива с помощью документа ""Изменение параметров НМА""';
		|en = 'The ""%2"" item which does not support the ""On other assets"" expense allocation option is used in depreciation expense recording method for ""%1"".
		|Change the expense allocation option for this asset using the ""Change of IA parameters"" document'");
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СообщениеОбОшибке,
			Выборка.ОбъектУчета,
			Выборка.СтатьяРасходов);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СообщениеОбОшибке,,,,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает текст запроса временных таблиц для отражения амортизации в регламентированном учете.
//
// Параметры:
// 		ИмяДокумента - Строка - Строка имени метаданных.
//
// Возвращаемое значение:
// 		Строка - Текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизации(ИмяДокумента) Экспорт
	
	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТНачисленнаяАмортизация(ИмяДокумента));
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТОбъектыДокумента(ИмяДокумента));
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТСчетаОтражения());
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТМестонахожденияНМА());
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТНачисленнаяАмортизацияЦелевогоФинансирования());
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТПорядокУчетаНМА());
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

// Возвращает текст запроса временной таблицы остатков по счетам учета
//
// Возвращаемое значение:
// 		Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	втНачисленнаяАмортизация.ЭтоЦелевыеСредства КАК ЭтоЦелевыеСредства,
	|	СУММА(ЕСТЬNULL(втНачисленнаяАмортизация.СуммаБУ, 0)) КАК СуммаБУ,
	|	СУММА(ЕСТЬNULL(втНачисленнаяАмортизация.СуммаНУ, 0)) КАК СуммаНУ,
	|	СУММА(ЕСТЬNULL(втНачисленнаяАмортизация.СуммаПР, 0)) КАК СуммаПР,
	|	СУММА(ЕСТЬNULL(втНачисленнаяАмортизация.СуммаВР, 0)) КАК СуммаВР
	|ПОМЕСТИТЬ втАмортизацияСгруппированная
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.ОбъектУчета,
	|	втНачисленнаяАмортизация.ЭтоЦелевыеСредства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	ЭтоЦелевыеСредства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаОтражения.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втВсеСчета
	|ИЗ
	|	втСчетаОтражения КАК втСчетаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаОтражения.СчетУчетаЦФ
	|ИЗ
	|	втСчетаОтражения КАК втСчетаОтражения
	|ГДЕ
	|	втСчетаОтражения.ПрименениеЦелевогоФинансирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаОтражения.СчетНачисленияАмортизации
	|ИЗ
	|	втСчетаОтражения КАК втСчетаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаОтражения.СчетНачисленияАмортизацииЦФ
	|ИЗ
	|	втСчетаОтражения КАК втСчетаОтражения
	|ГДЕ
	|	втСчетаОтражения.ПрименениеЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втОбъектыДокумента.Организация КАК Организация,
	|	Местонахождение.Подразделение КАК Подразделение,
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	ВЫБОР КОГДА втОбъектыДокумента.ОбъектУчета.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|		ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК МножительНИОКР
	|ПОМЕСТИТЬ втОбъектыСгруппированные
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождение
	|		ПО втОбъектыДокумента.ОбъектУчета = Местонахождение.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК ОбъектыУчета
	|		ПО втОбъектыДокумента.ОбъектУчета = ОбъектыУчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОбъектУчета,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаБУ,
	|	ХозрасчетныйОстатки.СуммаНУОстаток КАК СуммаНУ,
	|	ХозрасчетныйОстатки.СуммаПРОстаток КАК СуммаПР,
	|	ХозрасчетныйОстатки.СуммаВРОстаток КАК СуммаВР
	|ПОМЕСТИТЬ втХозрасчетныйОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаМесяцОкончание,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					втВсеСчета КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			(Организация, Подразделение, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.ОбъектУчета
	|				ИЗ
	|					втОбъектыСгруппированные КАК Т)) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъектыСгруппированные.ОбъектУчета КАК ОбъектУчета,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаБУ, 0) КАК ВосстановительнаяСтоимостьБУ,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаНУ, 0) КАК ВосстановительнаяСтоимостьНУ,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаПР, 0) КАК ВосстановительнаяСтоимостьПР,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаВР, 0) КАК ВосстановительнаяСтоимостьВР,
	|	
	|	ЕСТЬNULL(-втОстаткиСчетАмортизации.СуммаБУ, 0) + ЕСТЬNULL(втАмортизация.СуммаБУ, 0) КАК НакопленнаяАмортизацияБУ,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизации.СуммаНУ, 0) + ЕСТЬNULL(втАмортизация.СуммаНУ, 0) КАК НакопленнаяАмортизацияНУ,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизации.СуммаПР, 0) + ЕСТЬNULL(втАмортизация.СуммаПР, 0) КАК НакопленнаяАмортизацияПР,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизации.СуммаВР, 0) + ЕСТЬNULL(втАмортизация.СуммаВР, 0) КАК НакопленнаяАмортизацияВР,
	|	
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаБУ, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизации.СуммаБУ, 0) + ЕСТЬNULL(втАмортизация.СуммаБУ, 0)) КАК ОстаточнаяСтоимостьБУ,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаНУ, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизации.СуммаНУ, 0) + ЕСТЬNULL(втАмортизация.СуммаНУ, 0)) КАК ОстаточнаяСтоимостьНУ,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаПР, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизации.СуммаПР, 0) + ЕСТЬNULL(втАмортизация.СуммаПР, 0)) КАК ОстаточнаяСтоимостьПР,
	|	ЕСТЬNULL(втОстаткиСчетУчета.СуммаВР, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизации.СуммаВр, 0) + ЕСТЬNULL(втАмортизация.СуммаВР, 0)) КАК ОстаточнаяСтоимостьВР,
	|	
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаБУ, 0) КАК ВосстановительнаяСтоимостьБУЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаНУ, 0) КАК ВосстановительнаяСтоимостьНУЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаПР, 0) КАК ВосстановительнаяСтоимостьПРЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаВР, 0) КАК ВосстановительнаяСтоимостьВРЦФ,
	|	
	|	ЕСТЬNULL(-втОстаткиСчетАмортизацииЦФ.СуммаБУ, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаБУ, 0) КАК НакопленнаяАмортизацияБУЦФ,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизацииЦФ.СуммаНУ, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаНУ, 0) КАК НакопленнаяАмортизацияНУЦФ,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизацииЦФ.СуммаПР, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаПР, 0) КАК НакопленнаяАмортизацияПРЦФ,
	|	ЕСТЬNULL(-втОстаткиСчетАмортизацииЦФ.СуммаВР, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаВР, 0) КАК НакопленнаяАмортизацияВРЦФ,
	|	
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаБУ, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизацииЦФ.СуммаБУ, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаБУ, 0)) КАК ОстаточнаяСтоимостьБУЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаНУ, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизацииЦФ.СуммаНУ, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаНУ, 0)) КАК ОстаточнаяСтоимостьНУЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаПР, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизацииЦФ.СуммаПР, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаПР, 0)) КАК ОстаточнаяСтоимостьПРЦФ,
	|	ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаВР, 0) - (-ЕСТЬNULL(втОстаткиСчетАмортизацииЦФ.СуммаВР, 0) + ЕСТЬNULL(втАмортизацияЦФ.СуммаВР, 0)) КАК ОстаточнаяСтоимостьВРЦФ,
	|	
	|	ВЫБОР КОГДА (ЕСТЬNULL(втОстаткиСчетУчета.СуммаБУ, 0) + ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаБУ, 0)) = 0 ТОГДА 0
	|	ИНАЧЕ ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаБУ, 0) / (ЕСТЬNULL(втОстаткиСчетУчета.СуммаБУ, 0) + ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаБУ, 0)) КОНЕЦ КАК ДоляЦФБУ,
	|	ВЫБОР КОГДА (ЕСТЬNULL(втОстаткиСчетУчета.СуммаНУ, 0) + ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаНУ, 0)) = 0 ТОГДА 0
	|	ИНАЧЕ ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаНУ, 0) / (ЕСТЬNULL(втОстаткиСчетУчета.СуммаНУ, 0) + ЕСТЬNULL(втОстаткиСчетУчетаЦФ.СуммаНУ, 0)) КОНЕЦ КАК ДоляЦФНУ
	|	
	|ПОМЕСТИТЬ втОстаткиПоСчетам
	|ИЗ
	|	втОбъектыСгруппированные КАК втОбъектыСгруппированные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаОтражения КАК втСчетаОтражения
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втСчетаОтражения.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСгруппированная КАК втАмортизация
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втАмортизация.ОбъектУчета
	|			И (НЕ втАмортизация.ЭтоЦелевыеСредства)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСгруппированная КАК втАмортизацияЦФ
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втАмортизацияЦФ.ОбъектУчета
	|			И (втАмортизацияЦФ.ЭтоЦелевыеСредства)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втХозрасчетныйОстатки КАК втОстаткиСчетУчета
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втОстаткиСчетУчета.ОбъектУчета
	|			И (втСчетаОтражения.СчетУчета = втОстаткиСчетУчета.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втХозрасчетныйОстатки КАК втОстаткиСчетАмортизации
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втОстаткиСчетАмортизации.ОбъектУчета
	|			И (втСчетаОтражения.СчетНачисленияАмортизации = втОстаткиСчетАмортизации.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втХозрасчетныйОстатки КАК втОстаткиСчетУчетаЦФ
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втОстаткиСчетУчетаЦФ.ОбъектУчета
	|			И (втСчетаОтражения.СчетУчетаЦФ = втОстаткиСчетУчетаЦФ.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втХозрасчетныйОстатки КАК втОстаткиСчетАмортизацииЦФ
	|		ПО втОбъектыСгруппированные.ОбъектУчета = втОстаткиСчетАмортизацииЦФ.ОбъектУчета
	|			И (втСчетаОтражения.СчетНачисленияАмортизацииЦФ = втОстаткиСчетАмортизацииЦФ.Счет)
	|ГДЕ
	|	НЕ (
	|		втОстаткиСчетУчета.ОбъектУчета ЕСТЬ NULL
	|		И втОстаткиСчетУчетаЦФ.ОбъектУчета ЕСТЬ NULL
	|		И втОстаткиСчетАмортизации.ОбъектУчета ЕСТЬ NULL
	|		И втОстаткиСчетАмортизацииЦФ.ОбъектУчета ЕСТЬ NULL
	|	)";
	
КонецФункции

// Возвращает текст запроса временной таблицы остатков резерва переоценки
//
// Возвращаемое значение:
// 		Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеОстаткиРезерваПереоценки() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ втСчетаДооценки
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриростСтоимостиПоПереоценке))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОбъектУчета,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаБУ,
	|	ХозрасчетныйОстатки.СуммаНУОстатокКт КАК СуммаНУ,
	|	ХозрасчетныйОстатки.СуммаПРОстатокКт КАК СуммаПР,
	|	ХозрасчетныйОстатки.СуммаВРОстатокКт КАК СуммаВР
	|ПОМЕСТИТЬ втОстаткиДооценкиИДвиженияДокумента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаМесяцОкончание,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					втСчетаДооценки КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			(Организация, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.ОбъектУчета
	|				ИЗ
	|					втОбъектыСгруппированные КАК Т)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйСубконто.Значение,
	|	Хозрасчетный.СчетДт,
	|	Хозрасчетный.Сумма,
	|	Хозрасчетный.СуммаНУДт,
	|	Хозрасчетный.СуммаПРДт,
	|	Хозрасчетный.СуммаВРДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаДооценки КАК втВсеСчета
	|		ПО Хозрасчетный.СчетДт = втВсеСчета.Счет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъектыСгруппированные КАК втОбъектыСгруппированные
	|		ПО Хозрасчетный.Организация = втОбъектыСгруппированные.Организация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (втОбъектыСгруппированные.ОбъектУчета = ХозрасчетныйСубконто.Значение)
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйСубконто.Значение,
	|	Хозрасчетный.СчетКт,
	|	-Хозрасчетный.Сумма,
	|	-Хозрасчетный.СуммаНУКт,
	|	-Хозрасчетный.СуммаПРКт,
	|	-Хозрасчетный.СуммаВРКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаДооценки КАК втВсеСчета
	|		ПО Хозрасчетный.СчетКт = втВсеСчета.Счет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъектыСгруппированные КАК втОбъектыСгруппированные
	|		ПО Хозрасчетный.Организация = втОбъектыСгруппированные.Организация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (втОбъектыСгруппированные.ОбъектУчета = ХозрасчетныйСубконто.Значение)
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|ГДЕ
	|	Хозрасчетный.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиДооценкиИДвиженияДокумента.ОбъектУчета КАК ОбъектУчета,
	|	втОстаткиДооценкиИДвиженияДокумента.Счет КАК Счет,
	|	СУММА(втОстаткиДооценкиИДвиженияДокумента.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втОстаткиДооценкиИДвиженияДокумента.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втОстаткиДооценкиИДвиженияДокумента.СуммаПР) КАК СуммаПР,
	|	СУММА(втОстаткиДооценкиИДвиженияДокумента.СуммаВР) КАК СуммаВР
	|ПОМЕСТИТЬ втОстаткиРезерваДооценки
	|ИЗ
	|	втОстаткиДооценкиИДвиженияДокумента КАК втОстаткиДооценкиИДвиженияДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстаткиДооценкиИДвиженияДокумента.ОбъектУчета,
	|	втОстаткиДооценкиИДвиженияДокумента.Счет";
	
КонецФункции

// Возвращает текст запроса для отражения амортизации по документу в регламентированном учете.
//
// Возвращаемое значение:
// 		Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеНачисленнойАмортизации() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизация());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияБезНУ());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияОтправитель());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияБезНУОтправитель());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияПолучатель());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияБезНУПолучатель());	
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияНаПрочиеОперации());
	ТекстыОтражения.Добавить(ТекстОтраженияАмортизацияБезНУНаПрочиеОперации());
	ТекстыОтражения.Добавить(ТекстОтраженияДоходыОтЦелевогоФинансирования());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Возвращает таблицу начисленной амортизации
//
// Параметры:
// 		ТаблицаОбъектовУчета - ТаблицаЗначений - Таблица объектов для начисления амортизации
// 		ТаблицаРеквизитов - ТаблицаЗначений - Таблица реквизитов документа
// 		Отказ - Булево - Признак удачного завершения функции начисления амортизации.
//
// Возвращаемое значение:
// 		ТаблицаЗначений - Таблица начисленных расходов по амортизации (формат представлен в функции УчетОСВызовСервера.ПустаяТаблицаЗначенийНачисленнойАмортизации).
//
Функция НачисленнаяАмортизация(ТаблицаОбъектовУчета, ТаблицаРеквизитов, ТаблицаПараметровРасчетаАмортизации = Неопределено, Отказ) Экспорт
	
	Возврат ПодготовитьТаблицуАмортизацияНМА(ТаблицаОбъектовУчета, ТаблицаРеквизитов, ТаблицаПараметровРасчетаАмортизации, Отказ);
	
КонецФункции

#Область БлокировкаПриОбновленииИБ

// Заполняет входящие данные расчета и отражения амортизации, необходимые для проверки выполненных обновлений ИБ.
//
// Параметры:
// 		ВходящиеДанные - Соответствие из КлючИЗначение- Данные расчета и отражения амортизации:
// 			* Ключ - Строка, ОбъектМетаданных - входящие данные, используемые  операцией
// 			* Значение - см. ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки.Отбор
//
Процедура ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные) Экспорт
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокУчетаНМА);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокУчетаНМАБУ);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПараметрыАмортизацииНМАБУ);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ВыработкаНМА);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОтражениеАмортизацииВРегламентированномУчете

Функция ТекстЗапросаВТНачисленнаяАмортизация(ИмяДокумента)
	
	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втНачисленнаяАмортизация
	|"+
	"ВЫБРАТЬ
	|	ТаблицаАмортизации.Ссылка.Организация КАК Организация,
	|	ТаблицаАмортизации.ОбъектУчета КАК ОбъектУчета,
	|	ТаблицаАмортизации.НаправлениеДеятельности КАК НаправлениеДеятельностиАмортизации,
	|	ТаблицаАмортизации.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СправочникОбъектыУчета.ВидОбъектаУчета, ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)) = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР) КАК ЭтоНИОКР,
	|	ТаблицаАмортизации.Подразделение КАК Подразделение,
	|	ТаблицаАмортизации.СуммаБУ КАК СуммаБУ,
	|	ТаблицаАмортизации.СуммаНУ КАК СуммаНУ,
	|	ТаблицаАмортизации.СуммаПР КАК СуммаПР,
	|	ТаблицаАмортизации.СуммаВР КАК СуммаВР,
	|	ТаблицаАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизации.СчетПередачиРасходов КАК СчетПередачиРасходов,
	|	ТаблицаАмортизации.ЭтоЦелевыеСредства КАК ЭтоЦелевыеСредства
	|ПОМЕСТИТЬ втНачисленнаяАмортизация
	|ИЗ
	|	Документ.СписаниеНМА.НачисленнаяАмортизация КАК ТаблицаАмортизации
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК СправочникОбъектыУчета
	|		ПО ТаблицаАмортизации.ОбъектУчета = СправочникОбъектыУчета.Ссылка
	|ГДЕ
	|	ТаблицаАмортизации.Ссылка = &Ссылка";
	
	ТекстыЗапроса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекстЗапроса);
	
	Если ИмяДокумента = "АмортизацияНМА" Тогда
		
		ТекстВыборкиТаблицыПараметра = 
		"ВЫБРАТЬ
		|	ТаблицаАмортизации.Организация КАК Организация,
		|	ТаблицаАмортизации.ОбъектУчета КАК ОбъектУчета,
		|	ТаблицаАмортизации.НаправлениеДеятельности КАК НаправлениеДеятельностиАмортизации,
		|	ТаблицаАмортизации.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ЕСТЬNULL(СправочникОбъектыУчета.ВидОбъектаУчета, ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)) = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР) КАК ЭтоНИОКР,
		|	ТаблицаАмортизации.Подразделение КАК Подразделение,
		|	ТаблицаАмортизации.СуммаБУ КАК СуммаБУ,
		|	ТаблицаАмортизации.СуммаНУ КАК СуммаНУ,
		|	ТаблицаАмортизации.СуммаПР КАК СуммаПР,
		|	ТаблицаАмортизации.СуммаВР КАК СуммаВР,
		|	ТаблицаАмортизации.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаАмортизации.АналитикаРасходов КАК АналитикаРасходов,
		|	ТаблицаАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
		|	ТаблицаАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
		|	ТаблицаАмортизации.СчетПередачиРасходов КАК СчетПередачиРасходов,
		|	ТаблицаАмортизации.ЭтоЦелевыеСредства КАК ЭтоЦелевыеСредства
		|ИЗ
		|	втТаблицаАмортизации КАК ТаблицаАмортизации
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК СправочникОбъектыУчета
		|		ПО ТаблицаАмортизации.ОбъектУчета = СправочникОбъектыУчета.Ссылка";
		
		ТекстыЗапроса.Добавить(ТекстВыборкиТаблицыПараметра);
		
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.СписаниеНМА", "Документ."+ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТОбъектыДокумента(ИмяДокумента)
	
	ТекстЗапросаВТВсеОбъектыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втВсеОбъектыДокумента
	|"+
	"ВЫБРАТЬ
	|	ТаблицаАмортизации.Организация КАК Организация,
	|	ТаблицаАмортизации.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ втВсеОбъектыДокумента
	|ИЗ
	|	втНачисленнаяАмортизация КАК ТаблицаАмортизации";
	ТекстыЗапроса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекстЗапросаВТВсеОбъектыДокумента);
	
	Если ИмяДокумента <> "АмортизацияНМА" Тогда
		
		ТекстВыборкиТаблицыПараметра = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.НематериальныйАктив КАК ОбъектУчета
		|ИЗ
		|	Документ.СписаниеНМА КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
		
		ТекстыЗапроса.Добавить(ТекстВыборкиТаблицыПараметра);
		
	КонецЕсли;
	
	ТекстЗапросаВТВсеОбъектыДокумента = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекстЗапросаВТВсеОбъектыДокумента);
	
	ТекстЗапросаВТОбъектыДокумента = 
	"// Временная таблица втОбъектыДокумента
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВсеОбъектыДокумента.Организация КАК Организация,
	|	втВсеОбъектыДокумента.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ втОбъектыДокумента
	|ИЗ
	|	втВсеОбъектыДокумента КАК втВсеОбъектыДокумента";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТОбъектыДокумента);
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.СписаниеНМА", "Документ."+ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТСчетаОтражения()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ВТСчетаОтражения
	|"+
	"ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета,
	|	ЕСТЬNULL(СчетаОтражения.СчетНачисленияАмортизации, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетНачисленияАмортизации,
	|	НЕ ПараметрыЦелевогоФинансирования.НематериальныйАктив ЕСТЬ NULL КАК ПрименениеЦелевогоФинансирования,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.СтатьяДоходов, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)) КАК СтатьяДоходов,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.АналитикаДоходов, НЕОПРЕДЕЛЕНО) КАК АналитикаДоходов,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.СчетАмортизации, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетНачисленияАмортизацииЦФ
	|ПОМЕСТИТЬ втСчетаОтражения
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|			И НЕ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|			И (Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					Т.Организация, Т.ОбъектУчета
	|				ИЗ
	|					втОбъектыДокумента КАК Т)) КАК СчетаОтражения
	|		ПО втОбъектыДокумента.ОбъектУчета = СчетаОтражения.НематериальныйАктив
	|			И втОбъектыДокумента.Организация = СчетаОтражения.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(
	|				&Дата,
	|				Регистратор <> &Ссылка
	|					И НематериальныйАктив В
	|						(ВЫБРАТЬ
	|							Т.ОбъектУчета
	|						ИЗ
	|							втОбъектыДокумента КАК Т)) КАК ПараметрыЦелевогоФинансирования
	|		ПО втОбъектыДокумента.ОбъектУчета = ПараметрыЦелевогоФинансирования.НематериальныйАктив
	|			И (ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования)";
	
КонецФункции

Функция ТекстЗапросаВТМестонахожденияНМА()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ВТМестонахожденияНМА
	|"+
	"ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	ЕСТЬNULL(ДанныеСчетУчета.Подразделение, ЕСТЬNULL(ДанныеСчетУчетаЦФ.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК втСчетаОтражения
	|		ПО втОбъектыДокумента.ОбъектУчета = втСчетаОтражения.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&Дата,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчета
	|					ИЗ
	|						втСчетаОтражения КАК Т),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|				(Организация, Субконто1) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втОбъектыДокумента КАК Т)) КАК ДанныеСчетУчета
	|		ПО втОбъектыДокумента.ОбъектУчета = ДанныеСчетУчета.Субконто1
	|			И (втСчетаОтражения.СчетУчета = ДанныеСчетУчета.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&Дата,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчетаЦФ
	|					ИЗ
	|						втСчетаОтражения КАК Т),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|				(Организация, Субконто1) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втОбъектыДокумента КАК Т)) КАК ДанныеСчетУчетаЦФ
	|		ПО втОбъектыДокумента.ОбъектУчета = ДанныеСчетУчетаЦФ.Субконто1
	|			И (втСчетаОтражения.СчетУчетаЦФ = ДанныеСчетУчетаЦФ.Счет)";
	
КонецФункции

Функция ТекстЗапросаВТПорядокУчетаНМА()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица втПорядокУчетаНМА
	|"+
	"ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	ПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втПорядокУчетаНМА
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|			И (НематериальныйАктив, Организация) В
	|				(ВЫБРАТЬ
	|					Т.ОбъектУчета,
	|					Т.Организация
	|				ИЗ
	|					втОбъектыДокумента КАК Т)) КАК ПорядокУчетаНМА
	|		ПО втОбъектыДокумента.Организация = ПорядокУчетаНМА.Организация
	|			И втОбъектыДокумента.ОбъектУчета = ПорядокУчетаНМА.НематериальныйАктив";
	
КонецФункции

Функция ТекстЗапросаВТНачисленнаяАмортизацияЦелевогоФинансирования()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ВТНачисленнаяАмортизацияЦелевыхСредств
	|"+
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.Организация КАК Организация,
	|	втНачисленнаяАмортизация.Подразделение КАК Подразделение,
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	втНачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втНачисленнаяАмортизация.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиАмортизации,
	|	СУММА(втНачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втНачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втНачисленнаяАмортизация.СуммаПР) КАК СуммаПР,
	|	СУММА(втНачисленнаяАмортизация.СуммаВР) КАК СуммаВР
	|ПОМЕСТИТЬ втНачисленнаяАмортизацияЦелевыхСредств
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|ГДЕ
	|	втНачисленнаяАмортизация.ЭтоЦелевыеСредства
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.Организация,
	|	втНачисленнаяАмортизация.Подразделение,
	|	втНачисленнаяАмортизация.ОбъектУчета,
	|	втНачисленнаяАмортизация.НаправлениеДеятельности,
	|	втНачисленнаяАмортизация.НаправлениеДеятельностиАмортизации";
	
КонецФункции


Функция ТекстОтраженияАмортизация()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчетаЦФ
	|		КОГДА Строки.ЭтоНИОКР И НЕ Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчета
	|		КОГДА НЕ Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетНачисленияАмортизацииЦФ
	|		ИНАЧЕ СчетаОтражения.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|ГДЕ
	|	Строки.СтатьяРасходов.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ИСТИНА
	|	И НЕ Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции

Функция ТекстОтраженияАмортизацияБезНУ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	Строки.СуммаНУ + Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчетаЦФ
	|		КОГДА Строки.ЭтоНИОКР И НЕ Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчета
	|		КОГДА НЕ Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетНачисленияАмортизацииЦФ
	|		ИНАЧЕ СчетаОтражения.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|ГДЕ
	|	Строки.СтатьяРасходов.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ЛОЖЬ
	|	И НЕ Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции


Функция ТекстОтраженияАмортизацияОтправитель()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетПередачиРасходов КАК СчетДт,
	|	Строки.ОрганизацияПолучательРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчетаЦФ
	|		КОГДА Строки.ЭтоНИОКР И НЕ Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчета
	|		КОГДА НЕ Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетНачисленияАмортизацииЦФ
	|		ИНАЧЕ СчетаОтражения.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ИСТИНА
	|	И Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции

Функция ТекстОтраженияАмортизацияБезНУОтправитель()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетПередачиРасходов КАК СчетДт,
	|	Строки.ОрганизацияПолучательРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	Строки.СуммаНУ + Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчетаЦФ
	|		КОГДА Строки.ЭтоНИОКР И НЕ Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетУчета
	|		КОГДА НЕ Строки.ЭтоНИОКР И Строки.ЭтоЦелевыеСредства
	|			ТОГДА СчетаОтражения.СчетНачисленияАмортизацииЦФ
	|		ИНАЧЕ СчетаОтражения.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ЛОЖЬ
	|	И Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции


Функция ТекстОтраженияАмортизацияПолучатель()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	ЕСТЬNULL(ОрганизацияПолучатель.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.ОбъектУчета КАК СубконтоДт2,
	|	Строки.АналитикаРасходов КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиКт,
	|	
	|	Строки.СчетПередачиРасходов КАК СчетКт,
	|	Строки.Организация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацияПолучатель
	|		ПО Строки.ОрганизацияПолучательРасходов = ОрганизацияПолучатель.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ИСТИНА
	|	И Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции

Функция ТекстОтраженияАмортизацияБезНУПолучатель()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	ЕСТЬNULL(ОрганизацияПолучатель.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.ОбъектУчета КАК СубконтоДт2,
	|	Строки.АналитикаРасходов КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	Строки.СуммаНУ + Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиКт,
	|	
	|	Строки.СчетПередачиРасходов КАК СчетКт,
	|	Строки.Организация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	Строки.СуммаНУ + Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацияПолучатель
	|		ПО Строки.ОрганизацияПолучательРасходов = ОрганизацияПолучатель.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ЛОЖЬ
	|	И Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции


Функция ТекстОтраженияАмортизацияНаПрочиеОперации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР КОГДА Строки.ОбъектУчета.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|		ТОГДА СчетаОтражения.СчетНачисленияАмортизации
	|		ИНАЧЕ СчетаОтражения.СчетУчета
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьяПрочиеОперации
	|	ПО Строки.СтатьяРасходов = СтатьяПрочиеОперации.Ссылка
	|		И СтатьяПрочиеОперации.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ИСТИНА
	|	И НЕ Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции

Функция ТекстОтраженияАмортизацияБезНУНаПрочиеОперации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизация НМА (Дт 20, 25, 44 :: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	Строки.СуммаНУ + Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождения.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА Строки.ОбъектУчета.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|		ТОГДА СчетаОтражения.СчетНачисленияАмортизации
	|		ИНАЧЕ СчетаОтражения.СчетУчета
	|	КОНЕЦ КАК СчетКт,
	|	Строки.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Начисление амортизации"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизация КАК Строки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьяПрочиеОперации
	|	ПО Строки.СтатьяРасходов = СтатьяПрочиеОперации.Ссылка
	|		И СтатьяПрочиеОперации.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождения
	|		ПО Строки.ОбъектУчета = Местонахождения.ОбъектУчета
	|	
	|ГДЕ
	|	Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету = ЛОЖЬ
	|	И НЕ Строки.ПередаватьРасходыВДругуюОрганизацию
	|";
	
КонецФункции


Функция ТекстОтраженияДоходыОтЦелевогоФинансирования()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов от целевого финансирования (Дт 98 :: Кт 91)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Местонахождение.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияНМА) КАК СчетДт,
	|	Строки.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ + Строки.СуммаПР + Строки.СуммаВР КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СчетаОтражения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Местонахождение.Подразделение КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельностиАмортизации КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СчетаОтражения.СтатьяДоходов КАК СубконтоКт1,
	|	СчетаОтражения.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ + Строки.СуммаПР + Строки.СуммаВР КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Признание доходов от целевого финансирования"" КАК Содержание
	|ИЗ
	|	втНачисленнаяАмортизацияЦелевыхСредств КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК Местонахождение
	|	ПО Строки.ОбъектУчета = Местонахождение.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|	ПО Строки.ОбъектУчета = СчетаОтражения.ОбъектУчета
	|ГДЕ
	|	СчетаОтражения.ПрименениеЦелевогоФинансирования
	|";
	
КонецФункции

#КонецОбласти

#Область ОбщиеПроцедуры

// АМОРТИЗАЦИЯ НМА

Функция ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПорядокУчетаНМАБУ.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокНМА
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК ПорядокУчетаНМАБУ
	|ГДЕ
	|	ПорядокУчетаНМАБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.ПринятКУчету)";

	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	&ТаблицаНМА КАК ТаблицаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаНМАБУ.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокНМА
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(
	|			&НачалоПериода,
	|			НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						ТаблицаНМА.НематериальныйАктив
	|					ИЗ
	|						ТаблицаНМА КАК ТаблицаНМА)
	|				И Организация = &Организация) КАК ПорядокУчетаНМАБУ
	|ГДЕ
	|	ПорядокУчетаНМАБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.ПринятКУчету)";

	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьТекстЗапросаСчетаУчета()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУчетаНМАБУ.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ПорядокУчетаНМАБУ.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ПорядокУчетаНМАБУ.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|
	|	ПорядокУчетаНМАБУ.СтатьяРасходовБУ КАК СтатьяРасходов,
	|	ПорядокУчетаНМАБУ.АналитикаРасходовБУ КАК АналитикаРасходов,
	|
	|	ПорядокУчетаНМАБУ.СпособОтраженияРасходовБУ КАК СпособОтраженияРасходов,
	|	НЕ ПорядокУчетаНМАБУ.СпособОтраженияРасходовБУ В (
	|			ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка),
	|			НЕОПРЕДЕЛЕНО) КАК СпособОтраженияРасходовЗаданДокументом,
	|
	|	ПорядокУчетаНМАБУ.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаНМАБУ.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ПорядокУчетаНМАБУ.СчетПередачиРасходов КАК СчетПередачиРасходов
	|ПОМЕСТИТЬ ПорядокУчетаНМАБУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И Регистратор <> &Регистратор
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПорядокУчетаНМАБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПорядокУчетаНМА.ОбъемНаработки КАК ОбъемНаработки,
	|	ПорядокУчетаНМА.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ПорядокУчетаНМА.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ПорядокУчетаНМА.СчетУчета
	|		ИНАЧЕ ПорядокУчетаНМА.СчетНачисленияАмортизации
	|	КОНЕЦ КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ ПорядокУчетаНМА
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПорядокУчетаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования КАК ПрименениеЦелевогоФинансирования,
	|	ПараметрыЦелевогоФинансирования.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыЦелевогоФинансирования.СчетУчета КАК СчетУчета,
	|	ПараметрыЦелевогоФинансирования.СчетАмортизации КАК СчетАмортизации,
	|	ПараметрыЦелевогоФинансирования.СтатьяДоходов КАК СтатьяДоходов,
	|	ПараметрыЦелевогоФинансирования.АналитикаДоходов КАК АналитикаДоходов
	|ПОМЕСТИТЬ ПараметрыЦелевогоФинансирования
	|ИЗ
	|	РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(
	|			&НачалоПериода,
	|			НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокНМА КАК СписокНМА)) КАК ПараметрыЦелевогоФинансирования
	|ГДЕ
	|	ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПорядокУчетаНМА.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втВсеСчета
	|ИЗ
	|	ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУчетаНМА.НематериальныйАктив,
	|	ПорядокУчетаНМА.СчетНачисленияАмортизации
	|ИЗ
	|	ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансирования.НематериальныйАктив,
	|	ПараметрыЦелевогоФинансирования.СчетУчета
	|ИЗ
	|	ПараметрыЦелевогоФинансирования КАК ПараметрыЦелевогоФинансирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансирования.НематериальныйАктив,
	|	ПараметрыЦелевогоФинансирования.СчетАмортизации
	|ИЗ
	|	ПараметрыЦелевогоФинансирования КАК ПараметрыЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПорядокУчетаНМА.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втВсеСчетаУчетаСтоимости
	|ИЗ
	|	ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансирования.НематериальныйАктив,
	|	ПараметрыЦелевогоФинансирования.СчетУчета
	|ИЗ
	|	ПараметрыЦелевогоФинансирования КАК ПараметрыЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПорядокУчетаНМА.СчетНачисленияАмортизации КАК Счет
	|ПОМЕСТИТЬ втВсеСчетаНакопленнойАмортизации
	|ИЗ
	|	ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансирования.НематериальныйАктив,
	|	ПараметрыЦелевогоФинансирования.СчетАмортизации
	|ИЗ
	|	ПараметрыЦелевогоФинансирования КАК ПараметрыЦелевогоФинансирования";
	
КонецФункции

Функция ПолучитьТекстЗапросаНачисленнаяАмортизацияЗаПериод()
	
	Возврат
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.ОбъектУчета КАК НМА
	|ПОМЕСТИТЬ втАктивыВДокументахЗаПериод
	|ИЗ
	|	Документ.АмортизацияНМА КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АмортизацияНМА.НачисленнаяАмортизация КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериодаДата И &КонецПериода
	|	И Документ.Проведен
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.Ссылка <> &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.ОбъектУчета КАК НМА
	|ИЗ
	|	Документ.ПереоценкаНМА КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаНМА.НачисленнаяАмортизация КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериодаДата И &КонецПериода
	|	И Документ.Проведен
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.Ссылка <> &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.ОбъектУчета
	|ИЗ
	|	Документ.ПодготовкаКПередачеНМА КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеНМА.НачисленнаяАмортизация КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериодаДата И &КонецПериода
	|	И Документ.Проведен
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.Ссылка <> &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.ОбъектУчета
	|ИЗ
	|	Документ.СписаниеНМА КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеНМА.НачисленнаяАмортизация КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериодаДата И &КонецПериода
	|	И Документ.Проведен
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.Ссылка <> &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&НачалоПериодаДата,
	|			&КонецПериода,
	|			Регистратор <> &Регистратор
	|				И Регистратор ССЫЛКА Документ.АмортизацияНМА
	|				И СчетКт В
	|					(ВЫБРАТЬ
	|						Т.СчетНачисленияАмортизации
	|					ИЗ
	|						ПорядокУчетаНМА КАК Т),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втАктивыВДокументахЗаПериод.НМА
	|ПОМЕСТИТЬ втАмортизированныеАктивы
	|ИЗ
	|	втАктивыВДокументахЗаПериод КАК втАктивыВДокументахЗаПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втАктивыВДокументахЗаПериод";
	
КонецФункции

Функция ПолучитьТекстЗапросаСпособовОтраженияРасходов()
	
	Возврат
	"ВЫБРАТЬ
	|	ИзменениеПараметровНМА.Ссылка КАК СпособОтраженияРасходов,
	|	ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов.(
	|		НомерСтроки,
	|		Подразделение,
	|		НаправлениеДеятельности,
	|		СтатьяРасходов,
	|		АналитикаРасходов,
	|		Коэффициент,
	|		ПередаватьРасходыВДругуюОрганизацию,
	|		ОрганизацияПолучательРасходов,
	|		СчетПередачиРасходов
	|	)
	|ИЗ
	|	ПорядокУчетаНМАБУ КАК СпособыОтраженияРасходовБУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА КАК ИзменениеПараметровНМА
	|		ПО СпособыОтраженияРасходовБУ.СпособОтраженияРасходов = ИзменениеПараметровНМА.Ссылка
	|ГДЕ
	|	СпособыОтраженияРасходовБУ.СпособОтраженияРасходовЗаданДокументом";
	
КонецФункции

Процедура СообщитьОбОшибкеПриНачисленииАмортизации(Выборка, ТекстСообщения, РегОперация, Отказ, Ссылка = Неопределено)
	
	ШаблонСообщения = НСтр("ru = '%1 для нематериального актива %2 (%3).';
							|en = '%1 for intangible asset %2 (%3).'");
	ТекстСообщенияОбОшибке = СтрШаблон(ШаблонСообщения, ТекстСообщения, Выборка.НематериальныйАктивПредставление, Выборка.Код);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияОбОшибке, Ссылка,,, Отказ);
	
КонецПроцедуры // СообщитьОбОшибкеПриНачисленииАмортизации()

Функция ПодготовитьТаблицуАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты, ТаблицаПараметровРасчетаАмортизации, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Параметры = ПодготовитьПараметрыАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаАмортизации = УчетОСВызовСервера.ПустаяТаблицаЗначенийНачисленнойАмортизации();
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	МетодНачисленияАмортизацииНУ = УчетнаяПолитика.МетодНачисленияАмортизацииНУ(Реквизиты.Организация, Реквизиты.Период);
	
	Если ОтражатьВНалоговомУчете И НЕ ЗначениеЗаполнено(МетодНачисленияАмортизацииНУ) Тогда
		Если Реквизиты.ВыдаватьСообщения Тогда
			ШаблонСообщения = НСтр("ru = 'Не указан метод начисления амортизации (НУ) в учетной политике организации %1
				|Укажите метод начисления амортизации на %2.';
				|en = 'Accrual method of accounting (TA) is not specified in the %1 company accounting policy.
				|Specify accrual method of accounting on %2.'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Реквизиты.Организация, Формат(НачалоГода(Реквизиты.ДатаРасчета)));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;
		Возврат ТаблицаАмортизации;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("НачалоПериодаДата", НачалоМесяца(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("НачалоПериода", Новый Граница(НачалоМесяца(Реквизиты.ДатаРасчета), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("Период", Реквизиты.ДатаРасчета);
	Запрос.УстановитьПараметр("ДатаПереходаКЛинейномуМетоду", УправлениеВнеоборотнымиАктивамиПереопределяемый.ПолучитьДатуПереходаКЛинейномуМетоду(Реквизиты.ДатаРасчета, Реквизиты.Организация));
	Запрос.УстановитьПараметр("Регистратор", Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("МетодНачисленияАмортизацииНУ", МетодНачисленияАмортизацииНУ);
	
	// Получим список нематериальных активов для расчета амортизации
	Если Параметры.ТаблицаНМА.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ТаблицаНМА", Параметры.ТаблицаНМА);
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице();
	Иначе
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе();
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст = ПолучитьТекстЗапросаСчетаУчета();
	Запрос.Выполнить();
	
	Запрос.Текст = ПолучитьТекстЗапросаНачисленнаяАмортизацияЗаПериод();
	Запрос.Выполнить();
	
	// Заполнение соответствия способов отражения расходов
	СоответствиеСпособовОтражения = Новый Соответствие;
	Запрос.Текст = ПолучитьТекстЗапросаСпособовОтраженияРасходов();
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СоответствиеСпособовОтражения.Вставить(Выборка.СпособОтраженияРасходов, Выборка.ОтражениеАмортизационныхРасходов.Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияНМА.Период КАК Период,
	|	ПервоначальныеСведенияНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМА.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМА.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМА
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПервоначальныеСведенияНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПараметрыАмортизацииНМАБУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизацииНМАБУ.КоэффициентБУ КАК КоэффициентБУ,
	|	ПараметрыАмортизацииНМАБУ.СпециальныйКоэффициент КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАБУ
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАБУ.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПараметрыАмортизацииНМАБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НематериальныйАктив,
	|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
	|	ХозрасчетныйОстаткиИОбороты.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУНачальныйОстатокДт КАК СуммаНУНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокДт КАК СуммаПРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокДт КАК СуммаВРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СуммаНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУНачальныйОстатокКт КАК СуммаНУНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокКт КАК СуммаПРНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокКт КАК СуммаВРНачальныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУКонечныйОстатокДт КАК СуммаНУКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокДт КАК СуммаПРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт КАК СуммаВРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНУКонечныйОстатокКт КАК СуммаНУКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокКт КАК СуммаПРКонечныйОстатокКт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокКт КАК СуммаВРКонечныйОстатокКт
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Счет
	|				ИЗ
	|					втВсеСчета КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ХозрасчетныйОстаткиИОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.Субконто1 КАК НематериальныйАктив,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК СуммаОборотКт,
	|	СУММА(ХозрасчетныйОбороты.СуммаНУОборотКт) КАК СуммаНУОборотКт,
	|	СУММА(ХозрасчетныйОбороты.СуммаПРОборотКт) КАК СуммаПРОборотКт,
	|	СУММА(ХозрасчетныйОбороты.СуммаВРОборотКт) КАК СуммаВРОборотКт
	|ПОМЕСТИТЬ ХозрасчетныйОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Счет
	|				ИЗ
	|					втВсеСчетаНакопленнойАмортизации КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Регистратор <> &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК НематериальныйАктив,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СтоимостьНаНачалоГода
	|ПОМЕСТИТЬ СтоимостьНМАНаНачалоГода
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоГода,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Счет
	|				ИЗ
	|					втВсеСчета КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаНМАОбороты.НематериальныйАктив,
	|	ВыработкаНМАОбороты.КоличествоОборот
	|ПОМЕСТИТЬ ВыработкаНМАОбороты
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ВыработкаНМАОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК НематериальныйАктив
	|ПОМЕСТИТЬ НМАТолькоЛинейныйМетодНУ
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокНМА КАК СписокНМА)
	|	И НематериальныеАктивы.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АмортизацияНМАНачалоПериода.Счет КАК Счет,
	|	АмортизацияНМАНачалоПериода.Субконто1 КАК НематериальныйАктив,
	|	АмортизацияНМАНачалоПериода.СуммаНУОстатокКт КАК АмортизацияНачальныйОстаток
	|ПОМЕСТИТЬ АмортизацияНМАПриПереходеКЛинейномуМетоду
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаПереходаКЛинейномуМетоду,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Счет
	|				ИЗ
	|					втВсеСчетаНакопленнойАмортизации КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И (НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ))) КАК АмортизацияНМАНачалоПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокНМА.НематериальныйАктив,
	|	СписокНМА.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР) КАК ЭтоНИОКР,
	|	ПРЕДСТАВЛЕНИЕ(СписокНМА.НематериальныйАктив) КАК НематериальныйАктивПредставление,
	|	СписокНМА.НематериальныйАктив.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	СписокНМА.НематериальныйАктив.Код КАК Код,
	|	ПорядокУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СтоимостьНМА.Подразделение, СтоимостьНМАЦФ.Подразделение) КАК Подразделение,
	|	
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНачальныйОстатокДт, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНУНачальныйОстатокДт, 0) КАК СтоимостьНУНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМАЦФ.СуммаПРНачальныйОстатокДт, 0) КАК СтоимостьПРНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНачальныйОстатокКт, 0) + ЕСТЬNULL(АмортизацияНМАЦФ.СуммаНачальныйОстатокКт, 0) КАК АмортизацияНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНУНачальныйОстатокКт, 0) + ЕСТЬNULL(АмортизацияНМАЦФ.СуммаНУНачальныйОстатокКт, 0) КАК АмортизацияНУНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаПРНачальныйОстатокКт, 0) + ЕСТЬNULL(АмортизацияНМАЦФ.СуммаПРНачальныйОстатокКт, 0) КАК АмортизацияПРНачальныйОстаток,
	|	
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) + ЕСТЬNULL(ХозрасчетныйОборотыЦФ.СуммаОборотКт, 0) КАК АмортизацияОборот,
	|	
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0) + ЕСТЬNULL(ХозрасчетныйОборотыЦФ.СуммаНУОборотКт, 0) КАК АмортизацияНУОборот,
	|	
	|	ЕСТЬNULL(ВыработкаНМАОбороты.КоличествоОборот, 0) КАК Количество,
	|	ПервоначальныеСведенияНМА.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) КАК ПервоначальнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПорядокУчетаНМА.ОбъемНаработки, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.КоэффициентБУ, 0) КАК Коэффициент,
	|	ПервоначальныеСведенияНМА.Период КАК Период,
	|	ЕСТЬNULL(ПорядокУчетаНМАБУ.НачислятьАмортизациюБУ, ЛОЖЬ) КАК НачислятьАмортизациюБУ,
	|	ПорядокУчетаНМА.СчетУчета,
	|	ПорядокУчетаНМА.СчетНачисленияАмортизации,
	|	ПорядокУчетаНМАБУ.СтатьяРасходов КАК СтатьяРасходов,
	|	ПорядокУчетаНМАБУ.АналитикаРасходов КАК АналитикаРасходов,
	|	ПорядокУчетаНМАБУ.СпособОтраженияРасходовЗаданДокументом КАК СпособОтраженияРасходовЗаданДокументом,
	|	ПорядокУчетаНМАБУ.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ПорядокУчетаНМАБУ.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаНМАБУ.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ПорядокУчетаНМАБУ.СчетПередачиРасходов КАК СчетПередачиРасходов,
	|	ЕСТЬNULL(СтоимостьНМАНаНачалоГода.СтоимостьНаНачалоГода, 0) КАК СтоимостьНаНачалоГода,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМА.Период КАК ДатаПринятияКУчету,
	|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
	|	ЕСТЬNULL(ПорядокУчетаНМАБУ.НачислятьАмортизациюНУ, ЛОЖЬ) КАК НачислятьАмортизациюНУ,
	|	
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансирования.ПрименениеЦелевогоФинансирования, ЛОЖЬ) КАК ПрименениеЦелевогоФинансирования,
	|	ПараметрыЦелевогоФинансирования.СчетУчета КАК СчетУчетаЦФ,
	|	ПараметрыЦелевогоФинансирования.СчетАмортизации КАК СчетНачисленияАмортизацииЦФ,
	|	
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0) - ЕСТЬNULL(АмортизацияНМА.СуммаНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьБУ,
	|	ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНачальныйОстатокДт, 0)- ЕСТЬNULL(АмортизацияНМАЦФ.СуммаНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьБУЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0) - ЕСТЬNULL(АмортизацияНМА.СуммаНУНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьНУ,
	|	ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНУНачальныйОстатокДт, 0)- ЕСТЬNULL(АмортизацияНМАЦФ.СуммаНУНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьНУЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0) - ЕСТЬNULL(АмортизацияНМА.СуммаПРНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьПР,
	|	ЕСТЬNULL(СтоимостьНМАЦФ.СуммаПРНачальныйОстатокДт, 0)- ЕСТЬNULL(АмортизацияНМАЦФ.СуммаПРНачальныйОстатокКт, 0) КАК ОстаточнаяСтоимостьПРЦФ,
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0) = 0
	|		ТОГДА 1
	|		КОГДА ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНачальныйОстатокДт, 0) = 0
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНачальныйОстатокДт, 0) / (ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0))
	|	КОНЕЦ КАК ПропорцияЦелевыхСредствКСобственнымБУ,
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0) = 0
	|		ТОГДА 1
	|		КОГДА ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНУНачальныйОстатокДт, 0) = 0
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНУНачальныйОстатокДт, 0) / (ЕСТЬNULL(СтоимостьНМАЦФ.СуммаНУНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0))
	|	КОНЕЦ КАК ПропорцияЦелевыхСредствКСобственнымНУ,
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0) = 0
	|		ТОГДА 1
	|		КОГДА ЕСТЬNULL(СтоимостьНМАЦФ.СуммаПРНачальныйОстатокДт, 0) = 0
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМАЦФ.СуммаПРНачальныйОстатокДт, 0) / (ЕСТЬNULL(СтоимостьНМАЦФ.СуммаПРНачальныйОстатокДт, 0) + ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0))
	|	КОНЕЦ КАК ПропорцияЦелевыхСредствКСобственнымПР,
	|	
	|	ВЫБОР
	|		КОГДА ПервоначальныеСведенияНМА.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПервоначальныеСведенияНМА.НематериальныйАктив В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|				ИНАЧЕ &МетодНачисленияАмортизацииНУ
	|			КОНЕЦ
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.СпециальныйКоэффициент, 0) КАК СпециальныйКоэффициент,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.АмортизацияДо2009, 0)
	|		ИНАЧЕ ЕСТЬNULL(АмортизацияНМАПриПереходеКЛинейномуМетоду.АмортизацияНачальныйОстаток, 0)
	|	КОНЕЦ КАК АмортизацияПриПереходеКЛинейномуМетоду,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ФактическийСрокИспользованияДо2009, 0)
	|		ИНАЧЕ (ГОД(&ДатаПереходаКЛинейномуМетоду) - ГОД(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ)) * 12 + (МЕСЯЦ(&ДатаПереходаКЛинейномуМетоду) - МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ)) - 1
	|	КОНЕЦ КАК ФактическийСрокПриПереходеКЛинейномуМетоду,
	|	ВЫБОР КОГДА втАмортизированныеАктивы.НМА ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ КАК ЕстьНачислениеАмортизацииЗаПериод
	|ИЗ
	|	СписокНМА КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведенияНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАБУ КАК ПорядокУчетаНМАБУ
	|		ПО СписокНМА.НематериальныйАктив = ПорядокУчетаНМАБУ.НематериальныйАктив
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО СписокНМА.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыЦелевогоФинансирования КАК ПараметрыЦелевогоФинансирования
	|		ПО СписокНМА.НематериальныйАктив = ПараметрыЦелевогоФинансирования.НематериальныйАктив
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК СтоимостьНМА
	|		ПО ПорядокУчетаНМА.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|			И ПорядокУчетаНМА.СчетУчета = СтоимостьНМА.Счет
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК СтоимостьНМАЦФ
	|		ПО ПорядокУчетаНМА.НематериальныйАктив = СтоимостьНМАЦФ.НематериальныйАктив
	|			И ПараметрыЦелевогоФинансирования.СчетУчета = СтоимостьНМАЦФ.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК АмортизацияНМА
	|		ПО ПорядокУчетаНМА.НематериальныйАктив = АмортизацияНМА.НематериальныйАктив
	|			И ПорядокУчетаНМА.СчетНачисленияАмортизации = АмортизацияНМА.Счет
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК АмортизацияНМАЦФ
	|		ПО ПорядокУчетаНМА.НематериальныйАктив = АмортизацияНМАЦФ.НематериальныйАктив
	|			И ПараметрыЦелевогоФинансирования.СчетАмортизации = АмортизацияНМАЦФ.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ПО СписокНМА.НематериальныйАктив = ХозрасчетныйОбороты.НематериальныйАктив
	|			И ПорядокУчетаНМА.СчетНачисленияАмортизации = ХозрасчетныйОбороты.Счет
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОбороты КАК ХозрасчетныйОборотыЦФ
	|		ПО СписокНМА.НематериальныйАктив = ХозрасчетныйОборотыЦФ.НематериальныйАктив
	|			И ПараметрыЦелевогоФинансирования.СчетАмортизации = ХозрасчетныйОборотыЦФ.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМАНаНачалоГода КАК СтоимостьНМАНаНачалоГода
	|		ПО СписокНМА.НематериальныйАктив = СтоимостьНМАНаНачалоГода.НематериальныйАктив
	|			И ПорядокУчетаНМА.СчетУчета = СтоимостьНМАНаНачалоГода.Счет
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМАНаНачалоГода КАК СтоимостьНМАНаНачалоГодаЦФ
	|		ПО СписокНМА.НематериальныйАктив = СтоимостьНМАНаНачалоГодаЦФ.НематериальныйАктив
	|			И ПараметрыЦелевогоФинансирования.СчетУчета = СтоимостьНМАНаНачалоГодаЦФ.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМАПриПереходеКЛинейномуМетоду КАК АмортизацияНМАПриПереходеКЛинейномуМетоду
	|		ПО СписокНМА.НематериальныйАктив = АмортизацияНМАПриПереходеКЛинейномуМетоду.НематериальныйАктив
	|			И ПорядокУчетаНМА.СчетУчета = АмортизацияНМАПриПереходеКЛинейномуМетоду.Счет
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМАПриПереходеКЛинейномуМетоду КАК АмортизацияНМАПриПереходеКЛинейномуМетодуЦФ
	|		ПО СписокНМА.НематериальныйАктив = АмортизацияНМАПриПереходеКЛинейномуМетодуЦФ.НематериальныйАктив
	|			И ПараметрыЦелевогоФинансирования.СчетУчета = АмортизацияНМАПриПереходеКЛинейномуМетодуЦФ.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаНМАОбороты КАК ВыработкаНМАОбороты
	|		ПО СписокНМА.НематериальныйАктив = ВыработкаНМАОбороты.НематериальныйАктив
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАБУ КАК ПараметрыАмортизацииНМАБУ
	|		ПО СписокНМА.НематериальныйАктив = ПараметрыАмортизацииНМАБУ.НематериальныйАктив
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизированныеАктивы КАК втАмортизированныеАктивы
	|		ПО СписокНМА.НематериальныйАктив = втАмортизированныеАктивы.НМА
	|ГДЕ
	|	(НЕ(ЕСТЬNULL(ПорядокУчетаНМАБУ.НачислятьАмортизациюБУ, ЛОЖЬ) = ЛОЖЬ
	|				И ЕСТЬNULL(ПорядокУчетаНМАБУ.НачислятьАмортизациюНУ, ЛОЖЬ) = ЛОЖЬ))";
	
	ВыборкаПоНМА = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаПоНМА.Следующий() Цикл
		
		СуммаАмортизацииБУ = 0;
		СуммаАмортизацииНУ = 0;
		СуммаАмортизацииПР = 0;
		
		СписатьОстатокБУ = Ложь;
		СписатьОстатокНУ = Ложь;
		СписатьОстатокПР = Ложь;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СчетНачисленияАмортизации) Тогда
			Если Реквизиты.ВыдаватьСообщения Тогда
				СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан счет для начисления амортизации';
																			|en = 'Depreciation accrual account is not specified'"),
					Реквизиты.Регистратор, Отказ);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		// Параметры расчета
		Если ТаблицаПараметровРасчетаАмортизации <> Неопределено Тогда
			
			ПараметрыАмортизации = ТаблицаПараметровРасчетаАмортизации.Добавить();
			ПараметрыАмортизации.ОбъектУчета = ВыборкаПоНМА.НематериальныйАктив;
			
			Если ВыборкаПоНМА.НачислятьАмортизациюБУ Тогда
				ПараметрыАмортизации.МетодНачисленияБУ = ВыборкаПоНМА.СпособНачисленияАмортизации;
				ПараметрыАмортизации.СрокИспользованияБУ = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации;
				ПараметрыАмортизации.СтоимостьБУ = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации;
				ПараметрыАмортизации.ОбъемПродукцииБУ = ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
				ПараметрыАмортизации.ВыработкаЗаМесяцБУ = ВыборкаПоНМА.Количество;
				ПараметрыАмортизации.КоэффициентАмортизацииБУ = ВыборкаПоНМА.Коэффициент;
			КонецЕсли; 
			
			Если ВыборкаПоНМА.НачислятьАмортизациюНУ Тогда
				ПараметрыАмортизации.МетодНачисленияНУ = ВыборкаПоНМА.МетодНачисленияАмортизации;
				ПараметрыАмортизации.СрокИспользованияНУ = ВыборкаПоНМА.СрокПолезногоИспользования;
				ПараметрыАмортизации.КоэффициентНУ = ВыборкаПоНМА.СпециальныйКоэффициент;
			КонецЕсли; 
			
			// Остальные параметры устанавливаются по месту расчета
			
		КонецЕсли; 
		
		Пропустить = Ложь;
		
		Если ВыборкаПоНМА.НачислятьАмортизациюБУ Тогда
			
			Если ВыборкаПоНМА.ЕстьНачислениеАмортизацииЗаПериод Тогда
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СпособНачисленияАмортизации) Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан способ амортизации';
																				|en = 'Depreciation method is not specified'"),
						Реквизиты.Регистратор, Отказ);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации = 0 Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Стоимость равна нулю';
																				|en = 'Cost is zero'"),
						Реквизиты.Регистратор, Отказ);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации = 0
				И (ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный 
					ИЛИ ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования равен нулю';
																				|en = 'Useful life is equal to zero'"),
						Реквизиты.Регистратор, Отказ);
				КонецЕсли;
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ Пропустить Тогда
			
				СуммаАмортизации = 0;

				Если ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный Тогда
		 
					СуммаГодовойАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации / ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации * 12;
					СуммаАмортизации        = СуммаГодовойАмортизации / 12;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.Количество) Тогда
						Пропустить = Истина;
					КонецЕсли;

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации) Тогда
						Если Реквизиты.ВыдаватьСообщения Тогда
							СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Объем продукции для вычисления не определен';
																						|en = 'Product volume for calculation is not determined'"),
								Реквизиты.Регистратор, Отказ);
						КонецЕсли; 
						Пропустить = Истина;
					КонецЕсли;

					Если Пропустить Тогда
						СуммаАмортизации = 0 ;
					Иначе
						СуммаАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации * ВыборкаПоНМА.Количество / ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
					КонецЕсли;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда
				
					ОстаточнаяСтоимость = Макс(0, ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток);
					
					ФактическийСрокИспользования = УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(
														ВыборкаПоНМА.Период, Реквизиты.ДатаРасчета);
														
					ОставшийсяСрокПолезногоИспользования = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации 
														 - ФактическийСрокИспользования
														 + 1;
					Коэффициент = ?(ВыборкаПоНМА.Коэффициент = 0, 1, ВыборкаПоНМА.Коэффициент);
					
					СуммаАмортизации = ?(ОставшийсяСрокПолезногоИспользования > 0,
										 ОстаточнаяСтоимость * (Коэффициент / ОставшийсяСрокПолезногоИспользования),
										 0);
										 
					Если ТаблицаПараметровРасчетаАмортизации <> Неопределено Тогда
						ПараметрыАмортизации.ФактическийСрокИспользования = ФактическийСрокИспользования;
						ПараметрыАмортизации.ОставшийсяСрокИспользования = ОставшийсяСрокПолезногоИспользования;
					КонецЕсли;
										 
				КонецЕсли;
				
				ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток;
				СписатьОстатокБУ = (ВсегоОсталосьСписать < СуммаАмортизации);
				СуммаАмортизацииБУ   = Окр(Мин(СуммаАмортизации, ВсегоОсталосьСписать), 2);
				
				СтоимостьНМА = ?(ВыборкаПоНМА.ЭтоНИОКР, 
									ВыборкаПоНМА.СтоимостьНачальныйОстаток, 
									ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации);
									
				Если СтоимостьНМА <> 0 Тогда
					СуммаАмортизацииПР = Окр(ВыборкаПоНМА.СтоимостьПРНачальныйОстаток * (СуммаАмортизации / СтоимостьНМА), 2);
				Иначе
					СуммаАмортизацииПР = 0;
				КонецЕсли;
				
				ВсегоОсталосьСписатьПР = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток - ВыборкаПоНМА.АмортизацияПРНачальныйОстаток;
				
				Если ВсегоОсталосьСписатьПР > 0 Тогда
					СписатьОстатокПР = (ВсегоОсталосьСписатьПР < СуммаАмортизацииПР);
					СуммаАмортизацииПР = Окр(Макс(0, Мин(СуммаАмортизацииПР, ВсегоОсталосьСписатьПР)), 2);
				Иначе
					СписатьОстатокПР = (ВсегоОсталосьСписатьПР > СуммаАмортизацииПР);
					СуммаАмортизацииПР = Окр(Мин(0, Макс(СуммаАмортизацииПР, ВсегоОсталосьСписатьПР)), 2);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Пропустить = Ложь;
		
		Если ОтражатьВНалоговомУчете Тогда
			
			Если ВыборкаПоНМА.НачислятьАмортизациюНУ Тогда
				
				Если ВыборкаПоНМА.ЕстьНачислениеАмортизацииЗаПериод Тогда
					Пропустить = Истина; 
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.МетодНачисленияАмортизации) Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан метод начисления амортизации (НУ) в учетной политике организации';
																					|en = 'Accrual method of accounting (TA) is not specified in the company accounting policy'"),
							Реквизиты.Регистратор, Отказ);
					КонецЕсли;
					Пропустить = Истина;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СтоимостьНУНачальныйОстаток) Тогда
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный
					И ВыборкаПоНМА.СрокПолезногоИспользования = 0 Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования (НУ) равен нулю';
																					|en = 'Useful life (TA) is equal to zero'"),
							Реквизиты.Регистратор, Отказ);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный
					И НЕ ЗначениеЗаполнено(ВыборкаПоНМА.АмортизационнаяГруппа) Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'В справочнике не указана амортизационная группа';
																					|en = 'Depreciation group is not specified in the catalog'"),
							Реквизиты.Регистратор, Отказ);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				Если НЕ Пропустить Тогда
					
					СуммаАмортизации = 0;
					
					Если ВыборкаПоНМА.СчетУчета = ВыборкаПоНМА.СчетНачисленияАмортизации Тогда // НИОКР.
						СтоимостьДляВычисления = ВыборкаПоНМА.ПервоначальнаяСтоимостьНУ;
					Иначе
						СтоимостьДляВычисления = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток;
					КонецЕсли; 
					
					СрокИспользованияДляВычисления = Макс(1, ВыборкаПоНМА.СрокПолезногоИспользования);
					
					СпециальныйКоэффициент = ?(ВыборкаПоНМА.СпециальныйКоэффициент = 0, 1, ВыборкаПоНМА.СпециальныйКоэффициент);
					
					Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный Тогда
						
						СтоимостьДляВычисления         = СтоимостьДляВычисления - ВыборкаПоНМА.АмортизацияПриПереходеКЛинейномуМетоду;
						СрокИспользованияДляВычисления = СрокИспользованияДляВычисления - ВыборкаПоНМА.ФактическийСрокПриПереходеКЛинейномуМетоду;
						
						СуммаАмортизации = СтоимостьДляВычисления / СрокИспользованияДляВычисления * СпециальныйКоэффициент;
						
						Если ТаблицаПараметровРасчетаАмортизации <> Неопределено Тогда
							ПараметрыАмортизации.СтоимостьНУ = СтоимостьДляВычисления;
						КонецЕсли;
						
					ИначеЕсли ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
						
						НормаАмортизации = УправлениеВнеоборотнымиАктивамиПереопределяемый.ПолучитьНормуПоАмортизационнойГруппе(ВыборкаПоНМА.АмортизационнаяГруппа);
						
						Если НЕ (НормаАмортизации = Неопределено) Тогда
							
							СуммаАмортизации = (ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток)
													* НормаАмортизации / 100 
													* СпециальныйКоэффициент;
							
							Если ТаблицаПараметровРасчетаАмортизации <> Неопределено Тогда
								
								ПараметрыАмортизации.СтоимостьНУ = 
									ВыборкаПоНМА.СтоимостьНУНачальныйОстаток 
									- ВыборкаПоНМА.АмортизацияНУНачальныйОстаток;
									
								ПараметрыАмортизации.НормаАмортизацииНУ = НормаАмортизации;
									
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
					ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток;
					СписатьОстатокНУ = (ВсегоОсталосьСписать < СуммаАмортизации);
					СуммаАмортизацииНУ = Окр(Мин(ВсегоОсталосьСписать, СуммаАмортизации), 2);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СпособОтражения = Неопределено;
		Если ВыборкаПоНМА.СпособОтраженияРасходовЗаданДокументом Тогда
			СпособОтражения = СоответствиеСпособовОтражения.Получить(ВыборкаПоНМА.СпособОтраженияРасходов);
		КонецЕсли;
		
		НовыеСтроки = ПодготовитьСтрокиВТаблицуАмортизации(
			ВыборкаПоНМА,
			СуммаАмортизацииБУ, СуммаАмортизацииНУ, СуммаАмортизацииПР,
			СписатьОстатокБУ, СписатьОстатокНУ, СписатьОстатокПР);
		ДобавитьСтрокиВТаблицуАмортизации(ТаблицаАмортизации, ВыборкаПоНМА, НовыеСтроки, СпособОтражения);
		
	КонецЦикла;
	
	Возврат ТаблицаАмортизации;
КонецФункции

Функция ПодготовитьСтрокиВТаблицуАмортизации(
		ВыборкаДанных,
		СуммаАмортизацииБУ, СуммаАмортизацииНУ, СуммаАмортизацииПР,
		СписатьОстатокБУ=Ложь, СписатьОстатокНУ=Ложь, СписатьОстатокПР=Ложь)
	
	ПоляСтроки = "ОбъектУчета, СуммаБУ, СуммаНУ, СуммаПР, СуммаВР, Коэффициент, ЭтоЦелевыеСредства";
	СтрокаАмортизации = Новый Структура(ПоляСтроки);
	СтрокаАмортизацииЦФ = Новый Структура(ПоляСтроки);
	
	МассивРезультат = Новый Массив;
	
	Если СуммаАмортизацииБУ > 0
		ИЛИ СуммаАмортизацииНУ > 0
		ИЛИ СуммаАмортизацииПР <> 0 Тогда
		
		МассивРезультат.Добавить(СтрокаАмортизации);
		
		ЗаполнитьЗначенияСвойств(СтрокаАмортизации, ВыборкаДанных);
		
		СтрокаАмортизации.ОбъектУчета = ВыборкаДанных.НематериальныйАктив;
		
		СтрокаАмортизации.СуммаБУ = СуммаАмортизацииБУ;
		СтрокаАмортизации.СуммаНУ = СуммаАмортизацииНУ;
		СтрокаАмортизации.СуммаПР = СуммаАмортизацииПР;
		СтрокаАмортизации.СуммаВР = СтрокаАмортизации.СуммаБУ - СтрокаАмортизации.СуммаНУ - СтрокаАмортизации.СуммаПР;
		
		Если ВыборкаДанных.СтоимостьНачальныйОстаток <= ВыборкаДанных.АмортизацияНачальныйОстаток Тогда
			СтрокаАмортизации.Коэффициент = 0;
		Иначе
			СтрокаАмортизации.Коэффициент = СуммаАмортизацииБУ / (ВыборкаДанных.СтоимостьНачальныйОстаток - ВыборкаДанных.АмортизацияНачальныйОстаток);
		КонецЕсли;
		
		СтрокаАмортизации.ЭтоЦелевыеСредства = Ложь;
		Если ВыборкаДанных.ПрименениеЦелевогоФинансирования Тогда
			
			Если ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымБУ = 0
				И ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымНУ = 0
				И ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымПР = 0 Тогда
				
				// Остались только собственные средства к амортизации
				СтрокаАмортизации.ЭтоЦелевыеСредства = Ложь;
				
			ИначеЕсли ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымБУ = 1
				И ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымНУ = 1
				И ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымПР = 1 Тогда
				
				// Остались только целевые средства к амортизации
				СтрокаАмортизации.ЭтоЦелевыеСредства = Истина;
				
			Иначе
				
				МассивРезультат.Добавить(СтрокаАмортизацииЦФ);
				
				ЗаполнитьЗначенияСвойств(СтрокаАмортизацииЦФ, СтрокаАмортизации);
				СтрокаАмортизацииЦФ.ЭтоЦелевыеСредства = Истина;
				
				СтрокаАмортизацииЦФ.СуммаБУ = ?(СписатьОстатокБУ, ВыборкаДанных.ОстаточнаяСтоимостьБУЦФ, СтрокаАмортизации.СуммаБУ * ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымБУ);
				СтрокаАмортизацииЦФ.СуммаНУ = ?(СписатьОстатокНУ, ВыборкаДанных.ОстаточнаяСтоимостьНУЦФ, СтрокаАмортизации.СуммаНУ * ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымНУ);
				СтрокаАмортизацииЦФ.СуммаПР = ?(СписатьОстатокПР, ВыборкаДанных.ОстаточнаяСтоимостьПРЦФ, СтрокаАмортизации.СуммаПР * ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымПР);
				СтрокаАмортизацииЦФ.СуммаВР = СтрокаАмортизацииЦФ.СуммаБУ - СтрокаАмортизацииЦФ.СуммаНУ - СтрокаАмортизацииЦФ.СуммаПР;
				СтрокаАмортизацииЦФ.Коэффициент = ?(СписатьОстатокБУ, 1, СтрокаАмортизации.Коэффициент * ВыборкаДанных.ПропорцияЦелевыхСредствКСобственнымБУ);
				
				СтрокаАмортизации.СуммаБУ = ?(СписатьОстатокБУ, ВыборкаДанных.ОстаточнаяСтоимостьБУ, СтрокаАмортизации.СуммаБУ - СтрокаАмортизацииЦФ.СуммаБУ);
				СтрокаАмортизации.СуммаНУ = ?(СписатьОстатокНУ, ВыборкаДанных.ОстаточнаяСтоимостьНУ, СтрокаАмортизации.СуммаНУ - СтрокаАмортизацииЦФ.СуммаНУ);
				СтрокаАмортизации.СуммаПР = ?(СписатьОстатокПР, ВыборкаДанных.ОстаточнаяСтоимостьПР, СтрокаАмортизации.СуммаПР - СтрокаАмортизацииЦФ.СуммаПР);
				СтрокаАмортизации.СуммаВР = СтрокаАмортизации.СуммаБУ - СтрокаАмортизации.СуммаНУ - СтрокаАмортизации.СуммаПР;
				СтрокаАмортизации.Коэффициент = СтрокаАмортизации.Коэффициент - СтрокаАмортизацииЦФ.Коэффициент;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивРезультат;
	
КонецФункции

Процедура ДобавитьСтрокиВТаблицуАмортизации(Таблица, ВыборкаДанных, НовыеСтроки, ТаблицаРаспределения)
	
	Для Каждого Строка Из НовыеСтроки Цикл
		
		Если (Строка.СуммаБУ = 0 И Строка.СуммаНУ=0 И Строка.СуммаПР=0 И Строка.СуммаВР=0) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТаблицаРаспределения = Неопределено Тогда
			СтрокаТаблицыЗатрат = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ВыборкаДанных);
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, Строка);
		Иначе
			МассивКоэффициентов = ТаблицаРаспределения.ВыгрузитьКолонку("Коэффициент");
			СуммыБУ = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаБУ, МассивКоэффициентов);
			СуммыНУ = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаНУ, МассивКоэффициентов);
			СуммыПР = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаПР, МассивКоэффициентов);
			СуммыВР = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.СуммаВР, МассивКоэффициентов);
			Коэффициенты = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Строка.Коэффициент, МассивКоэффициентов, 100);
			
			Если СуммыБУ = Неопределено И СуммыНУ = Неопределено И СуммыПР = Неопределено И СуммыВР = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Индекс=0 По ТаблицаРаспределения.Количество()-1 Цикл
				ПравилоОтражения = ТаблицаРаспределения[Индекс];
				СтрокаТаблицыЗатрат = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ВыборкаДанных);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, Строка);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗатрат, ПравилоОтражения);
				СтрокаТаблицыЗатрат.СуммаБУ = ?(СуммыБУ=Неопределено, 0, СуммыБУ[Индекс]);
				СтрокаТаблицыЗатрат.СуммаНУ = ?(СуммыНУ=Неопределено, 0, СуммыНУ[Индекс]);
				СтрокаТаблицыЗатрат.СуммаПР = ?(СуммыПР=Неопределено, 0, СуммыПР[Индекс]);
				СтрокаТаблицыЗатрат.СуммаВР = ?(СуммыВР=Неопределено, 0, СуммыВР[Индекс]);
				СтрокаТаблицыЗатрат.Коэффициент = ?(Коэффициенты=Неопределено, 0, Коэффициенты[Индекс]);
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьПараметрыАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ТаблицаНМА

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"         // <Число, 5, 0>
	+ "НематериальныйАктив"; // <СправочникСсылка.НематериальныйАктив>

	Параметры.Вставить("ТаблицаНМА",
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПараметрыПроведения(ТаблицаНМА, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"      // <Дата>
	+ "ДатаРасчета," // <Дата>
	+ "Организация," // <СправочникСсылка.Организации>
	+ "Регистратор," // <ДокументСсылка.*>
	+ "ИмяСписка,"   // <Строка>
	+ "ВыдаватьСообщения";

	Параметры.Вставить("Реквизиты",
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПараметрыПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;

КонецФункции

// ПРИНЯТИЕ К УЧЕТУ НМА

// Регистрация состояний НМА

Процедура ПроверитьВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|    ПорядокУчетаНМАБУ.НематериальныйАктив,
	|    ПорядокУчетаНМАБУ.НематериальныйАктив.Код КАК КодНМА,
	|    ПорядокУчетаНМАБУ.Регистратор,
	|    ПорядокУчетаНМАБУ.НомерСтроки
	|ИЗ
	|    РегистрСведений.ПорядокУчетаНМАБУ.СрезПоследних(
	|        &Период,
	|        Организация = &Организация
	|            И НематериальныйАктив В (&НематериальныеАктивы)
	|            И СостояниеБУ = &Состояние
	|            И (НЕ Регистратор = &ТекущийРегистратор)) КАК ПорядокУчетаНМАБУ";
	
	Запрос.УстановитьПараметр("Организация",          Реквизиты.Организация);
	Запрос.УстановитьПараметр("Состояние",            Реквизиты.Состояние);
	Запрос.УстановитьПараметр("НематериальныеАктивы", Реквизиты.НематериальныйАктив);
	Запрос.УстановитьПараметр("ТекущийРегистратор",   Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("Период",               Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();

		ШаблонСообщения = НСтр("ru = 'Для нематериального актива <%1 (%2)> в организации <%3> уже зафиксировано состояние <%4>
			|документом <%5>';
			|en = 'State <%4> is already recorded for the <%1 (%2)> intangible asset in the <%3> company with
			|the <%5> document'");

		Пока Выборка.Следующий() Цикл
			ТекстСообщения = СтрШаблон(ШаблонСообщения, 
											Выборка.НематериальныйАктив,
											Выборка.КодНМА, 
											Реквизиты.Организация, 
											Реквизиты.Состояние, 
											Выборка.Регистратор);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Реквизиты.Регистратор, Реквизиты.ИмяСписка + "["
				+ Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].НематериальныйАктив", "Объект", Отказ);
		КонецЦикла;

		Возврат;
	КонецЕсли;

КонецПроцедуры

Функция ПодготовитьПараметрыВозможностьИзмененияСостоянияНМА(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив," // <СправочникСсылка.НематериальныхАктивов>
	+ "Период,"              // <Дата>
	+ "ИмяСписка,"           // <Строка, 0>
	+ "Организация,"         // <СправочникСсылка.Организации>
	+ "Состояние,"           // <ПеречислениеСсылка.СостоянияНМА> - состояние НМА, которое требуется установить
	+ "Регистратор";         // <ДокументСсылка.*>

	Параметры.Вставить("Реквизиты",
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПараметрыПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

// СОСТОЯНИЕ НМА

// Получает документ и дату выбытия нематериального актива по бух учету.
//
// Параметры:
//
//
// Возвращаемое значение
//  Дата и документ - через указанные параметры процедуры.
//
Процедура СведенияОВыбытииНМА(НематериальныйАктив, Организация, ДокРегистратор, ДатаДокРегистратора) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияНМА.ДокументСписания КАК Документ,
	|	ПервоначальныеСведенияНМА.ДокументСписания.Дата КАК Дата
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(, Организация = &Организация И НематериальныйАктив = &НМА) КАК ПервоначальныеСведенияНМА
	|ГДЕ
	|	НЕ ПервоначальныеСведенияНМА.ДокументСписания.Дата ЕСТЬ NULL";

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НМА", НематериальныйАктив);
	Запрос.УстановитьПараметр("Организация", Организация);
	ВыборкаДоков = Запрос.Выполнить().Выбрать();

	Если ВыборкаДоков.Следующий()  Тогда
		ДокРегистратор      = ВыборкаДоков.Документ;
		ДатаДокРегистратора = ВыборкаДоков.Дата;

	Иначе
		ДокРегистратор      = Неопределено;
		ДатаДокРегистратора = '00010101';

	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьСоответствиеНМАПодразделению(Объект, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Новый Граница(Новый МоментВремени(Объект.Дата, Объект.Ссылка), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("НематериальныйАктив", Объект.НематериальныйАктив);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаОтражения.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ СчетаОтражения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМА.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив = &НематериальныйАктив
	|				И Регистратор <> &Ссылка) КАК СчетаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаОтражения.СчетУчета
	|ИЗ
	|	РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(
	|			&Период,
	|			НематериальныйАктив = &НематериальныйАктив
	|				И Регистратор <> &Ссылка) КАК СчетаОтражения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеСчетУчета.Субконто1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаОтражения.СчетУчета
	|				ИЗ
	|					СчетаОтражения),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Подразделение = &Подразделение
	|				И Субконто1 = &НематериальныйАктив) КАК ДанныеСчетУчета";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нематериальный актив ""%1"" не отражался в учете в подразделении ""%2""';
				|en = 'Intangible asset ""%1"" was not recorded in accounting in business unit ""%2""'"),
			Объект.НематериальныйАктив,
			Объект.Подразделение);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Объект.Ссылка,,,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
