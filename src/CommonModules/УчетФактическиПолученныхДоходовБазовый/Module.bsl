
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает Таблицу соответствия КодовДоходаНДФЛ, Категории начисления (для кода дохода 4800) и Категории дохода
// Если заполнен параметр КодДоходаНДФЛ тип СправочникСсылка.ВидыДоходовНДФЛ, то соответствие будет возвращено только
// для этого кода.
//
Функция СопоставлениеКодовИКатегорийДоходовНДФЛ(КодДоходаНДФЛ = Неопределено) Экспорт
	
	СоответствиеКодов = Новый ТаблицаЗначений;
	СоответствиеКодов.Колонки.Добавить("КодДохода", Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	СоответствиеКодов.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	СоответствиеКодов.Колонки.Добавить("КатегорияНачисления", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииНачисленийИНеоплаченногоВремени"));
	СоответствиеКодов.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)));
	
	ДополнительныеДивиденды = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Перечисления.КатегорииДоходовНДФЛ.Дивиденды(), ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.КатегорииДоходовНДФЛ.Дивиденды));
	АвторскиеРоялти = Перечисления.КатегорииДоходовНДФЛ.АвторскиеРоялти();
	ПрочиеДоходыПоОблигациям = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыПоОблигациям);
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВидыДоходовНДФЛ.Ссылка КАК КодДохода,
	|	ВидыДоходовНДФЛ.КодПрименяемыйВНалоговойОтчетностиС2021Года КАК Код,
	|	ВидыДоходовНДФЛ.Код КАК ДополнительныйКод
	|ИЗ
	|	Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|ГДЕ
	|	&Условие";
	Если ЗначениеЗаполнено(КодДоходаНДФЛ) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ВидыДоходовНДФЛ.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", КодДоходаНДФЛ);
	Иначе
		Запрос.УстановитьПараметр("Условие", Истина);
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(Выборка.КодДохода);
		
		Если Выборка.Код = "2002" Тогда
			Если КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда Тогда
				ДополнительныеКатегории = Новый Массив;
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности);
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТрудаАУСН);
				УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
			Иначе // как прочие доходы от трудовой деятельности в денежной форме
				ДополнительныеКатегории = Новый Массив;
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
				УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
			КонецЕсли;
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТрудаАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавки Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавкиАУСН);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТрудаАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ДовольствиеСВОвДенежнойФорме Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДовольствиеСВОвНатуральнойФорме);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыЗаймЦБ
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыИИС
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыЦФА
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыПоСтрахованиюИПенсОбеспечению
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыОтДолевогоУчастия
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыПоВыигрышам
			Или КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыРЕПО Тогда
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода);
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыЦБ Тогда
			Если Выборка.Код = "1532"
				Или Выборка.Код = "1533"
				Или Выборка.Код = "1535" Тогда
				УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода);
			Иначе
				УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ПрочиеДоходыПоОблигациям);
			КонецЕсли;
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.Дивиденды Тогда
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеДивиденды);
			
			// Оплата труда в натуральной форме
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
			
		// Прочие доходы от трудовой деятельности в денежной форме
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			Если Выборка.Код = "2003" Или Выборка.Код = "2010" Тогда
				ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			КонецЕсли;
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода, ДополнительныеКатегории);
			
		ИначеЕсли Выборка.КодДохода = Справочники.ВидыДоходовНДФЛ.Код2720 Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы, ДополнительныеКатегории);
											
		ИначеЕсли Выборка.КодДохода = Справочники.ВидыДоходовНДФЛ.Код2720c2021 Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы, ДополнительныеКатегории);
											
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы Тогда
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода);
			
		ИначеЕсли КатегорияДохода = Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ Тогда
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, КатегорияДохода);
			
		ИначеЕсли Выборка.Код = "2015" Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы,
											ДополнительныеКатегории);
											
		ИначеЕсли Выборка.Код = "2017" Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы,
											ДополнительныеКатегории);
											
		ИначеЕсли Выборка.Код = "2018" Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ДоходВНатуральнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности,
											ДополнительныеКатегории);
											
		// ПрочиеДоходы
		ИначеЕсли Выборка.Код = "1011"
			Или Выборка.Код = "1110"
			Или Выборка.Код = "1300"
			Или Выборка.Код = "1301"
			Или Выборка.Код = "2210" Тогда
			
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеКатегории, АвторскиеРоялти);
			
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы, 
											ДополнительныеКатегории);

		ИначеЕсли Выборка.Код = "2201"
			Или Выборка.Код = "2202"
			Или Выборка.Код = "2203"
			Или Выборка.Код = "2204"
			Или Выборка.Код = "2205"
			Или Выборка.Код = "2206"
			Или Выборка.Код = "2207"
			Или Выборка.Код = "2208"
			Или Выборка.Код = "2209" Тогда
			
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеКатегории, АвторскиеРоялти);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы, 
											ДополнительныеКатегории);
			
		ИначеЕсли Выборка.Код = "2760" Тогда
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы,
											ДополнительныеКатегории);
			
		// код 4800 обрабатываем отдельно
		ИначеЕсли Выборка.Код = "4800" Тогда
			
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности, 
											ДополнительныеКатегории,
											Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности, 
											ДополнительныеКатегории,
											Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности, 
											ДополнительныеКатегории,
											Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности, 
											ДополнительныеКатегории,
											Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаКомандировки);
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы, 
											ДополнительныеКатегории,
											Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее);
			
		// все остальные
		Иначе
			ДополнительныеКатегории = Новый Массив;
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеНатуральныеДоходы);
			ДополнительныеКатегории.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
			УчетФактическиПолученныхДоходов.НовоеСоответствиеКодуДохода(СоответствиеКодов, Выборка.КодДохода, 
											Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходы, 
											ДополнительныеКатегории);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоответствиеКодов;
	
КонецФункции

// Регистрирует полученный доход "Начислятелей" на новую дату получения дохода
// Параметры:
//		Регистратор - ДокументСсылка - документ выплаты
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - должен содержать временные таблицы 
//      	ВТСписокСотрудников, с данными о выплатах вида:
//				ФизическоеЛицо: должно быть непустым
//          	СуммаВыплаты.
//          	ДокументОснование, необязательная
//          	СтатьяФинансирования, необязательная
//          	СтатьяРасходов, необязательная
//          	СуммаНачисленная, необязательная
//          	СуммаВыплаченная, необязательная, 
//			Если колонки СуммаНачисленная, СуммаВыплаченная отсутствуют, возможная частичная выплата не будет учтена.
//		Движения - коллекция движений регистратора.
//		ДатаВыплаты - дата - новая дата получения дохода.
//		ДатаОперации - дата - дата, которой будет зарегистрировано движение.
//		Отказ - признак отказа от заполнения движений.
//
Процедура ЗарегистрироватьНовуюДатуПолученияДохода(Регистратор, Движения, МенеджерВременныхТаблиц, ДатаВыплаты, ДатаОперации, Отказ, Записывать = Ложь) Экспорт
	
	Если Отказ Или ДатаВыплаты < УчетФактическиПолученныхДоходов.ДатаНачалаИспользованияПодсистемы() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицыКУдалению = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ДоляПолнойВыплаты", УчетНДФЛ.ДоляПолнойВыплаты());
	
	ПеренесенныхДоходы = Новый ТаблицаЗначений;
	ПеренесенныхДоходы.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ПеренесенныхДоходы.Колонки.Добавить("КатегорияДохода", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииДоходовНДФЛ"));
	ПеренесенныхДоходы.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ДокументыОснованияНДФЛ.Тип);
	ПеренесенныхДоходы.Колонки.Добавить("СуммаДохода", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ПеренесенныхДоходы.Колонки.Добавить("ОбщаяСуммаДохода", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ПеренесенныхДоходы.Колонки.Добавить("РанееПеренесеннаяСуммаДохода", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	ВТСписокСотрудников КАК ДанныеВедомости
	|ГДЕ
	|	ДанныеВедомости.СуммаВыплаты < 0";
	Если Запрос.Выполнить().Пустой() Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеВедомости.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование КАК ДокументОснование,
		|	ДанныеВедомости.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеВедомости.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(ДанныеВедомости.СуммаВыплаты) КАК СуммаВыплаты,
		|	СУММА(ДанныеВедомости.СуммаНачисленная) КАК СуммаНачисленная,
		|	СУММА(ДанныеВедомости.СуммаВыплаченная) КАК СуммаВыплаченная
		|ПОМЕСТИТЬ ВТДанныеДляОтбора
		|ИЗ
		|	ВТСписокСотрудников КАК ДанныеВедомости
		|ГДЕ
		|	ДанныеВедомости.ДокументОснование <> НЕОПРЕДЕЛЕНО
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВедомости.ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование,
		|	ДанныеВедомости.СтатьяФинансирования,
		|	ДанныеВедомости.СтатьяРасходов
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеВедомости.СуммаВыплаты) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо,
		|	ДокументОснование";
		КолонкиРезультата = МенеджерВременныхТаблиц.Таблицы["ВТСписокСотрудников"].Колонки;
		Если КолонкиРезультата.Найти("ДокументОснование") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.ДокументОснование", "НЕОПРЕДЕЛЕНО") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СуммаНачисленная") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СуммаНачисленная", "0") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СуммаВыплаченная") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СуммаВыплаченная", "0") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СтатьяФинансирования") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СтатьяФинансирования", "НЕОПРЕДЕЛЕНО");
		КонецЕсли;
		Если КолонкиРезультата.Найти("СтатьяРасходов") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СтатьяРасходов", "НЕОПРЕДЕЛЕНО") 
		КонецЕсли;
		Запрос.Текст = ТекстЗапроса;
		
		ТаблицыКУдалению.Добавить("ВТДанныеДляОтбора");
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеВедомости.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование КАК ДокументОснование
		|ПОМЕСТИТЬ ВТПоложительныеДоходы
		|ИЗ
		|	ВТСписокСотрудников КАК ДанныеВедомости
		|ГДЕ
		|	ДанныеВедомости.ДокументОснование <> НЕОПРЕДЕЛЕНО
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВедомости.ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеВедомости.СуммаВыплаты) > 0";
		КолонкиРезультата = МенеджерВременныхТаблиц.Таблицы["ВТСписокСотрудников"].Колонки;
		Если КолонкиРезультата.Найти("ДокументОснование") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.ДокументОснование", "НЕОПРЕДЕЛЕНО") 
		КонецЕсли;
		Запрос.Текст = ТекстЗапроса;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеВедомости.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование КАК ДокументОснование,
		|	ДанныеВедомости.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеВедомости.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(ДанныеВедомости.СуммаВыплаты) КАК СуммаВыплаты,
		|	СУММА(ДанныеВедомости.СуммаНачисленная) КАК СуммаНачисленная,
		|	СУММА(ДанныеВедомости.СуммаВыплаченная) КАК СуммаВыплаченная
		|ПОМЕСТИТЬ ВТДанныеДляОтбора
		|ИЗ
		|	ВТСписокСотрудников КАК ДанныеВедомости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоложительныеДоходы КАК ПоложительныеДоходы
		|		ПО ДанныеВедомости.ФизическоеЛицо = ПоложительныеДоходы.ФизическоеЛицо
		|			И ДанныеВедомости.ДокументОснование = ПоложительныеДоходы.ДокументОснование
		|ГДЕ
		|	ДанныеВедомости.ДокументОснование <> НЕОПРЕДЕЛЕНО
		|	И ПоложительныеДоходы.ФизическоеЛицо ЕСТЬ НЕ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВедомости.ФизическоеЛицо,
		|	ДанныеВедомости.ДокументОснование,
		|	ДанныеВедомости.СтатьяФинансирования,
		|	ДанныеВедомости.СтатьяРасходов
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеВедомости.СуммаВыплаты) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо,
		|	ДокументОснование";
		КолонкиРезультата = МенеджерВременныхТаблиц.Таблицы["ВТСписокСотрудников"].Колонки;
		Если КолонкиРезультата.Найти("ДокументОснование") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.ДокументОснование", "НЕОПРЕДЕЛЕНО") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СуммаНачисленная") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СуммаНачисленная", "0") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СуммаВыплаченная") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СуммаВыплаченная", "0") 
		КонецЕсли;
		Если КолонкиРезультата.Найти("СтатьяФинансирования") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СтатьяФинансирования", "НЕОПРЕДЕЛЕНО");
		КонецЕсли;
		Если КолонкиРезультата.Найти("СтатьяРасходов") = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеВедомости.СтатьяРасходов", "НЕОПРЕДЕЛЕНО") 
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов() + ТекстЗапроса;
		
		ТаблицыКУдалению.Добавить("ВТПоложительныеДоходы");
		ТаблицыКУдалению.Добавить("ВТДанныеДляОтбора");
	КонецЕсли;
	Запрос.Выполнить();
	
	// Коэффициенты
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Выплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Выплаты.ДокументОснование КАК ДокументОснование,
	|	Выплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Выплаты.СтатьяРасходов КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА Выплаты.СуммаНачисленная = 0
	|			ТОГДА 1
	|		КОГДА Выплаты.СуммаНачисленная < 0
	|				И Выплаты.СуммаВыплаты = Выплаты.СуммаНачисленная
	|			ТОГДА 1
	|		КОГДА Выплаты.СуммаВыплаченная + Выплаты.СуммаВыплаты > Выплаты.СуммаНачисленная * &ДоляПолнойВыплаты
	|			ТОГДА 1
	|		ИНАЧЕ Выплаты.СуммаВыплаты / Выплаты.СуммаНачисленная
	|	КОНЕЦ КАК ДоляВыплаты
	|ПОМЕСТИТЬ ВТКоэффициентыВыплатыПоСтатьям
	|ИЗ
	|	ВТДанныеДляОтбора КАК Выплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Выплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Выплаты.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТОснованияФизическихЛиц
	|ИЗ
	|	ВТКоэффициентыВыплатыПоСтатьям КАК Выплаты
	|ГДЕ
	|	Выплаты.ДоляВыплаты > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ Различные
	|	Выплаты.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	ВТДанныеДляОтбора КАК Выплаты";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Количество() = 1 Тогда
		РезультатЗапроса.Следующий();
		ОтборДокументОснование = РезультатЗапроса.ДокументОснование;
	Иначе
		ОтборДокументОснование = Ложь;
	КонецЕсли;
	
	ТаблицыКУдалению.Добавить("ВТКоэффициентыВыплатыПоСтатьям");
	ТаблицыКУдалению.Добавить("ВТОснованияФизическихЛиц");
	
	// Учет доходов для исчисления НДФЛ
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
	|	СведенияОДоходахНДФЛ.КодВычета КАК КодВычета,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СведенияОДоходахНДФЛ.СтавкаНалогообложения КАК СтавкаНалогообложения,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СведенияОДоходахНДФЛ.Организация КАК Организация,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление,
	|	СведенияОДоходахНДФЛ.Подразделение КАК Подразделение,
	|	СведенияОДоходахНДФЛ.Сотрудник КАК Сотрудник,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ДоходМежрасчетногоПериода КАК ДоходМежрасчетногоПериода,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.НеУчитыватьДоходВ6НДФЛ КАК НеУчитыватьДоходВ6НДФЛ,
	|	СведенияОДоходахНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СведенияОДоходахНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДоходаФиксирована КАК ДатаПолученияДоходаФиксирована,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ЗарегистрированоПриНачисленииАванса КАК ЗарегистрированоПриНачисленииАванса,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВычета,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА СведенияОДоходахНДФЛ.Регистратор = СведенияОДоходахНДФЛ.ДокументОснование
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА СведенияОДоходахНДФЛ.Регистратор = СведенияОДоходахНДФЛ.ДокументОснование
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаВычета,
	|	СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода КАК УстаревшаяДатаПолученияДохода,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ СведенияОДоходахНДФЛ.СуммаДохода
	|		КОНЕЦ) КАК ПеренесеннаяСуммаДохода
	|ПОМЕСТИТЬ ВТЗаписиДоходов
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснованияФизическихЛиц КАК Выплаты
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = Выплаты.ФизическоеЛицо
	|			И (&СоединениеДокументОснование)
	|ГДЕ
	|	&ОтборДокументОснование
	|	И СведенияОДоходахНДФЛ.Регистратор <> &Регистратор
	|	И НЕ СведенияОДоходахНДФЛ.ДатаПолученияДоходаФиксирована
	|	И СведенияОДоходахНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И СведенияОДоходахНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДоходыПредыдущихРедакций)
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.НеУчитыватьДоходВ6НДФЛ,
	|	СведенияОДоходахНДФЛ.СтатьяРасходов,
	|	СведенияОДоходахНДФЛ.СтавкаНалогообложения,
	|	СведенияОДоходахНДФЛ.ДокументОснование,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	СведенияОДоходахНДФЛ.СтатьяФинансирования,
	|	СведенияОДоходахНДФЛ.МесяцНалоговогоПериода,
	|	СведенияОДоходахНДФЛ.ГоловнаяОрганизация,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.КодВычета,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДоходаФиксирована,
	|	СведенияОДоходахНДФЛ.КатегорияДохода,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.ДоходМежрасчетногоПериода,
	|	СведенияОДоходахНДФЛ.Сотрудник,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.Организация,
	|	СведенияОДоходахНДФЛ.РегистрацияВНалоговомОргане,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.Подразделение,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ЗарегистрированоПриНачисленииАванса
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВЫБОР
	|				КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				КОГДА СведенияОДоходахНДФЛ.Регистратор = СведенияОДоходахНДФЛ.ДокументОснование
	|					ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ СведенияОДоходахНДФЛ.СуммаДохода
	|			КОНЕЦ) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	СУММА(СведенияОДоходахНДФЛ.ВаловаяСуммаДохода) КАК СуммаДохода,
	|	СУММА(СведенияОДоходахНДФЛ.ПеренесеннаяСуммаДохода) КАК ПеренесеннаяСуммаДохода
	|ПОМЕСТИТЬ ВТИтогиПоДокументам
	|ИЗ
	|	ВТЗаписиДоходов КАК СведенияОДоходахНДФЛ
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.ДокументОснование,
	|	СведенияОДоходахНДФЛ.КатегорияДохода,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
	|	СведенияОДоходахНДФЛ.КодВычета КАК КодВычета,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СведенияОДоходахНДФЛ.СтавкаНалогообложения КАК СтавкаНалогообложения,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СведенияОДоходахНДФЛ.Организация КАК Организация,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление,
	|	СведенияОДоходахНДФЛ.Подразделение КАК Подразделение,
	|	СведенияОДоходахНДФЛ.Сотрудник КАК Сотрудник,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ДоходМежрасчетногоПериода КАК ДоходМежрасчетногоПериода,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.НеУчитыватьДоходВ6НДФЛ КАК НеУчитыватьДоходВ6НДФЛ,
	|	СведенияОДоходахНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СведенияОДоходахНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДоходаФиксирована КАК ДатаПолученияДоходаФиксирована,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	СведенияОДоходахНДФЛ.СуммаДохода КАК СуммаДохода,
	|	СведенияОДоходахНДФЛ.СуммаВычета КАК СуммаВычета,
	|	СведенияОДоходахНДФЛ.ВаловаяСуммаДохода КАК ВаловаяСуммаДохода,
	|	СведенияОДоходахНДФЛ.ВаловаяСуммаВычета КАК ВаловаяСуммаВычета,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ЗарегистрированоПриНачисленииАванса КАК ЗарегистрированоПриНачисленииАванса,
	|	ЕСТЬNULL(ВаловыеСуммы.СуммаДохода, 0) КАК СуммаДоходаДокумента,
	|	ЕСТЬNULL(ВаловыеСуммы.ПеренесеннаяСуммаДохода, 0) КАК ПеренесеннаяСуммаДохода,
	|	Выплаты.ДоляВыплаты КАК ДоляВыплаты
	|ИЗ
	|	ВТЗаписиДоходов КАК СведенияОДоходахНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоэффициентыВыплатыПоСтатьям КАК Выплаты
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = Выплаты.ФизическоеЛицо
	|			И СведенияОДоходахНДФЛ.ДокументОснование = Выплаты.ДокументОснование
	|			И (СведенияОДоходахНДФЛ.СтатьяФинансирования = Выплаты.СтатьяФинансирования
	|				ИЛИ Выплаты.СтатьяФинансирования = НЕОПРЕДЕЛЕНО)
	|			И (СведенияОДоходахНДФЛ.СтатьяРасходов = Выплаты.СтатьяРасходов
	|				ИЛИ Выплаты.СтатьяРасходов = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиПоДокументам КАК ВаловыеСуммы
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = ВаловыеСуммы.ФизическоеЛицо
	|			И СведенияОДоходахНДФЛ.КатегорияДохода = ВаловыеСуммы.КатегорияДохода
	|			И СведенияОДоходахНДФЛ.ДокументОснование = ВаловыеСуммы.ДокументОснование
	|ГДЕ
	|	СведенияОДоходахНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|	И Выплаты.ДоляВыплаты > 0";
	ТаблицыКУдалению.Добавить("ВТЗаписиДоходов");
	ТаблицыКУдалению.Добавить("ВТИтогиПоДокументам");
	
	Если ОтборДокументОснование = Ложь Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СоединениеДокументОснование","СведенияОДоходахНДФЛ.ДокументОснование = Выплаты.ДокументОснование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОтборДокументОснование","Истина");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СоединениеДокументОснование","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОтборДокументОснование","СведенияОДоходахНДФЛ.ДокументОснование = &ОтборДокументОснование");
		Запрос.УстановитьПараметр("ОтборДокументОснование",ОтборДокументОснование);
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
		
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ФизическиеЛица = Новый Массив;
		Организация = Неопределено;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СуммаДохода = 0;
			СуммаВычета = 0;
			Если Выборка.СуммаДохода * Выборка.СуммаДохода > (Выборка.ВаловаяСуммаДохода * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаДохода * Выборка.ДоляВыплаты) Тогда
				СуммаДохода = Выборка.ВаловаяСуммаДохода * Выборка.ДоляВыплаты;
			Иначе
				СуммаДохода = Выборка.СуммаДохода;
			КонецЕсли;
			Если Выборка.СуммаВычета * Выборка.СуммаВычета > (Выборка.ВаловаяСуммаВычета * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаВычета * Выборка.ДоляВыплаты) Тогда
				СуммаВычета = Выборка.ВаловаяСуммаВычета * Выборка.ДоляВыплаты;
			Иначе
				СуммаВычета = Выборка.СуммаВычета;
			КонецЕсли;
			
			ПеренесенныйДоход = ПеренесенныхДоходы.Добавить();
			ПеренесенныйДоход.ФизическоеЛицо = Выборка.ФизическоеЛицо;
			ПеренесенныйДоход.ДокументОснование = Выборка.ДокументОснование;
			ПеренесенныйДоход.КатегорияДохода = Выборка.КатегорияДохода;
			ПеренесенныйДоход.СуммаДохода = СуммаДохода;
			ПеренесенныйДоход.ОбщаяСуммаДохода = Выборка.СуммаДоходаДокумента;
			ПеренесенныйДоход.РанееПеренесеннаяСуммаДохода = Выборка.ПеренесеннаяСуммаДохода;
			
			Если СуммаДохода = 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			НовоеДвижение = Движения.СведенияОДоходахНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
			НовоеДвижение.СуммаДохода = - СуммаДохода;
			НовоеДвижение.СуммаВычета = - СуммаВычета;
			НовоеДвижение.Период = ДатаОперации;
			
			НовоеДвижение = Движения.СведенияОДоходахНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
			НовоеДвижение.СуммаДохода = СуммаДохода;
			НовоеДвижение.СуммаВычета = СуммаВычета;
			НовоеДвижение.УстаревшаяДатаПолученияДохода = Выборка.ДатаПолученияДохода;
			НовоеДвижение.ДатаПолученияДохода = ДатаВыплаты;
			НовоеДвижение.МесяцНалоговогоПериода = НачалоМесяца(ДатаВыплаты);
			НовоеДвижение.Период = ДатаОперации;
			
			ФизическиеЛица.Добавить(Выборка.ФизическоеЛицо);
			Если Не ЗначениеЗаполнено(Организация) Тогда
				Организация = Выборка.Организация
			КонецЕсли;
		КонецЦикла;
		Если Записывать Тогда
			Движения.СведенияОДоходахНДФЛ.Записать();
			Движения.СведенияОДоходахНДФЛ.Записывать = Ложь;
		Иначе
			Движения.СведенияОДоходахНДФЛ.Записывать = Истина;
		КонецЕсли;
		УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеДляМежрасчетногоДокумента(Движения, Отказ, Организация, ФизическиеЛица, Регистратор);
	КонецЕсли;
	
	// Расчеты налогоплательщиков с бюджетом по НДФЛ
	ПеренесенныхДоходы.Свернуть("ФизическоеЛицо, ДокументОснование, КатегорияДохода, ОбщаяСуммаДохода, РанееПеренесеннаяСуммаДохода", "СуммаДохода");
	ВсегоСтрок = ПеренесенныхДоходы.Количество();
	Для Инд = 1 По ВсегоСтрок Цикл
		СтрокаДохода = ПеренесенныхДоходы[ВсегоСтрок - Инд];
		Если СтрокаДохода.СуммаДохода = 0 Тогда
			ПеренесенныхДоходы.Удалить(СтрокаДохода);
		КонецЕсли;
	КонецЦикла;
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ПеренесенныхДоходы, "ВТПеренесенныеДоходы");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИСТИНА КАК СоединятьПоДокументам,
	|	ПеренесенныеДоходы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПеренесенныеДоходы.КатегорияДохода КАК КатегорияДохода,
	|	ПеренесенныеДоходы.ДокументОснование КАК ДокументОснование,
	|	ПеренесенныеДоходы.СуммаДохода КАК СуммаДохода,
	|	ПеренесенныеДоходы.ОбщаяСуммаДохода КАК ОбщаяСуммаДохода,
	|	ПеренесенныеДоходы.РанееПеренесеннаяСуммаДохода КАК РанееПеренесеннаяСуммаДохода
	|ПОМЕСТИТЬ ВТДолиПеренесенныхДоходов
	|ИЗ
	|	ВТПеренесенныеДоходы КАК ПеренесенныеДоходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ПеренесенныеДоходы.ФизическоеЛицо,
	|	ПеренесенныеДоходы.КатегорияДохода,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ПеренесенныеДоходы.СуммаДохода),
	|	СУММА(ПеренесенныеДоходы.ОбщаяСуммаДохода),
	|	0
	|ИЗ
	|	ВТПеренесенныеДоходы КАК ПеренесенныеДоходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеренесенныеДоходы.ФизическоеЛицо,
	|	ПеренесенныеДоходы.КатегорияДохода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДляОтбора.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеДляОтбора.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТОснованияИсчисленногоНалога
	|ИЗ
	|	ВТДанныеДляОтбора КАК ДанныеДляОтбора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВЫБОР
	|		КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|		ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода
	|	КОНЕЦ КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода КАК УстаревшаяДатаПолученияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания КАК ВариантУдержания,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода КАК РасчетМежрасчетногоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления КАК СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты КАК КрайнийСрокУплаты,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УчитыватьВыплаченныйДоходВ6НДФЛ КАК УчитыватьВыплаченныйДоходВ6НДФЛ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ЗарегистрированоПриНачисленииАванса КАК ЗарегистрированоПриНачисленииАванса,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаСПревышения,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаСПревышенияПоСтавке18,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаСПревышенияПоСтавке20,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаСПревышенияПоСтавке22,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСумма,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаСПревышения,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаСПревышенияПоСтавке18,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаСПревышенияПоСтавке20,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор = РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|				ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВаловаяСуммаСПревышенияПоСтавке22,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|		КОНЕЦ) КАК ПеренесеннаяСумма,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
	|		КОНЕЦ) КАК ПеренесеннаяСуммаСПревышения,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18
	|		КОНЕЦ) КАК ПеренесеннаяСуммаСПревышенияПоСтавке18,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20
	|		КОНЕЦ) КАК ПеренесеннаяСуммаСПревышенияПоСтавке20,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22
	|		КОНЕЦ) КАК ПеренесеннаяСуммаСПревышенияПоСтавке22
	|ПОМЕСТИТЬ ВТРасчетыНалогоплательщиков
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснованияИсчисленногоНалога КАК Основания
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = Основания.ФизическоеЛицо
	|			И (&СоединениеДокументОснование)
	|ГДЕ
	|	&ОтборДокументОснование
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор <> &Регистратор
	|	И НЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДатаПолученияДоходаФиксирована
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода <> ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДоходыПредыдущихРедакций)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УчитыватьВыплаченныйДоходВ6НДФЛ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	ВЫБОР
	|		КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|		ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода
	|	КОНЕЦ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ЗарегистрированоПриНачисленииАванса,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22
	|				ИНАЧЕ 0
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20
	|			КОНЕЦ) <> 0
	|		ИЛИ СУММА(ВЫБОР
	|				КОГДА РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА 0
	|				ИНАЧЕ РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22
	|			КОНЕЦ) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация КАК Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания КАК ВариантУдержания,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода КАК РасчетМежрасчетногоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления КАК СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты КАК КрайнийСрокУплаты,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УчитыватьВыплаченныйДоходВ6НДФЛ КАК УчитыватьВыплаченныйДоходВ6НДФЛ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСумма КАК ВаловаяСумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения КАК СуммаСПревышения,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18 КАК СуммаСПревышенияПоСтавке18,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20 КАК СуммаСПревышенияПоСтавке20,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22 КАК СуммаСПревышенияПоСтавке22,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышения КАК ВаловаяСуммаСПревышения,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке18 КАК ВаловаяСуммаСПревышенияПоСтавке18,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке20 КАК ВаловаяСуммаСПревышенияПоСтавке20,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке22 КАК ВаловаяСуммаСПревышенияПоСтавке22,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ЗарегистрированоПриНачисленииАванса КАК ЗарегистрированоПриНачисленииАванса,
	|	ЕСТЬNULL(ПеренесенныеСуммыНалога.ПеренесеннаяСумма, 0) КАК ПеренесеннаяСумма,
	|	ЕСТЬNULL(ПеренесенныеСуммыНалога.ПеренесеннаяСуммаСПревышения, 0) КАК ПеренесеннаяСуммаСПревышения,
	|	ЕСТЬNULL(ПеренесенныеСуммыНалога.ПеренесеннаяСуммаСПревышенияПоСтавке18, 0) КАК ПеренесеннаяСуммаСПревышенияПоСтавке18,
	|	ЕСТЬNULL(ПеренесенныеСуммыНалога.ПеренесеннаяСуммаСПревышенияПоСтавке20, 0) КАК ПеренесеннаяСуммаСПревышенияПоСтавке20,
	|	ЕСТЬNULL(ПеренесенныеСуммыНалога.ПеренесеннаяСуммаСПревышенияПоСтавке22, 0) КАК ПеренесеннаяСуммаСПревышенияПоСтавке22,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Выплаты.ОбщаяСуммаДохода, 0) = 0
	|			ТОГДА 1
	|		ИНАЧЕ Выплаты.СуммаДохода / Выплаты.ОбщаяСуммаДохода
	|	КОНЕЦ КАК ДоляВыплаты,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Выплаты.ОбщаяСуммаДохода, 0) = 0
	|			ТОГДА 1
	|		ИНАЧЕ (Выплаты.СуммаДохода + Выплаты.РанееПеренесеннаяСуммаДохода) / Выплаты.ОбщаяСуммаДохода
	|	КОНЕЦ КАК ДоляВыплатыСУчетомПеренесенныхСумм
	|ИЗ
	|	ВТРасчетыНалогоплательщиков КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДолиПеренесенныхДоходов КАК Выплаты
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = Выплаты.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = Выплаты.КатегорияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = Выплаты.ДокументОснование
	|			И (Выплаты.СоединятьПоДокументам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРасчетыНалогоплательщиков КАК ПеренесенныеСуммыНалога
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ПеренесенныеСуммыНалога.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УчитыватьВыплаченныйДоходВ6НДФЛ = ПеренесенныеСуммыНалога.УчитыватьВыплаченныйДоходВ6НДФЛ
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты = ПеренесенныеСуммыНалога.КрайнийСрокУплаты
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления = ПеренесенныеСуммыНалога.СрокПеречисления
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода = ПеренесенныеСуммыНалога.РасчетМежрасчетногоПериода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль = ПеренесенныеСуммыНалога.ВключатьВДекларациюПоНалогуНаПрибыль
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания = ПеренесенныеСуммыНалога.ВариантУдержания
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ПеренесенныеСуммыНалога.ДокументОснование
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение = ПеренесенныеСуммыНалога.Подразделение
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане = ПеренесенныеСуммыНалога.РегистрацияВНалоговомОргане
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = ПеренесенныеСуммыНалога.КатегорияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ПеренесенныеСуммыНалога.СтавкаНалогообложенияРезидента
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация = ПеренесенныеСуммыНалога.ГоловнаяОрганизация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = ПеренесенныеСуммыНалога.Организация
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода = ПеренесенныеСуммыНалога.КодДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода = ПеренесенныеСуммыНалога.МесяцНалоговогоПериода
	|			И (ПеренесенныеСуммыНалога.УстаревшаяДатаПолученияДохода <> ДАТАВРЕМЯ(1, 1, 1))
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|	И ВЫБОР
	|			КОГДА ЕСТЬNULL(Выплаты.ОбщаяСуммаДохода, 0) = 0
	|				ТОГДА 1
	|			ИНАЧЕ Выплаты.СуммаДохода / Выплаты.ОбщаяСуммаДохода
	|		КОНЕЦ > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ГоловнаяОрганизация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РегистрацияВНалоговомОргане,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВариантУдержания,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.РасчетМежрасчетногоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КрайнийСрокУплаты,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УчитыватьВыплаченныйДоходВ6НДФЛ,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышения,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке18,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке20,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВаловаяСуммаСПревышенияПоСтавке22,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ЗарегистрированоПриНачисленииАванса,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Выплаты.ОбщаяСуммаДохода, 0) = 0
	|			ТОГДА 1
	|		ИНАЧЕ Выплаты.СуммаДохода / Выплаты.ОбщаяСуммаДохода
	|	КОНЕЦ,
	|	0
	|ИЗ
	|	ВТРасчетыНалогоплательщиков КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДолиПеренесенныхДоходов КАК Выплаты
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = Выплаты.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = Выплаты.КатегорияДохода
	|			И (Выплаты.СоединятьПоДокументам = ЛОЖЬ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДолиПеренесенныхДоходов КАК УчтенныеВыплаты
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = УчтенныеВыплаты.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = УчтенныеВыплаты.КатегорияДохода
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = УчтенныеВыплаты.ДокументОснование
	|			И (УчтенныеВыплаты.СоединятьПоДокументам)
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.УстаревшаяДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1)
	|	И УчтенныеВыплаты.ФизическоеЛицо ЕСТЬ NULL
	|	И ВЫБОР
	|			КОГДА ЕСТЬNULL(Выплаты.ОбщаяСуммаДохода, 0) = 0
	|				ТОГДА 1
	|			ИНАЧЕ Выплаты.СуммаДохода / Выплаты.ОбщаяСуммаДохода
	|		КОНЕЦ > 0";
	ТаблицыКУдалению.Добавить("ВТПеренесенныеДоходы");
	ТаблицыКУдалению.Добавить("ВТДолиПеренесенныхДоходов");
	ТаблицыКУдалению.Добавить("ВТОснованияИсчисленногоНалога");
	ТаблицыКУдалению.Добавить("ВТРасчетыНалогоплательщиков");
	
	Если ОтборДокументОснование = Ложь Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СоединениеДокументОснование",
												"РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = Основания.ДокументОснование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОтборДокументОснование","Истина");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СоединениеДокументОснование","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОтборДокументОснование",
												"РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = &ОтборДокументОснование");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Сумма = 0;
		СуммаСПревышения = 0;
		СуммаСПревышенияПоСтавке18 = 0;
		СуммаСПревышенияПоСтавке20 = 0;
		СуммаСПревышенияПоСтавке22 = 0;
		
		УчитыватьРанееПеренесенныеСуммы = Истина;
		Если Выборка.ДоляВыплаты = Выборка.ДоляВыплатыСУчетомПеренесенныхСумм 
			Или (Выборка.ПеренесеннаяСумма = 0 И Выборка.ПеренесеннаяСуммаСПревышения = 0 И Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке18 = 0 
				И Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке20 = 0 И Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке22 = 0) Тогда
			УчитыватьРанееПеренесенныеСуммы = Ложь;
		КонецЕсли;
		
		Если УчитыватьРанееПеренесенныеСуммы Тогда
			
			ДоляВыплаты = Выборка.ДоляВыплатыСУчетомПеренесенныхСумм;
			
			Если Выборка.Сумма * Выборка.Сумма > (Выборка.ВаловаяСумма * ДоляВыплаты - Выборка.ПеренесеннаяСумма) * (Выборка.ВаловаяСумма * ДоляВыплаты - Выборка.ПеренесеннаяСумма) Тогда
				Сумма = Выборка.ВаловаяСумма * ДоляВыплаты - Выборка.ПеренесеннаяСумма;
			Иначе
				Сумма = Выборка.Сумма;
				Если Сумма <> 0 Тогда
					УчитыватьРанееПеренесенныеСуммы = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Выборка.СуммаСПревышения * Выборка.СуммаСПревышения > (Выборка.ВаловаяСуммаСПревышения * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышения) * (Выборка.ВаловаяСуммаСПревышения * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышения) Тогда
				СуммаСПревышения = Выборка.ВаловаяСуммаСПревышения * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышения;
			Иначе
				СуммаСПревышения = Выборка.СуммаСПревышения;
				Если СуммаСПревышения <> 0 Тогда
					УчитыватьРанееПеренесенныеСуммы = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке18 * Выборка.СуммаСПревышенияПоСтавке18 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке18) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке18) Тогда
				СуммаСПревышенияПоСтавке18 = Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке18;
			Иначе
				СуммаСПревышенияПоСтавке18 = Выборка.СуммаСПревышенияПоСтавке18;
				Если СуммаСПревышенияПоСтавке18 <> 0 Тогда
					УчитыватьРанееПеренесенныеСуммы = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке20 * Выборка.СуммаСПревышенияПоСтавке20 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке20) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке20) Тогда
				СуммаСПревышенияПоСтавке20 = Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке20;
			Иначе
				СуммаСПревышенияПоСтавке20 = Выборка.СуммаСПревышенияПоСтавке20;
				Если СуммаСПревышенияПоСтавке20 <> 0 Тогда
					УчитыватьРанееПеренесенныеСуммы = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке22 * Выборка.СуммаСПревышенияПоСтавке22 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке22) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке22) Тогда
				СуммаСПревышенияПоСтавке22 = Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * ДоляВыплаты - Выборка.ПеренесеннаяСуммаСПревышенияПоСтавке22;
			Иначе
				СуммаСПревышенияПоСтавке22 = Выборка.СуммаСПревышенияПоСтавке22;
				Если СуммаСПревышенияПоСтавке22 <> 0 Тогда
					УчитыватьРанееПеренесенныеСуммы = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не УчитыватьРанееПеренесенныеСуммы Тогда
			
			ДоляВыплаты = Выборка.ДоляВыплаты;
			
			Если Выборка.Сумма * Выборка.Сумма > (Выборка.ВаловаяСумма * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСумма * Выборка.ДоляВыплаты) Тогда
				Сумма = Выборка.ВаловаяСумма * Выборка.ДоляВыплаты;
			Иначе
				Сумма = Выборка.Сумма;
			КонецЕсли;
			Если Выборка.СуммаСПревышения * Выборка.СуммаСПревышения > (Выборка.ВаловаяСуммаСПревышения * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаСПревышения * Выборка.ДоляВыплаты) Тогда
				СуммаСПревышения = Выборка.ВаловаяСуммаСПревышения * Выборка.ДоляВыплаты;
			Иначе
				СуммаСПревышения = Выборка.СуммаСПревышения;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке18 * Выборка.СуммаСПревышенияПоСтавке18 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * Выборка.ДоляВыплаты) Тогда
				СуммаСПревышенияПоСтавке18 = Выборка.ВаловаяСуммаСПревышенияПоСтавке18 * Выборка.ДоляВыплаты;
			Иначе
				СуммаСПревышенияПоСтавке18 = Выборка.СуммаСПревышенияПоСтавке18;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке20 * Выборка.СуммаСПревышенияПоСтавке20 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * Выборка.ДоляВыплаты) Тогда
				СуммаСПревышенияПоСтавке20 = Выборка.ВаловаяСуммаСПревышенияПоСтавке20 * Выборка.ДоляВыплаты;
			Иначе
				СуммаСПревышенияПоСтавке20 = Выборка.СуммаСПревышенияПоСтавке20;
			КонецЕсли;
			Если Выборка.СуммаСПревышенияПоСтавке22 * Выборка.СуммаСПревышенияПоСтавке22 > (Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * Выборка.ДоляВыплаты) * (Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * Выборка.ДоляВыплаты) Тогда
				СуммаСПревышенияПоСтавке22 = Выборка.ВаловаяСуммаСПревышенияПоСтавке22 * Выборка.ДоляВыплаты;
			Иначе
				СуммаСПревышенияПоСтавке22 = Выборка.СуммаСПревышенияПоСтавке22;
			КонецЕсли;
			
		КонецЕсли;
	
		Если Сумма = 0 И СуммаСПревышения = 0 И СуммаСПревышенияПоСтавке18 = 0 И СуммаСПревышенияПоСтавке20 = 0 И СуммаСПревышенияПоСтавке22 = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		НовоеДвижение = Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
		НовоеДвижение.Сумма = - Сумма;
		НовоеДвижение.СуммаСПревышения = - СуммаСПревышения;
		НовоеДвижение.СуммаСПревышенияПоСтавке18 = - СуммаСПревышенияПоСтавке18;
		НовоеДвижение.СуммаСПревышенияПоСтавке20 = - СуммаСПревышенияПоСтавке20;
		НовоеДвижение.СуммаСПревышенияПоСтавке22 = - СуммаСПревышенияПоСтавке22;
		НовоеДвижение.Период = ДатаОперации;
		
		НовоеДвижение = Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
		НовоеДвижение.Сумма = Сумма;
		НовоеДвижение.СуммаСПревышения = СуммаСПревышения;
		НовоеДвижение.СуммаСПревышенияПоСтавке18 = СуммаСПревышенияПоСтавке18;
		НовоеДвижение.СуммаСПревышенияПоСтавке20 = СуммаСПревышенияПоСтавке20;
		НовоеДвижение.СуммаСПревышенияПоСтавке22 = СуммаСПревышенияПоСтавке22;
		НовоеДвижение.УстаревшаяДатаПолученияДохода = Выборка.МесяцНалоговогоПериода;
		НовоеДвижение.МесяцНалоговогоПериода = ДатаВыплаты;
		НовоеДвижение.Период = ДатаОперации;
		
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, ТаблицыКУдалению);
	
	Если Записывать Тогда
		Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Записать();
		Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Записывать = Ложь;
	Иначе
		Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
