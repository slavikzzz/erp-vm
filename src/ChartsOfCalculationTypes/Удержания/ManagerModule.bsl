#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

	Настройки.ПриПолученииСлужебныхРеквизитов = Истина;
	
КонецПроцедуры

// Ограничивает видимость реквизитов объекта в отчете по версии.
//
// Параметры:
//  Реквизиты - Массив - список имен реквизитов объекта.
Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	
    Реквизиты.Добавить("УдалитьКодДоходаНДФЛ");
	Реквизиты.Добавить("УдалитьКодДоходаСтраховыеВзносы");
	Реквизиты.Добавить("ДублироватьДляВнутреннихСовместителейИПодработок");
	Реквизиты.Добавить("ДублироватьДляПодработок");
	Реквизиты.Добавить("ЕстьВытесняющиеНачисления");
	Реквизиты.Добавить("ЕстьНакапливаемыеПоказатели");
	Реквизиты.Добавить("КоличествоБаллов");
	Реквизиты.Добавить("КоличествоОтображаемыхПоказателей");
	Реквизиты.Добавить("КоличествоПостоянныхПоказателей");
	Реквизиты.Добавить("КоличествоПоказателейИспользуемыхПриРасчете");
	Реквизиты.Добавить("ОчередностьРасчета");
	Реквизиты.Добавить("ПоказательПремирования");
	Реквизиты.Добавить("ТребуетсяРасчетБазы");
	Реквизиты.Добавить("ТребуетсяРасчетВремени");
	Реквизиты.Добавить("ТребуетсяРасчетНормыВремени");
	Реквизиты.Добавить("ТребуетсяРасчетКалендарныхДней");
	Реквизиты.Добавить("ТребуетсяРасчетКалендарныхДнейБезПраздников");
	Реквизиты.Добавить("ТребуетсяРасчетСверхурочных");
	Реквизиты.Добавить("ТребуетсяРасчетОтработаноДнейВсего");
	Реквизиты.Добавить("ТребуетсяРасчетОтработаноЧасовВсего");
	Реквизиты.Добавить("УчетВремениВЧасах");
	Реквизиты.Добавить("УчетВремениВДняхЧасах");
	Реквизиты.Добавить("УчитыватьВесьЗаработок");
	Реквизиты.Добавить("УчитыватьИзменениеЗначенийПоказателейВБазовомПериоде");
    Реквизиты.Добавить("УчитыватьИзмененияВремяВЧасах");
	Реквизиты.Добавить("ИспользуетСдельныйЗаработок");
	Реквизиты.Добавить("ИспользуетСреднийЗаработокОбщий");
	Реквизиты.Добавить("ИспользуетБухучетПоказателей");
	Реквизиты.Добавить("УчитыватьИзмененияВремяВЧасах");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СоздатьУдержанияПоНастройкам(НастройкиРасчетаЗарплаты = Неопределено, КоллекторУдержаний = Неопределено, ПараметрыПланаВидовРасчета = Неопределено, ТекущиеНастройкиРасчетаЗарплаты = Неопределено) Экспорт
	
	Если НастройкиРасчетаЗарплаты = Неопределено Тогда
		НастройкиРасчетаЗарплаты = РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты();
	КонецЕсли;
	
	Если ПараметрыПланаВидовРасчета = Неопределено Тогда
		ПараметрыПланаВидовРасчета = РасчетЗарплатыРасширенный.ОписаниеПараметровПланаВидовРасчета();
	КонецЕсли;
	
	ЗаписыватьУдержания = Ложь;
	Если КоллекторУдержаний = Неопределено Тогда
		// Соответствие, в которое будем накапливать объекты для последующей "пакетной" записи.
		КоллекторУдержаний = Новый Соответствие;
		ЗаписыватьУдержания = Истина;
	КонецЕсли;
	
	СвойстваУдержаний = СвойстваУдержанийПоКатегориям();
	
	// Профсоюзные взносы
	Если РасчетЗарплатыРасширенный.ИзмененаНастройкаРасчетаЗарплаты("ИспользоватьПрофсоюзныеВзносы", НастройкиРасчетаЗарплаты, ТекущиеНастройкиРасчетаЗарплаты) Тогда
		Если НастройкиРасчетаЗарплаты.ИспользоватьПрофсоюзныеВзносы Тогда
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания	= Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы]);
			Описание.Код					= НСтр("ru = 'ПРФВЗ';
													|en = 'PRFVZ '");
			Описание.Наименование			= НСтр("ru = 'Профсоюзные взносы';
													|en = 'Labor union contributions'");
			Описание.КраткоеНаименование 	= НСтр("ru = 'Профвзносы';
													|en = 'Union contributions'");
			Описание.ФормулаРасчета			= "РасчетнаяБаза * ПроцентПрофсоюзныхВзносов / 100";
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы);
		КонецЕсли;
	КонецЕсли;
	
	// Исполнительные документы
	Если РасчетЗарплатыРасширенный.ИзмененаНастройкаРасчетаЗарплаты("ИспользоватьИсполнительныеЛисты", НастройкиРасчетаЗарплаты, ТекущиеНастройкиРасчетаЗарплаты) Тогда
		Если НастройкиРасчетаЗарплаты.ИспользоватьИсполнительныеЛисты Тогда
			// Исполнительный лист
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ИсполнительныйЛист;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ИсполнительныйЛист]);
			Описание.Код						= НСтр("ru = 'ИСПДК';
														|en = 'ISPDK'");
			Описание.Наименование				= НСтр("ru = 'Удержание по исполнительному документу';
														|en = 'Deduction based on garnishment document'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Исп. лист';
														|en = 'Enforc. order'");
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
			
			// Вознаграждение платежному агенту.
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента]);
			Описание.Код						= НСтр("ru = 'АГЕНТ';
														|en = 'AGENT'");
			Описание.Наименование				= НСтр("ru = 'Вознаграждение платежного агента';
														|en = 'Remuneration of paying agent'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Возн. плат. агента';
														|en = 'Remuner. of paying agent'");
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
			
			// Создание тарифов платежных агентов.
			Справочники.ТарифыПлатежныхАгентов.СоздатьТарифыПоНастройкам(НастройкиРасчетаЗарплаты);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ИсполнительныйЛист);
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента);
		КонецЕсли;
	КонецЕсли;
	
	// ДСВ
	Если РасчетЗарплатыРасширенный.ИзмененаНастройкаРасчетаЗарплаты("ИспользоватьДСВ", НастройкиРасчетаЗарплаты, ТекущиеНастройкиРасчетаЗарплаты) Тогда
		Если НастройкиРасчетаЗарплаты.ИспользоватьДСВ Тогда
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ДСВ;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ДСВ]);
			Описание.Код						= НСтр("ru = 'ДСВЗН';
														|en = 'DSVZN'");
			Описание.Наименование				= НСтр("ru = 'Добровольные страховые взносы';
														|en = 'Voluntary insurance contributions'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Добр. страх. взносы';
														|en = 'Volunt. insur. contributions'");
			Описание.ФормулаРасчета				= "РасчетнаяБазаСтраховыеВзносы * ПроцентДСВ / 100";
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ДСВ);
		КонецЕсли;
	КонецЕсли;
	
	СоздатьУдержанияВзносовВНПФ(КоллекторУдержаний, СвойстваУдержаний, НастройкиРасчетаЗарплаты, ТекущиеНастройкиРасчетаЗарплаты);
	
	// Удержание в счет возмещения ущерба.
	Описание = ОписаниеУдержания();
	Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.УдержаниеВСчетРасчетовПоПрочимОперациям;
	ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.УдержаниеВСчетРасчетовПоПрочимОперациям]);
	Описание.Код						= НСтр("ru = 'ВЗМУЩ';
												|en = 'VZMUS'");
	Описание.Наименование				= НСтр("ru = 'Удержание в счет возмещения ущерба';
												|en = 'Deduction against damage compensation '");
	Описание.КраткоеНаименование 		= НСтр("ru = 'Возмещ. ущерба';
												|en = 'Damage compensat.'");
	Описание.ФормулаРасчета 			= "";
	Описание.Рассчитывается 			= Ложь;
	Описание.СсылкаНаОбъект = ПараметрыПланаВидовРасчета.СсылкиНачисленийУдержаний.УдержаниеВСчетВозмещенияУщерба;
	Описание.Отбор = "Ссылка";
	Если ПараметрыПланаВидовРасчета.ИспользоватьУдержаниеВСчетВозмещенияУщерба Тогда
		СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
		ПараметрыПланаВидовРасчета.СсылкиНачисленийУдержаний.УдержаниеВСчетВозмещенияУщерба = Описание.СсылкаНаОбъект;
	ИначеЕсли ПараметрыПланаВидовРасчета.НачальнаяНастройкаПрограммы Тогда
		Отбор = Новый Структура("Ссылка", Описание.СсылкаНаОбъект);
		ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.УдержаниеВСчетРасчетовПоПрочимОперациям, Отбор);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоДовольствия") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоДовольствия");
		Модуль.СоздатьУдержаниеЗаНеотработанныеДниОтпускаВоеннослужащим(ПолучитьФункциональнуюОпцию("ИспользоватьВоеннуюСлужбу"), КоллекторУдержаний, СвойстваУдержаний);
	КонецЕсли;
	
	// Удержания по ученическому договору.
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		Модуль.СоздатьОтключитьУдержаниеПоУченическомуДоговору(КоллекторУдержаний);
	КонецЕсли;
	
	// Удержания за ДМС
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МедицинскоеСтрахование") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МедицинскоеСтрахование");
		Модуль.СоздатьОтключитьУдержаниеЗаДМС(КоллекторУдержаний);
	КонецЕсли;
	
	// Удержания за неотработанные дни отпуска
	НастройкиПриложения = ЗарплатаКадрыРасширенный.НастройкиПрограммыБюджетногоУчреждения();
	Если НастройкиПриложения.ИспользоватьРасчетСохраняемогоДенежногоСодержания Тогда
		КатегорияУдержания = Перечисления.КатегорииУдержаний.ДенежноеСодержаниеУдержаниеЗаНеотработанныеДниОтпуска;
	Иначе
		КатегорияУдержания = Перечисления.КатегорииУдержаний.УдержаниеЗаНеотработанныеДниОтпуска;
	КонецЕсли;
	Если НастройкиРасчетаЗарплаты.СпособУдержанияИзлишнеНачисленныхОтпускных = ПредопределенноеЗначение("Перечисление.СпособыУдержанияИзлишнеНачисленныхОтпускных.Удержание") Тогда
		
		Описание = ОписаниеУдержания();
		Описание.КатегорияУдержания = КатегорияУдержания;
		Описание.Отбор = "КатегорияУдержания"; 
		СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
	Иначе
		ОтключитьИспользованиеУдержания(КоллекторУдержаний, КатегорияУдержания);
	КонецЕсли;
	
	// Расставляем порядок для вновь созданных видов расчета.
	ЗаполнитьПорядокУдержаний(КоллекторУдержаний);
	
	Если ЗаписыватьУдержания Тогда
		РасчетЗарплатыРасширенный.ЗаписатьПакетВидовРасчета(КоллекторУдержаний);
	КонецЕсли;
	
КонецПроцедуры

// Упорядочивает удержания в плане видов расчета.
//
Процедура ЗаполнитьПорядокУдержаний(КоллекторУдержаний) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Удержания.РеквизитДопУпорядочивания КАК Порядок
		|ИЗ
		|	ПланВидовРасчета.Удержания КАК Удержания
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеквизитДопУпорядочивания УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Порядок = Выборка.Порядок + 1;
	Иначе
		Порядок = 1;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из КоллекторУдержаний Цикл
		УдержаниеОбъект = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(УдержаниеОбъект.РеквизитДопУпорядочивания) Тогда
			УдержаниеОбъект.РеквизитДопУпорядочивания = Порядок;
			Порядок = Порядок + 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура создает в плане видов расчета элементы для отпуска и компенсации, 
// используемые в документе "Отпуск" и "Увольнение".
//
Процедура СоздатьУдержаниеЗаНеотработанныеДниОтпуска(ВидОтпуска, НаименованиеОтпуска, ДополнениеКода, КоллекторУдержаний, ДенежноеСодержание = Ложь, ПроверятьНаличиеУдержаний = Истина) Экспорт
	
	СвойстваУдержаний = СвойстваУдержанийПоКатегориям();
	
	НастройкиРасчетаЗарплаты = РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты();
	
	Если ДенежноеСодержание Тогда
		
		НастройкиПриложения = ЗарплатаКадрыРасширенный.НастройкиПрограммыБюджетногоУчреждения();
		
		КатегорияУдержания	= Перечисления.КатегорииУдержаний.ДенежноеСодержаниеУдержаниеЗаНеотработанныеДниОтпуска;
		Если НастройкиПриложения.ИспользоватьРасчетСохраняемогоДенежногоСодержания
			И НастройкиРасчетаЗарплаты.СпособУдержанияИзлишнеНачисленныхОтпускных = ПредопределенноеЗначение("Перечисление.СпособыУдержанияИзлишнеНачисленныхОтпускных.Удержание") Тогда
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания = КатегорияУдержания;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ДенежноеСодержаниеУдержаниеЗаНеотработанныеДниОтпуска]);
			Описание.Наименование		= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Удержание за неотраб. отпуск (%1)';
																										|en = 'Unearned leave deduction (%1)'"), НаименованиеОтпуска); 
			Описание.КраткоеНаименование = КраткоеНаименованиеУдержанияПоВидуОтпуска(ВидОтпуска);
			Описание.Код 				= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'УОТ%1';
																										|en = 'ULD%1'"), ДополнениеКода);
			Описание.ФормулаРасчета		= "ОКР(МесячноеДенежноеСодержание / СреднемесячноеКоличествоКалендарныхДнейОтпускаГосслужащих, 2) * КоличествоДнейКомпенсации";
			Описание.ВидОтпуска			= ВидОтпуска; 
			Описание.Отбор				= "ВидОтпуска,КатегорияУдержания"; 
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание, ПроверятьНаличиеУдержаний);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, КатегорияУдержания);
		КонецЕсли;
		
	Иначе
		
		КатегорияУдержания = Перечисления.КатегорииУдержаний.УдержаниеЗаНеотработанныеДниОтпуска;
		Если НастройкиРасчетаЗарплаты.СпособУдержанияИзлишнеНачисленныхОтпускных = ПредопределенноеЗначение("Перечисление.СпособыУдержанияИзлишнеНачисленныхОтпускных.Удержание") Тогда
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания = КатегорияУдержания;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.УдержаниеЗаНеотработанныеДниОтпуска]);
			Описание.Наименование		= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Удержание за неотработанный отпуск (%1)';
																										|en = 'Unearned leave deduction (%1)'"), НаименованиеОтпуска); 
			Описание.КраткоеНаименование = КраткоеНаименованиеУдержанияПоВидуОтпуска(ВидОтпуска);
			Описание.Код 				= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'УОТ%1';
																										|en = 'ULD%1'"), ДополнениеКода);
			Описание.ФормулаРасчета		= "СреднийЗаработокОбщий * КоличествоДнейКомпенсации";
			Описание.ВидОтпуска			= ВидОтпуска; 
			Описание.Отбор				= "ВидОтпуска,КатегорияУдержания"; 
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание, ПроверятьНаличиеУдержаний);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, КатегорияУдержания);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьУдержанияВзносовВНПФ(КоллекторУдержаний = Неопределено, СвойстваУдержаний = Неопределено, НастройкиРасчетаЗарплаты = Неопределено, ТекущиеНастройкиРасчетаЗарплаты = Неопределено) Экспорт
	
	ЗаписыватьУдержания = Ложь;
	Если КоллекторУдержаний = Неопределено Тогда
		// Если метод используется отдельно от формирования плана видов расчета по настройкам, 
		// то запись удержаний производится в нем, локально.
		КоллекторУдержаний = Новый Соответствие;
		ЗаписыватьУдержания = Истина;
	КонецЕсли;
	
	Если СвойстваУдержаний = Неопределено Тогда
		СвойстваУдержаний = СвойстваУдержанийПоКатегориям();
	КонецЕсли;
	
	Если НастройкиРасчетаЗарплаты = Неопределено Тогда
		НастройкиРасчетаЗарплаты = РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты();
	КонецЕсли;
	
	Если РасчетЗарплатыРасширенный.ИзмененаНастройкаРасчетаЗарплаты("ИспользоватьДобровольныеВзносыВНПФ", НастройкиРасчетаЗарплаты, ТекущиеНастройкиРасчетаЗарплаты) Тогда
		Если НастройкиРасчетаЗарплаты.ИспользоватьДобровольныеВзносыВНПФ Тогда
			//Добровольные взносы в НПФ
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ]);
			Описание.Код						= НСтр("ru = 'ДВНПФ';
														|en = 'ДВНПФ'");
			Описание.Наименование				= НСтр("ru = 'Добровольные взносы в НПФ';
														|en = 'Добровольные взносы в НПФ'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Добр. взносы НПФ';
														|en = 'Добр. взносы НПФ'");
			Описание.ФормулаРасчета				= "РасчетнаяБазаСтраховыеВзносы * ПроцентНПФ / 100";
			Описание.ЗапрашиваемыеПоказатели 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ПроцентНПФ");
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
			
			//Взносы в НПФ на долгосрочные сбережения
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ВзносыНаДолгосрочныеСбережения;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ВзносыНаДолгосрочныеСбережения]);
			Описание.Код						= НСтр("ru = 'ВПДС';
														|en = 'ВПДС'");
			Описание.Наименование				= НСтр("ru = 'Взносы в НПФ на долгосрочные сбережения';
														|en = 'Взносы в НПФ на долгосрочные сбережения'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Взносы на ПДС';
														|en = 'Взносы на ПДС'");
			Описание.ФормулаРасчета				= "РасчетнаяБазаСтраховыеВзносы * ПроцентНПФ / 100";
			Описание.ЗапрашиваемыеПоказатели 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ПроцентНПФ");
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
			
			//Взносы в НПФ на негосударственную пенсию
			Описание = ОписаниеУдержания();
			Описание.КатегорияУдержания			= Перечисления.КатегорииУдержаний.ПенсионныеВзносыВНПФ;
			ЗаполнитьЗначенияСвойств(Описание, СвойстваУдержаний[Перечисления.КатегорииУдержаний.ПенсионныеВзносыВНПФ]);
			Описание.Код						= НСтр("ru = 'ПВНПФ';
														|en = 'ПВНПФ'");
			Описание.Наименование				= НСтр("ru = 'Взносы в НПФ на негосударственную пенсию';
														|en = 'Взносы в НПФ на негосударственную пенсию'");
			Описание.КраткоеНаименование 		= НСтр("ru = 'Взносы на негос.пенс.';
														|en = 'Взносы на негос.пенс.'");
			Описание.ФормулаРасчета				= "РасчетнаяБазаСтраховыеВзносы * ПроцентНПФ / 100";
			Описание.ЗапрашиваемыеПоказатели 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ПроцентНПФ");
			СоздатьИзменитьУдержанияПоОписанию(КоллекторУдержаний, Описание);
		Иначе
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ);
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ВзносыНаДолгосрочныеСбережения);
			ОтключитьИспользованиеУдержания(КоллекторУдержаний, Перечисления.КатегорииУдержаний.ПенсионныеВзносыВНПФ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаписыватьУдержания Тогда
		ЗаписатьВидыРасчетаКоллектора(КоллекторУдержаний);
	КонецЕсли;
	
КонецПроцедуры

Функция КраткоеНаименованиеУдержанияПоВидуОтпуска(ВидОтпуска)
	
	НаименованиеУдержания = "";
	
	Если ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускПострадавшимВАварииЧАЭС") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. ЧАЭС';
									|en = 'Unearned leave for persons harmed in disaster at the Chernobyl NPP'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускУчебный") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. учебн. отп.';
									|en = 'Unearned study leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускБезОплатыУчебный") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. учебн. отп. б/о';
									|en = 'Unearned unpaid study leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отпуск';
									|en = 'Unearned leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускЗаСвойСчет") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. за св. сч.';
									|en = 'Unearned unpaid leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускБезОплатыПоТКРФ") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. без опл.';
									|en = 'Unearned unpaid leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускЗаВредность") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. за вредн.';
									|en = 'Unearned leave for harmful condit.'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускЗаВыслугуЛет") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. высл. лет';
									|en = 'Unearned long service leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускЗаВыслугуЛетНаГосударственнойСлужбе") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. высл. лет';
									|en = 'Unearned long service leave'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускНаСанаторноКурортноеЛечение") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. отп. на лечение';
									|en = 'Unearned leave for treatment'")
	ИначеЕсли ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Северный") Тогда
		НаименованиеУдержания = НСтр("ru = 'Неотраб. северн. отп.';
									|en = 'Unearned northern leave'")
	КонецЕсли;
	
	Возврат НаименованиеУдержания;
	
КонецФункции

// Создает и заполняет соответствие, ключом которого является категория удержания, 
// а значением - структура значений, определяющая заполнение свойств удержания данной категории.
//
// Параметры:
//	- КатегорияУдержания - ПеречислениеСсылка.КатегорииУдержаний.
//
// Возвращаемое значение:
//  Соответствие - свойства удержаний по категориям:
//     * Ключ     - ПеречислениеСсылка.КатегорииУдержаний
//     * Значение - Структура
//
Функция СвойстваУдержанийПоКатегориям() Экспорт
	
	СвойстваПоКатегориям = Новый Соответствие;
	
	// Значение по умолчанию
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПустаяСсылка(), СвойстваУдержанияПоКатегорииПоУмолчанию());
	
	// Профсоюзные взносы
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.Профвзносы;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	Описание.ОтборБазовых = Новый Структура("КатегорияНачисления", КатегорииБазовыхНачисленийПрофсоюзныхВзносов());
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПрофсоюзныеВзносы, Описание);
	
	// Исполнительный лист
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ИсполнительныйЛист;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты;
	Описание.ОчередностьРасчета = 1;
	Описание.ЯвляетсяВзысканием = Истина;
	Описание.ДополнениеРасчетнойБазы = Истина;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	Описание.НедоступныеСвойства.Добавить("ЯвляетсяВзысканием");
	Описание.ОтборБазовых = Новый Структура;
	Описание.ОтборБазовых.Вставить("КатегорияНачисления", КатегорииБазовыхНачисленийУдержанийПоИсполнительнымДокументам());
	ВидыДоходовИсполнительногоПроизводства = Новый Массив;
	ВидыДоходовИсполнительногоПроизводства.Добавить(Перечисления.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий);
	ВидыДоходовИсполнительногоПроизводства.Добавить(Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения);
	ВидыДоходовИсполнительногоПроизводства.Добавить(Перечисления.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые);
	Описание.ОтборБазовых.Вставить("ВидДоходаИсполнительногоПроизводства", ВидыДоходовИсполнительногоПроизводства);
	Описание.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ИсполнительныйЛист, Описание);
	
	// Вознаграждение платежного агента.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ВознаграждениеПлатежногоАгента;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента;
	Описание.ОчередностьРасчета = 2;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	Описание.НедоступныеСвойства.Добавить("ЯвляетсяВзысканием");
	Описание.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента, Описание);
	
	// Удержание за неотработанные дни отпуска.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ПоОтдельномуДокументуДоОкончательногоРасчета;
	Описание.ВидДокументаУдержания = Перечисления.ВидыДокументовУдержания.Увольнение;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ФормулаРасчета = "СреднийЗаработокОбщий * КоличествоДнейКомпенсации";
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.УдержаниеЗаНеотработанныеДниОтпуска, Описание);
	
	// Удержание за неотработанные дни отпуска, сохраняемое денежное содержание.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ПоОтдельномуДокументуДоОкончательногоРасчета;
	Описание.ВидДокументаУдержания = Перечисления.ВидыДокументовУдержания.Увольнение;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ФормулаРасчета = "ОКР(МесячноеДенежноеСодержание / СреднемесячноеКоличествоКалендарныхДнейОтпускаГосслужащих, 2) * КоличествоДнейКомпенсации";
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДенежноеСодержаниеУдержаниеЗаНеотработанныеДниОтпуска, Описание);
	
	// Удержание за неотработанные дни отпуска, сохраняемое денежное довольствие.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ПоОтдельномуДокументуДоОкончательногоРасчета;
	Описание.ВидДокументаУдержания = Перечисления.ВидыДокументовУдержания.ИсключениеИзСписковЧасти;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ФормулаРасчета = "СохраняемоеДенежноеДовольствие / СреднемесячноеКоличествоДнейВС * КоличествоДнейКомпенсации";
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.УдержаниеЗаОтпуск;
	Описание.НедоступныеСвойства.Добавить("СпособВыполненияУдержания");
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДенежноеДовольствиеУдержаниеЗаНеотработанныеДниОтпуска, Описание);
	
	// Прочее удержание в пользу третих лиц.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ПрочиеУдержания;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПрочееУдержаниеВПользуТретьихЛиц, Описание);
	
	// Прочее удержание в пользу компании.
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ВозмещениеУщерба;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.УдержаниеВСчетРасчетовПоПрочимОперациям, Описание);
	
	// ДСВ
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДСВ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДСВ, Описание);
	
	// ДобровольныеВзносыВНПФ
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ, Описание);
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ВзносыНаДолгосрочныеСбережения, Описание);
	Описание = СвойстваУдержанияПоКатегорииПоУмолчанию();
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ДобровольныеВзносыВНПФ;
	СвойстваПоКатегориям.Вставить(Перечисления.КатегорииУдержаний.ПенсионныеВзносыВНПФ, Описание);
	
	// По ученическому договору
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
	    Модуль = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		Модуль.ДополнитьСвойстваПоКатегориямУдержаниемПоУченическимДоговорам(СвойстваПоКатегориям, СвойстваУдержанияПоКатегорииПоУмолчанию());
	КонецЕсли;
	
	// ДМС
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.МедицинскоеСтрахование") Тогда
		МодульМедицинскоеСтрахование = ОбщегоНазначения.ОбщийМодуль("МедицинскоеСтрахование");
		МодульМедицинскоеСтрахование.ДополнитьСвойстваПоКатегориямУдержаниемПоДМС(СвойстваПоКатегориям, СвойстваУдержанияПоКатегорииПоУмолчанию());
	КонецЕсли;
	
	Возврат СвойстваПоКатегориям;
	
КонецФункции

Функция КатегорииУдержанийУчитывающихНДФЛ() Экспорт
	
	КатегорииУдержаний = Новый Массив;
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ИсполнительныйЛист);
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента);
		
	Возврат КатегорииУдержаний;
	
КонецФункции

Функция КатегорииУдержанийВлияющихНаНДФЛ() Экспорт
	
	КатегорииУдержаний = Новый Массив;
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ДСВ);
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ);
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ПенсионныеВзносыВНПФ);
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ВзносыНаДолгосрочныеСбережения);
		
	Возврат КатегорииУдержаний;
	
КонецФункции

Функция КатегорииУдержанийЗависящиеОтСтраховыхВзносов() Экспорт
	
	КатегорииУдержаний = Новый Массив;
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ДСВ);
	КатегорииУдержаний.Добавить(Перечисления.КатегорииУдержаний.ДобровольныеВзносыВНПФ);
		
	Возврат КатегорииУдержаний;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьОперативныеПоказателиДляВыявленияУдержаний() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Показатели.Показатель КАК Показатель
	|ИЗ
	|	ПланВидовРасчета.Удержания.Показатели КАК Показатели
	|ГДЕ
	|	Показатели.ОпределяющийПоказатель
	|	И Показатели.Показатель.ОперативныйПоказательСотрудника";
	
	Набор = РегистрыСведений.ОперативныеПоказателиДляВыявленияУдержаний.СоздатьНаборЗаписей();
	Набор.Загрузить(Запрос.Выполнить().Выгрузить());
	Набор.Записать();		
КонецПроцедуры	

Функция СвойстваУдержанияПоКатегорииПоУмолчанию()
	
	Описание = Новый Структура(
		"СпособВыполненияУдержания, 
		|СпособРасчета, 
		|ФормулаРасчета, 
		|ВидДокументаУдержания, 
		|ВидОтпуска, 
		|ВидОперацииПоЗарплате,
		|ПорядокОпределенияРасчетногоПериодаСреднегоЗаработка,
		|ОтборБазовых,
		|ДополнениеРасчетнойБазы,
		|ИменаИнструкций, 
		|ПараметрыВыбора, 
		|Рассчитывается,
		|ОчередностьРасчета,
		|ЯвляетсяВзысканием,
		|НедоступныеСвойства,
		|СтратегияОтраженияВУчете");
	
	// Недоступные свойства: строка, в которой перечислены имена свойств начислений, 
	// выбор которых недоступен для указанной категории.
	
	// Свойства по умолчанию
	Описание.НедоступныеСвойства = Новый Массив;
	Описание.ИменаИнструкций = "НедоступныеСвойства,ИменаИнструкций,ПараметрыВыбора,ОтборБазовых,ДополнениеРасчетнойБазы"; 
	Описание.Рассчитывается = Истина;
	Описание.ОчередностьРасчета = 0;
	Описание.ЯвляетсяВзысканием = Ложь;
	Описание.ДополнениеРасчетнойБазы = Ложь;
	Описание.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям;
	
	Возврат Описание;
	
КонецФункции

Функция ОписаниеУдержания() Экспорт
	
	Описание = Новый Структура(
	"Код, 
	|Наименование,
	|КраткоеНаименование,
	|КатегорияУдержания,
	|СпособВыполненияУдержания,
	|СпособРасчета,
	|ФормулаРасчета,
	|ВидОперацииПоЗарплате,
	|УчаствуетВРасчетеПервойПоловиныМесяца,
	|ВидОтпуска,
	|ОтборБазовых,
	|ДополнениеРасчетнойБазы,
	|ОпределяющиеПоказатели,
	|Отбор,
	|Рассчитывается,
	|ЯвляетсяВзысканием,
	|СтратегияОтраженияВУчете,
	|ЗапрашиваемыеПоказатели,
	|СсылкаНаОбъект"); // Содержит ссылку на объект, который уже был создан Из помощника начальной настройки программы);
	
	Описание.СпособВыполненияУдержания = Перечисления.СпособыВыполненияУдержаний.ЕжемесячноПриОкончательномРасчете;
	Описание.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ПроизвольнаяФормула;
	Описание.УчаствуетВРасчетеПервойПоловиныМесяца = Истина;
	Описание.ДополнениеРасчетнойБазы = Ложь;
	
	Возврат Описание;
	
КонецФункции

Процедура СоздатьИзменитьУдержанияПоОписанию(Коллектор, Описание, ПроверятьНаличиеУдержаний = Истина) Экспорт
	
	Отбор = Неопределено;
	Если ЗначениеЗаполнено(Описание.Отбор) Тогда
		Отбор = Новый Структура(Описание.Отбор);
		Если Описание.Отбор = "Ссылка" Тогда
			Отбор.Ссылка = Описание.СсылкаНаОбъект;
		Иначе
			ЗаполнитьЗначенияСвойств(Отбор, Описание, Описание.Отбор);
		КонецЕсли;
	КонецЕсли;
	
	УдержанияПоКатегории = РасчетЗарплаты.УдержанияПоКатегории(Описание.КатегорияУдержания, Отбор);
	
	УдержаниеОбъект = Неопределено;
	Если ПроверятьНаличиеУдержаний Тогда
		Если УдержанияПоКатегории.Количество() > 0 Тогда
			
			Если Описание.Отбор = "Ссылка" Тогда
				УдержаниеОбъект = УдержанияПоКатегории[0].ПолучитьОбъект();
			Иначе
				Для Каждого УдержаниеСсылка Из УдержанияПоКатегории Цикл
					УдержаниеОбъект = Коллектор[УдержаниеСсылка];
					Если УдержаниеОбъект = Неопределено Тогда
						УдержаниеОбъект = УдержаниеСсылка.ПолучитьОбъект();
						Коллектор.Вставить(УдержаниеСсылка, УдержаниеОбъект);
					КонецЕсли;
					УдержаниеОбъект.ВАрхиве = Ложь;
					УдержаниеОбъект.ПометкаУдаления = Ложь;
				КонецЦикла;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если УдержаниеОбъект = Неопределено Тогда
		// Устанавливаем ссылку новому объекту.
		УдержаниеСсылка = ПланыВидовРасчета.Удержания.ПолучитьСсылку();
		УдержаниеОбъект = ПланыВидовРасчета.Удержания.СоздатьВидРасчета();
		УдержаниеОбъект.УстановитьСсылкуНового(УдержаниеСсылка);
		Описание.СсылкаНаОбъект = УдержаниеСсылка;
	Иначе
		УдержаниеСсылка = УдержаниеОбъект.Ссылка;
		УдержаниеОбъект.ВАрхиве = Ложь;
		УдержаниеОбъект.ПометкаУдаления = Ложь;
	КонецЕсли;
	
	Коллектор.Вставить(УдержаниеСсылка, УдержаниеОбъект);
	
	ЗаполнитьЗначенияСвойств(УдержаниеОбъект, Описание);
	
	Если Описание.ДополнениеРасчетнойБазы Тогда 
		ЗаполнитьПрочиеБазовыеДоходыУдержания(УдержаниеОбъект);
	КонецЕсли;
	
	РасчетЗарплатыРасширенный.ЗаполнитьТаблицуПоказателейВидаРасчета(УдержаниеОбъект, Коллектор);

	// Базовые начисления
	Если Описание.ОтборБазовых <> Неопределено Тогда
		ОтборБазовых = ОбщегоНазначения.СкопироватьРекурсивно(Описание.ОтборБазовых);
		УдержаниеОбъект.ДополнительныеСвойства.Вставить("ОтборБазовых", ОтборБазовых);
	КонецЕсли;
	
	// Определяющие показатели
	ЗарплатаКадрыРасширенный.ОтметитьОпределяющиеПоказатели(УдержаниеОбъект, Описание.ОпределяющиеПоказатели);
	
	// Запрашиваемые показатели
	ЗарплатаКадрыРасширенный.ОтметитьЗапрашиваемыеПоказатели(УдержаниеОбъект, Описание.ЗапрашиваемыеПоказатели);
	
КонецПроцедуры

Процедура ОтключитьИспользованиеУдержания(Коллектор, КатегорияУдержания, Отбор = Неопределено) Экспорт
		
	УдержанияПоКатегории = РасчетЗарплаты.УдержанияПоКатегории(КатегорияУдержания, Отбор);
	
	Для Каждого УдержаниеСсылка Из УдержанияПоКатегории Цикл
		УдержаниеОбъект = Коллектор[УдержаниеСсылка];
		Если УдержаниеОбъект = Неопределено Тогда
			УдержаниеОбъект = УдержаниеСсылка.ПолучитьОбъект();
			Коллектор.Вставить(УдержаниеСсылка, УдержаниеОбъект);
		КонецЕсли;
		УдержаниеОбъект.ВАрхиве = Истина;
	КонецЦикла;

КонецПроцедуры

Функция КатегорииБазовыхНачисленийУдержанийПоИсполнительнымДокументам() Экспорт 
	
	Категории = Новый Массив;
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СдельнаяОплатаТруда);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.МатериальнаяПомощь);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.МатериальнаяПомощьПриОтпуске);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПростояПоВинеРаботодателя);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПростояПоНезависящимОтРаботодателяПричинам);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаКомандировки);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПоСреднемуЗаработку);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоСреднегоЗаработка);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаЗаСовмещение);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособие);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаТрудаВНатуральнойФорме);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, ПланыВидовРасчета.Начисления.КатегорииНачисленийОплатыДолейРКиСН());
	
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодКомандировки);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеКомпенсацияОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СохраняемоеДенежноеСодержание);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособиеМесячноеДенежноеСодержание);
	
	// Добавляем еще и больничные листы, но при расчете будем включать сумму расчетной базы только для тех исполнительных
	// документов,  для которых указано "Учитывать больничные листы".
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоПрофзаболевание);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоСреднегоЗаработкаЗаДниБолезни);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоДенежногоСодержанияЗаДниБолезни);
	
	Возврат Категории;
	
КонецФункции

Функция КатегорииБазовыхНачисленийПрофсоюзныхВзносов()
	
	Категории = Новый Массив;
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СдельнаяОплатаТруда);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.МатериальнаяПомощьПриОтпуске);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПростояПоВинеРаботодателя);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПростояПоНезависящимОтРаботодателяПричинам);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаКомандировки);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПоСреднемуЗаработку);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодам);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоПрофзаболевание);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоСреднегоЗаработкаЗаДниБолезни);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособие);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоСреднегоЗаработка);
	Категории.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаЗаСовмещение);
	
	Возврат Категории;
	
КонецФункции

Процедура ЗаполнитьПрочиеБазовыеДоходыУдержания(Удержание) Экспорт 
	
	ВидыПрочихДоходов = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРегистрациюПрочихДоходов") Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыПрочихДоходов, Справочники.ВидыПрочихДоходовФизическихЛиц.ВидыВыплатДополненияРасчетнойБазыУдержаний());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВыплатыБывшимСотрудникам") Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыПрочихДоходов, Справочники.ВидыВыплатБывшимСотрудникам.ВидыВыплатДополненияРасчетнойБазыУдержаний());
	КонецЕсли;
	
	Удержание.ПрочиеБазовыеДоходы.Очистить();
	
	Для Каждого ВидДохода Из ВидыПрочихДоходов Цикл 
		НоваяСтрока = Удержание.ПрочиеБазовыеДоходы.Добавить();
		НоваяСтрока.ВидДохода = ВидДохода;
	КонецЦикла;
	
	Удержание.ПрочиеБазовыеДоходы.Сортировать("ВидДохода");
		
КонецПроцедуры

Процедура ЗаписатьВидыРасчетаКоллектора(КоллекторУдержаний)

	Для Каждого КлючИЗначение Из КоллекторУдержаний Цикл
		КлючИЗначение.Значение.ДополнительныеСвойства.Вставить("ИзменениеПланаВидовРасчетаПоНастройкам", Истина);
	КонецЦикла;
	
	ЗаполнитьПорядокУдержаний(КоллекторУдержаний);
	РасчетЗарплатыРасширенный.ЗаписатьПакетВидовРасчета(КоллекторУдержаний);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.30.196";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("aa2e1234-c9a8-4990-b3df-23260f7894b3");
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура       = "ПланыВидовРасчета.Удержания.СоздатьУдержанияВзносовВНПФПриОбновлении";
	Обработчик.Комментарий     = НСтр("ru = 'Создание удержаний для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""';
										|en = 'Создание удержаний для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""'");
	
КонецПроцедуры

Процедура ЗаполнитьПрочиеБазовыеДоходы(ПараметрыОбновления = Неопределено) Экспорт 
	
	ВидыПрочихДоходов = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВыплатыБывшимСотрудникам") Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыПрочихДоходов, Справочники.ВидыПрочихДоходовФизическихЛиц.ВидыВыплатДополненияРасчетнойБазыУдержаний());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРегистрациюПрочихДоходов") Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыПрочихДоходов, Справочники.ВидыВыплатБывшимСотрудникам.ВидыВыплатДополненияРасчетнойБазыУдержаний());
	КонецЕсли;
	
	Если ВидыПрочихДоходов.Количество() = 0 Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	    Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Удержания.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланВидовРасчета.Удержания КАК Удержания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.ПрочиеБазовыеДоходы КАК УдержанияПрочиеБазовыеДоходы
	               |		ПО Удержания.Ссылка = УдержанияПрочиеБазовыеДоходы.Ссылка
	               |ГДЕ
	               |	Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |	И УдержанияПрочиеБазовыеДоходы.Ссылка ЕСТЬ NULL";
				   
	Выборка = Запрос.Выполнить().Выбрать();			   
	Если Выборка.Количество() = 0 Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	    Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл 
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Удержания", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		УдержаниеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Для Каждого ВидДохода Из ВидыПрочихДоходов Цикл 
			НоваяСтрока = УдержаниеОбъект.ПрочиеБазовыеДоходы.Добавить();
			НоваяСтрока.ВидДохода = ВидДохода;
		КонецЦикла;
		
		УдержаниеОбъект.ПрочиеБазовыеДоходы.Сортировать("ВидДохода");
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(УдержаниеОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРегистрОперативныеПоказателиДляВыявленияУдержаний(ПараметрыОбновления = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Показатели.Показатель КАК Показатель
	|ИЗ
	|	ПланВидовРасчета.Удержания.Показатели КАК Показатели
	|ГДЕ
	|	Показатели.ОпределяющийПоказатель";
	
	РезультатЗапроса = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ВыполнитьЗапросПолученияОбновляемыхДанных(Запрос, ПараметрыОбновления,, Неопределено);
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбработчик(ПараметрыОбновления);
		Возврат;
	КонецЕсли;	
	
	ОписаниеБлокировки = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОписаниеБлокируемыхДанных(Метаданные.РегистрыСведений.ОперативныеПоказателиДляВыявленияУдержаний);
	
	Если ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.НачатьОбновлениеДанных(ОписаниеБлокировки, ПараметрыОбновления) Тогда
		Набор = РегистрыСведений.ОперативныеПоказателиДляВыявленияУдержаний.СоздатьНаборЗаписей();
		Набор.Загрузить(РезультатЗапроса.Выгрузить());
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбработчик(ПараметрыОбновления);	
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПродолжитьОбработчик(ПараметрыОбновления);		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура УдалитьДоходВНатуральнойФормеИзРасчетнойБазыИЛ(ПараметрыОбновления = Неопределено) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка КАК Ссылка,
	|	УдержанияБазовыеВидыРасчета.ВидРасчета КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.БазовыеВидыРасчета КАК УдержанияБазовыеВидыРасчета
	|		ПО Удержания.Ссылка = УдержанияБазовыеВидыРасчета.Ссылка
	|			И (УдержанияБазовыеВидыРасчета.ВидРасчета.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДоходВНатуральнойФорме))
	|ГДЕ
	|	Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	|	И НЕ УдержанияБазовыеВидыРасчета.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	    Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ВидРасчета");
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Удержания", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		УдержаниеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Пока Выборка.Следующий() Цикл
			СтруктураПоиска.ВидРасчета = Выборка.ВидРасчета;
			НайденныеСтроки = УдержаниеОбъект.БазовыеВидыРасчета.НайтиСтроки(СтруктураПоиска);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				УдержаниеОбъект.БазовыеВидыРасчета.Удалить(НайденнаяСтрока);
			КонецЦикла;
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(УдержаниеОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
КонецПроцедуры

Процедура СоздатьУдержанияВзносовВНПФПриОбновлении(ПараметрыОбновления = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		СоздатьУдержанияВзносовВНПФ();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Ошибка создания удержаний для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""';
				|en = 'Ошибка создания удержаний для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.ПланыВидовРасчета.Удержания, ,
			НСтр("ru = 'Не удалось создать удержания для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""';
				|en = 'Не удалось создать удержания для категорий ""Взносы в НПФ на негосударственную пенсию"", ""ВзносыНаДолгосрочныеСбережения""'",
				ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецПопытки;
		
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);

КонецПроцедуры
	
#КонецОбласти

#КонецЕсли